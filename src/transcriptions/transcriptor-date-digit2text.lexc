! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================= !
! The Finnish dates !
! ================= !


! 12.8.
! Borgekuun guoktelot beaivi

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG ! For substandard month names used in Oahpa
! They will be accepted but not generated.
 
LEXICON Root
 
@U.MONTH.1@:januaarikuun% @U.MONTH.1@ 31DAYS ;
@U.MONTH.2@:fepruaarikuun% @U.MONTH.2@ 29DAYS ;
@U.MONTH.3@:marsikuun% @U.MONTH.3@ 31DAYS ;
@U.MONTH.4@:aprillikuun% @U.MONTH.4@ 30DAYS ;
@U.MONTH.5@:maikuun% @U.MONTH.5@ 31DAYS ;
@U.MONTH.6@:juunikuun% @U.MONTH.6@ 30DAYS ;
@U.MONTH.7@:juulikuun% @U.MONTH.7@ 31DAYS ;
@U.MONTH.8@:aukustikuun% @U.MONTH.8@ 31DAYS ;
@U.MONTH.9@:septemperikuun% @U.MONTH.9@ 30DAYS ;
@U.MONTH.10@:oktooperikuun% @U.MONTH.10@ 31DAYS ;
@U.MONTH.11@:novemperikuun% @U.MONTH.11@ 30DAYS ;
@U.MONTH.12@:desemperikuun% @U.MONTH.12@ 31DAYS ;

 
LEXICON 29DAYS
1:ensimäinen B ;
2:toinen B ;
3:kolmas B ;
4:neljes B ;
5:viiđes B ;
6:kuuđes B ;
7:seittemes B ;
8:kahđekses B ;
9:yhđekses B ;
1%0:kymmenes B ;
11:yhđestoista B ;
12:kahđestoista B ;
13:kolmastoista B ;
14:neljestoista B ;
15:viiđestoista B ;
16:kuuđestoista B ;
17:seittemestoista B ;
18:kahđeksestoista B ;
19:yhđeksestoista B ;
2%0:kahđeskymmenes B ;
21:kahđeskymmenesensimäinen B ;
22:kahđeskymmenestoinen B ;
23:kahđeskymmeneskolmas B ;
24:kahđeskymmenesneljes B ;
25:kahđeskymmenesviiđes B ;
26:kahđeskymmeneskuuđes B ;
27:kahđeskymmenesseittemes B ;
28:kahđeskymmeneskahđekses B ;
29:kahđeskymmenesyhđekses B ;
 
LEXICON 30DAYS
3%0:kolmaskymmenes B ;
29DAYS ;
 
LEXICON 31DAYS
31:kolmaskymmenesensimäinen B ;
30DAYS ;
 
LEXICON B
%.:% päivä MONTH ;
 
LEXICON MONTH
1@U.MONTH.1@:@U.MONTH.1@ X ;
2@U.MONTH.2@:@U.MONTH.2@ X ;
3@U.MONTH.3@:@U.MONTH.3@ X ;
4@U.MONTH.4@:@U.MONTH.4@ X ;
5@U.MONTH.5@:@U.MONTH.5@ X ;
6@U.MONTH.6@:@U.MONTH.6@ X ;
7@U.MONTH.7@:@U.MONTH.7@ X ;
8@U.MONTH.8@:@U.MONTH.8@ X ;
9@U.MONTH.9@:@U.MONTH.9@ X ;
1%0@U.MONTH.10@:@U.MONTH.10@ X ;
11@U.MONTH.11@:@U.MONTH.11@ X ;
12@U.MONTH.12@:@U.MONTH.12@ X ;
 
LEXICON X
%.: # ;
! # ; ! sloppy punctuation
