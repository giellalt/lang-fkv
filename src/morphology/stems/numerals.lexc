!! !!!Numerals
!! Numerals are morphologically nominals, only exception to noun inflection
!! is that they form compounds with special agreeing patterns. Syntactically
!! and semantically numerals are clearly separate subset of nominals. Some
!! of the numerals stem variation patterns are rare to non-existent for other
!! nominal types. Numerals have two subclasses, cardinals and ordinals.

! TODO: unimpl.
LEXICON NUM 
!! Numerals have been split in three sections, the compounding parts
!! of cardinals and ordinals, and the non-compounding ones:
!!€gt-norm: Numeral
!!€ kaksikymmentäkolmetuhatta: kaksi+Num+Card+Sg+Nom#kymmenen+Num+Card+Sg+Par#kolme+Num+Card+Sg+Nom#tuhat+Num+Card+Sg+Par 23,000
!!€ kahdessadasneljäs: kahdes+Num+Ord+Sg+Nom#sadas+Num+Ord+Sg+Nom#neljäs+Num+Ord+Sg+Nom 204rd
!!€ viitisenkymmentä: viitisen+Num#kymmentä 50-ish
0    NUM_CARD_ALL    ;
0    NUM_ORD_ALL ;
0    NUM_OTHERS  ;

 +Use/Circ: ARABIC ;          ! for the arabic numerals        !
 +Use/Circ: ROMAN ;           ! for the roman numerals         !
 +Use/Circ: NUM-PREFIXES ;    ! for §34 etc.                   !
 +Use/Circ: ISOLATED-NUMEXP ; ! for ½ etc.                     !

LEXICON NUM_CARD_ALL
!! The compounding parts of cardinals are the number multiplier words.
!!€gt-norm: cardinal
!!€ yksi: yksi+Num+Card+Sg+Nom one
!!€ viidelle: viisi+Num+Card+Sg+All five
!!€ tuhatta: tuhat+Num+Card+Sg+Par thousand
yksi+Num+Card:y             NUM_YKSI  ;
kaksi+Num+Card:ka           NUM_KAKSI ;
kolme+Num+Card:kolm         NUM_KOLME ;
neljä+Num+Card:nelj         NUM_NELJÄ ;
viisi+Num+Card:vii          NUM_VIISI ;
kuusi+Num+Card:kuu          NUM_KUUSI ;
seitsemän+Num+Card:seitsem  NUM_YHDEKSÄN ;
kahdeksan+Num+Card:kahdeks  NUM_KAHDEKSAN ;
yhdeksän+Num+Card:yhdeks    NUM_YHDEKSÄN ;
kymmenen+Num+Card:kymmen    NUM_KYMMENEN ;
sata+Num+Card:sa            NUM_SATA ;
tuhat+Num+Card:tuha         NUM_TUHAT ;
miljoona+Num+Card:miljoon  NUM_MILJOONA ;
miljardi+Num+Card:miljard  NUM_MILJARDI ;
biljoona+Num+Card:biljoon  NUM_MILJOONA ;
biljardi+Num+Card:biljard  NUM_MILJARDI ;
triljoona+Num+Card:triljoon  NUM_MILJOONA ;
triljardi+Num+Card:triljard  NUM_MILJARDI ;
kvadriljoona+Num+Card:kvadriljoon  NUM_MILJOONA ;
kvadriljardi+Num+Card:kvadriljard  NUM_MILJARDI ;
kvintiljoona+Num+Card:kvintiljoon  NUM_MILJOONA ;
kvintiljardi+Num+Card:kvintiljard  NUM_MILJARDI ;
sekstiljoona+Num+Card:sekstiljoon  NUM_MILJOONA ;
sekstiljardi+Num+Card:sekstiljard  NUM_MILJARDI ;
oktiljoona+Num+Card:oktiljoon  NUM_MILJOONA ;
oktiljardi+Num+Card:oktiljard  NUM_MILJARDI ;
noviljoona+Num+Card:noviljoon  NUM_MILJOONA ;
noviljardi+Num+Card:noviljard  NUM_MILJARDI ;
undekiljoona+Num+Card:undekiljoon  NUM_MILJOONA ;
undekiljardi+Num+Card:undekiljard  NUM_MILJARDI ;
duodekiljoona+Num+Card:duodekiljoon  NUM_MILJOONA ;
duodekiljardi+Num+Card:duodekiljard  NUM_MILJARDI ;
tredekiljoona+Num+Card:tredekiljoon  NUM_MILJOONA ;
tredekiljardi+Num+Card:tredekiljard  NUM_MILJARDI ;
kvattuordekiljoona+Num+Card:kvattuordekiljoon  NUM_MILJOONA ;
kvattuordekiljardi+Num+Card:kvattuordekiljard  NUM_MILJARDI ;
kvindekiljoona+Num+Card:kvindekiljoon  NUM_MILJOONA ;
kvindekiljardi+Num+Card:kvindekiljard  NUM_MILJARDI ;
sedekiljoona+Num+Card:sedekiljoon  NUM_MILJOONA ;
sedekiljardi+Num+Card:sedekiljard  NUM_MILJARDI ;
septendekiljoona+Num+Card:septendekiljoon  NUM_MILJOONA ;
septendekiljardi+Num+Card:septendekiljard  NUM_MILJARDI ;
oktodekiljoona+Num+Card:oktodekiljoon  NUM_MILJOONA ;
oktodekiljardi+Num+Card:oktodekiljard  NUM_MILJARDI ;
novemdekiljoona+Num+Card:novemdekiljoon  NUM_MILJOONA ;
novemdekiljardi+Num+Card:novemdekiljard  NUM_MILJARDI ;
vidingtiljoona+Num+Card:vidingtiljoon  NUM_MILJOONA ;
vidingtiljardi+Num+Card:vidingtiljard  NUM_MILJARDI ;
tridingtiljoona+Num+Card:tridingtiljoon  NUM_MILJOONA ;
tridingtiljardi+Num+Card:tridingtiljard  NUM_MILJARDI ;
sentiljoona+Num+Card:sentiljoon  NUM_MILJOONA ;
sentiljardi+Num+Card:sentiljard  NUM_MILJARDI ;
kiljoona+Num+Card:kiljoon  NUM_MILJOONA ;
kiljardi+Num+Card:kiljard  NUM_MILJARDI ;
piljoona+Num+Card:piljoon  NUM_MILJOONA ;
piljardi+Num+Card:piljard  NUM_MILJARDI ;
Googol+Num+Card:Googol          NUM_GOOGOL  ;
Googolplex+Num+Card:Googolplex  NUM_GOOGOL  ;
seitsämän+Err/Orth+Num+Card:seitsäm  NUM_YHDEKSÄN    ;

LEXICON NUM_CARD_SUFFIX
!! The suffixes only appear after cardinal multipliers
!!€gt-norm: Cardinal multiplicants
!!€ viisikymmentä: viisi+Num+Card+Sg+Nom#kymmentä
!!€ neljäsataatuhatta: neljä+Num+Card+Sg+Nom#sata+Num+Card+Sg+Par#tuhatta
kymmentä    #   ;
sataa       #   ;
tuhatta     #   ;
kymmentätuhatta #   ;
sataatuhatta    #   ;
miljoonaa       #   ;
miljardia       #   ;
biljoonaa # ;
biljardia # ;
triljoonaa # ;
triljardia # ;
kvadriljoonaa # ;
kvadriljardia # ;
kvintiljoonaa # ;
kvintiljardia # ;
sekstiljoonaa # ;
sekstiljardia # ;
oktiljoonaa # ;
oktiljardia # ;
noviljoonaa # ;
noviljardia # ;
undekiljoonaa # ;
undekiljardia # ;
duodekiljoonaa # ;
duodekiljardia # ;
tredekiljoonaa # ;
tredekiljardia # ;
kvattuordekiljoonaa # ;
kvattuordekiljardia # ;
kvindekiljoonaa # ;
kvindekiljardia # ;
sedekiljoonaa # ;
sedekiljardia # ;
septendekiljoonaa # ;
septendekiljardia # ;
oktodekiljoonaa # ;
oktodekiljardia # ;
novemdekiljoonaa # ;
novemdekiljardia # ;
vidingtiljoonaa # ;
vidingtiljardia # ;
tridingtiljoonaa # ;
tridingtiljardia # ;
sentiljoonaa # ;
sentiljardia # ;
kiljoonaa # ;
kiljardia # ;
piljoonaa # ;
piljardia # ;
toista              #    ;
kolmatta          #    ;
neljättä          #    ;
kuudetta          #    ;
viidettä          #    ;
seitsemättä    #    ;
kahdeksatta    #    ;
yhdeksättä      #    ;
kymmenettä      #    ;

LEXICON NUM_ORD_ALL
!! The compounding parts of ordinals are the number multiplier words.
!!€gt-norm: Ordinal numerals
!!€ neljäs: neljäs+Num+Ord+Sg+Nom
!!€ viidennelle: viides+Num+Ord+Sg+All
!!€ tuhannetta: tuhannes+Num+Ord+Sg+Par
ensimmäinen+Num+Ord:ensimmäi    NUM_ENSIMMÄINEN ;
yhdes+Num+Ord:yhde              NUM_NELJÄS ;
toinen+Num+Ord:toi              NUM_TOINEN ;
kahdes+Num+Ord:kahde            NUM_KOLMAS ;
kolmas+Num+Ord:kolma            NUM_KOLMAS ;
neljäs+Num+Ord:neljä            NUM_NELJÄS ;
viides+Num+Ord:viide            NUM_NELJÄS ;
kuudes+Num+Ord:kuude            NUM_KOLMAS ;
seitsemäs+Num+Ord:seitsemä      NUM_NELJÄS ;
kahdeksas+Num+Ord:kahdeksa      NUM_KOLMAS ;
yhdeksäs+Num+Ord:yhdeksä        NUM_NELJÄS ;
kymmenes+Num+Ord:kymmene        NUM_NELJÄS ;
sadas+Num+Ord:sada              NUM_KOLMAS ;
tuhannes+Num+Ord:tuhanne        NUM_KOLMAS ;
miljoonas+Num+Ord:miljoona  NUM_KOLMAS ;
miljardis+Num+Ord:miljardi  NUM_KOLMAS   ;
biljoonas+Num+Ord:biljoona  NUM_KOLMAS ;
biljardis+Num+Ord:biljardi  NUM_KOLMAS   ;
triljoonas+Num+Ord:triljoona  NUM_KOLMAS ;
triljardis+Num+Ord:triljardi  NUM_KOLMAS   ;
kvadriljoonas+Num+Ord:kvadriljoona  NUM_KOLMAS ;
kvadriljardis+Num+Ord:kvadriljardi  NUM_KOLMAS   ;
kvintiljoonas+Num+Ord:kvintiljoona  NUM_KOLMAS ;
kvintiljardis+Num+Ord:kvintiljardi  NUM_KOLMAS   ;
sekstiljoonas+Num+Ord:sekstiljoona  NUM_KOLMAS ;
sekstiljardis+Num+Ord:sekstiljardi  NUM_KOLMAS   ;
oktiljoonas+Num+Ord:oktiljoona  NUM_KOLMAS ;
oktiljardis+Num+Ord:oktiljardi  NUM_KOLMAS   ;
noviljoonas+Num+Ord:noviljoona  NUM_KOLMAS ;
noviljardis+Num+Ord:noviljardi  NUM_KOLMAS   ;
undekiljoonas+Num+Ord:undekiljoona  NUM_KOLMAS ;
undekiljardis+Num+Ord:undekiljardi  NUM_KOLMAS   ;
duodekiljoonas+Num+Ord:duodekiljoona  NUM_KOLMAS ;
duodekiljardis+Num+Ord:duodekiljardi  NUM_KOLMAS   ;
tredekiljoonas+Num+Ord:tredekiljoona  NUM_KOLMAS ;
tredekiljardis+Num+Ord:tredekiljardi  NUM_KOLMAS   ;
kvattuordekiljoonas+Num+Ord:kvattuordekiljoona  NUM_KOLMAS ;
kvattuordekiljardis+Num+Ord:kvattuordekiljardi  NUM_KOLMAS   ;
kvindekiljoonas+Num+Ord:kvindekiljoona  NUM_KOLMAS ;
kvindekiljardis+Num+Ord:kvindekiljardi  NUM_KOLMAS   ;
sedekiljoonas+Num+Ord:sedekiljoona  NUM_KOLMAS ;
sedekiljardis+Num+Ord:sedekiljardi  NUM_KOLMAS   ;
septendekiljoonas+Num+Ord:septendekiljoona  NUM_KOLMAS ;
septendekiljardis+Num+Ord:septendekiljardi  NUM_KOLMAS   ;
oktodekiljoonas+Num+Ord:oktodekiljoona  NUM_KOLMAS ;
oktodekiljardis+Num+Ord:oktodekiljardi  NUM_KOLMAS   ;
novemdekiljoonas+Num+Ord:novemdekiljoona  NUM_KOLMAS ;
novemdekiljardis+Num+Ord:novemdekiljardi  NUM_KOLMAS   ;
vidingtiljoonas+Num+Ord:vidingtiljoona  NUM_KOLMAS ;
vidingtiljardis+Num+Ord:vidingtiljardi  NUM_KOLMAS   ;
tridingtiljoonas+Num+Ord:tridingtiljoona  NUM_KOLMAS ;
tridingtiljardis+Num+Ord:tridingtiljardi  NUM_KOLMAS   ;
sentiljoonas+Num+Ord:sentiljoona  NUM_KOLMAS ;
sentiljardis+Num+Ord:sentiljardi  NUM_KOLMAS   ;
kiljoonas+Num+Ord:kiljoona  NUM_KOLMAS ;
kiljardis+Num+Ord:kiljardi  NUM_KOLMAS   ;
piljoonas+Num+Ord:piljoona  NUM_KOLMAS ;
piljardis+Num+Ord:piljardi  NUM_KOLMAS   ;

LEXICON NUM_ORD_SUFFIX
!! The suffixes only appear after cardinal multipliers
!!€gt-norm: Ordinal multiplicants
!!€ viideskymmenes: viides+Num+Ord+Sg+Nom#kymmenes
!!€ neljässadastuhannes: neljäs+Num+Ord+Sg+Nom#sadas+Num+Ord+Sg+Nom#tuhannes
kymmenes # ;
sadas # ;
tuhannes # ;
miljoonas # ;
miljardis # ;
biljoonas # ;
biljardis # ;
triljoonas # ;
triljardis # ;
kvadriljoonas # ;
kvadriljardis # ;
kvintiljoonas # ;
kvintiljardis # ;
sekstiljoonas # ;
sekstiljardis # ;
oktiljoonas # ;
oktiljardis # ;
noviljoonas # ;
noviljardis # ;
undekiljoonas # ;
undekiljardis # ;
duodekiljoonas # ;
duodekiljardis # ;
tredekiljoonas # ;
tredekiljardis # ;
kvattuordekiljoonas # ;
kvattuordekiljardis # ;
kvindekiljoonas # ;
kvindekiljardis # ;
sedekiljoonas # ;
sedekiljardis # ;
septendekiljoonas # ;
septendekiljardis # ;
oktodekiljoonas # ;
oktodekiljardis # ;
novemdekiljoonas # ;
novemdekiljardis # ;
vidingtiljoonas # ;
vidingtiljardis # ;
tridingtiljoonas # ;
tridingtiljardis # ;
sentiljoonas # ;
sentiljardis # ;
kiljoonas # ;
kiljardis # ;
piljoonas # ;
piljardis # ;
toista # ;
kolmatta # ;
neljättä # ;
kuudetta # ;
viidettä # ;
seitsemättä # ;
kahdeksatta # ;
yhdeksättä # ;
kymmenettä # ;
toista # ;
kolmatta # ;
neljättä # ;
kuudetta # ;
viidettä # ;
seitsemättä # ;
kahdeksatta # ;
yhdeksättä # ;
kymmenettä # ;

LEXICON NUM_OTHERS
!! There is a set of numbers or corresponding expressions that work like them,
!! but are not basic cardinals or ordinals:
!!€gt-norm: Numeral others
!!€ viitisenkymmentä: viitisen+Num#kymmentä
!!€ puolikymmentä: puolikymmentä+Num+Sg+Nom
yksisen+Num#:yksisen#  NUM_CARD_SUFFIX   ;
kaksisen+Num#:kaksisen#    NUM_CARD_SUFFIX   ;
yhtisen+Num#:yhtisen#  NUM_CARD_SUFFIX   ;
kahtisen+Num#:kahtisen#    NUM_CARD_SUFFIX   ;
kolmisen+Num#:kolmisen#    NUM_CARD_SUFFIX   ;
nelisen+Num#:nelisen#  NUM_CARD_SUFFIX   ;
viitisen+Num#:viitisen#    NUM_CARD_SUFFIX   ;
kuutisen+Num#:kuutisen#    NUM_CARD_SUFFIX   ;
seitsemisen+Num#:seitsemisen#  NUM_CARD_SUFFIX   ;
kahdeksisen+Num#:kahdeksisen#  NUM_CARD_SUFFIX   ;
yhdeksisen+Num#:yhdeksisen#    NUM_CARD_SUFFIX   ;
pari+Num+Card:par           NUM_PARI ;

!! !!Numeral stem variation
!! Numerals follow the same stem variation patterns as nouns, some of these
!! being very rare to extinct for nouns. 
LEXICON NUM_YKSI
!!€gt-norm: Numerals 31
!!€ yksi: yksi+Num+Card+Sg+Nom
!!€ yhteen: yksi+Num+Card+Sg+Ill
!!€ yhtenä: yksi+Num+Card+Sg+Ess
!!€ yhdessä: yksi+Num+Card+Sg+Ine
!!€ yhtä: yksi+Num+Card+Sg+Par
!!€ yksiä: yksi+Num+Card+Pl+Par
!!€ yksien: yksi+Num+Card+Pl+Gen
!!€ yksiin: yksi+Num+Card+Pl+Ill
!!€ yksinä: yksi+Num+Card+Pl+Ess
!!€ yksissä: yksi+Num+Card+Pl+Ine
0:ksi   NUM_FRONT_NOMINATIVE ;
0:hte   NUM_FRONT_ILLATIVE_EN    ;
0:hte   NUM_FRONT_STRONG_SINGULARS   ;
0:hde   NUM_FRONT_WEAK_SINGULARS   ;
0:h     NUM_FRONT_PARTITIVE_TÄ   ;
0:ks    NUM_FRONT_PARTITIVE_IÄ   ;
0:ks    NUM_FRONT_GENITIVE_IEN   ;
0:ks    NUM_FRONT_ILLATIVE_IIN   ;
0:ks    NUM_FRONT_PLURALS ;

LEXICON NUM_KAKSI
!!€gt-norm: Numerals 31 back§
!!€ kaksi: kaksi+Num+Card+Sg+Nom
!!€ kahteen: kaksi+Num+Card+Sg+Ill
!!€ kahtena: kaksi+Num+Card+Sg+Ess
!!€ kahdessa: kaksi+Num+Card+Sg+Ine
!!€ kahta: kaksi+Num+Card+Sg+Par
!!€ kaksia: kaksi+Num+Card+Pl+Par
!!€ kaksien: kaksi+Num+Card+Pl+Gen
!!€ kaksiin: kaksi+Num+Card+Pl+Ill
!!€ kaksina: kaksi+Num+Card+Pl+Ess
!!€ kaksissa: kaksi+Num+Card+Pl+Ine
0:ksi   NUM_BACK_NOMINATIVE ;
+Use/NG:ks   NUM_BACK_NOMINATIVE ;
0:hte   NUM_BACK_ILLATIVE_EN    ;
0:hte   NUM_BACK_STRONG_SINGULARS   ;
0:hde   NUM_BACK_WEAK_SINGULARS   ;
0:h     NUM_BACK_PARTITIVE_TA   ;
0:ks    NUM_BACK_PARTITIVE_IA   ;
0:ks    NUM_BACK_GENITIVE_IEN   ;
0:ks    NUM_BACK_ILLATIVE_IIN   ;
0:ks    NUM_BACK_PLURALS ;

LEXICON NUM_KOLME
!!€gt-norm: Numerals 8~5
!!€ kolme: kolme+Num+Card+Sg+Nom
!!€ kolmeen: kolme+Num+Card+Sg+Ill
!!€ kolmena: kolme+Num+Card+Sg+Ess
!!€ kolmessa: kolme+Num+Card+Sg+Ine
!!€ kolmea: kolme+Num+Card+Sg+Par
!!€ kolmia: kolme+Num+Card+Pl+Par
!!€ kolmien: kolme+Num+Card+Pl+Gen
!!€ kolmiin: kolme+Num+Card+Pl+Ill
!!€ kolmina: kolme+Num+Card+Pl+Ess
!!€ kolmissa: kolme+Num+Card+Pl+Ine
0:e     NUM_BACK_NOMINATIVE  ;
0:e     NUM_BACK_ILLATIVE_EN    ;
0:e     NUM_BACK_SINGULARS  ;
0:e     NUM_BACK_PARTITIVE_A_AN ;
0       NUM_BACK_PARTITIVE_IA ;
0       NUM_BACK_GENITIVE_IEN ;
0       NUM_BACK_ILLATIVE_IIN ;
0       NUM_BACK_PLURALS ; 

LEXICON NUM_NELJÄ
!!€gt-norm: Numerals 10
!!€ neljä: neljä+Num+Card+Sg+Nom
!!€ neljää: neljä+Num+Card+Sg+Par
!!€ neljään: neljä+Num+Card+Sg+Ill
!!€ neljänä: neljä+Num+Card+Sg+Ess
!!€ neljässä: neljä+Num+Card+Sg+Ine
!!€ neljiä: neljä+Num+Card+Pl+Par
!!€ neljien: neljä+Num+Card+Pl+Gen
!!€ neljiin: neljä+Num+Card+Pl+Ill
!!€ neljinä: neljä+Num+Card+Pl+Ess
!!€ neljissä: neljä+Num+Card+Pl+Ine
!!€ neljäin: neljä+Num+Card+Pl+Gen+Use/Rare
0:ä  NUM_FRONT_NOMINATIVE ;
0:ä  NUM_FRONT_PARTITIVE_Ä    ;
0:ä  NUM_FRONT_ILLATIVE_ÄN    ;
0:ä  NUM_FRONT_GENITIVE_IN    ;
0:ä  NUM_FRONT_SINGULARS   ;
0    NUM_FRONT_PARTITIVE_IÄ   ;
0    NUM_FRONT_GENITIVE_IEN   ;
0    NUM_FRONT_ILLATIVE_IIN   ;
0    NUM_FRONT_PLURALS ;

LEXICON NUM_VIISI
!!€gt-norm: Numerals 27 front
!!€ viisi: viisi+Num+Card+Sg+Nom
!!€ viiteen: viisi+Num+Card+Sg+Ill
!!€ viitenä: viisi+Num+Card+Sg+Ess
!!€ viidessä: viisi+Num+Card+Sg+Ine
!!€ viisinä: viisi+Num+Card+Pl+Ess
!!€ viisissä: viisi+Num+Card+Pl+Ine
!!€ viittä: viisi+Num+Card+Sg+Par
!!€ viitten: viisi+Num+Card+Pl+Gen
!!€ viisiä: viisi+Num+Card+Pl+Par
!!€ viisien: viisi+Num+Card+Pl+Gen
!!€ viisiin: viisi+Num+Card+Pl+Ill
0:si    NUM_FRONT_NOMINATIVE ;
0:te    NUM_FRONT_ILLATIVE_EN    ;
0:te    NUM_FRONT_STRONG_SINGULARS   ;
0:de    NUM_FRONT_WEAK_SINGULARS   ;
0:s     NUM_FRONT_PLURALS ;
0:t     NUM_FRONT_PARTITIVE_TÄ   ;
0:t     NUM_FRONT_GENITIVE_TEN   ;
0:s     NUM_FRONT_PARTITIVE_IÄ   ;
0:s     NUM_FRONT_GENITIVE_IEN   ;
0:s     NUM_FRONT_ILLATIVE_IIN   ;

LEXICON NUM_KUUSI
!!€gt-norm: Numerals 27 back
!!€ kuusi: kuusi+Num+Card+Sg+Nom
!!€ kuutena: kuusi+Num+Card+Sg+Ess
!!€ kuudessa: kuusi+Num+Card+Sg+Ine
!!€ kuusina: kuusi+Num+Card+Pl+Ess
!!€ kuusissa: kuusi+Num+Card+Pl+Ine
!!€ kuutta: kuusi+Num+Card+Sg+Par
!!€ kuutten: kuusi+Num+Card+Pl+Gen
!!€ kuusia: kuusi+Num+Card+Pl+Par
!!€ kuusien: kuusi+Num+Card+Pl+Gen
!!€ kuusiin: kuusi+Num+Card+Pl+Ill
0:si    NUM_BACK_NOMINATIVE ;
0:te    NUM_BACK_ILLATIVE_EN    ;
0:te    NUM_BACK_STRONG_SINGULARS   ;
0:de    NUM_BACK_WEAK_SINGULARS   ;
0:s     NUM_BACK_PLURALS ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:t     NUM_BACK_GENITIVE_TEN   ;
0:s     NUM_BACK_PARTITIVE_IA   ;
0:s     NUM_BACK_GENITIVE_IEN   ;
0:s     NUM_BACK_ILLATIVE_IIN   ;


LEXICON NUM_KAHDEKSAN
!!€gt-norm: Numerals 10n
!!€ kahdeksan: kahdeksan+Num+Card+Sg+Nom
!!€ kahdeksaa: kahdeksan+Num+Card+Sg+Par
!!€ kahdeksaan: kahdeksan+Num+Card+Sg+Ill
!!€ kahdeksassa: kahdeksan+Num+Card+Sg+Ine
!!€ kahdeksana: kahdeksan+Num+Card+Sg+Ess
!!€ kahdeksia: kahdeksan+Num+Card+Pl+Par
!!€ kahdeksien: kahdeksan+Num+Card+Pl+Gen
!!€ kahdeksiin: kahdeksan+Num+Card+Pl+Ill
!!€ kahdeksissa: kahdeksan+Num+Card+Pl+Ine
!!€ kahdeksina: kahdeksan+Num+Card+Pl+Ess
!!€ kahdeksain: kahdeksan+Num+Card+Pl+Gen+Use/Rare
0:an    NUM_BACK_NOMINATIVE  ;
0:a     NUM_BACK_PARTITIVE_A    ;
0:a     NUM_BACK_ILLATIVE_AN    ;
0:a     NUM_BACK_GENITIVE_IN    ;
0:a     NUM_BACK_SINGULARS   ;
0       NUM_BACK_PARTITIVE_IA   ;
0       NUM_BACK_GENITIVE_IEN   ;
0       NUM_BACK_ILLATIVE_IIN   ;
0       NUM_BACK_PLURALS ;

LEXICON NUM_YHDEKSÄN
!!€gt-norm: Numerals 10n front
!!€ yhdeksän: yhdeksän+Num+Card+Sg+Nom
!!€ yhdeksää: yhdeksän+Num+Card+Sg+Par
!!€ yhdeksään: yhdeksän+Num+Card+Sg+Ill
!!€ yhdeksässä: yhdeksän+Num+Card+Sg+Ine
!!€ yhdeksänä: yhdeksän+Num+Card+Sg+Ess
!!€ yhdeksiä: yhdeksän+Num+Card+Pl+Par
!!€ yhdeksien: yhdeksän+Num+Card+Pl+Gen
!!€ yhdeksissä: yhdeksän+Num+Card+Pl+Ine
!!€ yhdeksinä: yhdeksän+Num+Card+Pl+Ess
!!€ yhdeksäin: yhdeksän+Num+Card+Pl+Gen+Use/Rare
0:än    NUM_FRONT_NOMINATIVE  ;
0:ä     NUM_FRONT_PARTITIVE_Ä    ;
0:ä     NUM_FRONT_ILLATIVE_ÄN    ;
0:ä     NUM_FRONT_GENITIVE_IN    ;
0:ä     NUM_FRONT_SINGULARS   ;
0       NUM_FRONT_PARTITIVE_IÄ   ;
0       NUM_FRONT_GENITIVE_IEN   ;
0       NUM_FRONT_ILLATIVE_IIN   ;
0       NUM_FRONT_PLURALS ;

LEXICON NUM_KYMMENEN
!!€gt-norm: Numerals 32
!!€ kymmenen: kymmenen+Num+Card+Sg+Nom
!!€ kymmeneen: kymmenen+Num+Card+Sg+Ill
!!€ kymmenenä: kymmenen+Num+Card+Sg+Ess
!!€ kymmenessä: kymmenen+Num+Card+Sg+Ine
!!€ kymmentä: kymmenen+Num+Card+Sg+Par
!!€ kymmenten: kymmenen+Num+Card+Pl+Gen
!!€ kymmenien: kymmenen+Num+Card+Pl+Gen
!!€ kymmeniin: kymmenen+Num+Card+Pl+Ill
!!€ kymmenissä: kymmenen+Num+Card+Pl+Ine
!!€ kymmeninä: kymmenen+Num+Card+Pl+Ess
0:en    NUM_FRONT_NOMINATIVE ;
0:e     NUM_FRONT_ILLATIVE_EN    ;
0:e     NUM_FRONT_SINGULARS   ;
0       NUM_FRONT_PARTITIVE_TÄ   ;
0       NUM_FRONT_GENITIVE_TEN   ;
0       NUM_FRONT_PARTITIVE_IÄ   ;
0       NUM_FRONT_GENITIVE_IEN   ;
0       NUM_FRONT_ILLATIVE_IIN   ;
0       NUM_FRONT_PLURALS ;

LEXICON NUM_SATA
!!€gt-norm: Numerals 9
!!€ sata: sata+Num+Card+Sg+Nom
!!€ satana: sata+Num+Card+Sg+Ess
!!€ sadassa: sata+Num+Card+Sg+Ine
!!€ sataan: sata+Num+Card+Sg+Ill
!!€ sataa: sata+Num+Card+Sg+Par
!!€ satojen: sata+Num+Card+Pl+Gen
!!€ satoihin: sata+Num+Card+Pl+Ill
!!€ sadoissa: sata+Num+Card+Pl+Ine
!!€ satoina: sata+Num+Card+Pl+Ess
0:ta    NUM_BACK_NOMINATIVE ;
0:da    NUM_BACK_WEAK_SINGULARS ;
0:ta    NUM_BACK_STRONG_SINGULARS   ;
0:do    NUM_BACK_WEAK_PLURALS   ;
0:to    NUM_BACK_STRONG_PLURALS ;
0:ta    NUM_BACK_ILLATIVE_AN    ;
0:ta    NUM_BACK_PARTITIVE_A    ;
0:to    NUM_BACK_PARTITIVE_JA   ;
0:to    NUM_BACK_GENITIVE_JEN   ;
0:ta    NUM_BACK_GENITIVE_IN    ;
0:to    NUM_BACK_ILLATIVE_IHIN  ;

LEXICON NUM_TUHAT
!!€gt-norm: Numerals 46
!!€ tuhat: tuhat+Num+Card+Sg+Nom
!!€ tuhanteen: tuhat+Num+Card+Sg+Ill
!!€ tuhantena: tuhat+Num+Card+Sg+Ess
!!€ tuhannessa: tuhat+Num+Card+Sg+Ine
!!€ tuhansien: tuhat+Num+Card+Pl+Gen
!!€ tuhatta: tuhat+Num+Card+Sg+Par
!!€ tuhansia: tuhat+Num+Card+Pl+Par
!!€ tuhansien: tuhat+Num+Card+Pl+Gen
!!€ tuhansiin: tuhat+Num+Card+Pl+Ill
!!€ tuhansina: tuhat+Num+Card+Pl+Ess
!!€ tuhansissa: tuhat+Num+Card+Pl+Ine
0:t     NUM_BACK_NOMINATIVE ;
0:nte   NUM_BACK_ILLATIVE_EN    ;
0:nte   NUM_BACK_STRONG_SINGULARS   ;
0:nne   NUM_BACK_WEAK_SINGULARS   ;
0:n     NUM_BACK_GENITIVE_TEN   ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:ns    NUM_BACK_PARTITIVE_IA   ;
0:ns    NUM_BACK_GENITIVE_IEN   ;
0:ns    NUM_BACK_ILLATIVE_IIN   ;
0:ns    NUM_BACK_PLURALS ;

LEXICON NUM_MILJOONA
!!€gt-norm: Numerals 10
!!€ miljoona: miljoona+Num+Card+Sg+Nom
!!€ miljoonana: miljoona+Num+Card+Sg+Ess
!!€ miljoonassa: miljoona+Num+Card+Sg+Ine
!!€ miljoonaa: miljoona+Num+Card+Sg+Par
!!€ miljoonaan: miljoona+Num+Card+Sg+Ill
!!€ miljoonia: miljoona+Num+Card+Pl+Par
!!€ miljoonien: miljoona+Num+Card+Pl+Gen
!!€ miljooniin: miljoona+Num+Card+Pl+Ill
!!€ miljoonissa: miljoona+Num+Card+Pl+Ine
!!€ miljoonina: miljoona+Num+Card+Pl+Ess
0:a     NUM_BACK_NOMINATIVE    ;
0:a     NUM_BACK_SINGULARS    ;
0       NUM_BACK_PLURALS  ;
0:a     NUM_BACK_PARTITIVE_A      ;
0:a     NUM_BACK_ILLATIVE_AN     ;
0       NUM_BACK_PARTITIVE_IA    ;
0       NUM_BACK_GENITIVE_IEN    ;
0:a     NUM_BACK_GENITIVE_IN    ;
0       NUM_BACK_ILLATIVE_IIN   ;


LEXICON NUM_MILJARDI
!!€gt-norm: Numerals 5
!!€ miljardi: miljardi+Num+Card+Sg+Nom
!!€ miljardina: miljardi+Num+Card+Sg+Ess
!!€ miljardissa: miljardi+Num+Card+Sg+Ine
!!€ miljardeina: miljardi+Num+Card+Pl+Ess
!!€ miljardeissa: miljardi+Num+Card+Pl+Ine
!!€ miljardia: miljardi+Num+Card+Sg+Par
!!€ miljardiin: miljardi+Num+Card+Sg+Ill
!!€ miljardeja: miljardi+Num+Card+Pl+Par
!!€ miljardien: miljardi+Num+Card+Pl+Gen
!!€ miljardeihin: miljardi+Num+Card+Pl+Ill
0:i NUM_BACK_NOMINATIVE ;
0:i NUM_BACK_SINGULARS   ;
0:e NUM_BACK_PLURALS ;
0:i NUM_BACK_PARTITIVE_A    ;
0:i NUM_BACK_ILLATIVE_IN    ;
0:e NUM_BACK_PARTITIVE_JA   ;
0:e NUM_BACK_ILLATIVE_IHIN  ;
0   NUM_BACK_GENITIVE_IEN   ;


LEXICON NUM_GOOGOL
!!€gt-norm: Numerals 5 more
!!€ Googol: Googol+Num+Card+Sg+Nom
0   NUM_BACK_NOMINATIVE ;
0:i NUM_BACK_SINGULARS   ;
0:e NUM_BACK_PLURALS ;
0:i NUM_BACK_PARTITIVE_A    ;
0:i NUM_BACK_ILLATIVE_IN    ;
0:e NUM_BACK_PARTITIVE_JA   ;
0:e NUM_BACK_ILLATIVE_IHIN  ;
0   NUM_BACK_GENITIVE_IEN   ;

LEXICON NUM_PARI
!!€gt-norm: Numerals 5 moremore
!!€ pari: pari+Num+Card+Sg+Nom
!!€ parina: pari+Num+Card+Sg+Ess
!!€ parissa: pari+Num+Card+Sg+Ine
!!€ pareissa: pari+Num+Card+Pl+Ine
!!€ pareina: pari+Num+Card+Pl+Ess
!!€ paria: pari+Num+Card+Sg+Par
!!€ pareja: pari+Num+Card+Pl+Par
!!€ parien: pari+Num+Card+Pl+Gen
!!€ pareihin: pari+Num+Card+Pl+Ill
!!€ pariin: pari+Num+Card+Sg+Ill
0:i     NUM_BACK_NOMINATIVE    ;
0:i     NUM_BACK_SINGULARS    ;
0:e     NUM_BACK_PLURALS   ;
0:i     NUM_BACK_PARTITIVE_A_AN  ;
0:i     NUM_BACK_ILLATIVE_IN ;
0:e     NUM_BACK_PARTITIVE_JA    ;
0       NUM_BACK_GENITIVE_IEN    ;
0:e     NUM_BACK_ILLATIVE_IHIN   ;


LEXICON NUM_ENSIMMÄINEN
!!€gt-norm: Numerals 38
!!€ ensimmäinen: ensimmäinen+Num+Ord+Sg+Nom
!!€ ensimmäisenä: ensimmäinen+Num+Ord+Sg+Ess
!!€ ensimmäisessä: ensimmäinen+Num+Ord+Sg+Ine
!!€ ensimmäistä: ensimmäinen+Num+Ord+Sg+Par
!!€ ensimmäisten: ensimmäinen+Num+Ord+Pl+Gen
!!€ ensimmäisiä: ensimmäinen+Num+Ord+Pl+Par
!!€ ensimmäisiin: ensimmäinen+Num+Ord+Pl+Ill
!!€ ensimmäisinä: ensimmäinen+Num+Ord+Pl+Ess
!!€ ensimmäisissä: ensimmäinen+Num+Ord+Pl+Ine
0:nen   NUM_FRONT_NOMINATIVE ;
0:se    NUM_FRONT_ILLATIVE_EN    ;
0:se    NUM_FRONT_SINGULARS   ;
0:s     NUM_FRONT_PARTITIVE_TÄ   ;
0:s     NUM_FRONT_GENITIVE_TEN   ;
0:s     NUM_FRONT_PARTITIVE_IÄ   ;
0:s     NUM_FRONT_GENITIVE_IEN   ;
0:s     NUM_FRONT_ILLATIVE_IIN   ;
0:s     NUM_FRONT_PLURALS ;
+Der/s:s    R ;

LEXICON NUM_TOINEN
!!€gt-norm: Numerals 38 back
!!€ toinen: toinen+Num+Ord+Sg+Nom
!!€ toiseen: toinen+Num+Ord+Sg+Ill
!!€ toista: toinen+Num+Ord+Sg+Par
!!€ toisten: toinen+Num+Ord+Pl+Gen
!!€ toisien: toinen+Num+Ord+Pl+Gen
!!€ toisia: toinen+Num+Ord+Pl+Par
!!€ toisiin: toinen+Num+Ord+Pl+Ill
!!€ toisena: toinen+Num+Ord+Sg+Ess
!!€ toisessa: toinen+Num+Ord+Sg+Ine
!!€ toisissa: toinen+Num+Ord+Pl+Ine
!!€ toisina: toinen+Num+Ord+Pl+Ess
0:nen   NUM_BACK_NOMINATIVE ;
0:se    NUM_BACK_ILLATIVE_EN    ;
0:se    NUM_BACK_SINGULARS   ;
0:s     NUM_BACK_PARTITIVE_TA   ;
0:s     NUM_BACK_GENITIVE_TEN   ;
0:s     NUM_BACK_PARTITIVE_IA   ;
0:s     NUM_BACK_GENITIVE_IEN   ;
0:s     NUM_BACK_ILLATIVE_IIN   ;
0:s     NUM_BACK_PLURALS ;
+Der/s:s    R ;

LEXICON NUM_KOLMAS
!!€gt-norm: Numerals 45
!!€ kolmas: kolmas+Num+Ord+Sg+Nom
!!€ kolmantena: kolmas+Num+Ord+Sg+Ess
!!€ kolmannessa: kolmas+Num+Ord+Sg+Ine
!!€ kolmanteen: kolmas+Num+Ord+Sg+Ill
!!€ kolmatta: kolmas+Num+Ord+Sg+Par
!!€ kolmansia: kolmas+Num+Ord+Pl+Par
!!€ kolmansien: kolmas+Num+Ord+Pl+Gen
!!€ kolmansissa: kolmas+Num+Ord+Pl+Ine
!!€ kolmansina: kolmas+Num+Ord+Pl+Ess
0:s     NUM_BACK_NOMINATIVE ;
0:nte   NUM_BACK_ILLATIVE_EN    ;
0:nte   NUM_BACK_STRONG_SINGULARS   ;
0:nne   NUM_BACK_WEAK_SINGULARS   ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:ns    NUM_BACK_PARTITIVE_IA   ;
0:ns    NUM_BACK_GENITIVE_IEN   ;
0:ns    NUM_BACK_ILLATIVE_IIN   ;
0:ns    NUM_BACK_PLURALS ;

LEXICON NUM_NELJÄS
!!€gt-norm: Numerals 45 fron
!!€ neljäs: neljäs+Num+Ord+Sg+Nom
!!€ neljänteen: neljäs+Num+Ord+Sg+Ill
!!€ neljäntenä: neljäs+Num+Ord+Sg+Ess
!!€ neljännessä: neljäs+Num+Ord+Sg+Ine
!!€ neljättä: neljäs+Num+Ord+Sg+Par
!!€ neljänsiä: neljäs+Num+Ord+Pl+Par
!!€ neljänsien: neljäs+Num+Ord+Pl+Gen
!!€ neljänsiin: neljäs+Num+Ord+Pl+Ill
!!€ neljänsissä: neljäs+Num+Ord+Pl+Ine
!!€ neljänsinä: neljäs+Num+Ord+Pl+Ess
0:s     NUM_FRONT_NOMINATIVE ;
0:nte   NUM_FRONT_ILLATIVE_EN    ;
0:nte   NUM_FRONT_STRONG_SINGULARS   ;
0:nne   NUM_FRONT_WEAK_SINGULARS   ;
0:t     NUM_FRONT_PARTITIVE_TÄ   ;
0:ns    NUM_FRONT_PARTITIVE_IÄ   ;
0:ns    NUM_FRONT_GENITIVE_IEN   ;
0:ns    NUM_FRONT_ILLATIVE_IIN   ;
0:ns    NUM_FRONT_PLURALS ;











! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES   !!= * __@CODE@__
 §+Use/Circ:§       ARABIC ; ! §24
 §§+Use/Circ:§§     ARABIC ; ! §§24
 §% +Use/Circ:§%    ARABIC ; ! § 24
 §§% +Use/Circ:§§%  ARABIC ; ! §§ 24
 %-+Use/Circ:%- ARABIC ; ! -24
 %‒+Use/Circ:%‒ ARABIC ; ! -24 U+2012
 %–+Use/Circ:%– ARABIC ; ! -24 U+2013
 %—+Use/Circ:%— ARABIC ; ! -24 U+2014
 %―+Use/Circ:%― ARABIC ; ! -24 U+2015
 %++Use/Circ:%+ ARABIC ; ! <plus>24
 %-% +Use/Circ:%-%  ARABIC ; ! - 24
 %‒% +Use/Circ:%‒%  ARABIC ; ! - 24 U+2012
 %–% +Use/Circ:%–%  ARABIC ; ! - 24 U+2013
 %—% +Use/Circ:%—%  ARABIC ; ! - 24 U+2014
 %―% +Use/Circ:%―%  ARABIC ; ! - 24 U+2015
 %+% +Use/Circ:%+%  ARABIC ; ! <plus> 24
 %*+Use/Circ:%* ARABIC ; ! *24
 %$+Use/Circ:%$ ARABIC ; ! $24
 %€+Use/Circ:%€ ARABIC ; ! €24
 %<+Use/Circ:%[%<%] ARABIC ; ! <24
 %>+Use/Circ:%[%>%] ARABIC ; ! >24
 s%.+Use/Circ:s%. ARABIC ; ! s.24




LEXICON ARABIC   !!= * __@CODE@__
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOP ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPORD ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;

LEXICON ARABICLOOPphone   !!= * __@CODE@__ +358(0)16671254
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;
 +Use/Circ: ARABICLOOPphone ;
 (+Use/Circ:( ARABICLOOPphone ;
 )+Use/Circ:) ARABICLOOPphone ;
 %—+Use/Circ:%— ARABICLOOPphone ; 
 ARABICCASEphone ;

LEXICON ARABICCASEphone   !!= * __@CODE@__  
 +Num+Sg+Nom: # ;


LEXICON ARABICLOOP   !!= * __@CODE@__
 +Use/Circ: ARABIC ;
 %-+Use/Circ:%- ARABIC ; ! 1-2 multipart numbers
 %–+Use/Circ:%– ARABIC ; ! 1–2 multipart numbers
 %—+Use/Circ:%— ARABIC ; ! 1—2 multipart numbers
 %.%-+Use/Circ:%.%- ARABIC ; ! 1.-2 multipart numbers ! !
 %.%–+Use/Circ:%.%– ARABIC ; ! 1.–2 multipart numbers ! !
 %.%—+Use/Circ:%.%— ARABIC ; ! 1.—2 multipart numbers ! !
 %-% +Use/Circ:%-%  ARABIC ; ! 1- 2 multipart numbers
 %–% +Use/Circ:%–%  ARABIC ; ! 1– 2 multipart numbers
 %—% +Use/Circ:%—%  ARABIC ; ! 1— 2 multipart numbers
 % %-+Use/Circ:% %- ARABIC ; ! 1 -2 multipart numbers
 % %–+Use/Circ:% %– ARABIC ; ! 1 –2 multipart numbers
 % %—+Use/Circ:% %— ARABIC ; ! 1 —2 multipart numbers
 % %-% +Use/Circ:% %-%  ARABIC ; ! 1 - 2 multipart numbers
 % %–% +Use/Circ:% %–%  ARABIC ; ! 1 – 2 multipart numbers
 % %—% +Use/Circ:% %—%  ARABIC ; ! 1 — 2 multipart numbers
 % %-%-% +Use/Circ:% %-%-%  ARABIC ; ! 1 -- 2 multipart numbers
 % %–%–% +Use/Circ:% %–%–%  ARABIC ; ! 1 –– 2 multipart numbers
 % %—%—% +Use/Circ:% %—%—%  ARABIC ; ! 1 —— 2 multipart numbers
 %.+Use/Circ:%. ARABIC ;
 %:+Use/Circ:%: ARABIC ;
 %,+Use/Circ:%, ARABIC ;
 % +Use/Circ:%  ARABIC ; ! Allowing for "23 500".
   +Use/Circ:  ARABIC ; ! Allowing for "23 500" w/nbsp (init char = alt-space)
 %/+Use/Circ:%/ ARABIC ; ! 24/12
 %/% +Use/Circ:%/%  ARABIC ; ! 24/ 12
 % %/+Use/Circ:% %/ ARABIC ; ! 24 /12
 '+Use/Circ:'7 ARABIC ; ! Since ' denotes cons.grad. This is not optimal either.

 +Num+Use/Circ: RNum ; ! Num Cmp Noun, with hyphen

 ,%-+Num+Use/Circ:,%- ARABICCASES ; ! 10,-
 ,%-+Num+Err/Orth+Use/Circ:.%- ARABICCASES ; ! 10.- It is wrong, but written.

  % %-+Err/Orth+Use/Circ:% %- ARABICDELIMITER; ! 1 - multipart numbers - "1 -:s"?
  % %-%-+Err/Orth+Use/Circ:% %-%- ARABICDELIMITER; ! 1 -- multipart numbers
  %%+Use/Circ:%%  ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%".
  % %%+Use/Circ:% %%  ARABICDELIMITER ; ! the correct ones as well... 50 % etc.
  ARABICDELIMITER ; ! list of number-case delimiters

LEXICON ARABICLOOPORD   !!= * __@CODE@__
 +A+Ord+Attr:%. # ;
 +A+Ord+Use/Circ: ARABICDELIMITERORD ;
 < [a|b|c|d|e|f|f %  f] > ACASETAG ;

LEXICON ACASETAG   !!= * __@CODE@__  is for 1e+Num+Sg+Num:1e
+Num: ARABICCASES ;



LEXICON ARABICDELIMITER   !!= * __@CODE@__  blank + 3 delim, what does the lg counc prefer?
 +Num:           ARABICCASE0 ; ! This is the 1984 case, ie NGA
 +Num:%:         ARABICCASE  ; ! This is the 1984:s case.
 +Num+Err/Orth:   ARABICCASE  ; ! This is the 1984s case.
 +Num+Err/Orth:' ARABICCASE  ; ! This is the 1984's case.
 +Num+Err/Orth:’  ARABICCASE  ; ! optio-B07 = ’
 +Num+Err/Orth:%- ARABICCASE  ; ! This is the 1984-s case.
 +Num:%- NUMERALCOMPOUNDS ; ! 6-jáhkasaš
 +Num+Err/Orth:´  ARABICCASE  ; ! This is the 1984´s case.


LEXICON ARABICDELIMITERORD   !!= * __@CODE@__
!            ARABICCASE0   ; ! This is the 1984 case, ie NGA
:%:         ARABICCASEORD ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASEORD ; ! This is the 1984s case.
 +Err/Orth:' ARABICCASEORD ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASEORD ; ! This is the 1984-s case.


LEXICON ARABICCASES   !!= * __@CODE@__  to distinguish between 0 and oblique
ARABICCASE0 ;
ARABICCASE ;

LEXICON ARABICCASE0   !!= * __@CODE@__
 +Sg+Nom: # ;
 +Sg+Gen:%:n # ;
 +Cmp/SplitR:%- ENDLEX ;
 +Cmp/SplitR:%– ENDLEX ;

LEXICON ARABICCASE   !!= * __@CODE@__
 +Sg+Loc:%>ss^A           K ;

 LEXICON ARABICCASEORD   !!= * __@CODE@__
 +Sg+Gen:%>nnen     K ;



! The Roman numerals !
! ------------------ !

LEXICON ROMAN   !!= * __@CODE@__ 
 +Use/Circ: ROM-THOUSAND ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ; !

LEXICON ROM-THOUSAND   !!= * __@CODE@__ 
 M+Use/Circ:M ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM ROM-THOUSAND-TAG; !

LEXICON ROM-THOUSAND-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ;				!
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-HUNDRED   !!= * __@CODE@__ 
 C+Use/Circ:C ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD ROM-HUNDRED-TAG; !
 D+Use/Circ:D ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM ROM-HUNDRED-TAG; !

LEXICON ROM-HUNDRED-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-TEN   !!= * __@CODE@__ 
 X+Use/Circ:X ROM-TEN-TAG; !
 XX+Use/Circ:XX ROM-TEN-TAG; !
 XXX+Use/Circ:XXX ROM-TEN-TAG; !
 XL+Use/Circ:XL ROM-TEN-TAG; !
 L+Use/Circ:L ROM-TEN-TAG; !
 LX+Use/Circ:LX ROM-TEN-TAG; !
 LXX+Use/Circ:LXX ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX ROM-TEN-TAG; !
 XC+Use/Circ:XC ROM-TEN-TAG; !

LEXICON ROM-TEN-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-ONE ; !
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-ONE   !!= * __@CODE@__ 
 I+Use/Circ:I ROM-ONE-TAG; !
 II+Use/Circ:II ROM-ONE-TAG; !
 III+Use/Circ:III ROM-ONE-TAG; !
 IV+Use/Circ:IV ROM-ONE-TAG; !
 V+Use/Circ:V ROM-ONE-TAG; !
 VI+Use/Circ:VI ROM-ONE-TAG; !
 VII+Use/Circ:VII ROM-ONE-TAG; !
 VIII+Use/Circ:VIII ROM-ONE-TAG; !
 IX+Use/Circ:IX ROM-ONE-TAG; !

LEXICON ROM-ONE-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
! +N: ROMNUMTAG ; ! !The Olav viđeš fix (Roman V now gets A)
 +Use/Circ: ROM-SPLIT ; !

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT   !!= * __@CODE@__ 
 +Use/Circ:%- 2ROMAN ; ! II-VI, etc. !


! Here goes loop 2.

LEXICON 2ROMAN   !!= * __@CODE@__ 
 +Use/Circ: 2ROM-THOUSAND ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-THOUSAND   !!= * __@CODE@__ 
 M+Use/Circ:M 2ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM 2ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM 2ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM 2ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM 2ROM-THOUSAND-TAG; !

LEXICON 2ROM-THOUSAND-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ;				 !
 +Use/Circ: 2ROM-ONE ;				!

LEXICON 2ROM-HUNDRED   !!= * __@CODE@__ 
 C+Use/Circ:C 2ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC 2ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC 2ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD 2ROM-HUNDRED-TAG; !
 D+Use/Circ:D 2ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC 2ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC 2ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC 2ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM 2ROM-HUNDRED-TAG; !

LEXICON 2ROM-HUNDRED-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ;				!
LEXICON 2ROM-TEN   !!= * __@CODE@__ 
 X+Use/Circ:X 2ROM-TEN-TAG; !
 XX+Use/Circ:XX 2ROM-TEN-TAG; !
 XXX+Use/Circ:XXX 2ROM-TEN-TAG; !
 XL+Use/Circ:XL 2ROM-TEN-TAG; !
 L+Use/Circ:L 2ROM-TEN-TAG; !
 LX+Use/Circ:LX 2ROM-TEN-TAG; !
 LXX+Use/Circ:LXX 2ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX 2ROM-TEN-TAG; !
 XC+Use/Circ:XC 2ROM-TEN-TAG; !

LEXICON 2ROM-TEN-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-ONE   !!= * __@CODE@__ 
 I+Use/Circ:I 2ROM-ONE-TAG; !
 II+Use/Circ:II 2ROM-ONE-TAG; !
 III+Use/Circ:III 2ROM-ONE-TAG; !
 IV+Use/Circ:IV 2ROM-ONE-TAG; !
 V+Use/Circ:V 2ROM-ONE-TAG; !
 VI+Use/Circ:VI 2ROM-ONE-TAG; !
 VII+Use/Circ:VII 2ROM-ONE-TAG; !
 VIII+Use/Circ:VIII 2ROM-ONE-TAG; !
 IX+Use/Circ:IX 2ROM-ONE-TAG; !

LEXICON 2ROM-ONE-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !

LEXICON ROMNUMTAG   !!= * __@CODE@__
 +Num+Nom+Use/Circ:%> # ; !
 +Num+Gen+Use/Circ:%> # ; !
 +Num+Acc+Use/Circ:%> # ; !
 +A+Ord+Use/Circ:%.%> # ; ! !III. as ordinal.
 +A+Ord+Use/Circ:%> # ; ! !Looking for trouble...
 +Num+Use/Circ:%: ROMNUMTAGOBL ; !
 +Num+Use/Circ:'  ROMNUMTAGOBL ; !
 +Num+Use/Circ:%- ROMNUMTAGOBL ; !
 +Num+Use/Circ:%´ ROMNUMTAGOBL ; !
! +Num: ROMNUMTAGOBL ; !

LEXICON ROMNUMTAGOBL   !!= * __@CODE@__ 
 +Gen+Err/Orth+Use/Circ:%>a # ; !  But here 123a 123
 +Acc+Err/Orth+Use/Circ:%>a # ; ! 
 +Ill+Use/Circ:%>i # ; !
 +Ill+Err/Orth+Use/Circ:%>ij # ; !
 +Ill+Err/Orth+Use/Circ:%>áj # ; !
 +Loc+Use/Circ:%>s # ; !
 +Com+Use/Circ:%>in # ; !
 +Ess+Use/Circ:%>n # ; !


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

LEXICON ISOLATED-NUMEXP   !!= * __@CODE@__
 ½+Num:½ # ;
 ¹+Num:¹ # ;
 ²+Num:² # ;
 ³+Num:³ # ;
 ¼+Num:¼ # ;
 ¾+Num:¾ # ;


LEXICON NUMERALCOMPOUNDS   !!= * __@CODE@__  ! numeral as first part
 +Use/NG: Rhyph ; ! golmma-
