!! !!!Numerals
!! Numerals are morphologically nominals, only exception to noun inflection
!! is that they form compounds with special agreeing patterns. Syntactically
!! and semantically numerals are clearly separate subset of nominals. Some
!! of the numerals stem variation patterns are rare to non-existent for other
!! nominal types. Numerals have two subclasses, cardinals and ordinals.

! TODO: unimpl.
LEXICON NUM 
!! Numerals have been split in three sections, the compounding parts
!! of cardinals and ordinals, and the non-compounding ones:
!!€gt-norm: Numeral
!!€ kaksikymmentäkolmetuhatta: kaksi+Num+Sg+Nom#kymmenen+Num+Sg+Par#kolme+Num+Sg+Nom#tuhat+Num+Sg+Par ! 23,000
!!€ kahdessadasneljäs: kahdes+Num+Ord+Sg+Nom#sadas+Num+Ord+Sg+Nom#neljäs+Num+Ord+Sg+Nom  ! 204rd
!!€ viitisenkymmentä: viitisen+Num#kymmentä  ! 50-ish
0    NUM_CARD_ALL    ;
0    NUM_ORD_ALL ;
0    NUM_OTHERS  ;

 +Use/Circ: ARABIC ;          ! for the arabic numerals        !
 +Use/Circ: ROMAN ;           ! for the roman numerals         !
 +Use/Circ: NUM-PREFIXES ;    ! for §34 etc.                   !
 +Use/Circ: ISOLATED-NUMEXP ; ! for ½ etc.                     !

LEXICON NUM_CARD_ALL
!! The compounding parts of cardinals are the number multiplier words.
!!€gt-norm: cardinal
!!€ yksi: yksi+Num+Sg+Nom !one
!!€ viidele: viisi+Num+Sg+All !five
!!€ tuhatta: tuhat+Num+Sg+Par !thousand
yksi+Num:y             NUM_YKSI  ;
kaksi+Num:ka           NUM_KAKSI ;
kolme+Num:kolm         NUM_KOLME ;
nelje+Num:nelj         NUM_NELJÄ ;
viisi+Num:vii          NUM_VIISI ;
kuusi+Num:kuu          NUM_KUUSI ;
seittemen+Num+Err/Orth:seitsem  NUM_YHDEKSÄN ;
seittemen+Num:seittem  NUM_YHDEKSÄN ;
kahđeksen+Num:kahđeks  NUM_KAHDEKSAN ;
yhđeksen+Num:yhđeks    NUM_YHDEKSÄN ;
kymmenen+Num:kymmen    NUM_KYMMENEN ;
sata+Num:sa            NUM_SATA ;
tuhat+Num:tuha         NUM_TUHAT ;
miljoona+Num:miljoon  NUM_MILJOONA ;
miljardi+Num:miljard  NUM_MILJARDI ;
biljoona+Num:biljoon  NUM_MILJOONA ;
biljardi+Num:biljard  NUM_MILJARDI ;
triljoona+Num:triljoon  NUM_MILJOONA ;
triljardi+Num:triljard  NUM_MILJARDI ;
kvadriljoona+Num:kvadriljoon  NUM_MILJOONA ;
kvadriljardi+Num:kvadriljard  NUM_MILJARDI ;
kvintiljoona+Num:kvintiljoon  NUM_MILJOONA ;
kvintiljardi+Num:kvintiljard  NUM_MILJARDI ;
sekstiljoona+Num:sekstiljoon  NUM_MILJOONA ;
sekstiljardi+Num:sekstiljard  NUM_MILJARDI ;
oktiljoona+Num:oktiljoon  NUM_MILJOONA ;
oktiljardi+Num:oktiljard  NUM_MILJARDI ;
noviljoona+Num:noviljoon  NUM_MILJOONA ;
noviljardi+Num:noviljard  NUM_MILJARDI ;
undekiljoona+Num:undekiljoon  NUM_MILJOONA ;
undekiljardi+Num:undekiljard  NUM_MILJARDI ;
duodekiljoona+Num:duodekiljoon  NUM_MILJOONA ;
duodekiljardi+Num:duodekiljard  NUM_MILJARDI ;
tredekiljoona+Num:tredekiljoon  NUM_MILJOONA ;
tredekiljardi+Num:tredekiljard  NUM_MILJARDI ;
kvattuordekiljoona+Num:kvattuordekiljoon  NUM_MILJOONA ;
kvattuordekiljardi+Num:kvattuordekiljard  NUM_MILJARDI ;
kvindekiljoona+Num:kvindekiljoon  NUM_MILJOONA ;
kvindekiljardi+Num:kvindekiljard  NUM_MILJARDI ;
sedekiljoona+Num:sedekiljoon  NUM_MILJOONA ;
sedekiljardi+Num:sedekiljard  NUM_MILJARDI ;
septendekiljoona+Num:septendekiljoon  NUM_MILJOONA ;
septendekiljardi+Num:septendekiljard  NUM_MILJARDI ;
oktodekiljoona+Num:oktodekiljoon  NUM_MILJOONA ;
oktodekiljardi+Num:oktodekiljard  NUM_MILJARDI ;
novemdekiljoona+Num:novemdekiljoon  NUM_MILJOONA ;
novemdekiljardi+Num:novemdekiljard  NUM_MILJARDI ;
vidingtiljoona+Num:vidingtiljoon  NUM_MILJOONA ;
vidingtiljardi+Num:vidingtiljard  NUM_MILJARDI ;
tridingtiljoona+Num:tridingtiljoon  NUM_MILJOONA ;
tridingtiljardi+Num:tridingtiljard  NUM_MILJARDI ;
sentiljoona+Num:sentiljoon  NUM_MILJOONA ;
sentiljardi+Num:sentiljard  NUM_MILJARDI ;
kiljoona+Num:kiljoon  NUM_MILJOONA ;
kiljardi+Num:kiljard  NUM_MILJARDI ;
piljoona+Num:piljoon  NUM_MILJOONA ;
piljardi+Num:piljard  NUM_MILJARDI ;
Googol+Num:Googol          NUM_GOOGOL  ;
Googolplex+Num:Googolplex  NUM_GOOGOL  ;
seitsämän+Err/Orth+Num:seitsäm  NUM_YHDEKSÄN    ;

LEXICON NUM_CARD_SUFFIX
!! The suffixes only appear after cardinal multipliers
!!€gt-norm: Cardinal multiplicants
!!€ viisikymmentä: viisi+Num+Sg+Nom#kymmentä
!!€ neljäsataatuhatta: neljä+Num+Sg+Nom#sata+Num+Sg+Par#tuhatta
kymmentä    #   ;
sataa       #   ;
tuhatta     #   ;
kymmentätuhatta #   ;
sataatuhatta    #   ;
miljoonaa       #   ;
miljardia       #   ;
biljoonaa # ;
biljardia # ;
triljoonaa # ;
triljardia # ;
kvadriljoonaa # ;
kvadriljardia # ;
kvintiljoonaa # ;
kvintiljardia # ;
sekstiljoonaa # ;
sekstiljardia # ;
oktiljoonaa # ;
oktiljardia # ;
noviljoonaa # ;
noviljardia # ;
undekiljoonaa # ;
undekiljardia # ;
duodekiljoonaa # ;
duodekiljardia # ;
tredekiljoonaa # ;
tredekiljardia # ;
kvattuordekiljoonaa # ;
kvattuordekiljardia # ;
kvindekiljoonaa # ;
kvindekiljardia # ;
sedekiljoonaa # ;
sedekiljardia # ;
septendekiljoonaa # ;
septendekiljardia # ;
oktodekiljoonaa # ;
oktodekiljardia # ;
novemdekiljoonaa # ;
novemdekiljardia # ;
vidingtiljoonaa # ;
vidingtiljardia # ;
tridingtiljoonaa # ;
tridingtiljardia # ;
sentiljoonaa # ;
sentiljardia # ;
kiljoonaa # ;
kiljardia # ;
piljoonaa # ;
piljardia # ;
toista              #    ;
kolmatta          #    ;
neljättä          #    ;
kuudetta          #    ;
viidettä          #    ;
seitsemättä    #    ;
kahdeksatta    #    ;
yhdeksättä      #    ;
kymmenettä      #    ;

LEXICON NUM_ORD_ALL
!! The compounding parts of ordinals are the number multiplier words.
!!€gt-norm: Ordinal numerals
!!€ neljäs: neljäs+Num+Ord+Sg+Nom
!!€ viidennelle: viides+Num+Ord+Sg+All
!!€ tuhannetta: tuhannes+Num+Ord+Sg+Par
ensimmäinen+Num+Ord:ensimmäi    NUM_ENSIMMÄINEN ;
yhdes+Num+Ord:yhde              NUM_NELJÄS ;
toinen+Num+Ord:toi              NUM_TOINEN ;
kahdes+Num+Ord:kahde            NUM_KOLMAS ;
kolmas+Num+Ord:kolma            NUM_KOLMAS ;
neljes+Num+Ord:nelje            NUM_NELJÄS ;
viides+Num+Ord:viide            NUM_NELJÄS ;
kuudes+Num+Ord:kuude            NUM_KOLMAS ;
seitsemäs+Num+Ord:seitsemä      NUM_NELJÄS ;
kahdeksas+Num+Ord:kahdeksa      NUM_KOLMAS ;
yhdeksäs+Num+Ord:yhdeksä        NUM_NELJÄS ;
kymmenes+Num+Ord:kymmene        NUM_NELJÄS ;
sadas+Num+Ord:sada              NUM_KOLMAS ;
tuhannes+Num+Ord:tuhanne        NUM_KOLMAS ;
miljoonas+Num+Ord:miljoona  NUM_KOLMAS ;
miljardis+Num+Ord:miljardi  NUM_KOLMAS   ;
biljoonas+Num+Ord:biljoona  NUM_KOLMAS ;
biljardis+Num+Ord:biljardi  NUM_KOLMAS   ;
triljoonas+Num+Ord:triljoona  NUM_KOLMAS ;
triljardis+Num+Ord:triljardi  NUM_KOLMAS   ;
kvadriljoonas+Num+Ord:kvadriljoona  NUM_KOLMAS ;
kvadriljardis+Num+Ord:kvadriljardi  NUM_KOLMAS   ;
kvintiljoonas+Num+Ord:kvintiljoona  NUM_KOLMAS ;
kvintiljardis+Num+Ord:kvintiljardi  NUM_KOLMAS   ;
sekstiljoonas+Num+Ord:sekstiljoona  NUM_KOLMAS ;
sekstiljardis+Num+Ord:sekstiljardi  NUM_KOLMAS   ;
oktiljoonas+Num+Ord:oktiljoona  NUM_KOLMAS ;
oktiljardis+Num+Ord:oktiljardi  NUM_KOLMAS   ;
noviljoonas+Num+Ord:noviljoona  NUM_KOLMAS ;
noviljardis+Num+Ord:noviljardi  NUM_KOLMAS   ;
undekiljoonas+Num+Ord:undekiljoona  NUM_KOLMAS ;
undekiljardis+Num+Ord:undekiljardi  NUM_KOLMAS   ;
duodekiljoonas+Num+Ord:duodekiljoona  NUM_KOLMAS ;
duodekiljardis+Num+Ord:duodekiljardi  NUM_KOLMAS   ;
tredekiljoonas+Num+Ord:tredekiljoona  NUM_KOLMAS ;
tredekiljardis+Num+Ord:tredekiljardi  NUM_KOLMAS   ;
kvattuordekiljoonas+Num+Ord:kvattuordekiljoona  NUM_KOLMAS ;
kvattuordekiljardis+Num+Ord:kvattuordekiljardi  NUM_KOLMAS   ;
kvindekiljoonas+Num+Ord:kvindekiljoona  NUM_KOLMAS ;
kvindekiljardis+Num+Ord:kvindekiljardi  NUM_KOLMAS   ;
sedekiljoonas+Num+Ord:sedekiljoona  NUM_KOLMAS ;
sedekiljardis+Num+Ord:sedekiljardi  NUM_KOLMAS   ;
septendekiljoonas+Num+Ord:septendekiljoona  NUM_KOLMAS ;
septendekiljardis+Num+Ord:septendekiljardi  NUM_KOLMAS   ;
oktodekiljoonas+Num+Ord:oktodekiljoona  NUM_KOLMAS ;
oktodekiljardis+Num+Ord:oktodekiljardi  NUM_KOLMAS   ;
novemdekiljoonas+Num+Ord:novemdekiljoona  NUM_KOLMAS ;
novemdekiljardis+Num+Ord:novemdekiljardi  NUM_KOLMAS   ;
vidingtiljoonas+Num+Ord:vidingtiljoona  NUM_KOLMAS ;
vidingtiljardis+Num+Ord:vidingtiljardi  NUM_KOLMAS   ;
tridingtiljoonas+Num+Ord:tridingtiljoona  NUM_KOLMAS ;
tridingtiljardis+Num+Ord:tridingtiljardi  NUM_KOLMAS   ;
sentiljoonas+Num+Ord:sentiljoona  NUM_KOLMAS ;
sentiljardis+Num+Ord:sentiljardi  NUM_KOLMAS   ;
kiljoonas+Num+Ord:kiljoona  NUM_KOLMAS ;
kiljardis+Num+Ord:kiljardi  NUM_KOLMAS   ;
piljoonas+Num+Ord:piljoona  NUM_KOLMAS ;
piljardis+Num+Ord:piljardi  NUM_KOLMAS   ;

LEXICON NUM_ORD_SUFFIX
!! The suffixes only appear after cardinal multipliers
!!€gt-norm: Ordinal multiplicants
!!€ viideskymmenes: viides+Num+Ord+Sg+Nom#kymmenes
!!€ neljässadastuhannes: neljäs+Num+Ord+Sg+Nom#sadas+Num+Ord+Sg+Nom#tuhannes
kymmenes # ;
sadas # ;
tuhannes # ;
miljoonas # ;
miljardis # ;
biljoonas # ;
biljardis # ;
triljoonas # ;
triljardis # ;
kvadriljoonas # ;
kvadriljardis # ;
kvintiljoonas # ;
kvintiljardis # ;
sekstiljoonas # ;
sekstiljardis # ;
oktiljoonas # ;
oktiljardis # ;
noviljoonas # ;
noviljardis # ;
undekiljoonas # ;
undekiljardis # ;
duodekiljoonas # ;
duodekiljardis # ;
tredekiljoonas # ;
tredekiljardis # ;
kvattuordekiljoonas # ;
kvattuordekiljardis # ;
kvindekiljoonas # ;
kvindekiljardis # ;
sedekiljoonas # ;
sedekiljardis # ;
septendekiljoonas # ;
septendekiljardis # ;
oktodekiljoonas # ;
oktodekiljardis # ;
novemdekiljoonas # ;
novemdekiljardis # ;
vidingtiljoonas # ;
vidingtiljardis # ;
tridingtiljoonas # ;
tridingtiljardis # ;
sentiljoonas # ;
sentiljardis # ;
kiljoonas # ;
kiljardis # ;
piljoonas # ;
piljardis # ;
toista # ;
kolmatta # ;
neljättä # ;
kuudetta # ;
viidettä # ;
seitsemättä # ;
kahdeksatta # ;
yhdeksättä # ;
kymmenettä # ;
toista # ;
kolmatta # ;
neljättä # ;
kuudetta # ;
viidettä # ;
seitsemättä # ;
kahdeksatta # ;
yhdeksättä # ;
kymmenettä # ;

LEXICON NUM_OTHERS
!! There is a set of numbers or corresponding expressions that work like them,
!! but are not basic cardinals or ordinals:
!!€gt-norm: Numeral others
!!€ viitisenkymmentä: viitisen+Num#kymmentä
!!€ puolikymmentä: puolikymmentä+Num+Sg+Nom
yksisen+Num#:yksisen#  NUM_CARD_SUFFIX   ;
kaksisen+Num#:kaksisen#    NUM_CARD_SUFFIX   ;
yhtisen+Num#:yhtisen#  NUM_CARD_SUFFIX   ;
kahtisen+Num#:kahtisen#    NUM_CARD_SUFFIX   ;
kolmisen+Num#:kolmisen#    NUM_CARD_SUFFIX   ;
nelisen+Num#:nelisen#  NUM_CARD_SUFFIX   ;
viitisen+Num#:viitisen#    NUM_CARD_SUFFIX   ;
kuutisen+Num#:kuutisen#    NUM_CARD_SUFFIX   ;
seitsemisen+Num#:seitsemisen#  NUM_CARD_SUFFIX   ;
kahdeksisen+Num#:kahdeksisen#  NUM_CARD_SUFFIX   ;
yhdeksisen+Num#:yhdeksisen#    NUM_CARD_SUFFIX   ;
pari+Num:par           NUM_PARI ;

!! !!Numeral stem variation
!! Numerals follow the same stem variation patterns as nouns, some of these
!! being very rare to extinct for nouns. 
LEXICON NUM_YKSI
!!€gt-norm: Numerals 31
!!€ yksi: yksi+Num+Sg+Nom
!!€ yhtheen: yksi+Num+Sg+Ill
!!€ yhtenä: yksi+Num+Sg+Ess
!!€ yhđessä: yksi+Num+Sg+Ine
!!€ yhđelä: yksi+Num+Sg+Ade
!!€ yhtä: yksi+Num+Sg+Par
!!€ yksii: yksi+Num+Pl+Par
!!€ yksiin: yksi+Num+Pl+Gen
!!€ ykshiin: yksi+Num+Pl+Ill
!!€ yksinä: yksi+Num+Pl+Ess
!!€ yksissä: yksi+Num+Pl+Ine
0:ksi   NUM_FRONT_NOMINATIVE ;
0:hthe  NUM_FRONT_ILLATIVE_EN    ;
0:hte   NUM_FRONT_STRONG_SINGULARS   ;
0:hte   NUM_FRONT_STRONG_SINGULARS_NA ;
0:hđe   NUM_FRONT_WEAK_SINGULARS   ;
0:hđe   NUM_FRONT_SINGULARS_L     ;
0:h     NUM_FRONT_PARTITIVE_TÄ   ;
0:ks    NUM_FRONT_PARTITIVE_II   ;
0:ks    NUM_FRONT_GENITIVE_IIN   ;
0:ks    NUM_FRONT_ILLATIVE_HIIN   ;
0:ks    NUM_FRONT_PLURALS ;

LEXICON NUM_KAKSI
!!€gt-norm: Numerals 31 back§
!!€ kaksi: kaksi+Num+Sg+Nom
!!€ kahtheen: kaksi+Num+Sg+Ill
!!€ kahtena: kaksi+Num+Sg+Ess
!!€ kahđessa: kaksi+Num+Sg+Ine
!!€ kahđela: kaksi+Num+Sg+Ade
!!€ kahta: kaksi+Num+Sg+Par
!!€ kaksii: kaksi+Num+Pl+Par
!!€ kaksiin: kaksi+Num+Pl+Gen
!!€ kakshiin: kaksi+Num+Pl+Ill
!!€ kaksina: kaksi+Num+Pl+Ess
!!€ kaksissa: kaksi+Num+Pl+Ine
0:ksi   NUM_BACK_NOMINATIVE ;
+Use/NG:ks   NUM_BACK_NOMINATIVE ;
0:hthe  NUM_BACK_ILLATIVE_EN    ;
0:hte   NUM_BACK_STRONG_SINGULARS   ;
0:hte   NUM_BACK_STRONG_SINGULARS_NA ;        
0:hđe   NUM_BACK_WEAK_SINGULARS   ;
0:hđe   NUM_BACK_SINGULARS_L     ;
0:h     NUM_BACK_PARTITIVE_TA   ;
0:ks    NUM_BACK_PARTITIVE_II   ;
0:ks    NUM_BACK_GENITIVE_IIN   ;
0:ks    NUM_BACK_ILLATIVE_HIIN  ;
0:ks    NUM_BACK_PLURALS ;

LEXICON NUM_KOLME
!!€gt-norm: Numerals 8~5
!!€ kolme: kolme+Num+Sg+Nom
!!€ kolmheen: kolme+Num+Sg+Ill
!!€ kolmena: kolme+Num+Sg+Ess
!!€ kolmessa: kolme+Num+Sg+Ine
!!€ kolmela: kolme+Num+Sg+Ade
!!€ kolmee: kolme+Num+Sg+Par
!!€ kolmii: kolme+Num+Pl+Par
!!€ kolmiin: kolme+Num+Pl+Gen
!!€ kolmhiin: kolme+Num+Pl+Ill
!!€ kolmina: kolme+Num+Pl+Ess
!!€ kolmissa: kolme+Num+Pl+Ine
0:e     NUM_BACK_NOMINATIVE  ;
0:he    NUM_BACK_ILLATIVE_EN    ;
0:e     NUM_BACK_SINGULARS  ;
0:e     NUM_BACK_STRONG_SINGULARS_NA ; 
0:e     NUM_BACK_SINGULARS_L     ;
0:e     NUM_BACK_PARTITIVE_E ;
0       NUM_BACK_PARTITIVE_II ;
0       NUM_BACK_GENITIVE_IIN ;
0       NUM_BACK_ILLATIVE_HIIN ;
0       NUM_BACK_PLURALS ; 

LEXICON NUM_NELJÄ
!!€gt-norm: Numerals 10
!!€ nelje: nelje+Num+Sg+Nom
!!€ neljee: nelje+Num+Sg+Par
!!€ neljheen: nelje+Num+Sg+Ill
!!€ neljenä: nelje+Num+Sg+Ess
!!€ neljessä: nelje+Num+Sg+Ine
!!€ neljelä: nelje+Num+Sg+Ade
!!€ neljii: nelje+Num+Pl+Par
!!€ neljiin: nelje+Num+Pl+Gen
!!€ neljhiin: nelje+Num+Pl+Ill
!!€ neljinä: nelje+Num+Pl+Ess
!!€ neljissä: nelje+Num+Pl+Ine
0:e  NUM_FRONT_NOMINATIVE ;
0:e  NUM_FRONT_PARTITIVE_E   ;
0:he NUM_FRONT_ILLATIVE_EN    ;
0:e  NUM_FRONT_GENITIVE_IN    ;
0:e  NUM_FRONT_SINGULARS   ;
0:e  NUM_FRONT_STRONG_SINGULARS_NA ;
0:e  NUM_FRONT_SINGULARS_L     ;
0    NUM_FRONT_PARTITIVE_II   ;
0    NUM_FRONT_GENITIVE_IIN   ;
0    NUM_FRONT_ILLATIVE_HIIN   ;
0    NUM_FRONT_PLURALS ;

LEXICON NUM_VIISI
!!€gt-norm: Numerals 27 front
!!€ viisi: viisi+Num+Sg+Nom
!!€ viitheen: viisi+Num+Sg+Ill
!!€ viitenä: viisi+Num+Sg+Ess
!!€ viiđessä: viisi+Num+Sg+Ine
!!€ viiđelä: viisi+Num+Sg+Ade
!!€ viisinä: viisi+Num+Pl+Ess
!!€ viisissä: viisi+Num+Pl+Ine
!!€ viittä: viisi+Num+Sg+Par
!!€ viissii: viisi+Num+Pl+Par
!!€ viissiin: viisi+Num+Pl+Gen
!!€ viishiin: viisi+Num+Pl+Ill
0:si    NUM_FRONT_NOMINATIVE ;
0:the   NUM_FRONT_ILLATIVE_EN    ;
0:te    NUM_FRONT_STRONG_SINGULARS   ;
0:te    NUM_FRONT_STRONG_SINGULARS_NA ;
0:đe    NUM_FRONT_WEAK_SINGULARS   ;
0:đe    NUM_FRONT_SINGULARS_L     ;
0:s     NUM_FRONT_PLURALS ;
0:t     NUM_FRONT_PARTITIVE_TÄ   ;
0:t     NUM_FRONT_GENITIVE_TEN   ;
0:ss    NUM_FRONT_PARTITIVE_II   ;
0:ss    NUM_FRONT_GENITIVE_IIN   ;
0:s     NUM_FRONT_ILLATIVE_HIIN  ;

LEXICON NUM_KUUSI
!!€gt-norm: Numerals 27 back
!!€ kuusi: kuusi+Num+Sg+Nom
!!€ kuutena: kuusi+Num+Sg+Ess
!!€ kuuđessa: kuusi+Num+Sg+Ine
!!€ kuuđela: kuusi+Num+Sg+Ade
!!€ kuusina: kuusi+Num+Pl+Ess
!!€ kuusissa: kuusi+Num+Pl+Ine
!!€ kuutta: kuusi+Num+Sg+Par
!!€ kuussii: kuusi+Num+Pl+Par
!!€ kuussiin: kuusi+Num+Pl+Gen
!!€ kuushiin: kuusi+Num+Pl+Ill
0:si    NUM_BACK_NOMINATIVE ;
0:the    NUM_BACK_ILLATIVE_EN    ;
0:te    NUM_BACK_STRONG_SINGULARS   ;
0:te    NUM_BACK_STRONG_SINGULARS_NA ; 
0:đe    NUM_BACK_WEAK_SINGULARS   ;
0:đe    NUM_BACK_SINGULARS_L     ;
0:s     NUM_BACK_PLURALS ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:t     NUM_BACK_GENITIVE_TEN   ;
0:ss    NUM_BACK_PARTITIVE_II   ;
0:ss    NUM_BACK_GENITIVE_IIN   ;
0:s     NUM_BACK_ILLATIVE_HIIN  ;


LEXICON NUM_KAHDEKSAN
!!€gt-norm: Numerals 10n
!!€ kahđeksen: kahđeksen+Num+Sg+Nom
!!€ kahđeksee: kahđeksen+Num+Sg+Par
!!€ kahđeksheen: kahđeksen+Num+Sg+Ill
!!€ kahđeksessa: kahđeksen+Num+Sg+Ine
!!€ kahđeksella: kahđeksen+Num+Sg+Ade
!!€ kahđeksenna: kahđeksen+Num+Sg+Ess
!!€ kahđeksii: kahđeksen+Num+Pl+Par
!!€ kahđeksiita: kahđeksen+Num+Pl+Par
!!€ kahđeksiitten: kahđeksen+Num+Pl+Gen
!!€ kahđekshiin: kahđeksen+Num+Pl+Ill
!!€ kahđeksissa: kahđeksen+Num+Pl+Ine
!!€ kahđeksinna: kahđeksen+Num+Pl+Ess
0:en    NUM_BACK_NOMINATIVE  ;
0:e     NUM_BACK_PARTITIVE_E    ;
0:he    NUM_BACK_ILLATIVE_EN    ;
!0:e     NUM_BACK_GENITIVE_IN    ;
0:e     NUM_BACK_SINGULARS   ;
0:e     NUM_BACK_STRONG_SINGULARS_NNA ;
0:e     NUM_BACK_SINGULARS_LL ;
0       NUM_BACK_PARTITIVE_II   ;
0       NUM_BACK_PARTITIVE_III  ;
0       NUM_BACK_GENITIVE_IITTEN   ;
0       NUM_BACK_ILLATIVE_HIIN  ;
0       NUM_BACK_PLURALS ;

LEXICON NUM_YHDEKSÄN
!!€gt-norm: Numerals 10n front
!!€ yhđeksen: yhđeksen+Num+Sg+Nom
!!€ yhđeksee: yhđeksen+Num+Sg+Par
!!€ yhđeksheen: yhđeksen+Num+Sg+Ill
!!€ yhđeksessä: yhđeksen+Num+Sg+Ine
!!€ yhđeksellä: yhđeksen+Num+Sg+Ade
!!€ yhđeksennä: yhđeksen+Num+Sg+Ess
!!€ yhđeksii: yhđeksen+Num+Pl+Par
!!€ yhđeksiitä: yhđeksen+Num+Pl+Par
!!€ yhđeksiitten: yhđeksen+Num+Pl+Gen
!!€ yhđekshiin: yhđeksen+Num+Pl+Ill
!!€ yhđeksissä: yhđeksen+Num+Pl+Ine
!!€ yhđeksinnä: yhđeksen+Num+Pl+Ess
0:en    NUM_FRONT_NOMINATIVE  ;
0:e     NUM_FRONT_PARTITIVE_E    ;
0:he    NUM_FRONT_ILLATIVE_EN    ;
!0:e     NUM_FRONT_GENITIVE_IN    ;
0:e     NUM_FRONT_SINGULARS   ;
0:e     NUM_FRONT_STRONG_SINGULARS_NNA ;
0:e     NUM_FRONT_SINGULARS_LL  ;
0       NUM_FRONT_PARTITIVE_II   ;
0       NUM_FRONT_PARTITIVE_III   ;
0       NUM_FRONT_GENITIVE_IITTEN   ;
0       NUM_FRONT_ILLATIVE_HIIN  ;
0       NUM_FRONT_PLURALS ;

LEXICON NUM_KYMMENEN
!!€gt-norm: Numerals 32
!!€ kymmenen: kymmenen+Num+Sg+Nom
!!€ kymmenheen: kymmenen+Num+Sg+Ill
!!€ kymmenennä: kymmenen+Num+Sg+Ess
!!€ kymmenessä: kymmenen+Num+Sg+Ine
!!€ kymmenellä: kymmenen+Num+Sg+Ade
!!€ kymmentä: kymmenen+Num+Sg+Par
!!€ kymmenten: kymmenen+Num+Pl+Gen
!!€ kymmeniitten: kymmenen+Num+Pl+Gen
!!€ kymmenhiin: kymmenen+Num+Pl+Ill
!!€ kymmenissä: kymmenen+Num+Pl+Ine
!!€ kymmeninnä: kymmenen+Num+Pl+Ess
0:en    NUM_FRONT_NOMINATIVE ;
0:he    NUM_FRONT_ILLATIVE_EN    ;
0:e     NUM_FRONT_SINGULARS   ;
0:e     NUM_FRONT_STRONG_SINGULARS_NNA ;
0:e     NUM_FRONT_SINGULARS_LL  ;
0       NUM_FRONT_PARTITIVE_TÄ   ;
0       NUM_FRONT_GENITIVE_TEN   ;
0       NUM_FRONT_PARTITIVE_II   ;
0       NUM_FRONT_PARTITIVE_III   ;
0       NUM_FRONT_GENITIVE_IITTEN    ;
0       NUM_FRONT_ILLATIVE_HIIN  ;
0       NUM_FRONT_PLURALS ;

LEXICON NUM_SATA
!!€gt-norm: Numerals 9
!!€ sata: sata+Num+Sg+Nom
!!€ satana: sata+Num+Sg+Ess
!!€ sađassa: sata+Num+Sg+Ine
!!€ sathaan: sata+Num+Sg+Ill
!!€ sattaa: sata+Num+Sg+Par
!!€ sattoin: sata+Num+Pl+Gen
!!€ sathoin: sata+Num+Pl+Ill
!!€ sađoissa: sata+Num+Pl+Ine
!!€ satoina: sata+Num+Pl+Ess
0:ta    NUM_BACK_NOMINATIVE ;
0:đa    NUM_BACK_WEAK_SINGULARS ;
0:ta    NUM_BACK_STRONG_SINGULARS_NA ;
0:đa    NUM_BACK_SINGULARS_L      ;
0:ta    NUM_BACK_STRONG_SINGULARS   ;
0:đo    NUM_BACK_WEAK_PLURALS   ;
0:to    NUM_BACK_STRONG_PLURALS ;
0:ta    NUM_BACK_ILLATIVE_AN    ;
0:ta    NUM_BACK_PARTITIVE_A    ;
0:to   NUM_BACK_PARTITIVE_OI   ;
0:to   NUM_BACK_GENITIVE_IN    ;
0:to    NUM_BACK_ILLATIVE_IHIN  ;

LEXICON NUM_TUHAT
!!€gt-norm: Numerals 46
!!€ tuhat: tuhat+Num+Sg+Nom
!!€ tuhantheen: tuhat+Num+Sg+Ill
!!€ tuhantena: tuhat+Num+Sg+Ess
!!€ tuhanessa: tuhat+Num+Sg+Ine
!!€ tuhanssiitten: tuhat+Num+Pl+Gen
!!€ tuhatta: tuhat+Num+Sg+Par
!!€ tuhansii: tuhat+Num+Pl+Par
!!€ tuhansiin: tuhat+Num+Pl+Gen
!!€ tuhanshiin: tuhat+Num+Pl+Ill
!!€ tuhansina: tuhat+Num+Pl+Ess
!!€ tuhansissa: tuhat+Num+Pl+Ine
0:t     NUM_BACK_NOMINATIVE ;
0:nthe   NUM_BACK_ILLATIVE_EN    ;
0:nte   NUM_BACK_STRONG_SINGULARS   ;
0:nte   NUM_BACK_STRONG_SINGULARS_NNA ;
0:ne   NUM_BACK_WEAK_SINGULARS   ;
0:ne    NUM_BACK_SINGULARS_LL    ;
0:n     NUM_BACK_GENITIVE_TEN   ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:nss    NUM_BACK_PARTITIVE_II   ;
0:ns    NUM_BACK_GENITIVE_IITTEN   ;
0:ns    NUM_BACK_ILLATIVE_HIIN  ;
0:ns    NUM_BACK_PLURALS ;

LEXICON NUM_MILJOONA
!!€gt-norm: Numerals 10 miljoona
!!€ miljoona: miljoona+Num+Sg+Nom
!!€ miljoonana: miljoona+Num+Sg+Ess
!!€ miljoonassa: miljoona+Num+Sg+Ine
!!€ miljoonaa: miljoona+Num+Sg+Par
!!€ miljoonhaan: miljoona+Num+Sg+Ill
!!€ miljoonii: miljoona+Num+Pl+Par
!!€ miljooniitten: miljoona+Num+Pl+Gen
!!€ miljoonhiin: miljoona+Num+Pl+Ill
!!€ miljoonissa: miljoona+Num+Pl+Ine
!!€ miljoonina: miljoona+Num+Pl+Ess
0:a     NUM_BACK_NOMINATIVE    ;
0:a     NUM_BACK_SINGULARS    ;
0:a     NUM_BACK_STRONG_SINGULARS_NNA ;
0:a     NUM_BACK_SINGULARS_LL    ;
0       NUM_BACK_PLURALS  ;
0:a     NUM_BACK_PARTITIVE_A      ;
0:ha    NUM_BACK_ILLATIVE_AN     ;
0       NUM_BACK_PARTITIVE_II    ;
0       NUM_BACK_GENITIVE_IITTEN ;
0:a     NUM_BACK_GENITIVE_IN    ;
0       NUM_BACK_ILLATIVE_HIIN   ;


LEXICON NUM_MILJARDI
!!€gt-norm: Numerals 5
!!€ miljardi: miljardi+Num+Sg+Nom
!!€ miljardina: miljardi+Num+Sg+Ess
!!€ miljardissa: miljardi+Num+Sg+Ine
!!€ miljardiina: miljardi+Num+Pl+Ess
!!€ miljardiissa: miljardi+Num+Pl+Ine
!!€ miljardii: miljardi+Num+Sg+Par
!!€ miljardhiin: miljardi+Num+Sg+Ill
!!€ miljardii: miljardi+Num+Pl+Par
!!€ miljardiitten: miljardi+Num+Pl+Gen
!!€ miljardhiin: miljardi+Num+Pl+Ill
0:i NUM_BACK_NOMINATIVE ;
0:i NUM_BACK_SINGULARS   ;
0:i NUM_BACK_STRONG_SINGULARS_NNA ;
0:i NUM_BACK_SINGULARS_LL    ;
0:i NUM_BACK_PLURALS ;
0   NUM_BACK_PARTITIVE_II_Sg ;
0   NUM_BACK_ILLATIVE_HIIN_Sg ;
0   NUM_BACK_PARTITIVE_II    ;
0   NUM_BACK_ILLATIVE_HIIN   ;
0   NUM_BACK_GENITIVE_IITTEN ;


LEXICON NUM_GOOGOL
!!€gt-norm: Numerals 5 more
!!€ Googol: Googol+Num+Sg+Nom
0   NUM_BACK_NOMINATIVE ;
0:i NUM_BACK_SINGULARS   ;
o:i NUM_BACK_STRONG_SINGULARS_NA ;
0:e NUM_BACK_PLURALS ;
0:i NUM_BACK_PARTITIVE_A    ;
0:i NUM_BACK_ILLATIVE_IN    ;
0:e NUM_BACK_PARTITIVE_JA   ;
0:e NUM_BACK_ILLATIVE_IHIN  ;
0   NUM_BACK_GENITIVE_IEN   ;

LEXICON NUM_PARI
!!€gt-norm: Numerals 5 moremore
!!€ pari: pari+Num+Sg+Nom
!!€ parina: pari+Num+Sg+Ess
!!€ parissa: pari+Num+Sg+Ine
!!€ pariissa: pari+Num+Pl+Ine
!!€ pariina: pari+Num+Pl+Ess
!!€ parrii: pari+Num+Sg+Par
!!€ parrii: pari+Num+Pl+Par
!!€ parriin: pari+Num+Pl+Gen
!!€ parhiin: pari+Num+Pl+Ill
!!€ parhiin: pari+Num+Sg+Ill
0:i     NUM_BACK_NOMINATIVE    ;
0:i     NUM_BACK_SINGULARS    ;
0:i     NUM_BACK_STRONG_SINGULARS_NA ;
0:i     NUM_BACK_SINGULARS_L  ;
0:i^WG  NUM_BACK_PLURALS   ;
0       NUM_BACK_PARTITIVE_II_Sg  ;
0       NUM_BACK_PARTITIVE_II     ;
0       NUM_BACK_ILLATIVE_HIIN_Sg ;
0       NUM_BACK_GENITIVE_IIN     ;
0       NUM_BACK_ILLATIVE_HIIN    ;


LEXICON NUM_ENSIMMÄINEN
!!€gt-norm: Numerals 38
!!€ ensimmäinen: ensimmäinen+Num+Ord+Sg+Nom
!!€ ensimmäisenä: ensimmäinen+Num+Ord+Sg+Ess
!!€ ensimmäisessä: ensimmäinen+Num+Ord+Sg+Ine
!!€ ensimmäistä: ensimmäinen+Num+Ord+Sg+Par
!!€ ensimmäisten: ensimmäinen+Num+Ord+Pl+Gen
!!€ ensimmäissii: ensimmäinen+Num+Ord+Pl+Par
!!€ ensimmäishiin: ensimmäinen+Num+Ord+Pl+Ill
!!€ ensimmäisinä: ensimmäinen+Num+Ord+Pl+Ess
!!€ ensimmäisissä: ensimmäinen+Num+Ord+Pl+Ine
0:nen   NUM_FRONT_NOMINATIVE ;
0:se    NUM_FRONT_ILLATIVE_EN    ;
0:se    NUM_FRONT_SINGULARS   ;
0:s     NUM_FRONT_PARTITIVE_TÄ   ;
0:s     NUM_FRONT_GENITIVE_TEN   ;
0:ss    NUM_FRONT_PARTITIVE_II   ;
0:ss    NUM_FRONT_GENITIVE_IIN   ;
0:s     NUM_FRONT_ILLATIVE_HIIN   ;
0:s     NUM_FRONT_PLURALS ;
+Der+Der/s:s    R ; !new PoS is missing

LEXICON NUM_TOINEN
!!€gt-norm: Numerals 38 back
!!€ toinen: toinen+Num+Ord+Sg+Nom
!!€ toisheen: toinen+Num+Ord+Sg+Ill
!!€ toista: toinen+Num+Ord+Sg+Par
!!€ toisten: toinen+Num+Ord+Pl+Gen
!!€ toissiin: toinen+Num+Ord+Pl+Gen
!!€ toissii: toinen+Num+Ord+Pl+Par
!!€ toishiin: toinen+Num+Ord+Pl+Ill
!!€ toisena: toinen+Num+Ord+Sg+Ess
!!€ toisessa: toinen+Num+Ord+Sg+Ine
!!€ toisissa: toinen+Num+Ord+Pl+Ine
!!€ toisina: toinen+Num+Ord+Pl+Ess
0:nen   NUM_BACK_NOMINATIVE ;
0:she    NUM_BACK_ILLATIVE_EN    ;
0:se    NUM_BACK_SINGULARS   ;
0:s     NUM_BACK_PARTITIVE_TA   ;
0:s     NUM_BACK_GENITIVE_TEN   ;
0:ss    NUM_BACK_PARTITIVE_II   ;
0:ss    NUM_BACK_GENITIVE_IIN   ;
0:s     NUM_BACK_ILLATIVE_HIIN   ;
0:s     NUM_BACK_PLURALS ;
+Der+Der/s:s    R ; !new PoS is missing

LEXICON NUM_KOLMAS
!!€gt-norm: Numerals 45
!!€ kolmas: kolmas+Num+Ord+Sg+Nom
!!€ kolmantena: kolmas+Num+Ord+Sg+Ess
!!€ kolmanessa: kolmas+Num+Ord+Sg+Ine
!!€ kolmantheen: kolmas+Num+Ord+Sg+Ill
!!€ kolmatta: kolmas+Num+Ord+Sg+Par
!!€ kolmanssii: kolmas+Num+Ord+Pl+Par
!!€ kolmanssiin: kolmas+Num+Ord+Pl+Gen
!!€ kolmansissa: kolmas+Num+Ord+Pl+Ine
!!€ kolmansina: kolmas+Num+Ord+Pl+Ess
0:s     NUM_BACK_NOMINATIVE ;
0:nthe  NUM_BACK_ILLATIVE_EN    ;
0:nte   NUM_BACK_STRONG_SINGULARS   ;
0:ne    NUM_BACK_WEAK_SINGULARS   ;
0:t     NUM_BACK_PARTITIVE_TA   ;
0:nss   NUM_BACK_PARTITIVE_II   ;
0:nss   NUM_BACK_GENITIVE_IIN   ;
0:ns    NUM_BACK_ILLATIVE_IIN   ;
0:ns    NUM_BACK_PLURALS ;

LEXICON NUM_NELJÄS
!!€gt-norm: Numerals 45 fron
!!€ neljes: neljes+Num+Ord+Sg+Nom
!!€ neljentheen: neljes+Num+Ord+Sg+Ill
!!€ neljentenä: neljes+Num+Ord+Sg+Ess
!!€ neljennessä: neljes+Num+Ord+Sg+Ine
!!€ neljettä: neljes+Num+Ord+Sg+Par
!!€ neljenssii: neljes+Num+Ord+Pl+Par
!!€ neljenssiin: neljes+Num+Ord+Pl+Gen
!!€ neljenshiin: neljes+Num+Ord+Pl+Ill
!!€ neljensissä: neljes+Num+Ord+Pl+Ine
!!€ neljensinä: neljes+Num+Ord+Pl+Ess
0:s     NUM_FRONT_NOMINATIVE ;
0:nthe  NUM_FRONT_ILLATIVE_EN    ;
0:nte   NUM_FRONT_STRONG_SINGULARS   ;
0:nne   NUM_FRONT_WEAK_SINGULARS   ;
0:t     NUM_FRONT_PARTITIVE_TÄ   ;
0:nss   NUM_FRONT_PARTITIVE_II   ;
0:nss   NUM_FRONT_GENITIVE_IIN   ;
0:ns    NUM_FRONT_ILLATIVE_HIIN   ;
0:ns    NUM_FRONT_PLURALS ;











! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES   !!= * __@CODE@__
 §+Use/Circ:§       ARABIC ; ! §24
 §§+Use/Circ:§§     ARABIC ; ! §§24
 §% +Use/Circ:§%    ARABIC ; ! § 24
 §§% +Use/Circ:§§%  ARABIC ; ! §§ 24
 %-+Use/Circ:%- ARABIC ; ! -24
 %‒+Use/Circ:%‒ ARABIC ; ! -24 U+2012
 %–+Use/Circ:%– ARABIC ; ! -24 U+2013
 %—+Use/Circ:%— ARABIC ; ! -24 U+2014
 %―+Use/Circ:%― ARABIC ; ! -24 U+2015
! %++Use/Circ:%+ ARABIC ; ! <plus>24 denne fungerer ikke som den skal
 %-% +Use/Circ:%-%  ARABIC ; ! - 24
 %‒% +Use/Circ:%‒%  ARABIC ; ! - 24 U+2012
 %–% +Use/Circ:%–%  ARABIC ; ! - 24 U+2013
 %—% +Use/Circ:%—%  ARABIC ; ! - 24 U+2014
 %―% +Use/Circ:%―%  ARABIC ; ! - 24 U+2015
 %+% +Use/Circ:%+%  ARABIC ; ! <plus> 24
 %*+Use/Circ:%* ARABIC ; ! *24
 %$+Use/Circ:%$ ARABIC ; ! $24
 %€+Use/Circ:%€ ARABIC ; ! €24
 %<+Use/Circ:%[%<%] ARABIC ; ! <24
 %>+Use/Circ:%[%>%] ARABIC ; ! >24
 s%.+Use/Circ:s%. ARABIC ; ! s.24




LEXICON ARABIC   !!= * __@CODE@__
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOP ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPORD ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;

LEXICON ARABICLOOPphone   !!= * __@CODE@__ +358(0)16671254
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;
 +Use/Circ: ARABICLOOPphone ;
 (+Use/Circ:( ARABICLOOPphone ;
 )+Use/Circ:) ARABICLOOPphone ;
 %—+Use/Circ:%— ARABICLOOPphone ; 
 ARABICCASEphone ;

LEXICON ARABICCASEphone   !!= * __@CODE@__  
 +Num+Sg+Nom: # ;


LEXICON ARABICLOOP   !!= * __@CODE@__
 +Use/Circ: ARABIC ;
 %-+Use/Circ:%- ARABIC ; ! 1-2 multipart numbers
 %–+Use/Circ:%– ARABIC ; ! 1–2 multipart numbers
 %—+Use/Circ:%— ARABIC ; ! 1—2 multipart numbers
 %.%-+Use/Circ:%.%- ARABIC ; ! 1.-2 multipart numbers ! !
 %.%–+Use/Circ:%.%– ARABIC ; ! 1.–2 multipart numbers ! !
 %.%—+Use/Circ:%.%— ARABIC ; ! 1.—2 multipart numbers ! !
 %-% +Use/Circ:%-%  ARABIC ; ! 1- 2 multipart numbers
 %–% +Use/Circ:%–%  ARABIC ; ! 1– 2 multipart numbers
 %—% +Use/Circ:%—%  ARABIC ; ! 1— 2 multipart numbers
 % %-+Use/Circ:% %- ARABIC ; ! 1 -2 multipart numbers
 % %–+Use/Circ:% %– ARABIC ; ! 1 –2 multipart numbers
 % %—+Use/Circ:% %— ARABIC ; ! 1 —2 multipart numbers
 % %-% +Use/Circ:% %-%  ARABIC ; ! 1 - 2 multipart numbers
 % %–% +Use/Circ:% %–%  ARABIC ; ! 1 – 2 multipart numbers
 % %—% +Use/Circ:% %—%  ARABIC ; ! 1 — 2 multipart numbers
 % %-%-% +Use/Circ:% %-%-%  ARABIC ; ! 1 -- 2 multipart numbers
 % %–%–% +Use/Circ:% %–%–%  ARABIC ; ! 1 –– 2 multipart numbers
 % %—%—% +Use/Circ:% %—%—%  ARABIC ; ! 1 —— 2 multipart numbers
 %.+Use/Circ:%. ARABIC ;
 %:+Use/Circ:%: ARABIC ;
 %,+Use/Circ:%, ARABIC ;
 % +Use/Circ:%  ARABIC ; ! Allowing for "23 500".
   +Use/Circ:  ARABIC ; ! Allowing for "23 500" w/nbsp (init char = alt-space)
 %/+Use/Circ:%/ ARABIC ; ! 24/12
 %/% +Use/Circ:%/%  ARABIC ; ! 24/ 12
 % %/+Use/Circ:% %/ ARABIC ; ! 24 /12
 '+Use/Circ:'7 ARABIC ; ! Since ' denotes cons.grad. This is not optimal either.

 +Num+Use/Circ: RNum ; ! Num Cmp Noun, with hyphen

 ,%-+Num+Use/Circ:,%- ARABICCASES ; ! 10,-
 ,%-+Num+Err/Orth+Use/Circ:.%- ARABICCASES ; ! 10.- It is wrong, but written.

  % %-+Err/Orth+Use/Circ:% %- ARABICDELIMITER; ! 1 - multipart numbers - "1 -:s"?
  % %-%-+Err/Orth+Use/Circ:% %-%- ARABICDELIMITER; ! 1 -- multipart numbers
  %%+Use/Circ:%%  ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%".
  % %%+Use/Circ:% %%  ARABICDELIMITER ; ! the correct ones as well... 50 % etc.
  ARABICDELIMITER ; ! list of number-case delimiters

LEXICON ARABICLOOPORD   !!= * __@CODE@__
 +A+Ord+Attr:%. # ;
 +A+Ord+Use/Circ: ARABICDELIMITERORD ;
 < [a|b|c|d|e|f|f %  f] > ACASETAG ;

LEXICON ACASETAG   !!= * __@CODE@__  is for 1e+Num+Sg+Num:1e
+Num: ARABICCASES ;



LEXICON ARABICDELIMITER   !!= * __@CODE@__  blank + 3 delim, what does the lg counc prefer?
 +Num:           ARABICCASE0 ; ! This is the 1984 case, ie NGA
 +Num:%:         ARABICCASE  ; ! This is the 1984:s case.
 +Num+Err/Orth:   ARABICCASE  ; ! This is the 1984s case.
 +Num+Err/Orth:' ARABICCASE  ; ! This is the 1984's case.
 +Num+Err/Orth:’  ARABICCASE  ; ! optio-B07 = ’
 +Num+Err/Orth:%- ARABICCASE  ; ! This is the 1984-s case.
 +Num:%- NUMERALCOMPOUNDS ; ! 6-jáhkasaš
 +Num+Err/Orth:´  ARABICCASE  ; ! This is the 1984´s case.


LEXICON ARABICDELIMITERORD   !!= * __@CODE@__
!            ARABICCASE0   ; ! This is the 1984 case, ie NGA
:%:         ARABICCASEORD ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASEORD ; ! This is the 1984s case.
 +Err/Orth:' ARABICCASEORD ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASEORD ; ! This is the 1984-s case.


LEXICON ARABICCASES   !!= * __@CODE@__  to distinguish between 0 and oblique
ARABICCASE0 ;
ARABICCASE ;

LEXICON ARABICCASE0   !!= * __@CODE@__
 +Sg+Nom: # ;
 +Sg+Gen:%:n # ;
 +Cmp/SplitR:%- ENDLEX ;
 +Cmp/SplitR:%– ENDLEX ;

LEXICON ARABICCASE   !!= * __@CODE@__
 +Sg+Ine:%>ss^A           K ;

 LEXICON ARABICCASEORD   !!= * __@CODE@__
 +Sg+Gen:%>nnen     K ;



! The Roman numerals !
! ------------------ !

LEXICON ROMAN   !!= * __@CODE@__ 
 +Use/Circ: ROM-THOUSAND ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ; !

LEXICON ROM-THOUSAND   !!= * __@CODE@__ 
 M+Use/Circ:M ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM ROM-THOUSAND-TAG; !

LEXICON ROM-THOUSAND-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ;				!
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-HUNDRED   !!= * __@CODE@__ 
 C+Use/Circ:C ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD ROM-HUNDRED-TAG; !
 D+Use/Circ:D ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM ROM-HUNDRED-TAG; !

LEXICON ROM-HUNDRED-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-TEN   !!= * __@CODE@__ 
 X+Use/Circ:X ROM-TEN-TAG; !
 XX+Use/Circ:XX ROM-TEN-TAG; !
 XXX+Use/Circ:XXX ROM-TEN-TAG; !
 XL+Use/Circ:XL ROM-TEN-TAG; !
 L+Use/Circ:L ROM-TEN-TAG; !
 LX+Use/Circ:LX ROM-TEN-TAG; !
 LXX+Use/Circ:LXX ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX ROM-TEN-TAG; !
 XC+Use/Circ:XC ROM-TEN-TAG; !

LEXICON ROM-TEN-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-ONE ; !
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-ONE   !!= * __@CODE@__ 
 I+Use/Circ:I ROM-ONE-TAG; !
 II+Use/Circ:II ROM-ONE-TAG; !
 III+Use/Circ:III ROM-ONE-TAG; !
 IV+Use/Circ:IV ROM-ONE-TAG; !
 V+Use/Circ:V ROM-ONE-TAG; !
 VI+Use/Circ:VI ROM-ONE-TAG; !
 VII+Use/Circ:VII ROM-ONE-TAG; !
 VIII+Use/Circ:VIII ROM-ONE-TAG; !
 IX+Use/Circ:IX ROM-ONE-TAG; !

LEXICON ROM-ONE-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
! +N: ROMNUMTAG ; ! !The Olav viđeš fix (Roman V now gets A)
 +Use/Circ: ROM-SPLIT ; !

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT   !!= * __@CODE@__ 
 +Use/Circ:%- 2ROMAN ; ! II-VI, etc. !


! Here goes loop 2.

LEXICON 2ROMAN   !!= * __@CODE@__ 
 +Use/Circ: 2ROM-THOUSAND ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-THOUSAND   !!= * __@CODE@__ 
 M+Use/Circ:M 2ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM 2ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM 2ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM 2ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM 2ROM-THOUSAND-TAG; !

LEXICON 2ROM-THOUSAND-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ;				 !
 +Use/Circ: 2ROM-ONE ;				!

LEXICON 2ROM-HUNDRED   !!= * __@CODE@__ 
 C+Use/Circ:C 2ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC 2ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC 2ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD 2ROM-HUNDRED-TAG; !
 D+Use/Circ:D 2ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC 2ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC 2ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC 2ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM 2ROM-HUNDRED-TAG; !

LEXICON 2ROM-HUNDRED-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ;				!
LEXICON 2ROM-TEN   !!= * __@CODE@__ 
 X+Use/Circ:X 2ROM-TEN-TAG; !
 XX+Use/Circ:XX 2ROM-TEN-TAG; !
 XXX+Use/Circ:XXX 2ROM-TEN-TAG; !
 XL+Use/Circ:XL 2ROM-TEN-TAG; !
 L+Use/Circ:L 2ROM-TEN-TAG; !
 LX+Use/Circ:LX 2ROM-TEN-TAG; !
 LXX+Use/Circ:LXX 2ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX 2ROM-TEN-TAG; !
 XC+Use/Circ:XC 2ROM-TEN-TAG; !

LEXICON 2ROM-TEN-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-ONE   !!= * __@CODE@__ 
 I+Use/Circ:I 2ROM-ONE-TAG; !
 II+Use/Circ:II 2ROM-ONE-TAG; !
 III+Use/Circ:III 2ROM-ONE-TAG; !
 IV+Use/Circ:IV 2ROM-ONE-TAG; !
 V+Use/Circ:V 2ROM-ONE-TAG; !
 VI+Use/Circ:VI 2ROM-ONE-TAG; !
 VII+Use/Circ:VII 2ROM-ONE-TAG; !
 VIII+Use/Circ:VIII 2ROM-ONE-TAG; !
 IX+Use/Circ:IX 2ROM-ONE-TAG; !

LEXICON 2ROM-ONE-TAG   !!= * __@CODE@__ 
 +Use/Circ: ROMNUMTAG ; !

LEXICON ROMNUMTAG   !!= * __@CODE@__
 +Num+Nom+Use/Circ:%> # ; !
 +Num+Gen+Use/Circ:%> # ; !
 +Num+Acc+Use/Circ:%> # ; !
 +A+Ord+Use/Circ:%.%> # ; ! !III. as ordinal.
 +A+Ord+Use/Circ:%> # ; ! !Looking for trouble...
 +Num+Use/Circ:%: ROMNUMTAGOBL ; !
 +Num+Use/Circ:'  ROMNUMTAGOBL ; !
 +Num+Use/Circ:%- ROMNUMTAGOBL ; !
 +Num+Use/Circ:%´ ROMNUMTAGOBL ; !
! +Num: ROMNUMTAGOBL ; !

LEXICON ROMNUMTAGOBL   !!= * __@CODE@__ 
 +Gen+Err/Orth+Use/Circ:%>%:n # ; !  But here 123a 123
 +Acc+Err/Orth+Use/Circ:%>%:n # ; ! 
 +Ill+Use/Circ:%>%:iin # ; !
 +Ine+Use/Circ:%>%:ssa # ; !


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

LEXICON ISOLATED-NUMEXP   !!= * __@CODE@__
 ½+Num:½ # ;
 ¹+Num:¹ # ;
 ²+Num:² # ;
 ³+Num:³ # ;
 ¼+Num:¼ # ;
 ¾+Num:¾ # ;


LEXICON NUMERALCOMPOUNDS   !!= * __@CODE@__  ! numeral as first part
 +Use/NG: Rhyph ; ! golmma-
