! ======================
!! !!!Sublexica for NounRoot
! ======================

!! Aloitin sovittaa analyysin Eiran kieliopin mukaan (Porsanki).
!! Leksikot ovat nyt (tai pitää olla) n11, n12, jne., eli siis
!! Eiran nominit tyyppi 1.1., 1.2., jne.

!!  2007 s.87
!! * 1. Yksitavuiset / Vi ( maa, tuorestai)		 = n_1
!! * 1.1. Yksitavuiset monikko/ Vi ( häät)		 = n_1pl
!! * 2. Vokaalirankaiset						 
!! ** 2.1 Kaksitavuiset ( aika, pappi)			 = n_21
!! *** 2.1.1. Kaksitavuiset monikko (housut)     = n_21pl
!! *** 2.1.2. Kaksitavuiset (joki: joke)         = n_21ie
!! ** 2.2. Usseempitav. (opettaaja)		      	 = n_22
!! *** 2.2.1. Usseempitav. odd (meininki, tunturi)	= n_22odd
!! *** 2.2.2. Usseempitav. monikko oi (kulkkiija)	= n_22oi
!! ** 2.3 Kokkoontumanom. ( valkkee)    		 = n_23
!! ** 2.3.1 Kokkoontumanom. (assii)             = n_23ii
!! * 3. Konsonanttirankaiset					  
!! ** 3.1 Kaksitavuiset ( pieni, kieli)			 = n_31
!! ** 3.1.1. Kaksitavuiset lumi)                 = n_31m
!! ** 3.1.2. (käsi, vuosi)						 = n_31si
!! ** 3.1.3. (lapsi)						      = n_31lapsi
!! ** 3.1.4. (mies)						         = n_31mies
!! ** 3.2 Usseempitavuiset 
!! ** 3.2.1 (tytär)                              = n_32
!! ** 3.2.2. (elläin)                            = n_32m
!! ** 3.2.3 (rakkhaus)							 = n_32s
!! ** 3.3 Kokkoontumanom. ( opas, huonet)		 
!! ** 3.3.1 (opas)                               = n_33s
!! ** 3.3.2 (asukas)                             = n_33sodd
!! ** 3.3.3. (ruvis)                           = n_33si
!! ** 3.3.4 (huonet)                            = n_33t
!! ** 3.3.5 (kirves)							 = n_33ts
!! * 4. Ekstrakonsonanttirankaiset				  
!! ** 4.1 veres-nominit ( veres, kokkous)		 = n_41
!! ** 4.1.1. veres-nominit monikko ( veljekset)		= n_41pl
!! ** 4.2 –inen-nominit ( kainulainen)			 = n_42 = nen
!! *** 4.2.1 -inen-nominit Pl (tervheiset)		 = n_42pl = nen
!! *** 4.2.2 -inen-nominit odd (vihrinen)		 = n_42odd = nen

!! !!Noun types

!! !!!INCOMING

LEXICON n_x 
+N: x_x ;

LEXICON x_x
+Sg+Nom: K ; ! Just to make it compile



LEXICON n_1 !!= * @CODE@ = maa
+N: x_1 ;

LEXICON x_1
 +Sg+Nom: K ; 
 +Sg+Nom: R ; 
 long_par ;
 weak_v_stem ;
+Sg: sgpl_cases_even ;      !! these two lexica for cases with
+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+Pl+Com:%>i2ne K ;
  1TAVU_VV-ill ;
+Pl:%>i2 tt_longgenpl ;
+Pl:%>i2 longparpl ;
+Pl:%>i2 shortillpl ;
+Sg+Nom: Px ;   ! mattoni
+Sg+Gen: Px ;   ! mattoni
+Pl+Nom: Px ;   ! mattoni
ess_even ;
 
LEXICON n_1pl !!= * @CODE@ = häät

 +N: long_par ;
 +N: weak_v_stem ;
+N+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl+Com:%>i2ne K ;

 +N: 1TAVU_VV-ill ;
 +N+Pl:%>i2 tt_longgenpl ;
 +N+Pl:%>i2 longparpl ;
 +N+Pl:%>i2 shortillpl ;
 +N+Pl+Ess:%>i2%>n%^A PxxK ;

LEXICON n_21C !!= * @CODE@  
+N+Sg+Nom:  K    ; ! Hans
         :i n_21 ; ! Hansi

LEXICON n_21	!!= * @CODE@  "poika:poi6ka", lyhyt vokali, yks vartalo
+N: x_21 ;

LEXICON x_21
nom_strong ;
x_21_oblique ;

LEXICON x_21_oblique
 strong_v_stem_even ;
:%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_even ;      ! to sgpl_cases_even for cases with
+Pl:%^WG%>%^AO%>i2 sgpl_cases_even ;  ! the same affix in sg and pl
 2TAVU_V_ill_sg ;
+Pl:%^AO%>i2 shortestgenpl ;  !! * shortestgenpl since we have the i
+Pl:%^AO%>i2 shortestparpl  ;  
+Pl+Ill:%^AO%>%^HMETAh%^V%>i2 n_PxK ;
+Pl+Com:%^AO%>i2ne K ;
 

LEXICON n_21pl	!!= * @CODE@  "villahousut", lyhyt vokali, yks vartalo
 +N+Pl+Nom:%>t K ; 
 +N+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
 +N+Pl+Ess:%>i2%>n%^A PxxK ;
 +N+Pl:%>i2 tt_longgenpl ;
 +N+Pl+Ill:%^AO%>%^HMETAh%^V%>i2 n_PxK ;
 +N+Pl:%>i2 shortestparpl ;
 +N+Pl+Com:%>i2ne K ;
 
 LEXICON kk-longgenpl
 +Gen:%>kk%>i2 # ; ! kk ?? Tsekkaa..

LEXICON kk-longgparpl
 +Par:%>kk%>i2 # ;! kk ?? Tsekkaa..

LEXICON n_21ie	!!= @CODE@ on ove, ovi, nom joki:joke
+N: x_21ie ;

LEXICON x_21ie 
+Sg+Nom:%^E2I K ; ! %^EI e to i
+Sg+Nom:%^E2I R ; ! %^EI e to i
 short_par ;
 obl_strong_even ;   
:%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_even ;      !! these two lexica for cases with
+Pl:%^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+Pl:%>i2i shortestgenpl ;
	2TAVU_edel-ill ;
+Pl:%>i2 shortparpl ;
+Pl+Com:%>i2ne K ;

LEXICON n_22	!!= * @CODE@  opettaaja
+N: x_22 ;

LEXICON x_22	
 nom_strong ;
 strong_v_stem_even ;
:%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_even ;
+Pl:%^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
 2TAVU_V_ill_sg ;
!+Sg+Ess:%>n%^A PxxK ; Lene removed this one
!+Pl+Tra:%>i2ksi K ; Lene removed this one
!!+Pl+Tra:%>i2kse PxxK ;
+Pl+Ess:%>i2n%^A PxxK ;
+Pl:%^WG%>i2 tt_longgenpl ;
+Pl:%>i2 longparpl ;
+Pl+Ill:%>%^HMETAhi%>i2 n_PxK ;
+Pl+Com:%>i2ne K ;

LEXICON n_22odd	!!= * @CODE@  meininki
+N: x_22odd ;

LEXICON x_22odd	!!= * @CODE@  meininki
 nom_strong ;
  short_par ;
 nomgen_px ;
 :%^WG weak_v_stem ;
 +Sg:%^WG sgpl_cases_odd ;      
 +Pl:%^WG%>i2 sgpl_cases_even ; 
 2TAVU_V_ill_sg ;
 ess_odd_sg ;
 ess_even_pl ;
!+Pl+Tra:%>i2ksi K ;
!+Pl+Tra:i%>ksi PxxK ; !Lene removed this
+Pl:%^WG%>i2 tt_longgenpl ;
+Pl:%>i2 longparpl ;
!+Pl+Ill:%>%^HMETAh%^V%^V n_PxK ;
+Pl+Ill:%>%^HMETAhi%>i2 n_PxK ;
+Pl+Com:%>i2ne K;

LEXICON n_22oi	!!= * @CODE@  
+N: x_22oi ;

LEXICON x_22oi	!!= * @CODE@  Kulkkiija
 nom_strong ;
 short_par ;
 obl_strong_even ;
   :%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_odd ;          !! these two lexica for cases with
+Pl:%^AO%^WG%>i2 sgpl_cases_even ; !! the same affix in sg and pl
    2TAVU_V_ill_sg ;
+Pl:%^AO%^WG%>i2 tt_longgenpl ;
+Pl:%^AO%^WG%>i2 longparpl ;
+Pl+Ill:%^AO%>i2hi n_PxK ;
+Pl+Com:%^AO%>i2ne K;


LEXICON n_23ii	!!= * @CODE@  assii
+N+Sg+Nom: K ; 
+N+Sg+Nom: R ;
+N: long_par_sg ;
+N+Sg+Ill:sseen  K ;
+N+Pl+Ill:ssiin K ;
+N+Pl: longparpl ;
+N+Pl: tt_longgenpl ;
+N: weak_v_stem ;
+N+Sg:%^WG sgpl_cases_even ;   !! * @CODE@  for same affix in sg and pl
+N+Pl:%^WG sgpl_cases_even ;  !! * @CODE@  for same affix in sg and pl
+N:e ess_even ;
+N+Pl+Com:%>ne K ;

LEXICON n_23	!!= * @CODE@  valkkee
+N: x_23 ;

LEXICON x_23
+Sg+Nom:e K ; 
+Sg:e sgpl_cases_even ;
+Sg+Gen:en K ; 
+Pl+Nom:et K ;
+Sg+Ill:esseen  K ;
+Pl+Ill:e%>i2ssiin K ;
:e long_par_sg ;
+Pl:e%>i2 longparpl ;
+Pl:e%>i2 tt_longgenpl ;
+Pl:e%>i2 sgpl_cases_even ;
ess_even ;
+Pl+Com:e%>i2ne K ;


LEXICON n_31	!!= @CODE@  kieli
+N: x_31 ;

LEXICON x_31
+Sg+Nom:%^E2I K ; ! %^E2I e to i
+Sg+Nom:%%^E2I R ; ! %^E2I e to i
:%^EDEL long_par_sg ;
 obl_strong_even ;   
:%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_even ;    !! * @CODE@  for same affix in sg and pl
+Pl:%>i2 sgpl_cases_even ;  !! * @CODE@  for same affix in sg and pl
 2TAVU_V_ill_sg ;
+Pl:%^E2I%^HMETAhii shortestillpl ;
+Pl:%>i2 shortparpl	 ;
+Pl:%>i2 shortgenpl	 ;
!!+Pl:%^EDEL consgenpl ;
+Pl+Com:%>i2ne K ;

LEXICON n_31m	!!= @CODE@ lumi lu
+N+Sg+Nom:mi K ; ! %^EI e to i
+N+Sg+Nom:mi R ; ! %^EI e to i
+N:n long_par_sg ;
+N:me obl_strong_even ;   
+N:me%^WG weak_v_stem ;
+N+Sg:me%^WG sgpl_cases_even ;      ! these two lexica for cases with
+N+Pl:m%^WG%>i2 sgpl_cases_even ;  ! the same affix in sg and pl
+N+Pl:mmi%>i2	shortestgenpl ;
+N+Sg+Ill:m%>%^HMETAhee n_PxK ;
+N+Pl+Ill:m%^EDEL%>hii n_PxK ;
+N+Pl:mmi%>i2 shortestparpl ;
+N+Pl+Com:m%>i2ne K ;

LEXICON n_31si	!!= @CODE@ vuosi, käsi
+N: x_31si ;

LEXICON x_31si
+Sg+Nom:%^E2I K ; ! %^E2I e to i
+Sg+Nom:%^E2I R ; ! %^E2I e to i
:%^EDEL long_par_sg ; ! parsg
 obl_strong_even ;   ! compl, ess
:%^WG weak_v_stem ;
+Sg:%^WG sgpl_cases_even ;      ! these two lexica for cases with
+Pl:%^E2I%>i2 sgpl_cases_even ;  ! the same affix in sg and pl
 2TAVU_V_ill_sg ;
:%^E2I%>i2 shortparpl ;
:%^E2I%>i2 shortgenpl ;
:%^E2Ihii shortestillpl ; 
+Pl+Com:%>i2ne K ;

LEXICON n_31lapsi	!!= @CODE@ lapsi la
+N+Sg+Nom:psi K ; ! %^EI e to i
+N+Sg+Nom:psi R ; ! %^EI e to i
+N:s long_par_sg ;
+N:pse obl_strong_even ;   
+N:pse%^WG weak_v_stem ;
+N+Sg:pse%^WG sgpl_cases_even ;      !! these two lexica for cases with
+N+Pl:ps%^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl:psi%>i2	shortestgenpl ;
+Pl:ps%>i2i shortestparpl ;
+N+Sg+Ill:ps%>%^HMETAhee n_PxK ;
+N+Pl+Ill:ps%^EDEL%>hii n_PxK ;
+N+Pl:psi%>i2 shortparpl ;
+N+Pl+Com:ps%>i2ne K ;

LEXICON n_31mies	!!= @CODE@ mies mie
+N+Sg+Nom:s K ; ! %^EI e to i
+N+Sg+Nom:s R ; ! %^EI e to i
+N+Sg+Gen:hen K ;
+N:s long_par_sg ;
+N:he obl_strong_even ;   
+N+Sg:he%^WG weak_v_stem ;
+N+Pl+Nom:het K ;
+N+Sg:he%^WG sgpl_cases_even ;      !! these two lexica for cases with
+N+Pl:h%^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl:h%>i2%>i	shortestgenpl ;
+N+Pl:h%>i2%>i shortestparpl ;
+N+Sg+Ill:h%>%^HMETAhee n_PxK ;
+N+Pl+Ill:hhii n_PxK ;
+N+Pl+Com:h%>i2ne K ;


LEXICON n_32 !!= @CODE@ tytär tyttär
+N: x_32 ;

LEXICON x_32
+Sg+Nom:%^WG K ;
+Sg+Nom:%^WG R ;
:e weak_v_stem ;
+Sg+Par:%^WG%>t%^A PxxK ;
:e obl_strong_odd ;
+Sg+Gen:e%>n K ;
:e 2TAVU_V_ill_sg ;
:e obl_strong_odd ;
:e 2TAVU_V_ill_sg ;
+Sg:e sgpl_cases_odd ;      
+Pl:%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+Pl:i%>i2 tt_longgenpl ;
+Pl:i%>i2 longparpl ;
+Pl:%^HMETAh%>i2i shortestillpl;
+Pl+Com:%>i2ne K ;

LEXICON n_32m !!= @CODE@ elläin elläi
+N: x_32m ;

LEXICON n_32m_pl !!= @CODE@ elläin elläi
+N: x_32m_pl ;



LEXICON x_32m !!= @CODE@ 
x_32m_sg ;
x_32m_pl ;

LEXICON x_32m_sg 
+Sg+Nom:%^WGn K ;
+Sg+Nom:%^WGn R ;
:me weak_v_stem_sg ;
+Sg+Par:%^WG%>nt%^A PxxK ;
:me obl_strong_odd ;
+Sg+Gen:me%>n K ;
:me weak_v_stem_pl ;  ! Nom pl
:me 2TAVU_V_ill_sg ;
:me 2TAVU_V_ill_sg ;
+Sg:me sgpl_cases_odd ;   
   
LEXICON x_32m_pl 
+Pl:me weak_v_stem_pl ;
+Pl:me weak_v_stem ;
+Pl:m%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+Pl:%>mi%>i2 longparpl ;
+Pl:mi%>i2 tt_longgenpl ;
+Pl:m%^HMETAh%>i2i shortestillpl;
+Pl+Com:m%>i2ne K ;

LEXICON n_32s !!= @CODE@  sairhaus 
+N+Sg+Nom:%^WGs # ;  !! * Konsonanttivartalo: poista -e ja heikko aste
+N+Sg+Par:%>tt%^A PxxK ;
!+N:te obl_strong_odd ;
+N:đe weak_v_stem ;
+N:te 2TAVU_V_ill_sg ;
!+N+Sg:đe sgpl_cases_odd ;
+N+Sg:đe sgpl_cases_common ;
+N+Sg+Ade:đe%>ll%^A PxxK ; !! * Pitkä perusmuoto
+N+Sg+All:đe%>lle PxxK ; !! * Pitkä perusmuoto
+N+Sg+Ess:te%>n%^A PxxK ;
+N+Pl+Ess:ks%>i2n%^A PxxK ;      
+N+Pl:ks%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+N+Pl:ksi%>i2 longparpl ;
+N+Pl:ksi%>i2 tt_longgenpl ;
+N+Pl:ks%^HMETAh%>i2i shortestillpl;
+N+Pl+Com:ks%>i2ne K ;


! LEXICON n_32s != @CODE@  sairhaus vanha versio
! +N:t long_par ;
! +N:te obl_strong ;
! +N:te%^WG weak_v_stem ;
! +N+Sg:%^WG sgpl_cases_odd ;      
! +N+Pl:%^WG%>i2 sgpl_cases_odd ;
! +N+Pl:ks%>i2 shortparpl ;
! +N+Pl:ks%>i2 shortgenpl ;
! +N+Pl:ks%>i2 shorterillpl ;

!LEXICON n_32t
!n_32 ; ! ei tehty

!LEXICON n_33 ! ei tehty
!n_33s ;

LEXICON n_33as !!= @CODE@ rakas
+N: x_33as ;

LEXICON x_33as
:a%^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
:haa mTAVU_VV-ill ;
+Pl+Ill:h%>aissiin K ;
+Pl+Com:h%>a%>ine K ;
:h%>aa weak_v_stem_sg ;
:h%>aa weak_v_stem_pl;
+Sg:h%>aa sgpl_cases_even ;
+Pl:h%>a%>i2 sgpl_cases_even ;
+Pl:h%>a%>i2 longparpl ;
+Pl:h%>a%>i2 tt_longgenpl ;
+N+Pl+Ess:h%>a%>i2%>na PxxK ;

!LEXICON n_33aes 
!+N: x_33aes ;

LEXICON x_33aes
:ä%^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
:hää mTAVU_VV-ill ;
+Pl+Ill:h%>äissiin K ;
+Pl+Com:h%>ä%>ine K ;
:h%>ää weak_v_stem_sg ;
:h%>ää weak_v_stem_pl;
+Sg:h%>ää sgpl_cases_even ;
+Pl:h%>ä%>i2 sgpl_cases_even ;
+Pl:h%>ä%>i2 longparpl ;
+Pl:h%>ä%>i2 tt_longgenpl ;
+N+Pl+Ess:h%>ä%>i2%>nä PxxK ;



LEXICON n_33ys !!= @CODE@ rakas
+N: x_33ys ;

LEXICON x_33ys
:%>y%^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
:h%>yy mTAVU_VV-ill ;
+Pl+Ill:h%>yissiin K ;
+Pl+Com:h%>y%>ine K ;
:h%>yy weak_v_stem_sg ;
:h%>yy weak_v_stem_pl;
+Sg:h%>yy sgpl_cases_even ;
+Pl:h%>y%>i2 sgpl_cases_even ;
+Pl:h%>y%>i2 longparpl ;
+Pl:h%>y%>i2 tt_longgenpl ;
+N+Pl+Ess:h%>y%>i2%>ny PxxK ;


LEXICON n_33is !!= @CODE@ rakas
+N: x_33is ;

LEXICON x_33is !!= @CODE@ ruvis
+N:i%^WG sConsCaselyhyempi ; ! nom, par 
+N:hii weak_v_stem_sg ;
+N:hii weak_v_stem_pl;
+N+Sg:hii sgpl_cases_even ;
+N+Pl:hi%>i2 sgpl_cases_even ;
+N+Pl:hi%>i2 longparpl ;
+N+Pl:hi%>i2 tt_longgenpl ;
+N+Pl+Ess:hi%>i2%>n%^A PxxK ;
+N+Sg+Ill:%>hiisseen K ;
+N+Pl+Ill:hi%>i2ssiin K ;
+N+Pl+Com:hi%>i2ne K ;

LEXICON n_33  !!= @CODE@ odottaessa viitataan n_33t-hen
n_33t ;

LEXICON n_33t !!= @CODE@ venet
+N: x_33t ;

LEXICON x_33t
:e tConsCase ; ! nom, par
:hee 33_t_ts_common ;
+Sg+Ill:hee mTAVU_VV-ill ;
+Pl+Com:hee%>i2ne K ;

LEXICON n_33ts !!= @CODE@ kirves
+N: x_33ts ;

LEXICON x_33ts !!= @CODE@ kirves
+Sg+Nom:es K ;
:es long_par_sg ; 
:hee 33_t_ts_common ;
+Sg+Ill:hee mTAVU_VV-ill ;
+Pl+Gen:hee%>i2 tt_longgenpl ;
+Pl+Com:hee%>i2ne K ;

LEXICON 33_t_ts_common
 obl_strong_even ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_even ;      !! these two lexica for cases with
 +Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl -- tsekkaa long
 mTAVU_VV-ill ;
 +Pl:%>i2 tt_longgenpl ;
 +Pl:%>i2 longparpl ;
!+Pl+Ill:heissiin K ;
 +Pl:%>i2ssii longillpl ;


LEXICON n_41 !!= * @CODE@ = veres
+N: x_41 ;

LEXICON x_41
 sConsCase ; ! toistaiseksi :-/

:kse obl_strong_odd ; ! muut vahvat
:kse weak_v_stem ;
 +Sg:kse sgpl_cases_odd ;     ! these two lexica for cases with
 +Pl:kse%>i2 sgpl_cases_odd ; ! the same affix in sg and pl -- tsekkaa long

+Pl+Gen:%>sten K ;
+Pl+Par:%>ksii K ;
+Sg+Ill:ksheen K ;
+Pl+Ill:ksh%>i2in K ;
+Pl+Com:ks%>i2ne K ;

LEXICON s_kse
+N:%^WG # ;

LEXICON n_41pl !!= * @CODE@ = veljekset
+N+Pl+Nom:%>kset K ;
+N:kse loput2 ;
+N+Pl+Gen:%>sten K ;
+N+Pl+Par:%>ksii K ;
+N+Pl+Ess:ks%>i2%>n%^A PxxK ;
+N+Pl+Ill:ksh%>i2in K ;
+N+Pl+Com:ks%>i2ne K ;



!LEXICON et_he
!+N:%^WG # ;

!LEXICON n_42 ! itse asiassa n_42even
!+N+Sg:se sgpl_cases_even ;      !! these two lexica for cases with
!+N+Pl:s%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
!+N: nen ;
!+N+Pl+Com:s%>i2ne K ;

LEXICON n_42
+N: x_42 ;

LEXICON x_42 
+Sg+Nom: nen K ; 
:se weak_v_stem ;
:s long_par_sg ;
:se 2TAVU_V_ill_sg ;
+Sg:se sgpl_cases_even ;      !! these two lexica for cases with
+Pl:s%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+Pl:s consgenpl ;
+Pl:ss%>i2i shortestparpl ;
+Pl:sh%>i2 shorterillpl ;
+Pl+Com:s%>i2ne K;


LEXICON n_42odd !!= @CODE@ ihminen
+N: x_42odd ;

LEXICON x_42odd 
+Sg+Nom: nen K;
!:s R ;
:se ess_odd ;
:se weak_v_stem ;
:s long_par_sg ;
:se 2TAVU_V_ill_sg ;
+Sg:se sgpl_cases_odd ;      !! these two lexica for cases with
+Pl:s%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl
+Pl:s consgenpl ;
+Pl:ss%>i2i shortestparpl ;
+Pl:sh%>i2 shorterillpl ;
+Pl+Com:s%>i2ne K;

LEXICON n_42pl ! !!= @CODE@ tervheiset tervhei
 +N+Pl+Nom:%>set K ;
 +N+Pl+Ess:s%>i2%>n%^A PxxK ;
 +N+Pl:s%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl
 +N+Pl:s consgenpl ;
 +N+Pl:s%>i2 shortparpl ;
 +N+Pl+Ill:sh%>i2in K ;
 +N+Pl+Com:s%>i2ne K ;

LEXICON nen  !!= @CODE@ on nainen-sanoille
 nen_sg ;
 nen_pl ;

LEXICON nen_sg 
  nom-nen ;
 :s R ;
 :se nomgen_px ;
 :se ess_even ;
 !make+Pl+kom:sine K ;
 :se weak_v_stem_sg ;
 :s long_par_sg ;
 :se 2TAVU_V_ill_sg ;

LEXICON nen_pl
 :se weak_v_stem_pl ;
 +Pl+Ill:sh%>i2in K ;
 +Pl:s consgenpl ;
 +Pl:ss%>i2i shortestparpl ;
 +Pl:sh%>i2 shorterillpl ;
 +Pl+Com:s%>i2ne K ;


LEXICON nom-nen
+Sg+Nom:nen K ;


LEXICON loput
 obl_strong_odd ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_odd ;      !! these two lexica for cases with
 +Pl:%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl -- tsekkaa long
 +Pl:%>i2 consgenpl ;
 +Pl:%>i2 tt_longgenpl ;
 +Pl:%>i2 shortparpl ;

LEXICON loput2
 obl_strong_odd ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_odd ;      !! these two lexica for cases with
 +Pl:%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl -- tsekkaa long
 +Pl:%>i2 shortparpl ;
 
LEXICON sConsCase
 +Sg+Nom:%>s K ;
 +Sg+Par:%>st%^A K ; ! Par
 +Pl+Gen:%>sten K ; ! Gen

LEXICON sConsCaselyhyempi
 +Sg+Nom:%>s K ;
 +Sg+Par:%>st%^A K ; ! Par
 

LEXICON tConsCase
 +Sg+Nom:%^WG%>t K ;
 +Sg+Par:%^WG%>tt%^A K ;



!! !!! Basic paradigms
!  ===================

!! !!Basic vowel stems
!  --------------------

LEXICON nom_strong
  +Sg+Nom: K ; 
  +Sg+Nom: R ; 

LEXICON strong_v_stem_even  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 short_par ;
 obl_strong_even ;

LEXICON strong_v_stem_odd  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 short_par ;
 obl_strong_odd ;


LEXICON weak_v_stem !!= @CODE@ 
 weak_v_stem_sg ;
 weak_v_stem_pl ;

LEXICON weak_v_stem_sg
 +Sg+Gen:%>n K ;
 +Sg+Gen:%>n R ;

LEXICON weak_v_stem_pl
 +Pl+Nom:%>t K ;
! +Pl+Ins:%>i2%>n K ;   !! Marginal case, in use only for some words.
                        !! Todo: identify these, and remove Ins from the
                        !! general weak_v_stem lexicon.

!! !Sublexica for the vowel stems
!  - - - - - - - - - - - - - - - 

LEXICON obl_strong_even 
 nomgen_px ;
 +Pl:%>i2 com ;
 ess_even ;
 
LEXICON obl_strong_odd 
 nomgen_px ;
 +Pl:%>i2 com ;
 ess_odd ;
 
LEXICON nomgen_px
 +Sg+Nom: Px ;   ! mattoni
 +Sg+Gen: Px ;   ! mattoni
 +Pl+Nom: Px ;   ! mattoni

LEXICON ess_even
ess_even_sg ;
ess_even_pl ;

LEXICON ess_even_sg 
 +Sg+Ess:%>n%^A PxxK ;

LEXICON ess_even_pl 
 +Pl+Ess:%>i2%>n%^A PxxK ;
 
LEXICON ess_odd
 ess_odd_sg ;
 ess_odd_pl ;

LEXICON ess_odd_sg
 +Sg+Ess:%>nn%^A PxxK ;      ! nn or n ? Eira s. 38 vs yaml

LEXICON ess_odd_pl
 +Pl+Ess:%>i2%>nn%^A PxxK ;  ! nn
 
LEXICON sgpl_cases_even ! kaikille paitsi neljän koplalle
 +Ade:%>l%^A PxxK ; !! * Lyhyt perusmuoto vuođela
 +All:%>le PxxK ; !! * Lyhyt perusmuoto
 sgpl_cases_common ;

LEXICON sgpl_cases_odd ! neljän koplalle
 +Ade:%>ll%^A PxxK ; !! * Pitkä perusmuoto
 +All:%>lle PxxK ; !! * Pitkä perusmuoto
 sgpl_cases_common ;

LEXICON sgpl_cases_common
 +Abl:%>lt%^A PxxK ;
 +Ine:%>ss%^A PxxK ;
 +Ela:%>st%^A PxxK ;
 +Tra:%>ksi K ;
 +Tra:%>kse Pxx ;
 +Abe:%>tt%^A PxxK ;
 
LEXICON ess
 +Ess:%>n%^A PxxK ; ! Essiivillä aina Eiran gramatiikin mukaan lyhytmuoto
                    ! Mutta Eira s. 38 kaupunkinna, tyttärennä

LEXICON com 
!! Komitatiivilla on vain -ine ja menee Pxx:iin
 +Com:%>ne Pxx ; ! Not to K, since Px must be present
                 ! Note that adjectives have -ine without Px


!! !Lexica for the non-uniform cases
! --------------------------------


LEXICON short_par
+Sg+Par:%>%^V PxxK ;
 

LEXICON long_par
 long_par_sg ;
 long_par_pl ;

LEXICON long_par_sg
 +Sg+Par:%>t%^A PxxK ;

LEXICON long_par_pl
  +Pl+Par:%>i2%>t%^A PxxK ;

LEXICON 1TAVU_VV-ill  !short maahan
 +Sg+Ill:%>h%^V n_PxK ;
 
LEXICON 2TAVU_V_ill_sg !long
+Sg+Ill:%>%^HMETAh%^V%^V n_PxK ;

! sykshyyn, linnhaan
!! linna pl.ill. kaksi muotoa po. linnhoin

LEXICON 2TAVU_edel-ill !suomhiin
+Sg+Ill:%>%^HMETAh%^V%^V n_PxK ;
+Pl+Ill:%^EDEL%>hii n_PxK ;


LEXICON mTAVU_VV-ill !longer kuninkhaa/sseen
!+Sg+Ill:%>ss%^V%^V n_PxK ;
+Sg+Ill:%>ssee n_PxK ; !koska tämä pääte aina sseen


LEXICON oldgenpl
 +Gen:%> n_PxK ;

LEXICON shortestgenpl !! * @CODE@ zero, because ii > i
 +Gen: n_PxK ;

LEXICON shortgenpl
 +Gen:%>%^V n_PxK ;

LEXICON longgenpl
 d_longgenpl ; ! oletaan, että -tten vaan
 tt_longgenpl ; 

LEXICON d_longgenpl
 +Gen:%>de n_PxK ;

LEXICON tt_longgenpl
 +Gen:%>tte n_PxK ;

LEXICON consgenpl
 +Gen:%>te n_PxK ;

LEXICON shortestparpl !! * @CODE@ zero, because ii > i
 +Par: PxxK ;

LEXICON shortparpl
 +Par:%>%^V PxxK ;

LEXICON longparpl
 +Par:%>t%^A PxxK ;

LEXICON shortestillpl
 +Ill:%> n_PxK ;

LEXICON shortillpl
 +Ill:%>hi n_PxK ;

LEXICON h_shortillpl
 +Ill:%^EDEL%>hii n_PxK ;


LEXICON shorterillpl
 +Ill:%>i2 n_PxK ;		

LEXICON longillpl
+Ill: n_PxK ;  ! En tiedä, toimiiko tämä

LEXICON longerillpl
  +Pl+Ill:%>s%^V%^V n_PxK ;



 

!! !Sublexica for possessive suffixes
! ---------------------------------

LEXICON n_PxK  !!= @CODE@ has either -n or goes to Px
 :n K ;
 Px ;

LEXICON i_PxK  !!= @CODE@ Tra: -i or -e and goes to Px
 :i K ;
 :e Px ;

LEXICON PxK !!= @CODE@ has only -nsA, and is currently not in use. Check
 K ;
 Px ;

LEXICON PxxK  !!= @CODE@ has also -Vn, thus both .. llensa and ..lleen.
 K ;
 Px ;
 Px-Vn ;

LEXICON Pxx
 Px ;
 Px-Vn ;

LEXICON Px
 +PxSg1:%>ni K ;
 +PxSg2:%>sti K ;
 +PxSg3:%>ns%^A K ;
 +PxPl1:%>mm^A K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns%^A K ;


LEXICON Px-Vn
 +PxSg3:%>%^Vn K ;
 +PxPl3:%>%^Vn K ;





