! ======================
!! !!!Sublexica for NounRoot
! ======================

!! Aloitin sovittaa analyysin Eiran kieliopin mukaan (Porsanki).
!! Leksikot ovat nyt (tai pitää olla) n11, n12, jne., eli siis
!! Eiran nominit tyyppi 1.1., 1.2., jne.

!!  2007 s.87
!! * 1. Yksitavuiset / Vi ( maa, tuorestai)                 = n_1
!! * 1.1. Yksitavuiset monikko/ Vi ( häät)                 = n_1pl
!! * 2. Vokaalirankaiset
!! ** 2.1 Kaksitavuiset ( pappi)                             = n_21
!! *** 2.1.1. Kaksitavuiset monikko (housut)     = n_21pl
!! *** 2.1.2. Kaksitavuiset (joki: joke)         = n_21ie
!! ** 2.2. Usseempitav. (opettaaja)                               = n_22
!! *** 2.2.1. Usseempitav. odd (kajava)                  = n_22odd
!! *** 2.2.2. Usseempitav. odd (meininki, tunturi)        = n_22oddi
!! *** 2.2.3. Usseempitav. monikko oi (kulkkiija)        = n_22oi
!! ** 2.3 Kokkoontumanom. ( valkkee)                     = n_23
!! ** 2.3.1 Kokkoontumanom. (assii)             = n_23ii
!! * 3. Konsonanttirankaiset
!! ** 3.1 Kaksitavuiset ( pieni, kieli)                         = n_31
!! ** 3.1.1. Kaksitavuiset lumi)                 = n_31m
!! ** 3.1.2. (käsi, vuosi)                                                 = n_31si
!! ** 3.1.3. (lapsi)                                                      = n_31lapsi
!! ** 3.1.4. (mies)                                                         = n_31mies
!! ** 3.2 Usseempitavuiset
!! ** 3.2.1 (tytär)                              = n_32
!! ** 3.2.2. (elläin)                            = n_32m
!! ** 3.2.3 (rakkhaus)                                                         = n_32s
!! ** 3.3 Kokkoontumanom. ( opas, huonet)
!! ** 3.3.1 (opas)                               = n_33s
!! ** 3.3.2 (asukas)                             = n_33sodd
!! ** 3.3.3. (ruvis)                           = n_33is
!! ** 3.3.4 (huonet)                            = n_33t
!! ** 3.3.5 (kirves)                                                         = n_33ts
!! * 4. Ekstrakonsonanttirankaiset
!! ** 4.1 veres-nominit ( veres, kokkous)                 = n_41
!! ** 4.1.1. veres-nominit monikko ( veljekset)                = n_41pl
!! ** 4.2 –inen-nominit ( kainulainen)                         = n_42 = nen
!! *** 4.2.1 -inen-nominit Pl (tervheiset)                 = n_42pl = nen
!! *** 4.2.2 -inen-nominit odd (vihrinen)                 = n_42odd = nen

!! !!Noun types

!! !!!INCOMING

LEXICON n_x
+N: x_x ;

LEXICON x_x
+Sg+Nom: K ; ! Just to make it compile



LEXICON n_1 !!= * @CODE@ = maa
+N: x_1 ;
+N+Sg+Nom: R ;

LEXICON x_1
 +Sg+Nom: K ;
 long_par ;
 weak_v_stem ;
+Sg: sgpl_cases_even ;      !! these two lexica for cases with
+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+Pl+Com:%>i2%>ne K ;
  1TAVU_VV-ill ;
+Pl:%>i2 tt_longgenpl ;
+Pl:%>i2 longparpl ;
+Pl:%>i2 shortillpl ;
! +Sg+Nom: Px ;   ! mattoni
! +Sg+Gen: Px ;   ! mattoni
! +Pl+Nom: Px ;   ! mattoni
ess_even ;

LEXICON n_1pl !!= * @CODE@ = häät

 +N: long_par ;
 +N: weak_v_stem ;
+N+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl+Com:%>i2%>ne K ;

 +N: 1TAVU_VV-ill ;
 +N+Pl:%>i2 tt_longgenpl ;
 +N+Pl:%>i2 longparpl ;
 +N+Pl:%>i2 shortillpl ;
 +N+Pl+Ess:%>i2%>n^A PxxK ;

LEXICON n_21C !!= * @CODE@
+N+Sg+Nom:  K    ; ! Hans
         :i n_21 ; ! Hansi
+N+Sg+Nom:i  R    ;

LEXICON n_21        !!= * @CODE@  "poika:poi6ka", lyhyt vokali, yks vartalo
+N: x_21 ;
+N+Sg+Nom: R ;

LEXICON x_21
nom_strong ;
x_21_oblique ;

LEXICON x_21_oblique
        : short_par ;
        : ess_even_sg ;
        :^AO ess_even_pl ;
        :^WG weak_v_stem ;
     +Sg:^WG     sgpl_cases_even ;     !to sgpl_cases_even for cases with
     +Pl:^AO^WG%>i2 sgpl_cases_even ; !the same affix in sg and pl
                    2TAVU_V_ill_sg ;
     +Pl+Use/NG:^AO%>i2 shortestgenpl ;  ! shortestgenpl since we have the i
     +Pl:^AO^WG%>i2 tt_longgenpl ;  ! longgenpl, eira 5322, korpus.
     +Pl:^AO%>i2 shortestparpl  ;
+Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK ;
+Pl+Com:^AO%>i2%>ne K ;


LEXICON n_21pl        !!= * @CODE@  "villahousut", lyhyt vokali, yks vartalo
 +N+Pl+Nom:%>t K ;
 +N+Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
 +N+Pl+Ess:%>i2%>n^A PxxK ;
 +N+Pl:%>i2 tt_longgenpl ;
 +N+Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK ;
 +N+Pl:%>i2 shortestparpl ;
 +N+Pl+Com:%>i2%>ne K ;


 LEXICON kk-longgenpl
 +Gen:%>kk%>i2 # ; ! kk ?? Tsekkaa..

LEXICON kk-longgparpl
 +Par:%>kk%>i2 # ;! kk ?? Tsekkaa..

LEXICON n_21ie        !!= @CODE@ on ove, ovi, nom joki:joke
+N: x_21ie ;
+N+Sg+Nom:^E2I R ; ! ^EI e to i

LEXICON x_21ie
+Sg+Nom:^E2I K ; ! ^EI e to i
 short_par ;
 obl_strong_even ;
:^WG weak_v_stem ;
+Sg:^WG sgpl_cases_even ;      !! these two lexica for cases with
+Pl:^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+Pl:%>i2 shortgenpl ;
        2TAVU_edel-ill ;
+Pl:%>i2 shortparpl ;
+Pl+Com:%>i2%>ne K ;


LEXICON n_22        !!= * @CODE@  opettaaja
+N: x_22 ;
+N+Sg+Nom: R ;

LEXICON x_22
 nom_strong ;
 strong_v_stem_even ;
:^WG weak_v_stem ;
+Sg:^WG       sgpl_cases_even ;  ! the same affix in sg and pl
+Pl:^WG%>i2 sgpl_cases_even ;  ! Adding  to block i:0
 2TAVU_V_ill_sg ;
+Pl+Ess:%>i2%>n^A PxxK ;
+Pl:^WG%>i2 tt_longgenpl ;
+Pl:%>i2    longparpl ;
+Pl+Ill:^HMETA%>hii n_PxK ;
+Pl+Com:%>i2%>ne K ;
!+Sg+Ess:%>n^A PxxK ; Lene removed this one
!+Pl+Tra:%>i2ksi K ; Lene removed this one
!!+Pl+Tra:%>i2kse PxxK ;

LEXICON n_22odd !!= * @CODE@  kajava
+N: x_22odd ;

LEXICON x_22odd !!= * @CODE@  kajava
                 nom_strong ;
                 short_par ;
       :^WG     weak_v_stem ;
    +Sg:^WG     sgpl_cases_odd ;
    +Pl:^WG%>i2 sgpl_cases_odd ; ! Adding  to block i:0
                 2TAVU_V_ill_sg ;
                 ess_odd    ;

                 !ess_odd_sg ;
                 !ess_odd_pl ;
    !+Pl+Tra:%>i2%>ksi   PxxK ;



    +Pl:^WG%>i2i tt_longgenpl ; ! kajaviitten
      :%>i        long_par_pl ;
+Pl+Ill:%^HMETA%>hii n_PxK ;
+Pl+Com:%>i2%>ne   K;




LEXICON n_22oddi !!= * @CODE@  meininki
+N: x_22oddi ;
+N+Sg+Nom: R ;
+N+Sg+Gen:^WGn R ;

LEXICON x_22oddi !!= * @CODE@  meininki
                 nom_strong ;
                 short_par ;
!                 nomgen_px ;
       :^WG     weak_v_stem ;
    +Sg:^WG     sgpl_cases_odd ;
    +Pl:^WG%>i2 sgpl_cases_even ; ! Adding  to block i:0
                 2TAVU_V_ill_sg ;
                 ess_odd_sg ;
+Pl:i ess ;
    +Pl:^WG%>i2 tt_longgenpl ;
    +Pl:%>i2     longparpl ;
+Pl+Ill:^HMETA%>hii n_PxK ;
+Pl+Com:ine   K;

!  Tarkista, onko yaml oikein:
!  [24/25][FAIL] meininki+N+Pl+Ess => Missing results: meininkiinä
!  [24/25][FAIL] meininki+N+Pl+Ess => Unexpected results: meininkkiinä
!  [25/25][FAIL] meininki+N+Pl+Com => Missing results: meininkiine
!  [25/25][FAIL] meininki+N+Pl+Com => Unexpected results: meininkkiine



LEXICON n_22oi        !!= * @CODE@
+N: x_22oi ;

LEXICON x_22oi        !!= * @CODE@  Kulkkiija
                     nom_strong ; ! sg nom
                     short_par ;
        :^WG         weak_v_stem ;
    +Sg:^WG         sgpl_cases_odd ;  ! these two lexica for cases with
    +Pl:^AO^WG%>i2 sgpl_cases_even ; ! the same affix in sg and pl
                     2TAVU_V_ill_sg ;
    +Pl:^AO^WG%>i2 tt_longgenpl ;
    +Pl:^AO^WG%>i2 longparpl ;
                     ess_odd_sg ;    ! paijukko/nna
       :^AO         ess_even_pl ; ! paijuko/i/na
+Pl+Com:^AO%>i2%>ne   K;
+Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK ;


LEXICON n_23        !!= * @CODE@  valkkee
+N: x_23 ;
+N+Sg+Nom: R ;
+N+Sg+Gen:en R ;

LEXICON x_23
+Sg+Nom:e          K ;
+Sg+Nom+Err/Orth:%>%^A          K ;
    +Sg:e          sgpl_cases_even ;
+Sg+Gen:en         K ;
+Pl+Nom:et         K ;
+Sg+Ill:esseen     K ;
+Pl+Ill:e%>i2ssiin K ;
       :e          long_par_sg ;
    +Pl:e%>i2      longparpl ;
    +Pl:e%>i2      tt_longgenpl ;
    +Pl:e%>i2      sgpl_cases_even ;
       :e          ess_even ;
+Pl+Com:e%>i2%>ne K ;

LEXICON n_23ii        !!= * @CODE@  assii
+N: x_23ii ;
+N+Sg+Nom: R ;

LEXICON x_23ii
+Sg+Nom: K ;
 long_par_sg ;
+Sg+Ill:sseen  K ;
+Pl+Ill:ssiin K ;
+Pl: longparpl ;
+Pl: tt_longgenpl ;
 weak_v_stem ;
+Sg:^WG sgpl_cases_even ;   !! * @CODE@  for same affix in sg and pl
+Pl:^WG sgpl_cases_even ;  !! * @CODE@  for same affix in sg and pl
 ess_even_ii ;
+Pl+Com:%>ne K ;


LEXICON n_31        !!= @CODE@  kieli
+N: x_31 ;
+N+Sg+Nom:^E2I R ; ! ^E2I e to i

LEXICON x_31
+Sg+Nom:^E2I K ; ! ^E2I e to i
:^EDEL long_par_sg ;
 obl_strong_even ;
:^WG weak_v_stem ;
+Sg:^WG sgpl_cases_even ;    !! * @CODE@  for same affix in sg and pl
+Pl:%>i2 sgpl_cases_even ;  !! * @CODE@  for same affix in sg and pl
 2TAVU_V_ill_sg ;
+Pl:^HMETA%>hii shortestillpl ;
+Pl:%>i2 shortparpl         ;
+Pl:%>i2 shortgenpl         ;
!!+Pl:^EDEL consgenpl ;
+Pl+Com:%>i2%>ne K ;

LEXICON n_31m        !!= @CODE@ lumi lu
+N: x_31m ;
+N+Sg+Nom:mi R ; ! ^EI e to i

LEXICON x_31m
+Sg+Nom:mi K ; ! ^EI e to i
:n long_par_sg ;
:me obl_strong_even ;
:me^WG weak_v_stem ;
+Sg:me^WG sgpl_cases_even ;     ! these two lexica for cases with
+Pl:m^WG%>i2 sgpl_cases_even ;  ! the same affix in sg and pl
+Pl:me%>i2        shortgenpl ;
+Sg+Ill:m^HMETA%>hee n_PxK ;
+Pl+Ill:m^EDEL%>hii n_PxK ;
+Pl:me%>i2 shortparpl ;
+Pl+Com:m%>i2%>ne K ;

LEXICON n_31si        !!= @CODE@ vuosi, käsi
+N: x_31si ;
+N+Sg+Nom:         R ;

LEXICON x_31si
+Sg+Nom:^E2I         K ;
!                      nomgen_px ;   ! sg nom/gen px
       :^EDEL        long_par_sg ; ! parsg
       :^WG          weak_v_stem ; ! gensg, nompl
                      2TAVU_V_ill_sg ;
    +Sg:^WG          sgpl_cases_even ; ! these two lexica for cases with
    +Pl:%>i2     sgpl_cases_even ; ! the same affix in sg and pl
                      ess_even_sg ;
       :         ess_even_pl ;
    +Pl:%>i2     shortparpl ; ! ^E2I to get t:s
    +Pl:%>i2     shortgenpl ; ! vuossiin, *vuottiin
    +Pl:^HMETA%>hii shortestillpl ;
+Pl+Com:%>i2%>ne        K ;

LEXICON n_31lapsi        !!= @CODE@ lapsi la
+N+Sg+Nom:psi K ; ! ^EI e to i
+N+Sg+Nom:psi R ; ! ^EI e to i
+N+Sg+Gen:psen R ;
+N+Pl+Gen:sten R ;
+N:s long_par_sg ;
+N:pse obl_strong_even ;
+N:pse^WG weak_v_stem ;
+N+Sg:pse^WG sgpl_cases_even ;      !! these two lexica for cases with
+N+Pl+Gen:sten K ;
+N+Pl:ps^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl:ps%>i2%>i        shortestgenpl ;
+N+Pl+Use/NG:ps%>i2 tt_longgenpl         ;
+N+Pl:ps%>i2%>i shortestparpl ;
+N+Sg+Ill:ps^HMETA%>hee n_PxK ;
+N+Pl+Ill:ps^EDEL%>hii n_PxK ;
+N+Pl+Com:ps%>i2%>ne K ;

LEXICON n_31mies        !!= @CODE@ mies mie
+N+Sg+Nom:s K ; ! ^EI e to i
+N+Sg+Nom:s R ; ! ^EI e to i
+N+Sg+Gen:hen K ;
+N:s long_par_sg ;
+N:he obl_strong_even ;
+N+Sg:he^WG weak_v_stem ;
+N+Pl+Nom:het K ;
+N+Sg:he^WG sgpl_cases_even ;      !! these two lexica for cases with
+N+Pl:h^WG%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
+N+Pl:h%>i2%>i        shortestgenpl ;
+N+Pl:h%>i2%>i shortestparpl ;
+N+Sg+Ill:h^HMETA%>hee n_PxK ;
+N+Pl+Ill:hhii n_PxK ;
+N+Pl+Com:h%>i2%>ne K ;


LEXICON n_32 !!= @CODE@ tytär tyttär
+N: x_32 ;
+N+Sg+Nom:^WG R ;

LEXICON x_32
+Sg+Nom:^WG K ;
:e weak_v_stem ;
+Sg+Par:^WG%>t^A PxxK ;
:e obl_strong_odd ;
+Sg+Gen:e%>n K ;
:e 2TAVU_V_ill_sg ;
:e obl_strong_odd ;
:e 2TAVU_V_ill_sg ;
+Sg:e sgpl_cases_odd ;
+Pl:%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+Pl:%>i%>i2 tt_longgenpl ;
+Pl:%>i%>i2 longparpl ;
+Pl:^HMETAh%>i2i shortestillpl;
+Pl+Com:%>i2%>ne K ;

LEXICON n_32m !!= @CODE@ elläin elläi
+N: x_32m ;
+N+Sg+Nom:^WGn R ;

LEXICON n_32m_pl !!= @CODE@ elläin elläi
+N: x_32m_pl ;

LEXICON x_32m !!= @CODE@
x_32m_sg ;
x_32m_pl ;

LEXICON x_32m_sg
+Sg+Nom:^WGn K ;
:me weak_v_stem_sg ;
+Sg+Par:^WG%>nt^A PxxK ;
:me obl_strong_odd ;
+Sg+Gen:me%>n K ;
:me weak_v_stem_pl ;  ! Nom pl
:me 2TAVU_V_ill_sg ;
:me 2TAVU_V_ill_sg ;
+Sg:me sgpl_cases_odd ;

LEXICON x_32m_pl
:me weak_v_stem_pl ;
:me weak_v_stem ;
+Pl:m%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+Pl:mi%>i2 longparpl ;
+Pl:m%>ii2 tt_longgenpl ;
+Pl:m^HMETAh%>i2i shortestillpl;
+Pl+Com:m%>i2%>ne K ;

LEXICON n_32s !!= @CODE@  sairhaus
+N+Sg+Nom:^WGs K ;  !! * Konsonanttivartalo: poista -e ja heikko aste
+N+Sg+Nom:^WGs R ;  !! * Konsonanttivartalo: poista -e ja heikko aste
+N+Sg+Par:%>tt^A PxxK ;
+N:đe weak_v_stem ;
+N:te 2TAVU_V_ill_sg ;
+N+Sg:đe sgpl_cases_common ;
+N+Sg+Ade:đe%>ll^A PxxK ; !! * Pitkä perusmuoto
+N+Sg+All:đe%>lle PxxK ; !! * Pitkä perusmuoto
+N:te ess_odd_sg ;
+N:ks ess_odd_pl ;
+N+Pl:ks%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+N+Pl:ksi%>i2 longparpl ;
+N+Pl:ksi%>i2 tt_longgenpl ;
+N+Pl:ks^HMETAh%>i2i shortestillpl;
+N+Pl+Com:ks%>i2%>ne K ;


! LEXICON n_32s != @CODE@  sairhaus vanha versio
! +N:t long_par ;
! +N:te obl_strong ;
! +N:te^WG weak_v_stem ;
! +N+Sg:^WG sgpl_cases_odd ;
! +N+Pl:^WG%>i2 sgpl_cases_odd ;
! +N+Pl:ks%>i2 shortparpl ;
! +N+Pl:ks%>i2 shortgenpl ;
! +N+Pl:ks%>i2 shorterillpl ;

!LEXICON n_32t
!n_32 ; ! ei tehty

!LEXICON n_33 ! ei tehty
!n_33s ;

LEXICON n_33as !!= @CODE@ rakas
+N: x_33as ;

LEXICON x_33as
       :a^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
       :haa mTAVU_VV-ill ;
+Pl+Ill:h%>aissiin K ;
+Pl+Com:h%>a%>ine K ;
       :h%>aa weak_v_stem_sg ;
       :h%>aa weak_v_stem_pl;
    +Sg:h%>aa sgpl_cases_even ;
    +Pl:h%>a%>i2 sgpl_cases_even ;
       :h%>aa ess_even ;
    +Pl:h%>a%>i2 longparpl ;
    +Pl:h%>a%>i2 tt_longgenpl ;
!+N+Pl+Ess:h%>a%>i2%>na PxxK ;

!LEXICON n_33aes
!+N: x_33aes ;

LEXICON x_33aes
:ä^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
:hää mTAVU_VV-ill ;
+Pl+Ill:h%>äissiin K ;
+Pl+Com:h%>ä%>ine K ;
:h%>ää weak_v_stem_sg ;
:h%>ää weak_v_stem_pl ;
:h%>ää ess_even ;
+Sg:h%>ää sgpl_cases_even ;
+Pl:h%>ä%>i2 sgpl_cases_even ;
+Pl:h%>ä%>i2 longparpl ;
+Pl:h%>ä%>i2 tt_longgenpl ;
+N+Pl+Ess:h%>ä%>i2%>nä PxxK ;



LEXICON n_33ys !!= @CODE@ rakas
+N: x_33ys ;
+N+Sg+Nom:%>y^WGs R ;

LEXICON x_33ys
:%>y^WG sConsCaselyhyempi ; ! nom, par hammas (WG)
:h%>yy mTAVU_VV-ill ;
+Pl+Ill:h%>yissiin K ;
+Pl+Com:h%>y%>ine K ;
:h%>yy weak_v_stem_sg ;
:h%>yy weak_v_stem_pl;
+Sg:h%>yy sgpl_cases_even ;
+Pl:h%>y%>i2 sgpl_cases_even ;
+Pl:h%>y%>i2 longparpl ;
+Pl:h%>y%>i2 tt_longgenpl ;
+N+Pl+Ess:h%>y%>i2%>ny PxxK ;


LEXICON n_33is !!= @CODE@ ruvis, ruumis
+N: x_33is ;

LEXICON x_33is !!= @CODE@ ruvis
       :i^WG sConsCaselyhyempi ; ! nom, par
       :hii weak_v_stem_sg ;
       :hii weak_v_stem_pl;
    +Pl:hii longparpl ;
    +Pl:hii tt_longgenpl ;
    +Sg:hii sgpl_cases_even ;
    +Pl:hii sgpl_cases_even ;
       :hii ess_even_sg ;
+Pl+Ess:%>hiin^A PxxK ; ! i in pl lex
+Sg+Ill:%>hiisseen K ;
+Pl+Ill:hiissiin K ;
+Pl+Com:hiine K ;

LEXICON n_33  !!= @CODE@ odottaessa viitataan n_33t-hen
n_33t ;

LEXICON n_33t !!= @CODE@ venet
+N: x_33t ;
+N+Sg+Nom:et R ;

LEXICON x_33t
:e tConsCase ; ! nom, par
:hee 33_t_ts_common ;
+Sg+Ill:hee mTAVU_VV-ill ;
+Pl+Com:hee%>i2%>ne K ;

LEXICON n_33ts !!= @CODE@ kirves
+N: x_33ts ;
+N+Sg+Nom:es R ;

LEXICON x_33ts !!= @CODE@ kirves
+Sg+Nom:es K ;
:es long_par_sg ;
:hee 33_t_ts_common ;
+Sg+Ill:hee mTAVU_VV-ill ;
+Pl+Gen:hee%>i2 tt_longgenpl ;
+Pl+Com:hee%>i2%>ne K ;

LEXICON 33_t_ts_common
 obl_strong_even ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_even ;      !! these two lexica for cases with
 +Pl:%>i2 sgpl_cases_even ;  !! the same affix in sg and pl -- tsekkaa long
 mTAVU_VV-ill ;
 +Pl:%>i2 tt_longgenpl ;
 +Pl:%>i2 longparpl ;
!+Pl+Ill:heissiin K ;
 +Pl:%>i2ssii longillpl ;


LEXICON n_41 !!= * @CODE@ = veres
+N: x_41 ;
+N+Sg+Nom:s R ;
+N+Sg+Gen:ksen R ;

LEXICON x_41
 sConsCase ; ! toistaiseksi :-/

:kse obl_strong_odd ; ! muut vahvat
:kse weak_v_stem ;
 +Sg:kse sgpl_cases_odd ;     ! these two lexica for cases with
 +Pl:kse%>i2 sgpl_cases_odd ; ! the same affix in sg and pl -- tsekkaa long

+Pl+Gen:%>sten K ;
+Pl+Par:%>ksii K ;
+Sg+Ill:ksheen K ;
+Pl+Ill:ksh%>i2in K ;
+Pl+Com:ks%>i2%>ne K ;

LEXICON s_kse
+N:^WG # ;

LEXICON n_41pl !!= * @CODE@ = veljekset
+N+Pl+Nom:%>kset K ;
+N:kse obl_strong_odd ; ! muut vahvat
+N:kse weak_v_stem ;
+N+Sg:kse     sgpl_cases_odd ;  !! these two lexica for cases with
+N+Pl:kse%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl -- tsekkaa long
+N+Pl+Gen:%>sten K ;
+N+Pl+Par:%>ksii K ;
+N+Pl+Ill:ksh%>i2in K ;
+N+Pl+Com:ks%>i2%>ne K ;



!LEXICON et_he
!+N:^WG # ;

!LEXICON n_42 ! itse asiassa n_42even
!+N+Sg:se sgpl_cases_even ;      !! these two lexica for cases with
!+N+Pl:s%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
!+N: nen ;
!+N+Pl+Com:s%>i2%>ne K ;

LEXICON n_42
+N: x_42 ;
+N+Sg+Nom:s R ;


LEXICON x_42
+Sg+Nom: nen K ;
   :se weak_v_stem ;
   :s long_par_sg ;
   :se 2TAVU_V_ill_sg ;
+Sg:se sgpl_cases_even ;      !! these two lexica for cases with
+Pl:s%>i2 sgpl_cases_even ;  !! the same affix in sg and pl
   :se ess_even ;
+Pl:s consgenpl ;
+Pl:ss%>i2i shortestparpl ;
+Pl:sh%>i2 shorterillpl ;
+Pl+Com:s%>i2%>ne K;


LEXICON n_42odd !!= @CODE@ ihminen
+N: x_42odd ;
+N+Sg+Nom:s R ; ! ihmiskala

LEXICON x_42odd
+Sg+Nom: nen K;
:se ess_odd ;
:se weak_v_stem ;
:s long_par_sg ;
:se 2TAVU_V_ill_sg ;
+Sg:se sgpl_cases_odd ;      !! these two lexica for cases with
+Pl:s%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl
+Pl:s consgenpl ;
+Pl:ss%>i2i shortestparpl ;
+Pl:sh%>i2 shorterillpl ;
+Pl+Com:s%>i2%>ne K;

LEXICON n_42pl ! !!= @CODE@ tervheiset tervhei
 +N+Pl+Nom:%>set K ;
 +N+Pl:s%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl
 +N+Pl:s consgenpl ;
 +N+Pl:ss%>i2 shortparpl ; ! tervhehissii
 +N+Pl+Ill:sh%>i2in K ;
 +N:se ess_odd_pl ;
 +N+Pl+Com:s%>i2%>ne K ;

LEXICON nen  !!= @CODE@ on nainen-sanoille
  nen_sg ;
  nen_pl ;

LEXICON nen_sg
 +N: nom-nen ;
! +N:se nomgen_px ;
 +N:se ess_even ;
!make+N+Pl+kom:sine K ;
 +N:se weak_v_stem_sg ;
 +N:s long_par_sg ;
 +N:se 2TAVU_V_ill_sg ;

LEXICON nen_pl
 +N:se weak_v_stem_pl ;
 +N+Pl+Ill:sh%>i2in K ;
 +N+Pl:s consgenpl ;
 +N+Pl:ss%>i2i shortestparpl ;
 +N+Pl:sh%>i2 shorterillpl ;
 +N+Pl+Com:s%>i2%>ne K ;


LEXICON nom-nen
+Sg+Nom:nen K ;


LEXICON loput
 obl_strong_odd ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_odd ;      !! these two lexica for cases with
 +Pl:%>i2 sgpl_cases_odd ;  !! the same affix in sg and pl -- tsekkaa long
 +Pl:%>i2 consgenpl ;
 +Pl+Use/NG:%>i2 tt_longgenpl ;
 +Pl:%>i2 shortparpl ;


LEXICON sConsCase
 +Sg+Nom:^WG%>s K ;
 +Sg+Par:%>st^A K ; ! Par
 +Pl+Gen:%>sten K ; ! Gen

LEXICON sConsCaselyhyempi
 +Sg+Nom:%>s K ;
 +Sg+Par:%>st^A K ; ! Par


LEXICON tConsCase
 +Sg+Nom:^WG%>t K ;
 +Sg+Par:^WG%>tt^A K ;



!! !!! Basic paradigms
!  ===================

!! !!Basic vowel stems
!  --------------------

LEXICON nom_strong
  +Sg+Nom: K ;

LEXICON strong_v_stem_even  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 short_par ;
 obl_strong_even ;

LEXICON strong_v_stem_odd  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 short_par ;
 obl_strong_odd ;


LEXICON weak_v_stem !!= @CODE@
 weak_v_stem_sg ;
 weak_v_stem_pl ;

LEXICON weak_v_stem_sg
 +Sg+Gen:%>n K ;
 +Sg+Gen:%>n R ; ! NB follow this.

LEXICON weak_v_stem_pl
 +Pl+Nom:%>t K ;
! +Pl+Ins:%>i2%>n K ;   !! Marginal case, in use only for some words.
                        !! Todo: identify these, and remove Ins from the
                        !! general weak_v_stem lexicon.

!! !Sublexica for the vowel stems
!  - - - - - - - - - - - - - - -

LEXICON obl_strong_even
! nomgen_px ;
 +Pl:%>i2 com ;
 ess_even ;

LEXICON obl_strong_odd
! nomgen_px ;
 +Pl:%>i2 com ;
 ess_odd ;

! LEXICON nomgen_px
!  +Sg+Nom: Px ;   ! mattoni
!  +Sg+Gen: Px ;   ! mattoni
!  +Pl+Nom: Px ;   ! mattoni

LEXICON ess_even
ess_even_sg ;
ess_even_pl ;


LEXICON ess_even_sg
 +Sg+Ess:%>n^A PxxK ;

LEXICON ess_even_pl
 +Pl+Ess:%>i2%>n^A PxxK ;

LEXICON ess_even_ii
+Sg+Ess:%>n^A PxxK ;
+Pl+Ess:%>n^A PxxK ;


LEXICON ess_odd
 ess_odd_sg ;
 ess_odd_pl ;

LEXICON ess_odd_sg
 +Sg+Ess:%>nn^A PxxK ;      ! nn or n ? Eira s. 38 vs yaml

LEXICON ess_odd_pl
 +Pl+Ess:%>i2%>nn^A PxxK ;  !

LEXICON sgpl_cases_even ! kaikille paitsi neljän koplalle
 +Ade:%>l^A PxxK ; ! Lyhyt perusmuoto vuođela
 +All:%>le   PxxK ; ! Lyhyt perusmuoto
             sgpl_cases_common ;

LEXICON sgpl_cases_odd ! neljän koplalle
 +Ade:%>ll^A PxxK ; ! Pitkä perusmuoto
 +All:%>lle  PxxK ;   ! Pitkä perusmuoto
             sgpl_cases_common ;

LEXICON sgpl_cases_common
 +Abl:%>lt^A PxxK ;
 +Ine:%>ss^A PxxK ;
 +Ela:%>st^A PxxK ;
 +Tra:%>ksi   K ;
! +Tra:%>kse   Pxx ;
 +Abe:%>tt^A PxxK ;

LEXICON ess
 +Ess:%>n^A PxxK ; ! Essiivillä aina Eiran gramatiikin mukaan lyhytmuoto
                    ! Mutta Eira s. 38 kaupunkinna, tyttärennä

LEXICON com      ! Komitatiivilla on vain -ine ja menee Pxx:iin
 +Com:%>ne Pxx ; ! Not to K, since Px must be present
                 ! Note that adjectives have -ine without Px


!! !Lexica for the non-uniform cases
! --------------------------------


LEXICON short_par
+Sg+Par:%>^V PxxK ;
+Sg+Par+Err/Orth:%>^A PxxK ;


LEXICON long_par
 long_par_sg ;
 long_par_pl ;

LEXICON long_par_sg
 +Sg+Par:%>t^A PxxK ;

LEXICON long_par_pl
  +Pl+Par:%>i2%>t^A PxxK ;

LEXICON 1TAVU_VV-ill  !short maahan
 +Sg+Ill:%>h^V n_PxK ;

LEXICON 2TAVU_V_ill_sg !long
+Sg+Ill:^HMETA%>h^V^V n_PxK ;

! sykshyyn, linnhaan
! linna pl.ill. kaksi muotoa po. linnhoin

LEXICON 2TAVU_edel-ill !suomhiin
+Sg+Ill:^HMETA%>h^V^V n_PxK ;
+Pl+Ill:^EDEL%>hii      n_PxK ;


LEXICON mTAVU_VV-ill   ! longer kuninkhaa/sseen
+Sg+Ill:%>ssee n_PxK ; ! koska tämä pääte aina sseen


LEXICON oldgenpl
 +Gen:%> n_PxK ;

LEXICON shortestgenpl
 +Gen: n_PxK ;

LEXICON shortgenpl
 +Gen:%>^V n_PxK ;

LEXICON longgenpl
! d_longgenpl ;   oletetaan, että -tten vaan
 tt_longgenpl ;

LEXICON d_longgenpl
 +Gen:%>de n_PxK ;

LEXICON tt_longgenpl
 +Gen:%>tte n_PxK ;

LEXICON consgenpl
 +Gen:%>te n_PxK ;

LEXICON shortestparpl !! * @CODE@ zero, because ii > i
 +Par: PxxK ;

LEXICON shortparpl
 +Par:%>^V PxxK ;

LEXICON longparpl
 +Par:%>t^A PxxK ;

LEXICON shortestillpl
 +Ill:%> n_PxK ;

LEXICON shortillpl
 +Ill:%>hi n_PxK ;

LEXICON h_shortillpl
 +Ill:^EDEL%>hii n_PxK ;


LEXICON shorterillpl
 +Ill:%>i2 n_PxK ;

LEXICON longillpl
 +Ill: n_PxK ;  ! En tiedä, toimiiko tämä

LEXICON longerillpl
 +Pl+Ill:%>s^V^V n_PxK ;





!! !Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!= @CODE@ has either -n or goes to Px
 :n K ;
! Px ;

LEXICON i_PxK  !!= @CODE@ Tra: -i or -e and goes to Px
 :i K ;
! :e Px ;

LEXICON PxK !!= @CODE@ has only -nsA, and is currently not in use. Check
 K ;
! Px ;

LEXICON PxxK  !!= @CODE@ has also -Vn, thus both .. llensa and ..lleen.
 K ;
! Px ;
! Px-Vn ;

LEXICON Pxx
 Px ;
 Px-Vn ;

LEXICON Px
 +PxSg1:%>ni K ;
 +PxSg2:%>sti K ;
 +PxSg3:%>ns^A K ;
 +PxPl1:%>mm^A K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns^A K ;


LEXICON Px-Vn
 +PxSg3:%>^Vn K ;
 +PxPl3:%>^Vn K ;
