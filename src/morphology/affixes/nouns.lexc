! ======================
!! !!!Sublexica for NounRoot
! ======================

!! Aloitin sovittaa analyysin Eiran kieliopin mukaan (Porsanki).
!! Leksikot ovat nyt (tai pitää olla) n11, n12, jne., eli siis
!! Eiran nominit tyyppi 1.1., 1.2., jne.

!!  2007 s.87
!! * 1. Yksitavuiset / Vi ( maa, tuorestai)                 = n_1
!! * 1.1. Yksitavuiset monikko/ Vi ( häät)                 = n_1pl
!! * 2. Vokaalirankaiset
!! ** 2.1 Kaksitavuiset ( pappi)                             = n_21
!! *** 2.1.1. Kaksitavuiset monikko (housut)     = n_21pl
!! *** 2.1.2. Kaksitavuiset (joki: joke)         = n_21ie
!! ** 2.2. Usseempitav. (opettaaja)                               = n_22
!! *** 2.2.1. Usseempitav. odd (kajava)                  = n_22odd
!! *** 2.2.2. Usseempitav. odd (meininki, tunturi)        = n_22oddi
!! *** 2.2.3. Usseempitav. monikko oi (kulkkiija)        = n_22oi
!! ** 2.3 Kokkoontumanom. ( valkkee)                     = n_23
!! ** 2.3.1 Kokkoontumanom. (assii)             = n_23ii
!! * 3. Konsonanttirankaiset
!! ** 3.1 Kaksitavuiset ( pieni, kieli)                         = n_31
!! ** 3.1.1. Kaksitavuiset lumi)                 = n_31m
!! ** 3.1.2. (käsi, vuosi)                                                 = n_31si
!! ** 3.1.3. (lapsi)                                                      = n_31lapsi
!! ** 3.1.4. (mies)                                                         = n_31mies
!! ** 3.2 Usseempitavuiset
!! ** 3.2.1 (tytär)                              = n_32
!! ** 3.2.2. (elläin)                            = n_32m
!! ** 3.2.3 (rakkhaus)                                                         = n_32s
!! ** 3.3 Kokkoontumanom. ( opas, huonet)
!! ** 3.3.1 (opas)                               = n_33s
!! ** 3.3.2 (asukas)                             = n_33sodd
!! ** 3.3.3. (ruvis)                           = n_33is
!! ** 3.3.4 (huonet)                            = n_33t
!! ** 3.3.5 (kirves)                                                         = n_33ts
!! * 4. Ekstrakonsonanttirankaiset
!! ** 4.1 veres-nominit ( veres, kokkous)                 = n_41
!! ** 4.1.1. veres-nominit monikko ( veljekset)                = n_41pl
!! ** 4.2 –inen-nominit ( kainulainen)                         = n_42 = nen
!! *** 4.2.1 -inen-nominit Pl (tervheiset)                 = n_42pl = nen
!! *** 4.2.2 -inen-nominit odd (vihrinen)                 = n_42odd = nen

!! !!Noun types

!! !!!INCOMING

LEXICON n_x
 +N: x_x ;

LEXICON x_x
 +Sg+Nom: K ; ! Just to make it compile



LEXICON n_1 !!= * @CODE@ = maa, tie
             +N: x_1 ;
      +N+Sg+Nom: R   ;
 +N+Pl+Dial/Por:%>i2ne   shortestgenpl   ;

 LEXICON n_1ai !!= * @CODE@ = tuorestai ja muut päivät
         +N: x_1 ;
  +N+Sg+Nom: R   ;

LEXICON x_1
  +Sg+Nom:         K               ;
                   long_par        ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with

         :         x_1pl           ;
! +Sg+Nom:         Px              ; ! mattoni
! +Sg+Gen:         Px              ; ! mattoni
! +Pl+Nom:         Px              ; ! mattoni
      +Sg:         ess_n           ;

LEXICON n_1pl !!= * @CODE@ = häät
        +N: x_1 ;

LEXICON x_1pl !!= * @CODE@ = häät

          :          long_par        ;
          :          weak_v_stem     ;
       +Pl:%>i2      sgpl_cases_even ; !! the same affix in sg and pl
       +Pl:%>i2      com               ;

          :          1TAVU_VV-ill    ;
            +Pl:%>i2      tt_longgenpl    ;
   +Pl+Dial/Por:%>i2%>ne shortestgenpl   ; ! häinen

       +Pl:%>i2      longparpl       ;
       +Pl:%>i2      shortillpl      ;
       +Pl:%>i2      ess_n           ;

LEXICON n_21C !!= * @CODE@
 +N+Sg+Nom:  K    ; ! Hans
          :i n_21 ; ! Hansi
 +N+Sg+Nom:i R    ;

LEXICON n_21        !!= * @CODE@  "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi
        +N: x_21 ;
 +N+Sg+Nom: R    ;

LEXICON x_21 ! "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi, tooli
  nom_strong   ;
  x_21_oblique ;

LEXICON x_21_oblique
           :                   short_par       ;
        +Sg:                   ess_n           ;
        +Pl:^AO%>i2            ess_n           ;

           :^WG                weak_v_stem     ;
        +Sg:^WG                sgpl_cases_even ; !to sgpl_cases_even for cases with
        +Pl:^AO^WG%>i2         sgpl_cases_even ; !the same affix in sg and pl
                               2TAVU_V_ill_sg  ;
 +Pl+Dial/Por:^AO%>i2          shortestgenpl   ; ! shortestgenpl since we have the i
+Pl+Dial/-Por:^AO^WG%>i2       tt_longgenpl    ;! syksyitten, toolitten. Dialect ! longgenpl, eira 5322, korpus.
 +Pl+Dial/Por:^AO%>i2          shortestparpl   ;
+Pl+Dial/-Por:^AO%>j           shortparpla     ; ! remove one
+Pl+Dial/-Por:^AO%>i2          shortparpla     ; ! remove one

    +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;
        +Pl:^AO%>i2            com             ;


LEXICON n_21pl        !!= * @CODE@  "villahousut", lyhyt vokali, yks vartalo, kasvot
           +N:       x_21pl    ;
    +N+Pl+Nom:%>t    K         ; ! housut, kasvot

LEXICON x_21pl        !!= * @CODE@

          +Pl:%>i2               sgpl_cases_even ; !! the same affix in sg and pl
          +Pl:%>i2               ess_n            ;
 +Pl+Dial/Por:%>i2               shortestgenpl   ; ! kasvoin
+Pl+Dial/-Por:%>i2               tt_longgenpl    ; ! kasvoitten, housuitten
 +Pl+Dial/Por:%>i2               shortestparpl   ; ! kasvoi
+Pl+Dial/-Por:%>j                shortparpla     ; ! kasvoja

      +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;
          +Pl:%>i2               com             ;


! LEXICON kk-longgenpl
!      +Gen:%>kk%>i2           #               ; ! kk ?? Tsekkaa..

!LEXICON kk-longgparpl
! +Par:%>kk%>i2 # ; ! kk ?? Tsekkaa..

LEXICON n_21ie        !!= @CODE@ on ove, ovi, nom joki:joke, hyksi, suomi
        +N:     x_21ie ;
 +N+Sg+Nom:^E2I R      ; ! ^EI e to i

LEXICON x_21ie
      +Sg+Nom:^E2I     K               ; ! ^EI e to i
    +Dial/Por:         short_par       ; ! suomee
   +Dial/-Por:^E2I     short_par_a     ; ! suomea
                       obl_strong_even ;
             :^WG      weak_v_stem     ;
          +Sg:^WG      sgpl_cases_even ; !! these two lexica for cases with
          +Pl:^WG%>i2  sgpl_cases_even ; !! the same affix in sg and pl
 +Pl+Dial/Por:%>i2     shortgenpl      ; ! hyksiin
+Pl+Dial/-Por:%>i2     tt_longgenpl    ; ! hyksitten
 +Pl+Dial/Por:%>i2     shortparpl      ; ! hyksii
+Pl+Dial/-Por:%>i2     shortparpla     ; ! hyksiä
                       2TAVU_edel-ill  ;
          +Pl:%>i2     com             ;


LEXICON n_22        !!= * @CODE@  opettaaja, karamelli
        +N: x_22 ;
 +N+Sg+Nom: R    ;

LEXICON x_22
                       nom_strong         ;
                       short_par          ; ! karamellia
       +Sg:            ess_n              ; ! karamellina
          :^WG         weak_v_stem        ;
       +Sg:^WG         sgpl_cases_even    ; ! the same affix in sg and pl
 +Pl+Dial/Por:^WG%>i2  sgpl_cases_even    ; ! Adding  to block i:0
+Pl+Dial/-Por:^WG%>    sgpl_cases_even    ; ! Adding  to block i:0
                       2TAVU_V_ill_sg     ;
   +Pl+Ill:^HMETA%>hii n_PxK              ;
!   +Pl:^WG%>i2       tt_longgenpl       ;
!   +Pl:%>i2          longparpl          ;
!   +Pl:%>i2          shortestparpl      ; !liberaali, dialissa on liberaalia?
!   +Pl:%>i2          ess_n                ;
!   +Pl:%>i2          com                ;
        +Pl:           x_22_pl_por        ;
        +Pl:           x_22_pl_var        ;

 LEXICON x_22_pl_por
  +Dial/Por:^WG%>i2     tt_longgenpl       ;
  +Dial/Por:%>i2        longparpl          ;
  +Dial/Por:%>i2        shortestparpl      ;
  +Dial/Por:%>i2        ess_n                ;
  +Dial/Por:%>i2        com                ;

 LEXICON x_22_pl_var
 +Dial/-Por:^WG%>       tt_longgenpl       ;
 +Dial/-Por:%>          shortparpla        ;
 +Dial/-Por:%>          ess_n                ; ! karamelline
 +Dial/-Por:%>          com                ; ! karamellina

! +Pl:%>i2        shortestgenpl      ; opettaajiin
! +Pl+Tra:%>i2ksi K ; Lene removed this one
! !+Pl+Tra:%>i2kse     PxxK               ;

LEXICON n_22odd !!= * @CODE@  kajava
 +N: x_22odd ;

LEXICON x_22odd !!= * @CODE@  kajava
                           nom_strong     ;
                           short_par      ;
             :^WG          weak_v_stem    ;
          +Sg:^WG          sgpl_cases_odd ;
          +Pl:^WG%>i2      sgpl_cases_odd ; ! Adding  to block i:0
                           2TAVU_V_ill_sg ;
          +Sg:             ess_nn         ;
 +Pl+Dial/Por:^WG%>i2i     tt_longgenpl   ; ! kajaviitten
+Pl+Dial/-Por:^WG%>i2      tt_longgenpl   ; ! kajavitten
 +Pl+Dial/Por:%>i2i        longparpl      ; ! kajaviita
 +Pl+Dial/Por:%>i2%>i      shortestparpl  ; ! %> i necessary, otherwise a:o NOTwork, kajavii
+Pl+Dial/-Por:%>i2%>       shortparpla    ; ! kajavia
      +Pl+Ill:%^HMETA%>hii n_PxK          ;
          +Pl:%>i2         ess_nn         ;
          +Pl:%>i2         com            ; ! kajavine
! +Pl+Tra:%>i2%>ksi    PxxK           ;






LEXICON n_22oddi !!= * @CODE@  meininki, tunturi
        +N:     x_22oddi ;
 +N+Sg+Nom:     R        ;
 +N+Sg+Gen:^WGn R        ;

LEXICON x_22oddi !!= * @CODE@  meininki, tunturi
                      nom_strong      ;
    +Dial/Por:        short_par       ;
   +Dial/-Por:        short_par_a     ;
!                     nomgen_px       ;
         :^WG         weak_v_stem     ;
      +Sg:^WG         sgpl_cases_odd  ;

 +Pl+Dial/Por:^WG%>i2  sgpl_cases_even ; ! Adding  to block i:0
+Pl+Dial/-Por:^WG%>   sgpl_cases_odd  ; ! Adding  to block i:0
                      2TAVU_V_ill_sg  ;
          +Sg:        ess_nn          ;
          +Pl+Ill:^HMETA%>hii n_PxK   ;
          +Pl:        ess_nn          ;

 +Pl+Dial/Por:^WG%>i2  tt_longgenpl   ;
 +Pl+Dial/Por:%>i2     longparpl      ;
 +Pl+Dial/Por:i2       com            ;

+Pl+Dial/-Por:^WG%>   tt_longgenpl    ;
+Pl+Dial/-Por:%>      shortparpla     ;
+Pl+Dial/-Por:        com             ;
!+Pl+Dial/Var:         com             ;


!  Tarkista, onko yaml oikein:
!  [24/25][FAIL] meininki+N+Pl+Ess => Missing results: meininkiinä
!  [24/25][FAIL] meininki+N+Pl+Ess => Unexpected results: meininkkiinä
!  [25/25][FAIL] meininki+N+Pl+Com => Missing results: meininkiine
!  [25/25][FAIL] meininki+N+Pl+Com => Unexpected results: meininkkiine



LEXICON n_22oi        !!= * @CODE@  Kulkkiija, paivukko, sivakka, perintö
 +N: x_22oi ;

LEXICON x_22oi        !!= * @CODE@  Kulkkiija, paivukko, sivakka, perintö, kurkkio
                            nom_strong      ; ! sg nom
 +Dial/Por:                 short_par       ;
+Dial/-Por:                 short_par_a     ;
        :^WG                weak_v_stem     ;
     +Sg:^WG                sgpl_cases_odd  ; ! these two lexica for cases with
     +Pl:^AO^WG%>i2         sgpl_cases_even ; ! the same affix in sg and pl
                            2TAVU_V_ill_sg  ;
     +Pl:^AO^WG%>i2         tt_longgenpl    ;
     +Pl:^AO^WG%>i2         longparpl       ;
     +Sg:                   ess_nn          ; ! paivukkonna
     +Pl:^AO%>i2            ess_n           ; ! paivukoina
     +Pl:^AO%>i2            com             ; ! paivukoine
 +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;


LEXICON n_23        !!= * @CODE@  valkkee, hoppee, kauhhee
      +N+Dial/Por:           x_23            ;
!    +N+Dial/-Por:           x_23_-Por       ;
 ! +N+Sg+Nom:   R    ; Is this extra and wrong?

LEXICON x_23
          +Sg+Nom:e          K               ;
 +Sg+Nom+Err/Orth:%>%^A      K               ;
              +Sg:e          sgpl_cases_even ;
          +Sg+Gen:en         K               ;
          +Sg+Gen:en         R               ; ! TODO check if different than k, maybe put this in n_23
          +Pl+Nom:et         K               ;
          +Sg+Ill:esseen     K               ;
          +Pl+Ill:e%>i2ssiin K               ;
              +Sg:e          longparpl       ;
              +Sg:e          ess_n           ;
              +Pl:e%>i2      longparpl       ;
              +Pl:e%>i2      tt_longgenpl    ;
              +Pl:e%>i2      sgpl_cases_even ;
              +Pl:e%>i2      ess_n           ;
              +Pl:e%>i2      com             ;

!LEXICON x_23_-Por ! look at x_22oi for inspiration, as the -ki cases work
!      +Sg:^E2I^A     nom             ; ! valkia
!      +Sg:^E2I^A     gen             ; ! valkian
!      +Sg:^E2I^A     par             ; ! valkiaa
!      +Sg:^E2I^A     tra             ; ! valkiaksi
!      +Sg:^E2I^A     abe             ; ! valkiatta
!      +Sg:^E2I^A     ine             ; ! valkiassa
!      +Sg:^E2I^A     ela             ; ! valkiasta
!      +Sg:^E2I       ill             ; ! valkihaan
!      +Sg:^E2I^A     ade             ; ! valkialla
!      +Sg:^E2I^A     abl             ; ! valkialta
!      +Sg:^E2I^A     all             ; ! valkialle
!      +Sg:^E2I^A     ess_n           ; ! valkianna

!      +Pl:^E2I^A     nom             ; ! valkiat
!      +Pl:%>i2       gen             ; ! valkeitten
!      +Pl:%>i2       par             ; ! valkeita
!      +Pl:%>i2       tra             ; ! valkeiksi
!      +Pl:%>i2       abe             ; ! valkeitta
!      +Pl:%>i2       ine             ; ! valkeissa
!      +Pl:%>i2       ela             ; ! valkeista
!      +Pl:%>i2       ill             ; ! valkeissiin
!      +Pl:%>i2       ade             ; ! valkeila
!      +Pl:%>i2       abl             ; ! valkeilta
!      +Pl:%>i2       all             ; ! valkeile
!      +Pl:%>i2       ess_n           ; ! valkeina
!      +Pl:%>i2       com             ; ! valkeine


LEXICON n_23ii        !!= * @CODE@  assii
 +N+Dial/Por:         x_23ii           ;
+N+Dial/-Por:         x_22oi           ; ! skriv dyp kommentar her
 +N+Sg+Nom:           R                ;

LEXICON x_23ii
      +Sg+Nom:        K               ;
          +Sg:        longparpl       ;
      +Sg+Ill:sseen   K               ;
      +Pl+Ill:ssiin   K               ;
          +Sg:        ess_n           ;
 +Pl+Dial/Por:        tt_longgenpl    ;
+Pl+Dial/-Por:^AO%>i2 tt_longgenpl    ;
 +Pl+Dial/Por:        longparpl       ;
+Pl+Dial/-Por:^AO%>i2 longparpl       ;
                      weak_v_stem     ;
          +Sg:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
 +Dial/Por+Pl:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
+Dial/-Por+Pl:^AO%>i2 sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
          +Pl:        ess_n           ;
          +Pl:^AO     com             ;


LEXICON n_31        !!= @CODE@  kieli
        +N:     x_31 ;
 +N+Sg+Nom:^E2I R    ; ! ^E2I e to i

LEXICON x_31
  +Sg+Nom:^E2I        K               ; ! ^E2I e to i
      +Sg:^EDEL       longparpl     ;
                      obl_strong_even ;
         :^WG         weak_v_stem     ;
      +Sg:^WG         sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
      +Pl:%>i2        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
                      2TAVU_V_ill_sg  ;
      +Pl:^HMETA%>hii shortestillpl   ;

 +Pl+Dial/Por:%>i2i   shortestgenpl   ;
+Pl+Dial/-Por:        consgenpl       ; ! kielten
+Pl+Dial/-Por:%>i2    tt_longgenpl    ; ! kielitten, sienitten
!     +Pl:%>i2        shortgenpl      ;
!    !+Pl:^EDEL       consgenpl       ;
 +Pl+Dial/Por:%>i2    shortparpl      ; ! kielii
+Pl+Dial/-Por:%>i2    shortparpla     ; ! kieliä
  +Pl+Com:%>i2%>ne    K               ;

LEXICON n_31m        !!= @CODE@ lumi lu
        +N:   x_31m ;
 +N+Sg+Nom:mi R     ; ! ^EI e to i

LEXICON x_31m
 +Sg+Nom:mi           K               ; ! ^EI e to i
     +Sg:n            longparpl       ;
        :me           obl_strong_even ;
        :me^WG        weak_v_stem     ;
     +Sg:me^WG        sgpl_cases_even ; ! these two lexica for cases with
     +Pl:m^WG%>i2     sgpl_cases_even ; ! the same affix in sg and pl

 +Pl+Dial/Por:me%>i2       shortgenpl      ; ! lumiin
+Pl+Dial/-Por:m%>i2        tt_longgenpl    ; ! lumitten
 +Pl+Dial/Por:me%>i2       shortparpl      ; ! lumii
+Pl+Dial/-Por:me%>i2       shortparpla     ; ! lumia

 +Sg+Ill:m^HMETA%>hee n_PxK           ;
 +Pl+Ill:m^EDEL%>hii  n_PxK           ;
 +Pl+Com:m%>i2%>ne    K               ;

LEXICON n_31si        !!= @CODE@ vuosi, käsi, hirsi
        +N: x_31si ;
 +N+Sg+Nom: R      ;

LEXICON x_31si
      +Sg+Nom:^E2I     K               ;
!                       nomgen_px       ; ! sg nom/gen px
          +Sg:^EDEL    longparpl       ; ! parsg
             :^WG      weak_v_stem     ; ! gensg, nompl
                       2TAVU_V_ill_sg  ;
          +Sg:^WG      sgpl_cases_even ; ! these two lexica for cases with
          +Pl:%>i2     sgpl_cases_even ; ! the same affix in sg and pl
          +Sg:         ess_n           ;
          +Pl:%>i2     ess_n           ;
 +Pl+Dial/Por:%>i2     shortgenpl      ; ! vuossiin, ei vuottiin, hirssiin
+Pl+Dial/-Por:%>i2     tt_longgenpl    ; !hirssitten, vuossitten
 +Pl+Dial/Por:%>i2     shortparpl      ; ! hirssii
+Pl+Dial/-Por:%>i2%>   shortparpla     ; ! hirsiä
       +Pl:^HMETA%>hii shortestillpl   ;
      +Pl+Com:%>i2%>ne K               ;

!  LEXICON n_31lapsi        !!= @CODE@ lapsi la
!           +N:              x_31lapsi       ;
!    +N+Sg+Nom:psi           K               ; ! ^EI e to i
!    +N+Sg+Nom:psi           R               ; ! ^EI e to i
! Add in when remember how to not make bug and crash compile, -Sindre

LEXICON n_31lapsi        !!= @CODE@ lapsi la
      +N+Sg+Nom:psi           K               ; ! ^EI e to i
      +N+Sg+Nom:psi           R               ; ! ^EI e to i
      +N+Sg+Gen:psen          R               ;
      +N+Pl+Gen:sten          R               ;
          +N+Sg:s             longparpl       ;
             +N:pse           obl_strong_even ;
             +N:pse^WG        weak_v_stem     ;
          +N+Sg:pse^WG        sgpl_cases_even ; !! these two lexica for cases with
      +N+Pl+Gen:sten          K               ;
          +N+Pl:ps^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
 +N+Pl+Dial/Por:ps%>i2%>i     shortestgenpl   ; ! lapsiin
+N+Pl+Dial/-Por:ps%>i2        tt_longgenpl    ; ! lapsitten
 +N+Pl+Dial/Por:ps%>i2%>i     shortestparpl   ; ! lapsii
+N+Pl+Dial/-Por:ps%>i2%>      shortparpla     ; ! lapsia
      +N+Sg+Ill:ps^HMETA%>hee n_PxK           ;
      +N+Pl+Ill:ps^EDEL%>hii  n_PxK           ;
      +N+Pl+Com:ps%>i2%>ne    K               ;

LEXICON n_31mies        !!= @CODE@ mies mie
      +N+Sg+Nom:s            K               ; ! ^EI e to i
      +N+Sg+Nom:s            R               ; ! ^EI e to i
      +N+Sg+Gen:hen          K               ;
          +N+Sg:s            longparpl       ;
             +N:he           obl_strong_even ;
          +N+Sg:he^WG        weak_v_stem     ;
      +N+Pl+Nom:het          K               ;
          +N+Sg:he^WG        sgpl_cases_even ; !! these two lexica for cases with
          +N+Pl:h^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +N+Pl:s            consgenpl       ;
 +N+Pl+Dial/Por:h%>i2%>i     shortestgenpl   ; ! miehiin
+N+Pl+Dial/-Por:h%>i2%>      tt_longgenpl    ; ! miehitten
 +N+Pl+Dial/Por:h%>i2%>i     shortestparpl   ; ! miehii
+N+Pl+Dial/-Por:h%>i2%>      shortparpla     ; ! miehiä
      +N+Sg+Ill:h^HMETA%>hee n_PxK           ;
      +N+Pl+Ill:hhii         n_PxK           ;
          +N+Pl:h%>i2        com             ;


LEXICON n_32 !!= @CODE@ tytär, taival
        +N:    x_32 ;
 +N+Sg+Nom:^WG R    ;

LEXICON x_32
 +Sg+Nom:^WG          K              ;
        :e            weak_v_stem    ;
 +Sg+Par:^WG%>t^A     PxxK           ;
        :e            obl_strong_odd ;
 +Sg+Gen:e%>n         K              ;
        :e            2TAVU_V_ill_sg ;
        :e            2TAVU_V_ill_sg ;
     +Sg:e            sgpl_cases_odd ;
     +Sg:e            ess_nn         ;
     +Pl:%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Pl+Dial/Por:%>i%>i2      tt_longgenpl   ; ! tyttäriitten
+Pl+Dial/-Por:%>i%>        tt_longgenpl   ; ! tyttäritten

 +Pl+Dial/Por:%>i2         shortparpl     ; ! tyttärii
 +Pl+Dial/Por:%>i%>i2      longparpl      ; ! tyttäriitä
+Pl+Dial/-Por:%>i%>        shortparpla    ; ! tyttäriä

          +Pl:^HMETAh%>i2i shortestillpl  ;
          +Pl:e%>i2     ess_nn         ;
          +Pl:e%>i2 com       ;


LEXICON n_32m !!= @CODE@ elläin elläi
        +N:     x_32m ;
 +N+Sg+Nom:^WGn R     ;

LEXICON x_32m !!= @CODE@
  x_32m_sg ;
  x_32m_pl ;

LEXICON x_32m_sg
 +Sg+Nom:^WGn      K              ;
        :me        weak_v_stem_sg ;
 +Sg+Par:^WG%>nt^A PxxK           ;
 +Sg+Gen:me%>n     K              ;
        :me        weak_v_stem_pl ; ! Nom pl
        :me        2TAVU_V_ill_sg ;
        :me        2TAVU_V_ill_sg ;
     +Sg:me        sgpl_cases_odd ;
     +Sg:me        ess_nn         ;
     +Pl:me%>i2    ess_nn         ;
     +Pl:me%>i2    com            ;

LEXICON n_32m_pl !!= @CODE@
      +N: x_32m_pl ;

LEXICON x_32m_pl
        :me            weak_v_stem_pl ;
        :me            weak_v_stem    ;
     +Pl:m%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Pl+Dial/Por:m%>ii2   tt_longgenpl   ; ! elläimiitten
+Pl+Dial/-Por:m%>i     tt_longgenpl   ; ! elläimitten

 +Pl+Dial/Por:mi%>i2   shortestparpl  ; ! elläimii
 +Pl+Dial/Por:mi%>i2   longparpl      ; ! elläimiitä
+Pl+Dial/-Por:mi%>     shortparpla    ; ! elläimiä

     +Pl:m^HMETAh%>i2i shortestillpl  ;
 +Pl+Com:m%>i2%>ne     K              ;

LEXICON n_32s !!= @CODE@  sairhaus
  +N+Sg+Nom:^WGs           K                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
  +N+Sg+Nom:^WGs           R                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
  +N+Sg+Par:%>tt^A         PxxK              ;
         +N:đe             weak_v_stem       ;
         +N:te             2TAVU_V_ill_sg    ;
      +N+Sg:đe             sgpl_cases_common ;
  +N+Sg+Ade:đe%>ll^A       PxxK              ; !! * Pitkä perusmuoto
  +N+Sg+All:đe%>lle        PxxK              ; !! * Pitkä perusmuoto
      +N+Sg:te             ess_nn            ;
      +N+Pl:ks%>i2         sgpl_cases_odd    ; ! TODO wtf is this comm --> hmm, tämä menee pieleen, sg ≠ pl
      +N+Pl:ksi%>i2        shortestparpl     ;
      +N+Pl:ksi%>i2        longparpl         ;
      +N+Pl:ksi%>i2        tt_longgenpl      ;
      +N+Pl:ks^HMETAh%>i2i shortestillpl     ;
      +N+Pl:ks%>i2         ess_nn            ;
      +N+Pl:ks%>i2         com               ;


! LEXICON n_32s != @CODE@  sairhaus vanha versio
!        +N:t              long_par          ;
!        +N:te             obl_strong        ;
!        +N:te^WG          weak_v_stem       ;
!     +N+Sg:^WG            sgpl_cases_odd    ;
!     +N+Pl:^WG%>i2        sgpl_cases_odd    ;
!     +N+Pl:ks%>i2         shortparpl        ;
!     +N+Pl:ks%>i2         shortgenpl        ;
!     +N+Pl:ks%>i2         shorterillpl      ;

!LEXICON n_32t
                           !n_32             ; ! ei tehty

!LEXICON n_33 ! ei tehty
                           !n_33s            ;

LEXICON n_33as !!= @CODE@ rakas, asukas
 +N: x_33as ;

LEXICON x_33as
             :a^WG         sConsCaselyhyempi ; ! nom, par hammas (WG)
             :haa          mTAVU_VV-ill      ;
      +Pl+Ill:h%>aissiin   K                 ;
      +Pl+Com:h%>a%>ine    K                 ;
             :h%>aa        weak_v_stem_sg    ;
             :h%>aa        weak_v_stem_pl    ;
          +Sg:h%>aa        ess_n             ;
          +Sg:h%>aa        sgpl_cases_even   ;
          +Pl:h%>a%>i2     sgpl_cases_even   ;
          +Pl:h%>a%>i2     tt_longgenpl      ; ! asukkhaitten
+Pl+Dial/-Por:h%>a%>je     shortestgenpl     ; ! rakkhajen
          +Pl:h%>a%>i2     longparpl         ; ! asukkhaita
+Pl+Dial/-Por:h%>a%>j      shortparpla       ; ! rakkhajen
  ! +N+Pl+Ess:h%>a%>i2%>na PxxK              ;
          +Pl:h%>aa%>i2    ess_n             ;


!LEXICON n_33aes
!        +N:             x_33aes           ;

LEXICON x_33aes
          :ä^WG         sConsCaselyhyempi ; ! nom, par hammas (WG)
          :hää          mTAVU_VV-ill      ;
   +Pl+Ill:h%>äissiin   K                 ;
   +Pl+Com:h%>ä%>ine    K                 ;
          :h%>ää        weak_v_stem_sg    ;
          :h%>ää        weak_v_stem_pl    ;
       +Sg:h%>ää        ess_n           ;
       +Pl:h%>ää%>i2    ess_n           ;
       +Sg:h%>ää        sgpl_cases_even   ;
       +Pl:h%>ä%>i2     sgpl_cases_even   ;
       +Pl:h%>ä%>i2     longparpl         ;
       +Pl:h%>ä%>i2     tt_longgenpl      ;
       +Pl:h%>ä%>i2     ess_n             ;



!LEXICON n_33ys !!= @CODE@ rakas
!        +N:        x_33ys ;
! +N+Sg+Nom:%>y^WGs R      ;

LEXICON x_33ys
          :%>y^WG       sConsCaselyhyempi ; ! nom, par hammas (WG)
          :h%>yy        mTAVU_VV-ill      ;
   +Pl+Ill:h%>yissiin   K                 ;
   +Pl+Com:h%>y%>ine    K                 ;
          :h%>yy        weak_v_stem_sg    ;
          :h%>yy        weak_v_stem_pl    ;
       +Sg:h%>yy        sgpl_cases_even   ;
       +Pl:h%>y%>i2     sgpl_cases_even   ;
       +Pl:h%>y%>i2     longparpl         ;
       +Pl:h%>y%>i2     tt_longgenpl      ;
 +N+Pl+Ess:h%>y%>i2%>ny PxxK              ; ! not to ess n pecause of the y I guess? wat


LEXICON n_33is !!= @CODE@ ruvis, ruumis
 +N: x_33is ;

LEXICON x_33is !!= @CODE@ ruvis
             :i^WG       sConsCaselyhyempi ; ! nom, par
             :hii        weak_v_stem_sg    ;
             :hii        weak_v_stem_pl    ;
          +Sg:hii        sgpl_cases_even   ;
          +Pl:hii        sgpl_cases_even   ;
          +Sg:hii        ess_n             ;
          +Pl:hii        tt_longgenpl      ; ! rukkhiitten
          +Pl:hii        longparpl         ; ! rukkhiita
+Pl+Dial/-Por:hi%>je     shortestgenpl     ; ! rukhijen
+Pl+Dial/-Por:hi%>j      shortparpla       ; ! rukhija
          +Pl:%>hii      ess_n             ;
      +Sg+Ill:%>hiisseen K                 ;
      +Pl+Ill:hiissiin   K                 ;
      +Pl+Com:hiine      K                 ;

!LEXICON n_33  !!= @CODE@ odottaessa viitataan n_33t-hen
!  n_33t ;

LEXICON n_33t !!= @CODE@ venet, hyljet, huonet
                 +N:   x_33t ;
! +Dial/Por+N+Sg+Nom:et R     ;   the nom cases are in tConsCase
!+Dial/-Por+N+Sg+Nom:e  R     ;   the nom cases are in tConsCase
! +Dial/Var+N+Sg+Nom:e  R     ;   the nom cases are in tConsCase

LEXICON x_33t
             :e           tConsCase      ; ! nom, par
             :hee         33_t_ts_common ;
      +Sg+Ill:hee         mTAVU_VV-ill   ;
+Pl+Dial/-Por:he%>je      shortestgenpl  ; ! venhejen
+Pl+Dial/-Por:he%>j       shortparpla    ; ! venhejä
      +Pl+Com:hee%>i2     com            ;

LEXICON n_33ts !!= @CODE@ kirves
        +N:   x_33ts ;
 +N+Sg+Nom:es R      ;

LEXICON x_33ts !!= @CODE@ kirves
      +Sg+Nom:es          K              ;
          +Sg:es          longparpl      ;
             :hee         33_t_ts_common ;
      +Sg+Ill:hee         mTAVU_VV-ill   ;
+Pl+Dial/-Por:he%>je      shortestgenpl  ; ! Kirvhejen
+Pl+Dial/-Por:he%>j       shortparpla    ; ! Kirvhejä
!      +Pl+Gen:hee%>i2     tt_longgenpl   ; ! Can this be commented out? yes, is in the common
 +Pl+Com:hee%>i2%>ne K              ;

LEXICON 33_t_ts_common
                   obl_strong_even ; ! muut vahvat
                   weak_v_stem     ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with
      +Pl:%>i2     sgpl_cases_even ; !! the same affix in sg and pl -- tsekkaa long
                   mTAVU_VV-ill    ;
      +Pl:%>i2     tt_longgenpl    ;
      +Pl:%>i2     longparpl       ;
! +Pl+Ill:heissiin K               ;
      +Pl:%>i2ssii longillpl       ;


LEXICON n_41 !!= * @CODE@ = veres
        +N:     x_41 ;
 +N+Sg+Nom:s    R    ;
 +N+Sg+Gen:ksen R    ;

LEXICON x_41
                         sConsCase      ; ! toistaiseksi :-/
             :kse        weak_v_stem    ;
          +Sg:kse        sgpl_cases_odd ; ! these two lexica for cases with
          +Sg:kse        ess_nn         ;
          +Pl:kse%>i2    sgpl_cases_odd ; ! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        consgenpl      ; ! veresten
          +Pl:ks%>i2     tt_longgenpl   ; ! vereksitten
 +Pl+Dial/Por:ks%>i2     shortparpl     ; ! vereksii
 +Pl+Dial/Por:ks%>i2i    longparpl      ; ! vereksiitä
+Pl+Dial/-Por:ks%>i2     shortparpla    ; ! vereksiä
      +Sg+Ill:ksheen     K              ;
      +Pl+Ill:ksh%>i2in  K              ;
          +Pl:kse%>i2    ess_nn         ;
          +Pl:kse%>i2    com            ;
 ! +Pl+Par:%>ksii     K              ;

!LEXICON s_kse
! +N:^WG # ;
LEXICON n_41pl !!= * @CODE@ = veljekset
           +N:           x_41pl          ;
    +N+Pl+Nom:%>kset     K               ;

LEXICON x_41pl !!= * @CODE@ = veljekset
             :kse        obl_strong_odd  ; ! muut vahvat
             :kse        weak_v_stem     ;
          +Sg:kse        sgpl_cases_odd  ; !! these two lexica for cases with
          +Pl:kse%>i2    sgpl_cases_odd  ; !! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        consgenpl       ; ! veljesten
          +Pl:ks%>i2     tt_longgenpl    ; ! veljeksitten
 +Pl+Dial/Por:ks%>i2     shortparpl      ; ! veljeksii
 +Pl+Dial/Por:ks%>i2i    longparpl       ; ! veljeksiitä
+Pl+Dial/-Por:ks%>i2     shortparpla     ; ! veljeksiä
      +Pl+Ill:ksh%>i2in  K               ;
      +Pl+Com:ks%> com               ;
  !    +Pl+Par:%>ksii     K               ;



!LEXICON et_he
!        +N:^WG        #               ;

!LEXICON n_42 ! itse asiassa n_42even
!     +N+Sg:se         sgpl_cases_even ; !! these two lexica for cases with
!     +N+Pl:s%>i2      sgpl_cases_even ; !! the same affix in sg and pl
!        +N:           nen             ;
! +N+Pl+Com:s%>i2%>ne  K               ;

LEXICON n_42 ! nainen, kainulainen
        +N:  x_42 ;
 +N+Sg+Nom:s R    ;


LEXICON x_42 ! nainen, kainulainen
      +Sg+Nom:nen       K               ;
             :se        weak_v_stem     ;
          +Sg:s         longparpl       ;
             :se        2TAVU_V_ill_sg  ;
          +Sg:se        sgpl_cases_even ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +Sg:se        ess_n           ;
          +Pl:s         consgenpl       ; ! kainulaisten
 +Pl+Dial/Por:ss%>i2i   shortestparpl   ; ! kainulaissii
 +Pl+Dial/Por:ss%>i2i   longparpl       ; ! kainulaissiita, naissiita
+Pl+Dial/-Por:s%>i2     shortparpla     ; ! kainulaisia
          +Pl:sh%>i2    shorterillpl    ;
          +Pl:se%>i2    ess_nn          ; ! veljeksinnä
          +Pl:s%>i2     com             ; ! veljeksine

LEXICON n_42odd !!= @CODE@ ihminen
        +N:  x_42odd ;
 +N+Sg+Nom:s R       ; ! ihmiskala

LEXICON x_42odd
      +Sg+Nom:nen       K              ;
          +Sg:se        ess_nn         ;
             :se        weak_v_stem    ;
          +Sg:s         longparpl      ;
             :se        2TAVU_V_ill_sg ;
          +Sg:se        sgpl_cases_odd ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_odd ; !! the same affix in sg and pl
          +Pl:s         consgenpl      ;
 +Dial/Por+Pl:ss%>i2i   shortestparpl  ; ! ihmissii
 +Dial/Por+Pl:ss%>i2i   longparpl      ; ! ihmissiitä
 +Dial/Var+Pl:s%>i2     shortparpla    ; ! ihmisiä
          +Pl:sh%>i2    shorterillpl   ;
          +Pl:s%>i2     ess_nn         ; !
          +Pl:s%>i2     com            ;

LEXICON n_42pl ! !!= @CODE@ tervheiset tervhei
      +N+Pl+Nom:%>set      K              ;
          +N+Pl:s%>i2      sgpl_cases_odd ; !! the same affix in sg and pl
          +N+Pl:s          consgenpl      ;
 +Dial/Por+N+Pl:ss%>i2     shortparpl     ; ! tervhehissii
 +Dial/Por+N+Pl:ss%>i2i    longparpl      ; ! tervhehissiitä
+Dial/-Por+N+Pl:s%>i2      shortparpla    ; ! tervhehisiä
      +N+Pl+Ill:sh%>i2in   K              ;
          +N+Pl:se%>i2     ess_nn         ;
      +N+Pl+Com:s%>i2%>nne K              ;

!LEXICON nen  !!= @CODE@ on nainen-sanoille
!  nen_sg ;
!  nen_pl ;

!LEXICON nen_sg
!             +N:     nom-nen        ;
!!            +N:se   nomgen_px      ;
!            +Sg:se     ess_n        ;
!            +Pl:se%>i2 ess_n        ;

!! make+N+Pl+kom:sine K              ;
!             +N:se   weak_v_stem_sg ;
!          +N+Sg:s    longparpl      ;
!             +N:se   2TAVU_V_ill_sg ;
!
!LEXICON nen_pl
!        +N:se        weak_v_stem_pl ;
! +N+Pl+Ill:sh%>i2in  K              ;
!     +N+Pl:s         consgenpl      ;
!     +N+Pl:ss%>i2i   shortestparpl  ;
!     +N+Pl:sh%>i2    shorterillpl   ;
! +N+Pl+Com:s%>i2%>ne K              ;


!LEXICON nom-nen
! +Sg+Nom:nen K ;


!LEXICON loput
!                 obl_strong_odd ; ! muut vahvat
!                 weak_v_stem    ;
!        +Sg:     sgpl_cases_odd ; !! these two lexica for cases with
!        +Pl:%>i2 sgpl_cases_odd ; !! the same affix in sg and pl -- tsekkaa long
!        +Pl:%>i2 consgenpl      ;
! +Pl+Use/NG:%>i2 tt_longgenpl   ;
!        +Pl:%>i2 shortparpl     ;


LEXICON sConsCase
 +Sg+Nom:^WG%>s K ;
 +Sg+Par:%>st^A K ; ! Par
 +Pl+Gen:%>sten K ; ! Gen

LEXICON sConsCaselyhyempi
 +Sg+Nom:%>s    K ;
 +Sg+Par:%>st^A K ; ! Par


LEXICON tConsCase
 +Dial/Por+Sg+Nom:^WG%>t    K ;
+Dial/-Por+Sg+Nom:^WG       K ;
 +Sg+Par:^WG%>tt^A K ;



!! !!! Basic paradigms
!  ===================

!! !!Basic vowel stems
!  --------------------

LEXICON nom_strong
 +Sg+Nom: K ;

LEXICON strong_v_stem_even  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
  short_par       ;
  obl_strong_even ;

!LEXICON strong_v_stem_odd  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
!  short_par      ;
!  obl_strong_odd ;


LEXICON weak_v_stem !!= @CODE@
  weak_v_stem_sg ;
  weak_v_stem_pl ;

LEXICON weak_v_stem_sg
 +Sg+Gen:%>n K ;
 +Sg+Gen:%>n R ; ! NB follow this.

LEXICON weak_v_stem_pl
  +Pl+Nom:%>t     K ;
! +Pl+Ins:%>i2%>n K ; !! Marginal case, in use only for some words.
                        !! Todo: identify these, and remove Ins from the
                        !! general weak_v_stem lexicon.

!! !Sublexica for the vowel stems
!  - - - - - - - - - - - - - - -

LEXICON obl_strong_even
!          nomgen_px ;
  +Pl:%>i2 com       ;
  +Sg:         ess_n           ;
  +Pl:%>i2     ess_n           ;

LEXICON obl_strong_odd
!              nomgen_px ;
      +Pl:%>i2 com       ;
      +Sg:         ess_nn           ;
      +Pl:%>i2     ess_nn           ;

! LEXICON nomgen_px
! +Sg+Nom:     Px        ; ! mattoni
! +Sg+Gen:     Px        ; ! mattoni
! +Pl+Nom:     Px        ; ! mattoni


LEXICON sgpl_cases_even ! kaikille paitsi neljän koplalle
 +Ade:%>l^A PxxK              ; ! Lyhyt perusmuoto vuođela
 +All:%>le  PxxK              ; ! Lyhyt perusmuoto
            sgpl_cases_common ;

LEXICON sgpl_cases_odd ! neljän koplalle
 +Ade:%>ll^A PxxK              ; ! Pitkä perusmuoto
 +All:%>lle  PxxK              ; ! Pitkä perusmuoto
             sgpl_cases_common ;

LEXICON sgpl_cases_common
  +Abl:%>lt^A PxxK ;
  +Ine:%>ss^A PxxK ;
  +Ela:%>st^A PxxK ;
  +Tra:%>ksi  K    ;
! +Tra:%>kse  Pxx  ;
  +Abe:%>tt^A PxxK ;

LEXICON ess_n
 +Ess:%>n^A PxxK ; ! Essiivillä aina Eiran gramatiikin mukaan lyhytmuoto
                    ! Mutta Eira s. 38 kaupunkinna, tyttärennä

LEXICON ess_nn
 +Ess:%>nn^A PxxK ; ! Essiivillä aina Eiran gramatiikin mukaan lyhytmuoto
                                        ! Mutta Eira s. 38 kaupunkinna, tyttärennä

LEXICON com      ! Komitatiivilla on vain -ine ja menee Pxx:iin
 +Com:%>ne PxxK ; ! Not to K, since Px must be present
                 ! Note that adjectives have -ine without Px


!! !Lexica for the non-uniform cases
! --------------------------------


LEXICON short_par
 +Dial/Por+Sg+Par:%>^V PxxK ;
+Dial/-Por+Sg+Par:%>^A PxxK ;
 +Sg+Par+Err/Orth:%>^A PxxK ;

LEXICON short_par_a
          +Sg+Par:%>^A PxxK ;

LEXICON long_par
 +Sg: longparpl ;
 +Pl:%>i2 longparpl ;

LEXICON 1TAVU_VV-ill  !short maahan
 +Sg+Ill:%>h^V n_PxK ;

LEXICON 2TAVU_V_ill_sg !long
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;

! sykshyyn, linnhaan
! linna pl.ill. kaksi muotoa po. linnhoin

LEXICON 2TAVU_edel-ill !suomhiin
 +Sg+Ill:^HMETA%>h^V^V n_PxK ;
 +Pl+Ill:^EDEL%>hii    n_PxK ;


LEXICON mTAVU_VV-ill   ! longer kuninkhaa/sseen
 +Sg+Ill:%>ssee n_PxK ; ! koska tämä pääte aina sseen


!LEXICON oldgenpl
! +Gen:%> n_PxK ;

LEXICON shortestgenpl
 +Gen: n_PxK ;

LEXICON shortgenpl
 +Gen:%>^V n_PxK ;

!LEXICON longgenpl
! ! d_longgenpl ;   oletetaan, että -tten vaan
!  tt_longgenpl ;

!LEXICON d_longgenpl
! +Gen:%>de n_PxK ;

LEXICON tt_longgenpl
 +Gen:%>tte n_PxK ;

LEXICON consgenpl
 +Gen:%>te n_PxK ;

LEXICON shortestparpl !! * @CODE@ zero, because ii > i
 +Par: PxxK ;

LEXICON shortparpl
 +Par:%>^V PxxK ;

 LEXICON shortparpla
  +Par:%>^A PxxK ;


LEXICON longparpl
 +Par:%>t^A PxxK ;

LEXICON shortestillpl
 +Ill:%> n_PxK ;

LEXICON shortillpl
 +Ill:%>hi n_PxK ;

!LEXICON h_shortillpl
! +Ill:^EDEL%>hii n_PxK ;


LEXICON shorterillpl
 +Ill:%>i2 n_PxK ;

LEXICON longillpl
 +Ill: n_PxK ; ! En tiedä, toimiiko tämä

!LEXICON longerillpl
! +Pl+Ill:%>s^V^V n_PxK ;





!! !Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!= @CODE@ has either -n or goes to Px
  :n K  ;
!    Px ;

LEXICON i_PxK  !!= @CODE@ Tra: -i or -e and goes to Px
  :i K  ;
! :e Px ;

LEXICON PxK !!= @CODE@ has only -nsA, and is currently not in use. Check
   K  ;
!  Px ;

LEXICON PxxK  !!= @CODE@ has also -Vn, thus both .. llensa and ..lleen.
   K     ;
!  Px    ;
!  Px-Vn ;

LEXICON Pxx
  Px    ;
  Px-Vn ;

LEXICON Px
 +PxSg1:%>ni   K ;
 +PxSg2:%>sti  K ;
 +PxSg3:%>ns^A K ;
 +PxPl1:%>mm^A K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns^A K ;


LEXICON Px-Vn
 +PxSg3:%>^Vn K ;
 +PxPl3:%>^Vn K ;
