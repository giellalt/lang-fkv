! ======================
!! !!!Sublexica for NounRoot
! ======================

!! Aloitin sovittaa analyysin Eiran kieliopin mukaan (Porsanki).
!! Leksikot ovat nyt (tai pitää olla) n11, n12, jne., eli siis
!! Eiran nominit tyyppi 1.1., 1.2., jne.

!!  2007 s.87
!! * 1. Yksitavuiset / Vi ( maa, tuorestai)                 = n_1
!! * 1.1. Yksitavuiset monikko/ Vi ( häät)                 = n_1pl
!! * 2. Vokaalirankaiset
!! ** 2.1 Kaksitavuiset ( pappi)                             = n_21
!! *** 2.1.1. Kaksitavuiset monikko (housut)     = n_21pl
!! *** 2.1.2. Kaksitavuiset (joki: joke)         = n_21ie
!! ** 2.2. Usseempitav. (opettaaja)                               = n_22
!! *** 2.2.1. Usseempitav. odd (kajava)                  = n_22odd
!! *** 2.2.2. Usseempitav. odd (meininki, tunturi)        = n_22oddi
!! *** 2.2.3. Usseempitav. monikko oi (kulkkiija)        = n_22oi
!! ** 2.3 Kokkoontumanom. ( valkkee)                     = n_23
!! ** 2.3.1 Kokkoontumanom. (assii)             = n_23ii
!! * 3. Konsonanttirankaiset
!! ** 3.1 Kaksitavuiset ( pieni, kieli)                         = n_31
!! ** 3.1.1. Kaksitavuiset lumi)                 = n_31m
!! ** 3.1.2. (käsi, vuosi)                                                 = n_31si
!! ** 3.1.3. (lapsi)                                                      = n_31lapsi
!! ** 3.1.4. (mies)                                                         = n_31mies
!! ** 3.2 Usseempitavuiset
!! ** 3.2.1 (tytär)                              = n_32
!! ** 3.2.2. (elläin)                            = n_32m
!! ** 3.2.3 (rakkhaus)                                                         = n_32s
!! ** 3.3 Kokkoontumanom. ( opas, huonet)
!! ** 3.3.1 (opas)                               = n_33s
!! ** 3.3.2 (asukas)                             = n_33sodd
!! ** 3.3.3. (ruvis)                           = n_33is
!! ** 3.3.4 (huonet)                            = n_33t
!! ** 3.3.5 (kirves)                                                         = n_33ts
!! * 4. Ekstrakonsonanttirankaiset
!! ** 4.1 veres-nominit ( veres, kokkous)                 = n_41
!! ** 4.1.1. veres-nominit monikko ( veljekset)                = n_41pl
!! ** 4.2 –inen-nominit ( kainulainen)                         = n_42 = nen
!! *** 4.2.1 -inen-nominit Pl (tervheiset)                 = n_42pl = nen
!! *** 4.2.2 -inen-nominit odd (vihrinen)                 = n_42odd = nen

!! !!Noun types

!! !!!INCOMING

LEXICON n_x
 +N: x_x ;

LEXICON x_x
 +Sg+Nom: K ; ! Just to make it compile



LEXICON n_1 !!= * @CODE@ = maa, tie
             +N: x_1 ;
      +N+Sg+Nom: R   ;
 +N+Pl+Dial/Por:%>i2ne   gen_n           ;

 LEXICON n_1ai !!= * @CODE@ = tuorestai ja muut päivät
         +N: x_1 ;
  +N+Sg+Nom: R   ;

LEXICON x_1
  +Sg+Nom:         K               ;
      +Sg:         par_tA          ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with

         :         x_1pl           ;
! +Sg+Nom:         Px              ; ! mattoni
! +Sg+Gen:         Px              ; ! mattoni
! +Pl+Nom:         Px              ; ! mattoni
      +Sg:         ess_n           ;



LEXICON n_1pl !!= * @CODE@ = häät
        +N: x_1 ;

LEXICON x_1pl !!= * @CODE@ = häät
          :          weak_v_stem     ;
       +Pl:%>i2      sgpl_cases_even ; !! the same affix in sg and pl

         +Pl:%>i2     gen_tten        ;
+Pl+Dial/Por:%>i2%>ne gen_n           ; ! häinen

       +Pl:%>i2      par_tA          ;
       +Pl:%>i2      shortillpl      ;
       +Pl:%>i2      ess_n           ;
       +Pl:%>i2      com               ;

LEXICON n_21C !!= * @CODE@
 +N+Sg+Nom:  K    ; ! Hans
          :i n_21 ; ! Hansi
 +N+Sg+Nom:i R    ;

LEXICON n_21        !!= * @CODE@  "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi
        +N: x_21 ;
 +N+Sg+Nom: R    ;

LEXICON x_21 ! "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi, tooli
+Sg+Nom:    K    ;
            x_21_oblique ;

LEXICON x_21_oblique
 +Sg+Dial/Por:                 par_V           ;
+Sg+Dial/-Por:                 par_A           ;
          +Sg:                 ess_n           ;
          +Pl:^AO%>i2          ess_n           ;
             :^WG              weak_v_stem     ;
          +Sg:^WG              sgpl_cases_even ; !to sgpl_cases_even for cases with
          +Pl:^AO^WG%>i2       sgpl_cases_even ; !the same affix in sg and pl
                               2TAVU_V_ill_sg  ;
 +Pl+Dial/Por:^AO%>i2          gen_n           ; ! gen_n         since we have the i
+Pl+Dial/-Por:^AO^WG%>i2       gen_tten        ;! syksyitten, toolitten. Dialect ! longgenpl, eira 5322, korpus.
 +Pl+Dial/Por:^AO%>i2          par_end         ;
+Pl+Dial/-Por:^AO%>j           par_A           ; ! remove one
+Pl+Dial/-Por:^AO%>i2          par_A           ; ! remove one

    +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;
        +Pl:^AO%>i2            com             ;


LEXICON n_21pl        !!= * @CODE@  "villahousut", lyhyt vokali, yks vartalo, kasvot
           +N:       x_21pl    ;
    +N+Pl+Nom:%>t    K         ; ! housut, kasvot

LEXICON x_21pl        !!= * @CODE@

          +Pl:%>i2               sgpl_cases_even ; !! the same affix in sg and pl
          +Pl:%>i2               ess_n            ;
 +Pl+Dial/Por:%>i2               gen_n           ; ! kasvoin
+Pl+Dial/-Por:%>i2               gen_tten        ; ! kasvoitten, housuitten
 +Pl+Dial/Por:%>i2               par_end         ; ! kasvoi
+Pl+Dial/-Por:%>j                par_A           ; ! kasvoja

      +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;
          +Pl:%>i2               com             ;


! LEXICON kk-longgenpl
!      +Gen:%>kk%>i2           #               ; ! kk ?? Tsekkaa..

!LEXICON kk-longgparpl
! +Par:%>kk%>i2 # ; ! kk ?? Tsekkaa..

LEXICON n_21ie        !!= @CODE@ on ove, ovi, nom joki:joke, hyksi, suomi
        +N:     x_21ie ;
 +N+Sg+Nom:^E2I R      ; ! ^EI e to i

LEXICON x_21ie
      +Sg+Nom:^E2I     K               ; ! ^EI e to i
 +Sg+Dial/Por:         par_V           ; ! suomee
+Sg+Dial/-Por:^E2I     par_A           ; ! suomea
             :^WG      weak_v_stem     ;
          +Sg:^WG      sgpl_cases_even ; !! these two lexica for cases with
          +Sg:         ess_n           ;
          +Pl:^WG%>i2  sgpl_cases_even ; !! the same affix in sg and pl
 +Pl+Dial/Por:%>i2     gen_Vn          ; ! hyksiin
+Pl+Dial/-Por:%>i2     gen_tten        ; ! hyksitten
 +Pl+Dial/Por:%>i2     par_V           ; ! hyksii
+Pl+Dial/-Por:%>i2     par_A           ; ! hyksiä
                       2TAVU_edel-ill  ;
          +Pl:%>i2     ess_n           ;
          +Pl:%>i2     com             ;




LEXICON n_22        !!= * @CODE@  opettaaja, karamelli
        +N: x_22 ;
 +N+Sg+Nom: R    ;

LEXICON x_22
   +Sg+Nom:            K                  ;
 +Sg+Dial/Por:         par_V              ;
+Sg+Dial/-Por:         par_A              ; ! karamellia
       +Sg:            ess_n              ; ! karamellina
          :^WG         weak_v_stem        ;
       +Sg:^WG         sgpl_cases_even    ; ! the same affix in sg and pl
 +Pl+Dial/Por:^WG%>i2  sgpl_cases_even    ; ! Adding  to block i:0
+Pl+Dial/-Por:^WG%>    sgpl_cases_even    ; ! Adding  to block i:0
                       2TAVU_V_ill_sg     ;
   +Pl+Ill:^HMETA%>hii n_PxK              ;
!   +Pl:^WG%>i2       gen_tten           ;
!   +Pl:%>i2          par_tA             ;
!   +Pl:%>i2          par_end            ; !liberaali, dialissa on liberaalia?
!   +Pl:%>i2          ess_n                ;
!   +Pl:%>i2          com                ;
        +Pl:           x_22_pl_por        ;
        +Pl:           x_22_pl_var        ;

 LEXICON x_22_pl_por
  +Dial/Por:^WG%>i2     gen_tten           ;
  +Dial/Por:%>i2        par_tA             ;
  +Dial/Por:%>i2        par_end            ;
  +Dial/Por:%>i2        ess_n                ;
  +Dial/Por:%>i2        com                ;

 LEXICON x_22_pl_var
 +Dial/-Por:^WG%>       gen_tten           ;
 +Dial/-Por:%>          par_A              ;
 +Dial/-Por:%>          ess_n                ; ! karamelline
 +Dial/-Por:%>          com                ; ! karamellina

! +Pl:%>i2        gen_n              ; opettaajiin
! +Pl+Tra:%>i2ksi K ; Lene removed this one
! !+Pl+Tra:%>i2kse     PxxK               ;

LEXICON n_22odd !!= * @CODE@  kajava
 +N: x_22odd ;

LEXICON x_22odd !!= * @CODE@  kajava
      +Sg+Nom:             K              ;
 +Sg+Dial/Por:             par_V          ;
+Sg+Dial/-Por:             par_A          ;
             :^WG          weak_v_stem    ;
          +Sg:^WG          sgpl_cases_odd ;
          +Pl:^WG%>i2      sgpl_cases_odd ; ! Adding  to block i:0
                           2TAVU_V_ill_sg ;
          +Sg:             ess_nn         ;
 +Pl+Dial/Por:^WG%>i2i     gen_tten       ; ! kajaviitten
+Pl+Dial/-Por:^WG%>i2      gen_tten       ; ! kajavitten
 +Pl+Dial/Por:%>i2i        par_tA         ; ! kajaviita
 +Pl+Dial/Por:%>i2%>i      par_end        ; ! %> i necessary, otherwise a:o NOTwork, kajavii
+Pl+Dial/-Por:%>i2%>       par_A          ; ! kajavia
      +Pl+Ill:%^HMETA%>hii n_PxK          ;
          +Pl:%>i2         ess_nn         ;
          +Pl:%>i2         com            ; ! kajavine
! +Pl+Tra:%>i2%>ksi    PxxK           ;



LEXICON n_22oddi !!= * @CODE@  meininki, tunturi
        +N:     x_22oddi ;
 +N+Sg+Nom:     R        ;
     +N+Sg:^WG  gen_n    ;

LEXICON x_22oddi !!= * @CODE@  meininki, tunturi
          +Sg+Nom:        K               ;
     +Sg+Dial/Por:        par_V           ;
    +Sg+Dial/-Por:        par_A           ;
                 :^WG     weak_v_stem     ;
              +Sg:^WG     sgpl_cases_odd  ;
     +Pl+Dial/Por:^WG%>i2 sgpl_cases_even ; ! Adding  to block i:0
    +Pl+Dial/-Por:^WG%>   sgpl_cases_odd  ; ! Adding  to block i:0
                          2TAVU_V_ill_sg  ;
              +Sg:        ess_nn          ;
              +Pl+Ill:^HMETA%>hii n_PxK   ;
              +Pl:        ess_nn          ;
     +Pl+Dial/Por:^WG%>i2  gen_tten       ;
     +Pl+Dial/Por:%>i2     par_tA         ;
     +Pl+Dial/Por:i2       com            ;

    +Pl+Dial/-Por:^WG%>   gen_tten        ;
    +Pl+Dial/-Por:%>      par_A           ;
    +Pl+Dial/-Por:        com             ;
!+Pl+Dial/Var:         com             ;


!  Tarkista, onko yaml oikein:
!  [24/25][FAIL] meininki+N+Pl+Ess => Missing results: meininkiinä
!  [24/25][FAIL] meininki+N+Pl+Ess => Unexpected results: meininkkiinä
!  [25/25][FAIL] meininki+N+Pl+Com => Missing results: meininkiine
!  [25/25][FAIL] meininki+N+Pl+Com => Unexpected results: meininkkiine



LEXICON n_22oi        !!= * @CODE@  Kulkkiija, paivukko, sivakka, perintö
 +N: x_22oi ;

LEXICON x_22oi        !!= * @CODE@  Kulkkiija, paivukko, sivakka, perintö, kurkkio
      +Sg+Nom:                   K               ; ! sg nom
 +Sg+Dial/Por:                   par_V           ;
+Sg+Dial/-Por:                   par_A           ;
             :^WG                weak_v_stem     ;
          +Sg:^WG                sgpl_cases_odd  ; ! these two lexica for cases with
          +Pl:^AO^WG%>i2         sgpl_cases_even ; ! the same affix in sg and pl
                                 2TAVU_V_ill_sg  ;
          +Pl:^AO^WG%>i2         gen_tten        ;
          +Pl:^AO^WG%>i2         par_tA          ;
          +Sg:                   ess_nn          ; ! paivukkonna
          +Pl:^AO%>i2            ess_n           ; ! paivukoina
          +Pl:^AO%>i2            com             ; ! paivukoine
      +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;


LEXICON n_23        !!= * @CODE@  valkkee, hoppee, kauhhee
      +N+Dial/Por:           x_23            ;
!    +N+Dial/-Por:           x_23_-Por       ;
 ! +N+Sg+Nom:   R    ; Is this extra and wrong?

LEXICON x_23
          +Sg+Nom:e          K               ;
 +Sg+Nom+Err/Orth:%>%^A      K               ;
              +Sg:e          sgpl_cases_even ;
          +Sg+Gen:en         K               ;
          +Sg+Gen:en         R               ; ! TODO check if different than k, maybe put this in n_23
          +Pl+Nom:et         K               ;
          +Sg+Ill:esseen     K               ;
          +Pl+Ill:e%>i2ssiin K               ;
              +Sg:e          par_tA          ;
              +Sg:e          ess_n           ;
              +Pl:e%>i2      par_tA          ;
              +Pl:e%>i2      gen_tten        ;
              +Pl:e%>i2      sgpl_cases_even ;
              +Pl:e%>i2      ess_n           ;
              +Pl:e%>i2      com             ;

!LEXICON x_23_-Por ! look at x_22oi for inspiration, as the -ki cases work
!      +Sg:^E2I^A     nom             ; ! valkia
!      +Sg:^E2I^A     gen             ; ! valkian
!      +Sg:^E2I^A     par             ; ! valkiaa
!      +Sg:^E2I^A     tra             ; ! valkiaksi
!      +Sg:^E2I^A     abe             ; ! valkiatta
!      +Sg:^E2I^A     ine             ; ! valkiassa
!      +Sg:^E2I^A     ela             ; ! valkiasta
!      +Sg:^E2I       ill             ; ! valkihaan
!      +Sg:^E2I^A     ade             ; ! valkialla
!      +Sg:^E2I^A     abl             ; ! valkialta
!      +Sg:^E2I^A     all             ; ! valkialle
!      +Sg:^E2I^A     ess_n           ; ! valkianna

!      +Pl:^E2I^A     nom             ; ! valkiat
!      +Pl:%>i2       gen             ; ! valkeitten
!      +Pl:%>i2       par             ; ! valkeita
!      +Pl:%>i2       tra             ; ! valkeiksi
!      +Pl:%>i2       abe             ; ! valkeitta
!      +Pl:%>i2       ine             ; ! valkeissa
!      +Pl:%>i2       ela             ; ! valkeista
!      +Pl:%>i2       ill             ; ! valkeissiin
!      +Pl:%>i2       ade             ; ! valkeila
!      +Pl:%>i2       abl             ; ! valkeilta
!      +Pl:%>i2       all             ; ! valkeile
!      +Pl:%>i2       ess_n           ; ! valkeina
!      +Pl:%>i2       com             ; ! valkeine


LEXICON n_23ii        !!= * @CODE@  assii
 +N+Dial/Por:         x_23ii           ;
+N+Dial/-Por:         x_22oi           ; ! skriv dyp kommentar her
 +N+Sg+Nom:           R                ;

LEXICON x_23ii
      +Sg+Nom:        K               ;
          +Sg:        par_tA          ;
      +Sg+Ill:sseen   K               ;
      +Pl+Ill:ssiin   K               ;
          +Sg:        ess_n           ;
 +Pl+Dial/Por:        gen_tten        ;
+Pl+Dial/-Por:^AO%>i2 gen_tten        ;
 +Pl+Dial/Por:        par_tA          ;
+Pl+Dial/-Por:^AO%>i2 par_tA          ;
                      weak_v_stem     ;
          +Sg:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
 +Dial/Por+Pl:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
+Dial/-Por+Pl:^AO%>i2 sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
          +Pl:        ess_n           ;
          +Pl:^AO     com             ;


LEXICON n_31        !!= @CODE@  kieli
        +N:     x_31 ;
 +N+Sg+Nom:^E2I R    ; ! ^E2I e to i

LEXICON x_31
  +Sg+Nom:^E2I        K               ; ! ^E2I e to i
      +Sg:^EDEL       par_tA        ;
         :^WG         weak_v_stem     ;
      +Sg:^WG         sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
      +Sg:            ess_n           ;
      +Pl:%>i2        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
                      2TAVU_V_ill_sg  ;
      +Pl:^HMETA%>hii shortestillpl   ;

 +Pl+Dial/Por:%>i2i   gen_n           ;
+Pl+Dial/-Por:        gen_ten         ; ! kielten
+Pl+Dial/-Por:%>i2    gen_tten        ; ! kielitten, sienitten
!     +Pl:%>i2        gen_Vn          ;
!    !+Pl:^EDEL       gen_ten         ;
 +Pl+Dial/Por:%>i2    par_V           ; ! kielii
+Pl+Dial/-Por:%>i2    par_A           ; ! kieliä
          +Pl:%>i2    ess_n           ;
          +Pl:%>i2    com             ;

LEXICON n_31m        !!= @CODE@ lumi lu
        +N:   x_31m ;
 +N+Sg+Nom:mi R     ; ! ^EI e to i

LEXICON x_31m
 +Sg+Nom:mi           K               ; ! ^EI e to i
     +Sg:n            par_tA          ;
        :me^WG        weak_v_stem     ;
     +Sg:me^WG        sgpl_cases_even ; ! these two lexica for cases with
     +Sg:me           ess_n           ;
     +Pl:m^WG%>i2     sgpl_cases_even ; ! the same affix in sg and pl

 +Pl+Dial/Por:me%>i2       gen_Vn          ; ! lumiin
+Pl+Dial/-Por:m%>i2        gen_tten        ; ! lumitten
 +Pl+Dial/Por:me%>i2       par_V           ; ! lumii
+Pl+Dial/-Por:me%>i2       par_A           ; ! lumia

 +Sg+Ill:m^HMETA%>hee n_PxK           ;
 +Pl+Ill:m^EDEL%>hii  n_PxK           ;
     +Pl:me%>i2       ess_n           ;
     +Pl:m%>i2        com             ; ! kanskje me


LEXICON n_31si        !!= @CODE@ vuosi, käsi, hirsi
        +N: x_31si ;
 +N+Sg+Nom: R      ;

LEXICON x_31si
      +Sg+Nom:^E2I     K               ;
!                       nomgen_px       ; ! sg nom/gen px
          +Sg:^EDEL    par_tA          ; ! parsg
             :^WG      weak_v_stem     ; ! gensg, nompl
                       2TAVU_V_ill_sg  ;
          +Sg:^WG      sgpl_cases_even ; ! these two lexica for cases with
          +Pl:%>i2     sgpl_cases_even ; ! the same affix in sg and pl
          +Sg:         ess_n           ;
          +Pl:%>i2     ess_n           ;
 +Pl+Dial/Por:%>i2     gen_Vn          ; ! vuossiin, ei vuottiin, hirssiin
+Pl+Dial/-Por:%>i2     gen_tten        ; !hirssitten, vuossitten
 +Pl+Dial/Por:%>i2     par_V           ; ! hirssii
+Pl+Dial/-Por:%>i2%>   par_A           ; ! hirsiä
       +Pl:^HMETA%>hii shortestillpl   ;
          +Pl:%>i2     com             ;

!  LEXICON n_31lapsi        !!= @CODE@ lapsi la
!           +N:              x_31lapsi       ;
!    +N+Sg+Nom:psi           K               ; ! ^EI e to i
!    +N+Sg+Nom:psi           R               ; ! ^EI e to i
! Add in when remember how to not make bug and crash compile, -Sindre

LEXICON n_31lapsi        !!= @CODE@ lapsi la
      +N+Sg+Nom:psi           K               ; ! ^EI e to i
      +N+Sg+Nom:psi           R               ; ! ^EI e to i
      +N+Sg+Gen:psen          R               ;
          +N+Pl:s             gen_ten         ;
          +N+Sg:s             par_tA          ;
             +N:pse^WG        weak_v_stem     ;
          +N+Sg:pse^WG        sgpl_cases_even ; !! these two lexica for cases with
          +N+Sg:pse           ess_n           ;
          +N+Pl:s             gen_ten         ; ! lasten
          +N+Pl:ps^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
 +N+Pl+Dial/Por:ps%>i2%>i     gen_n           ; ! lapsiin
+N+Pl+Dial/-Por:ps%>i2        gen_tten        ; ! lapsitten
 +N+Pl+Dial/Por:ps%>i2%>i     par_end         ; ! lapsii
+N+Pl+Dial/-Por:ps%>i2%>      par_A           ; ! lapsia
      +N+Sg+Ill:ps^HMETA%>hee n_PxK           ;
      +N+Pl+Ill:ps^EDEL%>hii  n_PxK           ;
          +N+Pl:pse%>i2       ess_n           ;
          +N+Pl:ps%>i2        com             ; ! kanskje pse


LEXICON n_31mies        !!= @CODE@ mies mie
      +N+Sg+Nom:s            K               ; ! ^EI e to i
      +N+Sg+Nom:s            R               ; ! ^EI e to i
          +N+Sg:he           gen_n           ;
          +N+Sg:s            par_tA          ;
          +N+Sg:he^WG        weak_v_stem     ;
      +N+Pl+Nom:het          K               ;
          +N+Sg:he^WG        sgpl_cases_even ; !! these two lexica for cases with
          +N+Sg:he           ess_n           ;
          +N+Pl:h^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +N+Pl:s            gen_ten         ;
 +N+Pl+Dial/Por:h%>i2%>i     gen_n           ; ! miehiin
+N+Pl+Dial/-Por:h%>i2%>      gen_tten        ; ! miehitten
 +N+Pl+Dial/Por:h%>i2%>i     par_end         ; ! miehii
+N+Pl+Dial/-Por:h%>i2%>      par_A           ; ! miehiä
      +N+Sg+Ill:h^HMETA%>hee n_PxK           ;
      +N+Pl+Ill:hhii         n_PxK           ;
          +N+Pl:he%>i2       ess_n           ;
          +N+Pl:h%>i2        com             ;


LEXICON n_32 !!= @CODE@ tytär, taival
        +N:    x_32 ;
 +N+Sg+Nom:^WG R    ;

LEXICON x_32
 +Sg+Nom:^WG          K              ;
        :e            weak_v_stem    ;
     +Sg:^WG          par_tA         ;
     +Sg:e            gen_n          ;
        :e            2TAVU_V_ill_sg ;
     +Sg:e            sgpl_cases_odd ;
     +Sg:e            ess_nn         ;
     +Pl:%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Pl+Dial/Por:%>i%>i2      gen_tten       ; ! tyttäriitten
+Pl+Dial/-Por:%>i%>        gen_tten       ; ! tyttäritten

 +Pl+Dial/Por:%>i2         par_V          ; ! tyttärii
 +Pl+Dial/Por:%>i%>i2      par_tA         ; ! tyttäriitä
+Pl+Dial/-Por:%>i%>        par_A          ; ! tyttäriä

          +Pl:^HMETAh%>i2i shortestillpl  ;
          +Pl:e%>i2     ess_nn         ;
          +Pl:e%>i2 com       ;


LEXICON n_32m !!= @CODE@ elläin elläi
        +N:     x_32m ;
 +N+Sg+Nom:^WGn R     ;

LEXICON x_32m !!= @CODE@
  x_32m_sg ;
  x_32m_pl ;

LEXICON x_32m_sg
 +Sg+Nom:^WGn      K              ;
        :me        weak_v_stem    ;
     +Sg:^WG%>n    par_tA         ;
     +Sg:me        gen_n          ;
        :me        weak_v_stem ; ! Nom pl
        :me        2TAVU_V_ill_sg ;
     +Sg:me        sgpl_cases_odd ;
     +Sg:me        ess_nn         ;
     +Pl:me%>i2    ess_nn         ;
     +Pl:me%>i2    com            ;

LEXICON n_32m_pl !!= @CODE@
      +N: x_32m_pl ;

LEXICON x_32m_pl
        :me            weak_v_stem ;
        :me            weak_v_stem    ;
     +Pl:m%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Pl+Dial/Por:m%>ii2   gen_tten       ; ! elläimiitten
+Pl+Dial/-Por:m%>i     gen_tten       ; ! elläimitten

 +Pl+Dial/Por:mi%>i2   par_end        ; ! elläimii
 +Pl+Dial/Por:mi%>i2   par_tA         ; ! elläimiitä
+Pl+Dial/-Por:mi%>     par_A          ; ! elläimiä

     +Pl:m^HMETAh%>i2i shortestillpl  ;
     +Pl:m%>i2         com            ;

LEXICON n_32s !!= @CODE@  sairhaus
  +N+Sg+Nom:^WGs           K                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
  +N+Sg+Nom:^WGs           R                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
      +N+Sg:%>t            par_tA            ;
         +N:đe             weak_v_stem       ;
         +N:te             2TAVU_V_ill_sg    ;
      +N+Sg:đe             sgpl_cases_common ;
  +N+Sg+Ade:đe%>ll^A       PxxK              ; !! * Pitkä perusmuoto
  +N+Sg+All:đe%>lle        PxxK              ; !! * Pitkä perusmuoto
      +N+Sg:te             ess_nn            ;
      +N+Pl:ks%>i2         sgpl_cases_odd    ; ! TODO wtf is this comm --> hmm, tämä menee pieleen, sg ≠ pl
      +N+Pl:ksi%>i2        par_end           ;
      +N+Pl:ksi%>i2        par_tA            ;
      +N+Pl:ksi%>i2        gen_tten          ;
      +N+Pl:ks^HMETAh%>i2i shortestillpl     ;
      +N+Pl:ks%>i2         ess_nn            ;
      +N+Pl:ks%>i2         com               ;


! LEXICON n_32s != @CODE@  sairhaus vanha versio
!        +N:t              long_par          ;
!        +N:te             obl_strong        ;
!        +N:te^WG          weak_v_stem       ;
!     +N+Sg:^WG            sgpl_cases_odd    ;
!     +N+Pl:^WG%>i2        sgpl_cases_odd    ;
!     +N+Pl:ks%>i2         par_V             ;
!     +N+Pl:ks%>i2         gen_Vn            ;
!     +N+Pl:ks%>i2         shorterillpl      ;

!LEXICON n_32t
                           !n_32             ; ! ei tehty

!LEXICON n_33 ! ei tehty
                           !n_33s            ;

LEXICON n_33as !!= @CODE@ rakas, asukas
 +N: x_33as ;

LEXICON x_33as
      +Sg+Nom:a^WG%>s      K                 ;
          +Sg:a^WG%>s      par_tA            ;
             :haa          mTAVU_VV-ill      ;
      +Pl+Ill:h%>aissiin   K                 ;
             :h%>aa        weak_v_stem    ;
             :h%>aa        weak_v_stem    ;
          +Sg:h%>aa        ess_n             ;
          +Sg:h%>aa        sgpl_cases_even   ;
          +Pl:h%>a%>i2     sgpl_cases_even   ;
          +Pl:h%>a%>i2     gen_tten          ; ! asukkhaitten
+Pl+Dial/-Por:h%>a%>je     gen_n             ; ! rakkhajen
          +Pl:h%>a%>i2     par_tA            ; ! asukkhaita
+Pl+Dial/-Por:h%>a%>j      par_A             ; ! rakkhajen
  ! +N+Pl+Ess:h%>a%>i2%>na PxxK              ;
          +Pl:h%>aa%>i2    ess_n             ;
          +Pl:h%>a%>i2     com               ;


!LEXICON n_33aes
!        +N:             x_33aes           ;

LEXICON x_33aes
   +Sg+Nom:ä^WG%>s      K                 ;
       +Sg:ä^WG%>s      par_tA            ;
          :hää          mTAVU_VV-ill      ;
   +Pl+Ill:h%>äissiin   K                 ;
          :h%>ää        weak_v_stem       ;
          :h%>ää        weak_v_stem       ;
       +Sg:h%>ää        ess_n             ;
       +Pl:h%>ää%>i2    ess_n             ;
       +Sg:h%>ää        sgpl_cases_even   ;
       +Pl:h%>ä%>i2     sgpl_cases_even   ;
       +Pl:h%>ä%>i2     par_tA            ;
       +Pl:h%>ä%>i2     gen_tten          ;
       +Pl:h%>ä%>i2     ess_n             ;
       +Pl:h%>ä%>i2     com               ;



!LEXICON n_33ys !!= @CODE@ rakas
!        +N:        x_33ys ;
! +N+Sg+Nom:%>y^WGs R      ;

LEXICON x_33ys
   +Sg+Nom:%>y^WG%>s    K                 ;
       +Sg:%>y^WG%>s    par_tA            ;
          :h%>yy        mTAVU_VV-ill      ;
   +Pl+Ill:h%>yissiin   K                 ;
          :h%>yy        weak_v_stem    ;
          :h%>yy        weak_v_stem    ;
       +Sg:h%>yy        sgpl_cases_even   ;
       +Pl:h%>y%>i2     sgpl_cases_even   ;
       +Pl:h%>y%>i2     par_tA            ;
       +Pl:h%>y%>i2     gen_tten          ;
 +N+Pl+Ess:h%>y%>i2%>ny PxxK              ; ! not to ess n pecause of the y I guess? wat
       +Pl:h%>y%>i2     com               ;


LEXICON n_33is !!= @CODE@ ruvis, ruumis
 +N: x_33is ;

LEXICON x_33is
      +Sg+Nom:i^WG%>s    K                 ;
          +Sg:i^WG%>s    par_tA            ;
             :hii        weak_v_stem       ;
          +Sg:hii        sgpl_cases_even   ;
          +Pl:hii        sgpl_cases_even   ;
          +Sg:hii        ess_n             ;
          +Pl:hii        gen_tten          ; ! rukkhiitten
          +Pl:hii        par_tA            ; ! rukkhiita
+Pl+Dial/-Por:hi%>je     gen_n             ; ! rukhijen
+Pl+Dial/-Por:hi%>j      par_A             ; ! rukhija
          +Pl:%>hii      ess_n             ;
      +Sg+Ill:%>hiisseen K                 ;
      +Pl+Ill:hiissiin   K                 ;
          +Pl:hi2i       com               ;

!LEXICON n_33  !!= @CODE@ odottaessa viitataan n_33t-hen
!  n_33t ;

LEXICON n_33t !!= @CODE@ venet, hyljet, huonet
                 +N:   x_33t ;
! +Dial/Por+N+Sg+Nom:et R     ;
!+Dial/-Por+N+Sg+Nom:e  R     ;
! +Dial/Var+N+Sg+Nom:e  R     ;

LEXICON x_33t
 +Dial/Por+Sg+Nom:e^WG%>t     K              ;
+Dial/-Por+Sg+Nom:e^WG        K              ;
              +Sg:e^WG%>t     par_tA         ;
                 :hee         33_t_ts_common ;
          +Sg+Ill:hee         mTAVU_VV-ill   ;
    +Pl+Dial/-Por:he%>je      gen_n          ; ! venhejen
    +Pl+Dial/-Por:he%>j       par_A          ; ! venhejä
              +Pl:hee%>i2     com            ;

LEXICON n_33ts !!= @CODE@ kirves
        +N:   x_33ts ;
 +N+Sg+Nom:es R      ;

LEXICON x_33ts !!= @CODE@ kirves
      +Sg+Nom:es          K              ;
          +Sg:es          par_tA         ;
             :hee         33_t_ts_common ;
      +Sg+Ill:hee         mTAVU_VV-ill   ;
+Pl+Dial/-Por:he%>je      gen_n          ; ! Kirvhejen
+Pl+Dial/-Por:he%>j       par_A          ; ! Kirvhejä
!      +Pl+Gen:hee%>i2     gen_tten       ; ! Can this be commented out? yes, is in the common
          +Pl:hee%>i2     com            ;

LEXICON 33_t_ts_common
                   weak_v_stem     ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with
      +Sg:         ess_n           ;
      +Pl:%>i2     sgpl_cases_even ; !! the same affix in sg and pl -- tsekkaa long
                   mTAVU_VV-ill    ;
      +Pl:%>i2     gen_tten        ;
      +Pl:%>i2     par_tA          ;
! +Pl+Ill:heissiin K               ;
  +Pl+Ill:%>i2ssii n_PxK           ;

      +Pl:%>i2     ess_n           ;
      +Pl:%>i2     com             ;


LEXICON n_41 !!= * @CODE@ = veres
        +N:     x_41 ;
 +N+Sg+Nom:s    R    ;
 +N+Sg+Gen:ksen R    ;

LEXICON x_41
      +Sg+Nom:^WG%>s     K              ;
             :kse        weak_v_stem    ;
          +Sg:kse        sgpl_cases_odd ; ! these two lexica for cases with
          +Sg:%>s        par_tA         ;
          +Sg:kse        ess_nn         ;
          +Pl:kse%>i2    sgpl_cases_odd ; ! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        gen_ten        ; ! veresten
          +Pl:ks%>i2     gen_tten       ; ! vereksitten
 +Pl+Dial/Por:ks%>i2     par_V          ; ! vereksii
 +Pl+Dial/Por:ks%>i2i    par_tA         ; ! vereksiitä
+Pl+Dial/-Por:ks%>i2     par_A          ; ! vereksiä
      +Sg+Ill:ksheen     K              ;
      +Pl+Ill:ksh%>i2in  K              ;
          +Pl:kse%>i2    ess_nn         ;
          +Pl:kse%>i2    com            ;
 ! +Pl+Par:%>ksii     K              ;

!LEXICON s_kse
! +N:^WG # ;
LEXICON n_41pl !!= * @CODE@ = veljekset
           +N:           x_41pl          ;
    +N+Pl+Nom:%>kset     K               ;

LEXICON x_41pl !!= * @CODE@ = veljekset
             :kse        weak_v_stem     ;
          +Sg:kse        sgpl_cases_odd  ; !! these two lexica for cases with
          +Pl:kse%>i2    sgpl_cases_odd  ; !! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        gen_ten         ; ! veljesten
          +Pl:ks%>i2     gen_tten        ; ! veljeksitten
 +Pl+Dial/Por:ks%>i2     par_V           ; ! veljeksii
 +Pl+Dial/Por:ks%>i2i    par_tA          ; ! veljeksiitä
+Pl+Dial/-Por:ks%>i2     par_A           ; ! veljeksiä
      +Pl+Ill:ksh%>i2in  K               ;
          +Sg:kse        ess_n           ;
          +Pl:kse%>i2    ess_n           ;
          +Pl:ks%>i2     com             ; ! veljeksine
  !    +Pl+Par:%>ksii     K               ;


!LEXICON et_he
!        +N:^WG        #               ;

!LEXICON n_42 ! itse asiassa n_42even
!     +N+Sg:se         sgpl_cases_even ; !! these two lexica for cases with
!     +N+Pl:s%>i2      sgpl_cases_even ; !! the same affix in sg and pl
!        +N:           nen             ;
! +N+Pl+Com:s%>i2%>ne  K               ;

LEXICON n_42 ! nainen, kainulainen
        +N:  x_42 ;
 +N+Sg+Nom:s R    ;


LEXICON x_42 ! nainen, kainulainen
      +Sg+Nom:nen       K               ;
             :se        weak_v_stem     ;
          +Sg:s         par_tA          ;
             :se        2TAVU_V_ill_sg  ;
          +Sg:se        sgpl_cases_even ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +Sg:se        ess_n           ;
          +Pl:s         gen_ten         ; ! kainulaisten
 +Pl+Dial/Por:ss%>i2i   par_end         ; ! kainulaissii
 +Pl+Dial/Por:ss%>i2i   par_tA          ; ! kainulaissiita, naissiita
+Pl+Dial/-Por:s%>i2     par_A           ; ! kainulaisia
          +Pl:sh%>i2    shorterillpl    ;
          +Pl:se%>i2    ess_n           ; ! naisina
          +Pl:s%>i2     com             ;

LEXICON n_42odd !!= @CODE@ ihminen
        +N:  x_42odd ;
 +N+Sg+Nom:s R       ; ! ihmiskala

LEXICON x_42odd
      +Sg+Nom:nen       K              ;
          +Sg:se        ess_nn         ;
             :se        weak_v_stem    ;
          +Sg:s         par_tA         ;
             :se        2TAVU_V_ill_sg ;
          +Sg:se        sgpl_cases_odd ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_odd ; !! the same affix in sg and pl
          +Pl:s         gen_ten        ;
 +Dial/Por+Pl:ss%>i2i   par_end        ; ! ihmissii
 +Dial/Por+Pl:ss%>i2i   par_tA         ; ! ihmissiitä
 +Dial/Var+Pl:s%>i2     par_A          ; ! ihmisiä
          +Pl:sh%>i2    shorterillpl   ;
          +Pl:s%>i2     ess_nn         ; !
          +Pl:s%>i2     com            ;

LEXICON n_42pl ! !!= @CODE@ tervheiset tervhei
      +N+Pl+Nom:%>set      K              ;
          +N+Pl:s%>i2      sgpl_cases_odd ; !! the same affix in sg and pl
          +N+Pl:s          gen_ten        ;
 +Dial/Por+N+Pl:ss%>i2     par_V          ; ! tervhehissii
 +Dial/Por+N+Pl:ss%>i2i    par_tA         ; ! tervhehissiitä
+Dial/-Por+N+Pl:s%>i2      par_A          ; ! tervhehisiä
      +N+Pl+Ill:sh%>i2in   K              ;
          +N+Pl:se%>i2     ess_nn         ;
      +N+Pl+Com:s%>i2%>nne K              ; ! TODO onko tässä kaksi nn:ää?

!LEXICON nen  !!= @CODE@ on nainen-sanoille
!  nen_sg ;
!  nen_pl ;

!LEXICON nen_sg
!             +N:     nom-nen        ;
!!            +N:se   nomgen_px      ;
!            +Sg:se     ess_n        ;
!            +Pl:se%>i2 ess_n        ;

!! make+N+Pl+kom:sine K              ;
!             +N:se   weak_v_stem ;
!          +N+Sg:s    par_tA         ;
!             +N:se   2TAVU_V_ill_sg ;
!
!LEXICON nen_pl
!        +N:se        weak_v_stem ;
! +N+Pl+Ill:sh%>i2in  K              ;
!     +N+Pl:s         gen_ten        ;
!     +N+Pl:ss%>i2i   par_end        ;
!     +N+Pl:sh%>i2    shorterillpl   ;
! +N+Pl+Com:s%>i2     com            ;


!LEXICON nom-nen
! +Sg+Nom:nen K ;


!LEXICON loput
!                 obl_strong_odd ; ! muut vahvat
!                 weak_v_stem    ;
!        +Sg:     sgpl_cases_odd ; !! these two lexica for cases with
!        +Pl:%>i2 sgpl_cases_odd ; !! the same affix in sg and pl -- tsekkaa long
!        +Pl:%>i2 gen_ten        ;
! +Pl+Use/NG:%>i2 gen_tten       ;
!        +Pl:%>i2 par_V          ;



!! !!! Basic paradigms
!  ===================

!! !!Basic vowel stems
!  --------------------


LEXICON weak_v_stem !!= @CODE@ This bs is gonna get deleted. Replase the genetives with gen_n and just add the nom,
! or perhaps make nom_t and/or add it to sgpl_cases which is gonna switch names to basic_cases or something.
 +Sg+Gen:%>n K ;
 +Sg+Gen:%>n R ; ! NB follow this.
 +Pl+Nom:%>t     K ;


!! !Sublexica for the basic unified cases, with even and odd variations
! ----------------------------------------------------------------------

LEXICON sgpl_cases_even ! kaikille paitsi neljän koplalle
 +Ade:%>l^A PxxK              ; ! Lyhyt perusmuoto vuođela
 +All:%>le  PxxK              ; ! Lyhyt perusmuoto
            sgpl_cases_common ;

LEXICON sgpl_cases_odd ! neljän koplalle. <- Mitä vittua tää kopla tarkoittaa tässä kontekstissa? Tämä ei ole aku ankka!
 +Ade:%>ll^A PxxK              ; ! Pitkä perusmuoto
 +All:%>lle  PxxK              ; ! Pitkä perusmuoto
             sgpl_cases_common ;

LEXICON sgpl_cases_common
  weak_v_stem ; ! for testing purposes here, TODO delete this line
  +Abl:%>lt^A PxxK ;
  +Ine:%>ss^A PxxK ;
  +Ela:%>st^A PxxK ;
  +Tra:%>ksi  K    ;
! +Tra:%>kse  Pxx  ;
  +Abe:%>tt^A PxxK ;



!! !Sublexica for Gen, Par, Ill, Ess and Com.
! ---------------------------------------------------------------------


LEXICON gen_n
 +Gen: n_PxK ;

LEXICON gen_Vn
 +Gen:%>^V n_PxK ;

LEXICON gen_tten  ! vereksitten
 +Gen:%>tte n_PxK ;

LEXICON gen_ten  ! veljesten
 +Gen:%>te n_PxK ;




LEXICON par_A
   +Par:%>^A PxxK ;

LEXICON par_V
+Par         :%>^V PxxK ;
+Par+Err/Orth:%>^A PxxK ;

LEXICON par_tA ! pitäiskö olla omat tt^A ja st^A ? Mange Sg er slik
 +Par:%>t^A PxxK ;

LEXICON par_end
  +Par: PxxK ;



!LEXICON 1TAVU_VV-ill  !short maahan <- This piece of shit was called only once, and that one time was fucking wrong111
!  +Sg+Ill:%>h^V n_PxK ;

LEXICON 2TAVU_V_ill_sg ! NB  USED 12 TIMES        |  sykshyyn, linnhaan | linna pl.ill. kaksi muotoa po. linnhoin
  +Sg+Ill:^HMETA%>h^V^V n_PxK ;

LEXICON 2TAVU_edel-ill ! NB USED ONCE 1 !         !suomhiin
  +Sg+Ill:^HMETA%>h^V^V n_PxK ; ! TODO add Twolc description about HMETA
  +Pl+Ill:^EDEL%>hii    n_PxK ; ! TODO add Twolc description about EDEL

LEXICON mTAVU_VV-ill ! NB USED 6 TIMES !!!!!!             longer kuninkhaa/sseen
  +Sg+Ill:%>ssee n_PxK ; ! koska tämä pääte aina sseen ! toimisko tää aina ^V^V:llä myös?

LEXICON shortestillpl ! NB USED 5 TIMES !!!!!
 +Ill:%> n_PxK ;

LEXICON shortillpl! ! NB USED ONCE 1 !
 +Ill:%>hi n_PxK ;

LEXICON shorterillpl ! NB USED TWICE 2 !!
 +Ill:%>i2 n_PxK ;! Tämä oikea käyttö monikon i:lle? Tuskin, sais olla viime vaiheessa. Consistent abstraction in layers




 LEXICON ess_n
  +Ess:%>n^A PxxK ;

 LEXICON ess_nn
  +Ess:%>nn^A PxxK ; ! Ess aina Eiran gramatiikin mukaan lyhytmuoto, Mutta Eira s. 38 kaupunkinna, tyttärennä




 LEXICON com      ! Komitatiivilla on vain -ine ja menee Pxx:iin
  +Com:%>ne PxxK ; ! Not to K, since Px must be present ! Note that adjectives have -ine without Px






 !LEXICON longgenpl
 ! ! d_longgenpl ;   oletetaan, että -tten vaan
 !  gen_tten     ;

 !LEXICON d_longgenpl
 ! +Gen:%>de n_PxK ;


!LEXICON longillpl
! +Ill: n_PxK ; ! En tiedä, toimiiko tämä

 !LEXICON h_shortillpl
 ! +Ill:^EDEL%>hii n_PxK ;

!LEXICON longerillpl
! +Pl+Ill:%>s^V^V n_PxK ;















!! !Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!= @CODE@ has either -n or goes to Px
  :n K  ;
!    Px ;

LEXICON i_PxK  !!= @CODE@ Tra: -i or -e and goes to Px
  :i K  ;
! :e Px ;

LEXICON PxK !!= @CODE@ has only -nsA, and is currently not in use. Check
   K  ;
!  Px ;

LEXICON PxxK  !!= @CODE@ has also -Vn, thus both .. llensa and ..lleen.
   K     ;
!  Px    ;
!  Px-Vn ;

LEXICON Pxx
  Px    ;
  Px-Vn ;

LEXICON Px
 +PxSg1:%>ni   K ;
 +PxSg2:%>sti  K ;
 +PxSg3:%>ns^A K ;
 +PxPl1:%>mm^A K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns^A K ;


LEXICON Px-Vn
 +PxSg3:%>^Vn K ;
 +PxPl3:%>^Vn K ;































 !!! -------- Graveyard





 !! !!! Basic paradigms
 !  ===================

 !! !!Basic vowel stems
 !  --------------------

! LEXICON nom_strong
!  +Sg+Nom: K ;

 !LEXICON strong_v_stem_even  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 !  short_par       ;
 !    +Pl:%>i2 com       ;
 !    +Sg:         ess_n           ;
 !    +Pl:%>i2     ess_n           ;
 ! TODO fix this mess

 !LEXICON strong_v_stem_odd  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 !  short_par      ;
 !  obl_strong_odd ;




 !! !Sublexica for the vowel stems
 !  - - - - - - - - - - - - - - -

 ! +Pl+Ins:%>i2%>n K ; !! Eira doesn't use this.



 !LEXICON obl_strong_even
 !          nomgen_px ;
 !  +Pl:%>i2 com       ;
 !  +Sg:         ess_n           ;
 !  +Pl:%>i2     ess_n           ;

 !LEXICON obl_strong_odd
 !              nomgen_px ;
 !      +Pl:%>i2 com       ;
 !      +Sg:         ess_nn           ;
 !      +Pl:%>i2     ess_nn           ;

 ! LEXICON nomgen_px
 ! +Sg+Nom:     Px        ; ! mattoni
 ! +Sg+Gen:     Px        ; ! mattoni
 ! +Pl+Nom:     Px        ; ! mattoni



 ! Random

! LEXICON sConsCase
!  +Sg+Nom:^WG%>s K ;
!  +Sg+Par:%>st^A K ; ! Par
!  +Pl+Gen:%>sten K ; ! Gen
