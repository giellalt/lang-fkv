! ======================
!! !!!Sublexica for NounRoot
! ======================

!! Aloitin sovittaa analyysin Eiran kieliopin mukaan (Porsanki).
!! Leksikot ovat nyt (tai pitää olla) n11, n12, jne., eli siis
!! Eiran nominit tyyppi 1.1., 1.2., jne.

!!  2007 s.87
!! * 1. Yksitavuiset / Vi ( maa, tuorestai)                 = n_1
!! * 1.1. Yksitavuiset monikko/ Vi ( häät)                 = n_1pl
!! * 2. Vokaalirankaiset
!! ** 2.1 Kaksitavuiset ( pappi)                             = n_21
!! *** 2.1.1. Kaksitavuiset monikko (housut)     = n_21pl
!! *** 2.1.2. Kaksitavuiset (joki: joke)         = n_21ie
!! ** 2.2. Usseempitav. (opettaaja)                               = n_22
!! *** 2.2.1. Usseempitav. odd (kajava)                  = n_22odd
!! *** 2.2.2. Usseempitav. odd (meininki, tunturi)        = n_22oddi
!! *** 2.2.3. Usseempitav. monikko oi (kulkkiija)        = n_22oi
!! ** 2.3 Kokkoontumanom. ( valkkee)                     = n_23
!! ** 2.3.1 Kokkoontumanom. (assii)             = n_23ii
!! * 3. Konsonanttirankaiset
!! ** 3.1 Kaksitavuiset ( pieni, kieli)                         = n_31
!! ** 3.1.1. Kaksitavuiset lumi)                 = n_31m
!! ** 3.1.2. (käsi, vuosi)                                                 = n_31si
!! ** 3.1.3. (lapsi)                                                      = n_31lapsi
!! ** 3.1.4. (mies)                                                         = n_31mies
!! ** 3.2 Usseempitavuiset
!! ** 3.2.1 (tytär)                              = n_32
!! ** 3.2.2. (elläin)                            = n_32m
!! ** 3.2.3 (rakkhaus)                                                         = n_32s
!! ** 3.3 Kokkoontumanom. ( opas, huonet)
!! ** 3.3.1 (opas)                               = n_33s
!! ** 3.3.2 (asukas)                             = n_33sodd
!! ** 3.3.3. (ruvis)                           = n_33is
!! ** 3.3.4 (huonet)                            = n_33t
!! ** 3.3.5 (kirves)                                                         = n_33ts
!! * 4. Ekstrakonsonanttirankaiset
!! ** 4.1 veres-nominit ( veres, kokkous)                 = n_41
!! ** 4.1.1. veres-nominit monikko ( veljekset)                = n_41pl
!! ** 4.2 –inen-nominit ( kainulainen)                         = n_42 = nen
!! *** 4.2.1 -inen-nominit Pl (tervheiset)                 = n_42pl = nen
!! *** 4.2.2 -inen-nominit odd (vihrinen)                 = n_42odd = nen

!! !!Noun types

!! !!!INCOMING

LEXICON n_x
 +N: x_x ;

LEXICON x_x
 +Sg+Nom: K ; ! Just to make it compile



LEXICON n_1 !!= * @CODE@ = maa, tie
             +N: x_1 ;
      +N+Sg+Nom: R   ;
 +N+Dial/Por+Pl:%>i2ne   gen_n           ;

 LEXICON n_1ai !!= * @CODE@ = tuorestai ja muut päivät
         +N: x_1 ;
  +N+Sg+Nom: R   ;

LEXICON x_1
  +Sg+Nom:         K               ;
      +Sg:         gen_n_sg        ;
      +Sg:         ill_hVn         ; ! maahan
      +Sg:         par_tA          ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with
         :         x_1pl           ;
! +Sg+Nom:         Px              ; ! mattoni
! +Sg+Gen:         Px              ; ! mattoni
! +Pl+Nom:         Px              ; ! mattoni
      +Sg:         ess_n           ;



LEXICON n_1pl !!= * @CODE@ = häät
        +N: x_1 ;

LEXICON x_1pl !!= * @CODE@ = häät
       +Sg: gen_n_sg ;
       +Pl: nom_t    ;
       +Pl:%>i2      sgpl_cases_even ; !! the same affix in sg and pl

         +Pl:%>i2     gen_tten       ;
+Dial/Por+Pl:%>i2%>ne gen_n          ; ! häinen

       +Pl:%>i2      par_tA          ;
       +Pl:%>i2      ill_hin         ;
       +Pl:%>i2      ess_n           ;
       +Pl:%>i2      com_ne          ;

LEXICON n_21C !!= * @CODE@
 +N+Sg+Nom:  K    ; ! Hans
          :i n_21 ; ! Hansi
 +N+Sg+Nom:i R    ;

LEXICON n_21        !!= * @CODE@  "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi
        +N: x_21 ;
 +N+Sg+Nom: R    ;

LEXICON x_21 ! "poika:poi6ka", lyhyt vokali, yks vartalo, kuva, linna, pappi, tooli, syksy
+Sg+Nom:    K    ;
            x_21_oblique ;

LEXICON x_21_oblique
 +Dial/Por+Sg:                 par_V           ;
+Dial/-Por+Sg:                 par_A           ;
          +Sg:                 ess_n           ;
          +Pl:^AO%>i2          ess_n           ;
          +Sg:^WG              gen_n_sg        ;
          +Pl:^WG              nom_t           ;
          +Sg:^WG              sgpl_cases_even ; !to sgpl_cases_even for cases with
          +Pl:^AO^WG%>i2       sgpl_cases_even ; !the same affix in sg and pl
          +Sg:^HMETA           ill_hVVn        ;
 +Dial/Por+Pl:^AO%>i2          gen_n           ; ! gen_n         since we have the i
+Dial/-Por+Pl:^AO^WG%>i2       gen_tten        ;! syksyitten, toolitten. Dialect ! longgenpl, eira 5322, korpus.
 +Dial/Por+Pl:^AO%>i2          par_end         ;
+Dial/-Por+Pl:^AO%>j           par_A           ; ! remove one
+Dial/-Por+Pl:^AO%>i2          par_A           ; ! remove one

        +Pl:^AO^HMETA          ill_hVin        ; ! sykshyin
        +Pl:^AO%>i2            com_ne          ;


LEXICON n_21pl        !!= * @CODE@  "villahousut", lyhyt vokali, yks vartalo, kasvot
           +N:       x_21pl    ;
        +N+Pl:       nom_t     ; ! housut, kasvot

LEXICON x_21pl        !!= * @CODE@

          +Pl:%>i2               sgpl_cases_even ; !! the same affix in sg and pl

 +Dial/Por+Pl:%>i2               gen_n           ; ! kasvoin
+Dial/-Por+Pl:%>i2               gen_tten        ; ! kasvoitten, housuitten
 +Dial/Por+Pl:%>i2               par_end         ; ! kasvoi
+Dial/-Por+Pl:%>j                par_A           ; ! kasvoja

          +Pl:^AO^HMETA          ill_hVin        ;
          +Pl:%>i2               ess_n           ;
          +Pl:%>i2               com_ne          ;


! LEXICON kk-longgenpl
!      +Gen:%>kk%>i2           #               ; ! kk ?? Tsekkaa..

!LEXICON kk-longgparpl
! +Par:%>kk%>i2 # ; ! kk ?? Tsekkaa..

LEXICON n_21ie        !!= @CODE@ on ove, ovi, nom joki:joke, hyksi, suomi
        +N:     x_21ie ;
 +N+Sg+Nom:^E2I R      ; ! ^EI e to i

LEXICON x_21ie
      +Sg+Nom:^E2I     K               ; ! ^EI e to i
 +Dial/Por+Sg:         par_V           ; ! suomee
+Dial/-Por+Sg:^E2I     par_A           ; ! suomea
          +Sg:^WG      gen_n_sg        ;
          +Pl:^WG      nom_t           ;
          +Sg:^WG      sgpl_cases_even ; !! these two lexica for cases with
          +Sg:         ess_n           ;
          +Pl:^WG%>i2  sgpl_cases_even ; !! the same affix in sg and pl
 +Dial/Por+Pl:%>i2     gen_Vn          ; ! hyksiin
+Dial/-Por+Pl:%>i2     gen_tten        ; ! hyksitten
 +Dial/Por+Pl:%>i2     par_V           ; ! hyksii
+Dial/-Por+Pl:%>i2     par_A           ; ! hyksiä
          +Sg:^HMETA   ill_hVVn        ;
!      +Pl+Ill:^EDEL%>hii n_PxK         ; ! TODO add Twolc description about EDEL ! suomhiin
          +Pl:^EDEL    ill_hiin        ; ! TODO add Twolc description about EDEL ! suomhiin

          +Pl:%>i2     ess_n           ;
          +Pl:%>i2     com_ne          ;




LEXICON n_22        !!= * @CODE@  opettaaja, karamelli
        +N: x_22 ;
 +N+Sg+Nom: R    ;

LEXICON x_22
   +Sg+Nom:            K                  ;
 +Dial/Por+Sg:         par_V              ;
+Dial/-Por+Sg:         par_A              ; ! karamellia
       +Sg:            ess_n              ; ! karamellina
       +Sg:^WG         gen_n_sg           ;
       +Pl:^WG         nom_t              ;
       +Sg:^WG         sgpl_cases_even    ; ! the same affix in sg and pl
 +Dial/Por+Pl:^WG%>i2  sgpl_cases_even    ; ! Adding  to block i:0
+Dial/-Por+Pl:^WG%>    sgpl_cases_even    ; ! Adding  to block i:0
          +Sg:^HMETA   ill_hVVn           ;
          +Pl:^HMETA   ill_hiin           ;
!   +Pl:^WG%>i2       gen_tten           ;
!   +Pl:%>i2          par_tA             ;
!   +Pl:%>i2          par_end            ; !liberaali, dialissa on liberaalia?
!   +Pl:%>i2          ess_n                ;
!   +Pl:%>i2          com_ne             ;
        +Pl:           x_22_pl_por        ;
        +Pl:           x_22_pl_var        ;

 LEXICON x_22_pl_por
  +Dial/Por:^WG%>i2     gen_tten           ;
  +Dial/Por:%>i2        par_tA             ;
  +Dial/Por:%>i2        par_end            ;
  +Dial/Por:%>i2        ess_n                ;
  +Dial/Por:%>i2        com_ne             ;

 LEXICON x_22_pl_var
 +Dial/-Por:^WG%>       gen_tten           ;
 +Dial/-Por:%>          par_A              ;
 +Dial/-Por:%>          ess_n                ; ! karamelline
 +Dial/-Por:%>          com_ne             ; ! karamellina

! +Pl:%>i2        gen_n              ; opettaajiin
! +Pl+Tra:%>i2ksi K ; Lene removed this one
! !+Pl+Tra:%>i2kse     PxxK               ;

LEXICON n_22odd !!= * @CODE@  kajava
      +N:  x_22odd ;
 +Sg+Nom:  K       ;

LEXICON x_22odd !!= * @CODE@  kajava
          +Sg:^WG          sgpl_cases_odd ;
          +Sg:^WG          gen_n_sg       ;
 +Dial/Por+Sg:             par_V          ;
+Dial/-Por+Sg:             par_A          ;
          +Sg:^HMETA       ill_hVVn       ;
          +Sg:             ess_nn         ;

          +Pl:^WG%>i2      sgpl_cases_odd ; ! Adding  to block i:0
          +Pl:^WG          nom_t          ;
 +Dial/Por+Pl:^WG%>i2i     gen_tten       ; ! kajaviitten
+Dial/-Por+Pl:^WG%>i2      gen_tten       ; ! kajavitten
 +Dial/Por+Pl:%>i2i        par_tA         ; ! kajaviita
 +Dial/Por+Pl:%>i2%>i      par_end        ; ! %> i necessary, otherwise a:o NOTwork, kajavii
+Dial/-Por+Pl:%>i2%>       par_A          ; ! kajavia
!      +Pl+Ill:%^HMETA%>hii n_PxK          ;
          +Pl:%^HMETA      ill_hiin       ;
          +Pl:%>i2         ess_nn         ;
          +Pl:%>i2         com_ne         ; ! kajavine
! +Pl+Tra:%>i2%>ksi    PxxK           ;



LEXICON n_22oddi !!= * @CODE@  meininki, tunturi
        +N:     x_22oddi ;
 +N+Sg+Nom:     R        ;
     +N+Sg:^WG  gen_n    ;

LEXICON x_22oddi !!= * @CODE@  meininki, tunturi
          +Sg+Nom:        K               ;
              +Sg:^WG     sgpl_cases_odd  ;
              +Sg:^WG     gen_n_sg        ;
     +Dial/Por+Sg:        par_V           ;
    +Dial/-Por+Sg:        par_A           ;
              +Sg:^HMETA  ill_hVVn        ;
              +Sg:        ess_nn          ;

              +Pl:^WG     nom_t           ;
     +Dial/Por+Pl:^WG%>i2 sgpl_cases_even ; ! Adding  to block i:0
    +Dial/-Por+Pl:^WG%>   sgpl_cases_odd  ; ! Adding  to block i:0
     +Dial/Por+Pl:^WG%>i2 gen_tten        ;
    +Dial/-Por+Pl:^WG%>   gen_tten        ;
     +Dial/Por+Pl:%>i2    par_tA          ;
    +Dial/-Por+Pl:%>      par_A           ;
              +Pl:^HMETA  ill_hiin        ;
              +Pl:        ess_nn          ;
     +Dial/Por+Pl:i2      com_ne          ;
    +Dial/-Por+Pl:        com_ne          ;


!  Tarkista, onko yaml oikein:
!  [24/25][FAIL] meininki+N+Pl+Ess => Missing results: meininkiinä
!  [24/25][FAIL] meininki+N+Pl+Ess => Unexpected results: meininkkiinä
!  [25/25][FAIL] meininki+N+Pl+Com => Missing results: meininkiine
!  [25/25][FAIL] meininki+N+Pl+Com => Unexpected results: meininkkiine



LEXICON n_22oi        !!= * @CODE@  Kulkkiija, paivukko, sivakka, perintö, kurkkio
 +N: x_22oi ;

LEXICON x_22oi
      +Sg+Nom:                   K               ; ! sg nom
 +Dial/Por+Sg:                   par_V           ;
+Dial/-Por+Sg:                   par_A           ;
          +Sg:^WG                gen_n_sg        ;
          +Pl:^WG                nom_t           ;
          +Sg:^WG                sgpl_cases_odd  ; ! these two lexica for cases with
          +Pl:^AO^WG%>i2         sgpl_cases_even ; ! the same affix in sg and pl
          +Sg:^HMETA             ill_hVVn        ;
          +Pl:^AO^WG%>i2         gen_tten        ;
          +Pl:^AO^WG%>i2         par_tA          ;
          +Sg:                   ess_nn          ; ! paivukkonna
          +Pl:^AO^HMETA          ill_hVin        ;
          +Pl:^AO%>i2            ess_n           ;! paivukoina, TODO kulkijoina ei toimi jos on ^AO, miksi?
          +Pl:^AO%>i2            com_ne          ;! paivukoine, TODO kulkijoine ei toimi jos on ^AO, miksi?
!      +Pl+Ill:^AO^HMETA%>h^V%>i2 n_PxK           ;
         !+Ill:         %>h^V%>i2 n_PxK           ;
!+Ill:h%>i2%>i2     n_PxK ; ! on Sg:ssä hVVn


LEXICON n_23        !!= * @CODE@  valkkee, hoppee, kauhhee
      +N+Dial/Por:           x_23            ;
!    +N+Dial/-Por:           x_23_-Por       ;
 ! +N+Sg+Nom:   R    ; Is this extra and wrong?

LEXICON x_23
          +Sg+Nom:e          K               ;
 +Sg+Nom+Err/Orth:%>%^A      K               ;
              +Sg:e          sgpl_cases_even ;
              +Sg:e          gen_n_sg        ;
              +Pl:e          nom_t           ;
              +Sg:e          ill_sseen       ;
              +Pl:e%>i2      ill_ssiin       ;
              +Sg:e          par_tA          ;
              +Sg:e          ess_n           ;
              +Pl:e%>i2      par_tA          ;
              +Pl:e%>i2      gen_tten        ;
              +Pl:e%>i2      sgpl_cases_even ;
              +Pl:e%>i2      ess_n           ;
              +Pl:e%>i2      com_ne          ;

!LEXICON x_23_-Por ! look at x_22oi for inspiration, as the -ki cases work
!      +Sg:^E2I^A     nom             ; ! valkia
!      +Sg:^E2I^A     gen             ; ! valkian
!      +Sg:^E2I^A     par             ; ! valkiaa
!      +Sg:^E2I^A     tra             ; ! valkiaksi
!      +Sg:^E2I^A     abe             ; ! valkiatta
!      +Sg:^E2I^A     ine             ; ! valkiassa
!      +Sg:^E2I^A     ela             ; ! valkiasta
!      +Sg:^E2I       ill             ; ! valkihaan
!      +Sg:^E2I^A     ade             ; ! valkialla
!      +Sg:^E2I^A     abl             ; ! valkialta
!      +Sg:^E2I^A     all             ; ! valkialle
!      +Sg:^E2I^A     ess_n           ; ! valkianna

!      +Pl:^E2I^A     nom             ; ! valkiat
!      +Pl:%>i2       gen             ; ! valkeitten
!      +Pl:%>i2       par             ; ! valkeita
!      +Pl:%>i2       tra             ; ! valkeiksi
!      +Pl:%>i2       abe             ; ! valkeitta
!      +Pl:%>i2       ine             ; ! valkeissa
!      +Pl:%>i2       ela             ; ! valkeista
!      +Pl:%>i2       ill             ; ! valkeissiin
!      +Pl:%>i2       ade             ; ! valkeila
!      +Pl:%>i2       abl             ; ! valkeilta
!      +Pl:%>i2       all             ; ! valkeile
!      +Pl:%>i2       ess_n           ; ! valkeina
!      +Pl:%>i2       com_ne          ; ! valkeine


LEXICON n_23ii        !!= * @CODE@  assii
 +N+Dial/Por:         x_23ii           ;
+N+Dial/-Por:         x_22oi           ; ! skriv dyp kommentar her
 +N+Sg+Nom:           R                ;

LEXICON x_23ii
      +Sg+Nom:        K               ;
          +Sg:        par_tA          ;
          +Sg:        ill_sseen       ;
          +Pl:        ill_ssiin       ;
          +Sg:        ess_n           ;
 +Dial/Por+Pl:        gen_tten        ;
+Dial/-Por+Pl:^AO%>i2 gen_tten        ;
 +Dial/Por+Pl:        par_tA          ;
+Dial/-Por+Pl:^AO%>i2 par_tA          ;
          +Sg:        gen_n_sg        ;
          +Pl:        nom_t           ;
          +Sg:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
 +Dial/Por+Pl:        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
+Dial/-Por+Pl:^AO%>i2 sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
          +Pl:        ess_n           ;
          +Pl:^AO     com_ne          ;


LEXICON n_31        !!= @CODE@  kieli
        +N:     x_31 ;
 +N+Sg+Nom:^E2I R    ; ! ^E2I e to i

LEXICON x_31
  +Sg+Nom:^E2I        K               ; ! ^E2I e to i
      +Sg:^EDEL       par_tA          ;
      +Sg:^WG         gen_n_sg        ;
      +Pl:^WG         nom_t           ;
      +Sg:^WG         sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
      +Sg:            ess_n           ;
      +Pl:%>i2        sgpl_cases_even ; !! * @CODE@  for same affix in sg and pl
      +Sg:^HMETA      ill_hVVn        ;
      +Pl:^HMETA      ill_hiin        ;

 +Dial/Por+Pl:%>i2i   gen_n           ;
+Dial/-Por+Pl:        gen_ten         ; ! kielten
+Dial/-Por+Pl:%>i2    gen_tten        ; ! kielitten, sienitten
!     +Pl:%>i2        gen_Vn          ;
!    !+Pl:^EDEL       gen_ten         ;
 +Dial/Por+Pl:%>i2    par_V           ; ! kielii
+Dial/-Por+Pl:%>i2    par_A           ; ! kieliä
          +Pl:%>i2    ess_n           ;
          +Pl:%>i2    com_ne          ;

LEXICON n_31m        !!= @CODE@ lumi lu
        +N:   x_31m ;
 +N+Sg+Nom:mi R     ; ! ^EI e to i

LEXICON x_31m
 +Sg+Nom:mi           K               ; ! ^EI e to i
     +Sg:n            par_tA          ;
     +Sg:me^WG        gen_n_sg        ;
     +Pl:me^WG        nom_t           ;

     +Sg:me^WG        sgpl_cases_even ; ! these two lexica for cases with
     +Sg:me           ess_n           ;
     +Pl:m^WG%>i2     sgpl_cases_even ; ! the same affix in sg and pl

 +Dial/Por+Pl:me%>i2       gen_Vn          ; ! lumiin
+Dial/-Por+Pl:m%>i2        gen_tten        ; ! lumitten
 +Dial/Por+Pl:me%>i2       par_V           ; ! lumii
+Dial/-Por+Pl:me%>i2       par_A           ; ! lumia

 +Sg+Ill:m^HMETA%>hee n_PxK           ;
     +Sg:m^HMETA       ill_hVVn        ; ! ei toimi viel, twolc issues
! +Pl+Ill:m^EDEL%>hii  n_PxK           ;
     +Pl:m^EDEL       ill_hiin        ;
     +Pl:me%>i2       ess_n           ;
     +Pl:m%>i2        com_ne          ; ! kanskje me


LEXICON n_31si        !!= @CODE@ vuosi, käsi, hirsi
        +N: x_31si ;
 +N+Sg+Nom: R      ;

LEXICON x_31si
      +Sg+Nom:^E2I     K               ;
!                       nomgen_px       ; ! sg nom/gen px
          +Sg:^EDEL    par_tA          ; ! parsg
          +Sg:^WG      gen_n_sg        ;
          +Pl:^WG      nom_t           ;
          +Sg:^HMETA   ill_hVVn        ;
          +Sg:^WG      sgpl_cases_even ; ! these two lexica for cases with
          +Pl:%>i2     sgpl_cases_even ; ! the same affix in sg and pl
          +Sg:         ess_n           ;
          +Pl:%>i2     ess_n           ;
 +Dial/Por+Pl:%>i2     gen_Vn          ; ! vuossiin, ei vuottiin, hirssiin
+Dial/-Por+Pl:%>i2     gen_tten        ; !hirssitten, vuossitten
 +Dial/Por+Pl:%>i2     par_V           ; ! hirssii
+Dial/-Por+Pl:%>i2%>   par_A           ; ! hirsiä
       +Pl:^HMETA      ill_hiin        ;
          +Pl:%>i2     com_ne          ;

!  LEXICON n_31lapsi        !!= @CODE@ lapsi la
!           +N:              x_31lapsi       ;
!    +N+Sg+Nom:psi           K               ; ! ^EI e to i
!    +N+Sg+Nom:psi           R               ; ! ^EI e to i
! Add in when remember how to not make bug and crash compile, -Sindre

LEXICON n_31lapsi        !!= @CODE@ lapsi la
      +N+Sg+Nom:psi           K               ; ! ^EI e to i
      +N+Sg+Nom:psi           R               ; ! ^EI e to i
          +N+Sg:pse           gen_n_sg        ; ! should be ^WG?
          +N+Pl:s             gen_ten         ;
          +N+Sg:s             par_tA          ;
          +N+Sg:pse^WG        gen_n_sg        ;
          +N+Pl:pse^WG        nom_t           ;
          +N+Sg:pse^WG        sgpl_cases_even ; !! these two lexica for cases with
          +N+Sg:pse           ess_n           ;
          +N+Pl:s             gen_ten         ; ! lasten
          +N+Pl:ps^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
 +N+Dial/Por+Pl:ps%>i2%>i     gen_n           ; ! lapsiin
+N+Dial/-Por+Pl:ps%>i2        gen_tten        ; ! lapsitten
 +N+Dial/Por+Pl:ps%>i2%>i     par_end         ; ! lapsii
+N+Dial/-Por+Pl:ps%>i2%>      par_A           ; ! lapsia
      +N+Sg+Ill:ps^HMETA%>hee n_PxK           ;
          +N+Sg:ps^HMETA      ill_hVVn        ;
          +N+Pl:ps^EDEL       ill_hiin        ;
          +N+Pl:pse%>i2       ess_n           ;
          +N+Pl:ps%>i2        com_ne          ; ! kanskje pse


LEXICON n_31mies        !!= @CODE@ mies mie
      +N+Sg+Nom:s            K               ; ! ^EI e to i
      +N+Sg+Nom:s            R               ; ! ^EI e to i
          +N+Sg:he           gen_n           ;
          +N+Sg:s            par_tA          ;
          +N+Sg:he^WG        gen_n_sg        ;
          +N+Pl:he^WG        nom_t           ; ! TODO remove one of these
          +N+Pl:he           nom_t           ; ! TODO remove one of these
          +N+Sg:he^WG        sgpl_cases_even ; !! these two lexica for cases with
          +N+Sg:he           ess_n           ;
          +N+Pl:h^WG%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +N+Pl:s            gen_ten         ;
 +N+Dial/Por+Pl:h%>i2%>i     gen_n           ; ! miehiin
+N+Dial/-Por+Pl:h%>i2%>      gen_tten        ; ! miehitten
 +N+Dial/Por+Pl:h%>i2%>i     par_end         ; ! miehii
+N+Dial/-Por+Pl:h%>i2%>      par_A           ; ! miehiä
      +N+Sg+Ill:h^HMETA%>hee n_PxK           ;
          +N+Sg:h^HMETA      ill_hVVn        ;
!      +N+Pl+Ill:hhii         n_PxK           ;
          +N+Pl:h            ill_hiin        ;
          +N+Pl:he%>i2       ess_n           ;
          +N+Pl:h%>i2        com_ne          ;


LEXICON n_32 !!= @CODE@ tytär, taival
        +N:    x_32 ;
 +N+Sg+Nom:^WG R    ;

LEXICON x_32
 +Sg+Nom:^WG          K              ;
     +Sg:e            gen_n_sg       ;
     +Pl:e            nom_t          ;
     +Sg:e^WG         gen_n_sg       ;
     +Pl:e^WG         nom_t          ;
     +Sg:^WG          par_tA         ;
     +Sg:e            gen_n          ;
     +Sg:e^HMETA      ill_hVVn       ;
     +Sg:e            sgpl_cases_odd ;
     +Sg:e            ess_nn         ;
     +Pl:%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Dial/Por+Pl:%>i%>i2      gen_tten  ; ! tyttäriitten
+Dial/-Por+Pl:%>i%>        gen_tten  ; ! tyttäritten

 +Dial/Por+Pl:%>i2         par_V     ; ! tyttärii
 +Dial/Por+Pl:%>i%>i2      par_tA    ; ! tyttäriitä
+Dial/-Por+Pl:%>i%>        par_A     ; ! tyttäriä

          +Pl:^HMETA   ill_hiin      ;
          +Pl:e%>i2     ess_nn       ;
          +Pl:e%>i2     com_ne       ;


LEXICON n_32m !!= @CODE@ elläin elläi
        +N:     x_32m ;
 +N+Sg+Nom:^WGn R     ;

LEXICON x_32m !!= @CODE@
  x_32m_sg ;
  x_32m_pl ;

LEXICON x_32m_sg
 +Sg+Nom:^WGn      K              ;
     +Sg:^WG%>n    par_tA         ;
     +Sg:me        gen_n_sg       ;
     +Pl:me        nom_t          ;
     +Sg:me^HMETA  ill_hVVn       ;
     +Sg:me        sgpl_cases_odd ;
     +Sg:me        ess_nn         ;
     +Pl:me%>i2    ess_nn         ;
     +Pl:me%>i2    com_ne         ;

LEXICON n_32m_pl !!= @CODE@
      +N: x_32m_pl ;

LEXICON x_32m_pl
     +Sg:me            gen_n_sg       ;
     +Pl:me            nom_t          ;
     +Pl:m%>i2         sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl

 +Dial/Por+Pl:m%>ii2   gen_tten       ; ! elläimiitten
+Dial/-Por+Pl:m%>i     gen_tten       ; ! elläimitten

 +Dial/Por+Pl:mi%>i2   par_end        ; ! elläimii
 +Dial/Por+Pl:mi%>i2   par_tA         ; ! elläimiitä
+Dial/-Por+Pl:mi%>     par_A          ; ! elläimiä

     +Pl:m^HMETA       ill_hiin       ;
     +Pl:m%>i2         com_ne         ;

LEXICON n_32s !!= @CODE@  sairhaus
  +N+Sg+Nom:^WGs           K                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
  +N+Sg+Nom:^WGs           R                 ; !! * Konsonanttivartalo: poista -e ja heikko aste
      +N+Sg:%>t            par_tA            ;
      +N+Sg:đe             gen_n_sg          ;
      +N+Pl:đe             nom_t             ;
      +N+Sg:te^HMETA       ill_hVVn          ;
      +N+Sg:đe             sgpl_cases_odd    ;
      +N+Sg:te             ess_nn            ;
      +N+Pl:ks%>i2         sgpl_cases_odd    ; ! TODO wtf is this comm --> hmm, tämä menee pieleen, sg ≠ pl
      +N+Pl:ksi%>i2        par_end           ;
      +N+Pl:ksi%>i2        par_tA            ;
      +N+Pl:ksi%>i2        gen_tten          ;
      +N+Pl:ks^HMETA       ill_hiin          ;
      +N+Pl:ks%>i2         ess_nn            ;
      +N+Pl:ks%>i2         com_ne            ;


! LEXICON n_32s != @CODE@  sairhaus vanha versio
!        +N:t              long_par          ;
!        +N:te             obl_strong        ;
!        +N:te^WG          weak_v_stem       ;
!     +N+Sg:^WG            sgpl_cases_odd    ;
!     +N+Pl:^WG%>i2        sgpl_cases_odd    ;
!     +N+Pl:ks%>i2         par_V             ;
!     +N+Pl:ks%>i2         gen_Vn            ;
!     +N+Pl:ks%>i2         horterillpl      ;

!LEXICON n_32t
                           !n_32             ; ! ei tehty

!LEXICON n_33 ! ei tehty
                           !n_33s            ;

LEXICON n_33as !!= @CODE@ rakas, asukas
 +N: x_33as ;

LEXICON x_33as
      +Sg+Nom:a^WG%>s      K                 ;
          +Sg:a^WG%>s      par_tA            ;
          +Sg:haa          ill_sseen         ;
!      +Pl+Ill:h%>aissiin   K                 ;
          +Sg:h%>aa        gen_n_sg          ;
          +Sg:h%>aa        ess_n             ;
          +Sg:h%>aa        sgpl_cases_even   ;
          +Pl:h%>a%>i2     sgpl_cases_even   ;
          +Pl:h%>aa        nom_t             ;
          +Pl:h%>a%>i2     gen_tten          ; ! asukkhaitten
+Dial/-Por+Pl:h%>a%>je     gen_n             ; ! rakkhajen
          +Pl:h%>a%>i2     par_tA            ; ! asukkhaita
+Dial/-Por+Pl:h%>a%>j      par_A             ; ! rakkhajen
  ! +N+Pl+Ess:h%>a%>i2%>na PxxK              ;
          +Pl:h%>ai        ill_ssiin         ;
          +Pl:h%>aa%>i2    ess_n             ;
          +Pl:h%>a%>i2     com_ne            ;

!LEXICON n_33aes
!        +N:             x_33aes           ;

LEXICON x_33aes
   +Sg+Nom:ä^WG%>s      K                 ;
       +Sg:ä^WG%>s      par_tA            ;
       +Sg:hää          ill_sseen         ;
       +Pl:h%>äi        ill_ssiin         ;
!    +Pl+Ill:h%>äissiin   K                 ;
       +Sg:h%>ää        gen_n_sg          ;
       +Pl:h%>ää        nom_t             ;
       +Sg:h%>ää        ess_n             ;
       +Pl:h%>ää%>i2    ess_n             ;
       +Sg:h%>ää        sgpl_cases_even   ;
       +Pl:h%>ä%>i2     sgpl_cases_even   ;
       +Pl:h%>ä%>i2     par_tA            ;
       +Pl:h%>ä%>i2     gen_tten          ;
       +Pl:h%>ä%>i2     ess_n             ;
       +Pl:h%>ä%>i2     com_ne            ;



!LEXICON n_33ys !!= @CODE@ rakas
!        +N:        x_33ys ;
! +N+Sg+Nom:%>y^WGs R      ;

LEXICON x_33ys ! TODO DELETE THIS PROBABLy! ONLY ONE WORD, AN ADJECTIVE, IS IN IT
   +Sg+Nom:%>y^WG%>s    K                 ;
       +Sg:%>y^WG%>s    par_tA            ;
       +Sg:h%>yy        ill_sseen         ;
       +Pl:h%>yi        ill_ssiin         ;
       +Sg:h%>yy        gen_n_sg          ;
       +Pl:h%>yy        nom_t             ;
       +Sg:h%>yy        sgpl_cases_even   ;
       +Pl:h%>y%>i2     sgpl_cases_even   ;
       +Pl:h%>y%>i2     par_tA            ;
       +Pl:h%>y%>i2     gen_tten          ;
 +N+Pl+Ess:h%>y%>i2%>ny PxxK              ; ! not to ess n pecause of the y I guess? wat
       +Pl:h%>y%>i2     com_ne            ;


LEXICON n_33is !!= @CODE@ ruvis, ruumis
 +N: x_33is ;

LEXICON x_33is
      +Sg+Nom:i^WG%>s    K                 ;
          +Sg:i^WG%>s    par_tA            ;
          +Sg:hii        gen_n_sg          ;
          +Pl:hii        nom_t             ;
          +Sg:hii        sgpl_cases_even   ;
          +Pl:hii        sgpl_cases_even   ;
          +Sg:hii        ess_n             ;
          +Pl:hii        gen_tten          ; ! rukkhiitten
          +Pl:hii        par_tA            ; ! rukkhiita
+Dial/-Por+Pl:hi%>je     gen_n             ; ! rukhijen
+Dial/-Por+Pl:hi%>j      par_A             ; ! rukhija
          +Pl:%>hii      ess_n             ;
          +Sg:%>hii      ill_sseen         ;
          +Pl:hii        ill_ssiin         ;
          +Pl:hi2i       com_ne            ;

!LEXICON n_33  !!= @CODE@ odottaessa viitataan n_33t-hen
!  n_33t ;

LEXICON n_33t !!= @CODE@ venet, hyljet, huonet
                 +N:   x_33t ;
! +Dial/Por+N+Sg+Nom:et R     ;
!+Dial/-Por+N+Sg+Nom:e  R     ;
! +Dial/Var+N+Sg+Nom:e  R     ;

LEXICON x_33t
     +Dial/Por+Sg:e^WG        nom_t          ;
+Dial/-Por+Sg+Nom:e^WG        K              ;
              +Sg:e^WG%>t     par_tA         ;
                 :hee         33_t_ts_common ;
    +Dial/-Por+Pl:he%>je      gen_n          ; ! venhejen
    +Dial/-Por+Pl:he%>j       par_A          ; ! venhejä
              +Pl:hee%>i2     com_ne         ;

LEXICON n_33ts !!= @CODE@ kirves
        +N:   x_33ts ;
 +N+Sg+Nom:es R      ;

LEXICON x_33ts !!= @CODE@ kirves
      +Sg+Nom:es          K              ;
          +Sg:es          par_tA         ;
             :hee         33_t_ts_common ;
+Dial/-Por+Pl:he%>je      gen_n          ; ! Kirvhejen
+Dial/-Por+Pl:he%>j       par_A          ; ! Kirvhejä
!      +Pl+Gen:hee%>i2     gen_tten       ; ! Can this be commented out? yes, is in the common
          +Pl:hee%>i2     com_ne         ;

LEXICON 33_t_ts_common
      +Sg:         gen_n_sg        ;
      +Pl:         nom_t           ;
      +Sg:         sgpl_cases_even ; !! these two lexica for cases with
      +Sg:         ess_n           ;
      +Pl:%>i2     sgpl_cases_even ; !! the same affix in sg and pl -- tsekkaa long
      +Sg:         ill_sseen       ;
      +Pl:%>i2     gen_tten        ;
      +Pl:%>i2     par_tA          ;
! +Pl+Ill:heissiin K               ;
      +Pl:%>i2     ill_ssiin       ;
      +Pl:%>i2     ess_n           ;
      +Pl:%>i2     com_ne          ;


LEXICON n_41 !!= * @CODE@ = veres
        +N:     x_41     ;
 +N+Sg+Nom:s    R        ;
     +N+Sg:kse gen_n_sg ;

LEXICON x_41
      +Sg+Nom:^WG%>s     K              ;
          +Sg:kse        gen_n_sg       ;
          +Pl:kse        nom_t          ;
          +Sg:kse        sgpl_cases_odd ; ! these two lexica for cases with
          +Sg:%>s        par_tA         ;
          +Sg:kse        ess_nn         ;
          +Pl:kse%>i2    sgpl_cases_odd ; ! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        gen_ten        ; ! veresten
          +Pl:ks%>i2     gen_tten       ; ! vereksitten
 +Dial/Por+Pl:ks%>i2     par_V          ; ! vereksii
 +Dial/Por+Pl:ks%>i2i    par_tA         ; ! vereksiitä
+Dial/-Por+Pl:ks%>i2     par_A          ; ! vereksiä
      +Sg+Ill:ksheen     K              ;
          +Sg:ks         ill_hVVn       ;
!      +Pl+Ill:ksh%>i2in  K              ;
          +Pl:ks         ill_hiin       ;
          +Pl:kse%>i2    ess_nn         ;
          +Pl:kse%>i2    com_ne         ;
 ! +Pl+Par:%>ksii     K              ;

!LEXICON s_kse
! +N:^WG # ;
LEXICON n_41pl !!= * @CODE@ = veljekset
           +N:           x_41pl          ;
    +N+Pl+Nom:%>kset     K               ;

LEXICON x_41pl !!= * @CODE@ = veljekset
          +Sg:kse        gen_n_sg        ;
          +Pl:kse        nom_t           ;
          +Sg:kse        sgpl_cases_odd  ; !! these two lexica for cases with
          +Pl:kse%>i2    sgpl_cases_odd  ; !! the same affix in sg and pl -- tsekkaa long
          +Pl:%>s        gen_ten         ; ! veljesten
          +Pl:ks%>i2     gen_tten        ; ! veljeksitten
 +Dial/Por+Pl:ks%>i2     par_V           ; ! veljeksii
 +Dial/Por+Pl:ks%>i2i    par_tA          ; ! veljeksiitä
+Dial/-Por+Pl:ks%>i2     par_A           ; ! veljeksiä
!      +Pl+Ill:ksh%>i2in  K               ;
          +Pl:ks         ill_hiin        ;
          +Sg:kse        ess_n           ;
          +Pl:kse%>i2    ess_nn          ; ! veljeksinnä
          +Pl:ks%>i2     com_ne          ; ! veljeksine
  !    +Pl+Par:%>ksii     K               ;


!LEXICON et_he
!        +N:^WG        #               ;

!LEXICON n_42 ! itse asiassa n_42even
!     +N+Sg:se         sgpl_cases_even ; !! these two lexica for cases with
!     +N+Pl:s%>i2      sgpl_cases_even ; !! the same affix in sg and pl
!        +N:           nen             ;
! +N+Pl+Com:s%>i2%>ne  K               ;

LEXICON n_42 ! nainen, kainulainen
        +N:  x_42 ;
 +N+Sg+Nom:s R    ;


LEXICON x_42 ! nainen, kainulainen
      +Sg+Nom:nen       K               ;
          +Sg:se        gen_n_sg        ;
          +Pl:se        nom_t           ;
          +Sg:s         par_tA          ;
          +Sg:se^HMETA  ill_hVVn        ;
          +Sg:se        sgpl_cases_even ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_even ; !! the same affix in sg and pl
          +Sg:se        ess_n           ;
          +Pl:s         gen_ten         ; ! kainulaisten
 +Dial/Por+Pl:ss%>i2i   par_end         ; ! kainulaissii
 +Dial/Por+Pl:ss%>i2i   par_tA          ; ! kainulaissiita, naissiita
+Dial/-Por+Pl:s%>i2     par_A           ; ! kainulaisia
          +Pl:s         ill_hVin        ; ! NB testing, used to be next line: UNDOMMENT
!          +Pl:sh%>i2    horterillpl    ;
          +Pl:se%>i2    ess_n           ; ! naisina
          +Pl:s%>i2     com_ne          ;

LEXICON n_42odd !!= @CODE@ ihminen
        +N:  x_42odd ;
 +N+Sg+Nom:s R       ; ! ihmiskala

LEXICON x_42odd
      +Sg+Nom:nen       K              ;
          +Sg:se        ess_nn         ;
          +Sg:se        gen_n_sg       ;
          +Pl:se        nom_t          ;
          +Sg:s         par_tA         ;
          +Sg:se^HMETA  ill_hVVn       ;
          +Sg:se        sgpl_cases_odd ; !! these two lexica for cases with
          +Pl:s%>i2     sgpl_cases_odd ; !! the same affix in sg and pl
          +Pl:s         gen_ten        ;
 +Dial/Por+Pl:ss%>i2i   par_end        ; ! ihmissii
 +Dial/Por+Pl:ss%>i2i   par_tA         ; ! ihmissiitä
 +Dial/Var+Pl:s%>i2     par_A          ; ! ihmisiä
          +Pl:s         ill_hiin       ; ! ihmishiin
          +Pl:s%>i2     ess_nn         ; !
          +Pl:s%>i2     com_ne         ;

LEXICON n_42pl ! !!= @CODE@ tervheiset tervhei
          +N+Pl:%>se       nom_t          ;
          +N+Pl:s%>i2      sgpl_cases_odd ; !! the same affix in sg and pl
          +N+Pl:s          gen_ten        ;
 +Dial/Por+N+Pl:ss%>i2     par_V          ; ! tervhehissii
 +Dial/Por+N+Pl:ss%>i2i    par_tA         ; ! tervhehissiitä
+Dial/-Por+N+Pl:s%>i2      par_A          ; ! tervhehisiä
!      +N+Pl+Ill:sh%>i2in   K              ;
          +N+Pl:s          ill_hiin       ;
          +N+Pl:se%>i2     ess_nn         ;
      +N+Pl+Com:s%>i2%>nne K              ; ! TODO onko tässä kaksi nn:ää?

!LEXICON nen  !!= @CODE@ on nainen-sanoille
!  nen_sg ;
!  nen_pl ;

!LEXICON nen_sg
!             +N:     nom-nen        ;
!!            +N:se   nomgen_px      ;
!            +Sg:se     ess_n        ;
!            +Pl:se%>i2 ess_n        ;

!! make+N+Pl+kom:sine K              ;
!             +N:se   weak_v_stem ;
!          +N+Sg:s    par_tA         ;
!             +N:se   2AVU_V_ill_sg ;
!
!LEXICON nen_pl
!        +N:se        weak_v_stem ;
! +N+Pl+Ill:sh%>i2in  K              ;
!     +N+Pl:s         gen_ten        ;
!     +N+Pl:ss%>i2i   par_end        ;
!     +N+Pl:sh%>i2    horterillpl   ;
! +N+Pl+Com:s%>i2     com_ne         ;


!LEXICON nom-nen
! +Sg+Nom:nen K ;


!LEXICON loput
!                 obl_strong_odd ; ! muut vahvat
!                 weak_v_stem    ;
!        +Sg:     sgpl_cases_odd ; !! these two lexica for cases with
!        +Pl:%>i2 sgpl_cases_odd ; !! the same affix in sg and pl -- tsekkaa long
!        +Pl:%>i2 gen_ten        ;
! +Pl+Use/NG:%>i2 gen_tten       ;
!        +Pl:%>i2 par_V          ;



!! !!! Basic paradigms
!  ===================


!LEXICON weak_v_stem !!= @CODE@ This bs is gonna get deleted. Replase the genetives with gen_n and just add the nom,
! +Sg: gen_n_sg ;
! +Pl: nom_t    ;


!! !Sublexica for the basic unified cases, with even and odd variations
! ----------------------------------------------------------------------

LEXICON sgpl_cases_even ! kaikille paitsi neljän koplalle
 +Ade:%>l^A PxxK              ; ! Lyhyt perusmuoto vuođela
 +All:%>le  PxxK              ; ! Lyhyt perusmuoto
            sgpl_cases_common ;

LEXICON sgpl_cases_odd ! neljän koplalle. <- Mitä vittua tää kopla tarkoittaa tässä kontekstissa? Tämä ei ole aku ankka!
 +Ade:%>ll^A PxxK              ; ! Pitkä perusmuoto
 +All:%>lle  PxxK              ; ! Pitkä perusmuoto
             sgpl_cases_common ;

LEXICON sgpl_cases_common
  +Abl:%>lt^A PxxK ;
  +Ine:%>ss^A PxxK ;
  +Ela:%>st^A PxxK ;
  +Tra:%>ksi  K    ;
! +Tra:%>kse  Pxx  ;
  +Abe:%>tt^A PxxK ;



!! !Sublexica for Gen, Par, Ill, Ess and Com.
! ---------------------------------------------------------------------


LEXICON nom_t
+Nom:%>t K     ; ! Why not PxxK ?


LEXICON gen_n_sg ! Only with sg and general separate, because of R! This is exception!
    gen_n       ; ! why not go from R -> K, then we don't have to write twice
 +Gen:%>n R     ; ! This (R) is for compound words, that's why sg is by itself

LEXICON gen_n
 +Gen: n_PxK ;

LEXICON gen_Vn
 +Gen:%>^V n_PxK ;

LEXICON gen_tten  ! vereksitten
 +Gen:%>tte n_PxK ;

LEXICON gen_ten  ! veljesten
 +Gen:%>te n_PxK ;



LEXICON par_A
   +Par:%>^A PxxK ;

LEXICON par_V
+Par         :%>^V PxxK ;
+Par+Err/Orth:%>^A PxxK ;

LEXICON par_tA ! pitäiskö olla omat tt^A ja st^A ? Mange Sg er slik
 +Par:%>t^A PxxK ;

LEXICON par_end
  +Par: PxxK ;


 ! <-------------- Singulaarit

LEXICON ill_hVn ! NB Used 1 ONCE, only: 1.  hVn > maahan
+Ill:%>h^V      n_PxK ; !  +Sg+Ill:%>h^V n_PxK ;

LEXICON ill_hVVn !  2.  hVVn > tunturhiin | sykshyyn, linnhaan | linna pl.ill. kaksi muotoa po. linnhoin,
+Ill:%>h^V^V    n_PxK ; ! TODO add Twolc description about HMETA   ! 2.  hVVn > tunturhiin

LEXICON ill_sseen !  3.  sseen > rukhiisseen, kuninkhaa>sseen, previously mTAVU_VV-ill
+Ill:%>ssee     n_PxK ;


! <--------------- Pluralit

LEXICON ill_hin ! NB Used 1 ONCE, only: 4.  hin > maihin
+Ill:%>hi       n_PxK ;

LEXICON ill_hVin !  5.  hVin > tunturhiin (ja sivakkhoin), ehkä pitäis pl:ssä olla i2 jos yhteiset sg & pl
+Ill:%>h^V%>i2     n_PxK ; ! > for twolc. Find good exlanation TODO fix this on twolc.
!+Ill:h%>i2%>i2     n_PxK ; ! on Sg:ssä hVVn
! TODO What about this i2>i2thing? ugly, but works on some things, should it be merged with ^V &/or hiin?

LEXICON ill_ssiin !  6.  ssiin > rukhiissiin, syksyissii
+Ill:%>ssii     n_PxK ;!      säänön mukhaan hVn (maahan) saa -hin monikossa (maihin)

LEXICON ill_hiin ! NB USED 5 TIMES, always pl & either ^HMETAh%>i2i or ^HMETA%>hii
 +Ill:%>hii%> n_PxK ; ! gives twolc errors if i2 and if h before >
! used to be in ihminen: shi2%>i2n, which is seen in hVin too.




 LEXICON ess_n
  +Ess:%>n^A PxxK ;

 LEXICON ess_nn
  +Ess:%>nn^A PxxK ; ! Ess aina Eiran gramatiikin mukaan lyhytmuoto, Mutta Eira s. 38 kaupunkinna, tyttärennä



 LEXICON com_ne    ! Komitatiivilla on vain -ine ja menee Pxx:iin
  +Com:%>ne PxxK ; ! Not to K, since Px must be present ! Note that adjectives have -ine without Px
! one exception, in terhveiset it is two n's



 !LEXICON longgenpl
 ! ! d_longgenpl ;   oletetaan, että -tten vaan
 !  gen_tten     ;

 !LEXICON d_longgenpl
 ! +Gen:%>de n_PxK ;


!LEXICON longillpl
! +Ill: n_PxK ; ! En tiedä, toimiiko tämä

 !LEXICON h_shortillpl
 ! +Ill:^EDEL%>hii n_PxK ;

!LEXICON longerillpl
! +Pl+Ill:%>s^V^V n_PxK ;













!! !Sublexica for possessive suffixes
! ---------------------------------

!! Px is now not in use, with one exception, comitative.

LEXICON n_PxK  !!= @CODE@ has either -n or goes to Px
  :n K  ;
!    Px ;

LEXICON i_PxK  !!= @CODE@ Tra: -i or -e and goes to Px
  :i K  ;
! :e Px ;

LEXICON PxK !!= @CODE@ has only -nsA, and is currently not in use. Check
   K  ;
!  Px ;

LEXICON PxxK  !!= @CODE@ has also -Vn, thus both .. llensa and ..lleen.
   K     ;
!  Px    ;
!  Px-Vn ;

LEXICON Pxx
  Px    ;
  Px-Vn ;

LEXICON Px
 +PxSg1:%>ni   K ;
 +PxSg2:%>sti  K ;
 +PxSg3:%>ns^A K ;
 +PxPl1:%>mm^A K ; ! NB! -m^A ei hoidettu
 +PxPl2:%>nn^A K ; ! NB! -n^A ei hoidettu
 +PxPl3:%>ns^A K ;


LEXICON Px-Vn
 +PxSg3:%>^Vn K ;
 +PxPl3:%>^Vn K ;































 !!! -------- Graveyard





 !! !!! Basic paradigms
 !  ===================

 !! !!Basic vowel stems
 !  --------------------

! LEXICON nom_strong
!  +Sg+Nom: K ;

 !LEXICON strong_v_stem_even  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 !  short_par       ;
 !    +Pl:%>i2 com_ne    ;
 !    +Sg:         ess_n           ;
 !    +Pl:%>i2     ess_n           ;
 ! TODO fix this mess

 !LEXICON strong_v_stem_odd  !!= @CODE@ ''Kaikki vahvan asteen sijamuodot tähän''
 !  short_par      ;
 !  obl_strong_odd ;




 !! !Sublexica for the vowel stems
 !  - - - - - - - - - - - - - - -

 ! +Pl+Ins:%>i2%>n K ; !! Eira doesn't use this.



 !LEXICON obl_strong_even
 !          nomgen_px ;
 !  +Pl:%>i2 com_ne    ;
 !  +Sg:         ess_n           ;
 !  +Pl:%>i2     ess_n           ;

 !LEXICON obl_strong_odd
 !              nomgen_px ;
 !      +Pl:%>i2 com_ne    ;
 !      +Sg:         ess_nn           ;
 !      +Pl:%>i2     ess_nn           ;

 ! LEXICON nomgen_px
 ! +Sg+Nom:     Px        ; ! mattoni
 ! +Sg+Gen:     Px        ; ! mattoni
 ! +Pl+Nom:     Px        ; ! mattoni



 ! Random

! LEXICON sConsCase
!  +Sg+Nom:^WG%>s K ;
!  +Sg+Par:%>st^A K ; ! Par
!  +Pl+Gen:%>sten K ; ! Gen
