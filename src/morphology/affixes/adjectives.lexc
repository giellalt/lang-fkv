
!! Affix file for Kven adjectives

!! !!!The basic lexica

!! Each a_ lexicon gets the +A tag and is then
!! redirected to a common x_ lexicon in the noun file.

LEXICON a_x  !!= * @CODE@ contains unclassified adjectives.
+A+Sg+Nom: K ; 


LEXICON ax  !!= * @CODE@ contains unclassified adjectives.
+A: K ; 

LEXICON a_21  !!= * @CODE@ corresponds to a_21 etc.
+A: x_21 ;
!+A: a_strong_v_stem ;
!+A:%^WG a_weak_v_stem ;
:%^AE%^WG comp ;

LEXICON a_21ie  !!= * @CODE@ corresponds to a_21ie etc.
+A: x_21ie ;
comp ;
superl ;

LEXICON a_21ai  !!= * @CODE@ corresponds to a_21ai etc.
+A: x_21ai ;
comp ;
superl ;

LEXICON a_22	!!= * @CODE@  liberaali
+A: x_22 ;
comp ;
superl; 

LEXICON a_22odd	!!= * @CODE@  simppeli
+A: x_22odd ;
comp ;
superl ;

LEXICON a_23	!!= * @CODE@  pimmee
+A: x_23 ;
:e comp ;
:e%>i2 superl;
+A+Sg+Superl:e%>i2 superl_n ;

LEXICON a_23ii  !!= * @CODE@ auttii
+A: x_23ii ;
comp ;

LEXICON a_31	!!= * @CODE@  pieni
+A: x_31 ;
comp ;
:%^E2I superl ;
+A+Sg+Superl:%^E2I superl_in ;

LEXICON a_31si	!!= * @CODE@ uusi
+A: x_31si ;
:%^WG comp ;
:%^E2I superl ;
+A+Sg+Superl:%^E2I superl_in ;

LEXICON a_32 !!= * @CODE@ tyven
+A: x_32 ;
:e comp ;

LEXICON a_32m !!= * @CODE@ irtain
+A: x_32m ;

LEXICON a_32ma !!= * @CODE@ vasen sg taipuu kuitenki -ma

+A+Sg+Nom:%^WGn K ;
+A:m%^A weak_v_stem_sg ;
+A+Sg+Par:%^WG%>nt%^A PxxK ;
!+A:m%^A obl_strong_odd ;
+A:m%^A ess_odd_sg ;
+A:m ess_odd_pl;
+A+Sg+Gen:m%^A%>n K ;
+A:m%^A weak_v_stem_pl ;  ! Nom pl
!+A:m%^A 2TAVU_V_ill_sg ;
+A+Sg+Ill:mh%^A%^A n_PxK ;
+A+Sg:m%^A sgpl_cases_odd ;   
+A:m%^A weak_v_stem_pl ;
+A:m%^A weak_v_stem ;
+A+Pl:m%>i2 sgpl_cases_odd ; ! hmm, tämä menee pieleen, sg ≠ pl
+A+Pl:%>mi%>i2 longparpl ;
+A+Pl:mi%>i2 tt_longgenpl ;
+A+Pl:m%^HMETAh%>i2i shortestillpl;
+A+Pl+Com:m%>i2ne K ;


LEXICON a_33as !!= * @CODE@ rakas
+A: x_33as ;
:aa comp ;
! comp, superl, puuttuvat

LEXICON a_33aes !!= * @CODE@ rakas
+A: x_33aes ;
! comp, superl, puuttuvat


LEXICON a_33is !!= * @CODE@ valmis
+A: x_33is ;
! comp, superl, puuttuvat

LEXICON a_33ys !!= * @CODE@ kärys
+A: x_33ys ;
! comp, superl, puuttuvat

 
LEXICON a_33t !!= * @CODE@ tuoret
+A: x_33t ;
:hee comp ;
! comp, superl, puuttuvat

LEXICON a_33u !!= * @CODE@ vaipunu:vaipun ! perfect participle
+A+Sg+Nom:u K ;
!+A+Sg+Par:u%>tt%^A K ; 
!+A:hee 33_t_ts_common ;
!+A+Sg+Ill:hee mTAVU_VV-ill ;
!+A+Pl+Gen:hee%>i2 tt_longgenpl ;
!+A+Pl+Com:hee%>i2ne K ;
+A: PRFPRC_OBL  ;

LEXICON a_33y !!= * @CODE@ väsyny:äsyn ! perfect participle
+A+Sg+Nom:y K ;
+A: PRFPRC_OBL  ;

LEXICON a_41 !!= * @CODE@ = veres
+A: x_41 ;
:kse comp ;


LEXICON a_41_s_kse
+A:%^WG # ;


!LEXICON adj-nen
!+A: nen ;

!LEXICON a_42inen 
!adj-nen ;
!+A: nen ;
!+A:s R ;
!+A:s a_long_par ;
!+A+Sg:se a_sgpl_cases_even ;      ! these two lexica for cases with
!+A+Pl:s%>i2 a_sgpl_cases_even ;  ! the same affix in sg and pl
!+A+Pl:ssh%>i2 a_shorterillpl ;
!+A+Pl+Com:s%>i2ne K ;

LEXICON a_42 !!= * @CODE@ varovainen
+A: x_42 ;

LEXICON a_42odd !!= * @CODE@ sininen
+A: x_42odd ;


LEXICON MATON !!= @CODE@ ! ajattelemattomalle lle lla nna
+A: a_43 ;

LEXICON a_43 !!= * @CODE@ muuttumaton

+A: a_ton ;
+A+Sg+Ill:%>tt%^Omh%^A%^A n_PxK ;
+A:%>tt%^Om%^A sgpl_cases_common ;
+A+Pl+Ill:%>tt%^Omh%>i2in K ;
+A:%>tt%^Om%^A ess_even_sg ;
!+A:%>tt%^Omm%^A%>i2 shortestgenpl ; ! naisien
!+A:%>tt%^Omi%>i2 shortestgenpl ; ! naisien
+A:%>tt%^Omi%> long_par_pl;
!+A:%>tt%^Omh%>i2 shorterillpl ;
+A:%>tt%^Om%^A a_loputon ;  
+A:%>tt%^Om a_loputonpl ;
+A+Pl+Com:%>tt%^Om%>i2ne K ;

LEXICON a_43odd !!= * @CODE@ onneton
+A: x_43odd ;

LEXICON MATONodd !!= @CODE@ ! käymättömäle le la na
+A: x_43odd ;

LEXICON x_43odd !!= * @CODE@ onneton 
: a_ton ;
+Sg+Ill:%>tt%^Omh%^A%^A n_PxK ;
:%>tt%^Om%^A sgpl_cases_common ;
+Pl+Ill:%>tt%^Omh%>i2in K ;
+Pl:%>tt%^Omm%>i2%>i a_shortestgenpl ; ! naisien
:%>tt%^Ommi%> long_par_pl;
:%>tt%^Omh%>i2 shorterillpl ;
+Pl+Com:%>tt%^Om%>i2ne K ;
:%>tt%^Om%^A a_loputoneven ;
:%>tt%^Om a_loputonevenpl ;
+Pl+Com:%>tt%^Om%>i2ne K ;



!! !!!Basic paradigms
!  ===================

!! Most cases are directed to affixes/nouns.lexc

LEXICON a_sgpl_cases_common  !!= * @CODE@ 
 +Abl:%>lt%^A PxxK ;
 +Ine:%>ss%^A PxxK ;
 +Ela:%>st%^A PxxK ;
 +Tra:%>ksi K ;
 +Tra:%>kse Pxx ;
 +Abe:%>tt%^A PxxK ;


!! !!Lexica for the non-uniform cases
!    ---------------------------------

  
LEXICON shortest-illativ_sg  !jos h ei siirry
 +Sg+Ill:%>%^V n_PxK ;

LEXICON a_shortestgenpl !! * @CODE@ zero, because ii > i
 +Gen: n_PxK ;


LEXICON a_shorterillpl
 +Ill:%>i2 n_PxK ;		


LEXICON a_loputon
! obl_strong_even ; ! muut vahvat
 weak_v_stem ;
 +Sg: sgpl_cases_odd ;      ! these two lexica for cases with

LEXICON a_loputonpl 
 +Pl:%>i2 sgpl_cases_odd ;  ! the same affix in sg and pl -- tsekkaa long
 +Pl:%>i2 shortgenpl ;
 :ess_odd_pl ;

LEXICON a_loputoneven
 weak_v_stem ;
+Sg:sgpl_cases_even ;      ! these two lexica for cases with
:ess_even_sg ;

LEXICON a_loputonevenpl
+Pl:%>i2 sgpl_cases_even ;  ! the same affix in sg and pl -- tsekkaa long
!+Pl:%>i2 shortgenpl ;
 :ess_even_pl ;
 
LEXICON a_ton
 +Sg+Nom:%>t%^On K ;
 +Sg+Par:%>t%^Ont%^A K ;
! +Sg+Ill:tt%^Omh%^A%^An K ;
! +Pl+Gen:tt%^Omm%>i2in K ;

LEXICON comp
+A+Sg+Comp+Nom:%>mpi K ;
+A+Sg+Comp+Gen:%>m%^A%>n K ;
!+A+Comp:%>m%^A weak_v_stem ; 
+A+Sg+Comp+Ill:%>mp%>h%^A%^An K ;
+A+Sg+Comp+Par:%>mp%^A%>%^A K  ; 
+A+Sg+Comp+Ess:%>mp%^A%>nn%^A K ;
+A+Sg+Comp:%>m%^A sgpl_cases_odd ;
+A+Pl+Comp:%>m%>i2 sgpl_cases_odd ;
+A+Pl+Comp+Nom:%>m%^A%>t K ;
+A+Pl+Comp+Gen:%>m%>i2%>tten K ;
+A+Pl+Comp+Ill:%>mp%>hi%>i2n K ;
+A+Pl+Comp+Par:%>mpp%>i2%>i K  ; 
+A+Pl+Comp+Ess:%>mp%>i2%>nn%^A    K ;
+A+Pl+Comp+Com:%>mp%>i2%>ne    K ;

LEXICON superl
!+A+Sg+Superl+Nom:%>in K ;
!+A+Sg+Superl: superl_in ;
!+A+Superl:%>m%^A weak_v_stem ;
+A+Sg+Superl+Gen:%>m%^A%>n K ;
+A+Pl+Superl+Nom:%>m%^A%>t K ;
+A+Sg+Superl+Ill:%>mp%>h%^A%^An K ;
!+A+Sg+Superl+Par:%>in%>t%^A K  ; 
+A+Sg+Superl+Ess:%>mp%^A%>nn%^A    K ;
+A+Sg+Superl:%>m%^A sgpl_cases_odd ;
+A+Pl+Superl:%>m%>i2 sgpl_cases_odd ;
+A+Pl+Superl+Gen:%>m%>i2%>tten K ;
+A+Pl+Superl+Ill:%>mp%>h%>i2in K ;
+A+Pl+Superl+Par:%>mpp%>i2%>i K  ; 
+A+Pl+Superl+Ess:%>mp%>i2%>nn%^A    K ;
+A+Pl+Superl+Com:%>mp%>i2%>ne    K ;

LEXICON superl_in
+Nom:%>in K ;
+Par:%>in%>t%^A K  ;

LEXICON superl_n
+Nom:%>n K ;
+Par:%>n%>t%^A K  ;