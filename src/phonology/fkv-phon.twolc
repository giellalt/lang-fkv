!! !!!Phonological rules for Kven
!! !!Alphabet

Alphabet
a b c d đ e f g h i j k l m n o p q         !!= * @CODE@
r s t u ü v w x y z å ä ö æ ø               !!= * @CODE@
A B C D Đ E F G H I J K L M N O P           !!= * @CODE@
R S T U V W X Y Z Å Ä Ö Æ Ø                 !!= * @CODE@
%+:0                                        !!= * @CODE@ gemination
%^A:ä %^O:ö %^U:y                !!= * @CODE@ archi vowels for harmony
%^V %^N:n                        !!= * @CODE@ vowel copy sanoo, LNR copy purra
%>                               !!= * @CODE@ border before inflection suffix
i2:i i3:i i4:i i5:i i6:i i7:i    !!= * @CODE@ 
!! **  i2 = noun pl, i3 = past tense, i4 = cond, i5 = superlative, i6 = pojan, i7 = cond of contr.v.
t2:t                      !!= * @CODE@ 
!! ** t2 = always t
%^HMETA:0                 !!= * @CODE@ h metateesi sykshyyn
%^WG:0                    !!= * @CODE@ weak grade hatun
%^E2I:0                   !!= * @CODE@ e to i in nom ove : ovi
%^VDEL:0 %^EDEL:0         !!= * @CODE@ -- DEL = deletion
%^AE:0                    !!= * @CODE@ -- a to e lauletaan
%^AO:0                    !!= * @CODE@ -- a to o sanoissa
#
;


!! !!Sets
Sets                                                            
FrontVowel = e i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø ;            !!= * @CODE@ 
BackVowel = a o u å ;                                       !!= * @CODE@ 
ArchiVowel =  %^A %^O %^U ;                                 !!= * @CODE@ 
SomeVowel = %^V ;                                           !!= * @CODE@ 
NeutralVowel = e i ;                                        !!= * @CODE@ 
Vowel   = FrontVowel BackVowel ArchiVowel SomeVowel ;       !!= * @CODE@ 
NotE    =   i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø a o u å ;       !!= * @CODE@ 
NotEY   =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø a o u å ;       !!= * @CODE@ 
NotEYU  =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø a o  å ;       !!= * @CODE@ 
NotOU   = e i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø a ;             !!= * @CODE@ 
NotOUI  = e                     y ä ö ü æ ø a ;             !!= * @CODE@ 
NotOUIY = e                       ä ö   æ ø a ;             !!= * @CODE@ 
                                
NotOUIF =                      y ä ö ü æ ø   ;              !!= * @CODE@ and e? (removed 29.1.)
NotOUIB =                                  a ;              !!= * @CODE@ and e? (removed 29.1.)
NotEYUF =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø         ;       !!= * @CODE@ 
NotEYUB =   i i2 i3 i4 i5 i6 i7             a o   å ;       !!= * @CODE@ 

ArchiCns = %^N ;                                            !!= * @CODE@ 
Cns = b c d đ f g h j k l m n p q r s t v w x z t2 %^N ;    !!= * @CODE@ 
Segment = Vowel Cns ;                                       !!= * @CODE@ 
Dummy = %> %+ %^WG %^E2I %^HMETA %^VDEL %^EDEL %^AE %^AO ;  !!= * @CODE@ 
NonFront = BackVowel ArchiVowel ArchiCns SomeVowel Cns NeutralVowel Dummy ;  !!= * @CODE@ 


!! !!!Rules
Rules 


!! !!Consonant rules
!     ===============


!! !Gemination

!! One rule for each vowel environment.

 
  "Gemination VXi"
  !! __@RULENAME@__ 
0:Cx <=> Cns:*  Vowel:* Vowel (Vowel) (Cx) _  :Vowel %>: (%^E2I:0) [:i - i3:] (i2:i) ;
         where Cx in ( k p t s ) ;

!0:Cx <=> Cns:*  Vowel:* Vowel (Vowel) (Cx) _  :Vowel %>: (%^E2I:0) [:i - i3:] (i2:i) ;
!        Cns:*  Vowel:* Vowel (Vowel):Cx e:0 _ %>:0 i2:i :i ;
!         where Cx in ( k p t s ) ;


!€ sop0i>^V
!€ soppi>i


!€ aik0a>i2
!€ aikko>i
       

!€ jok0e>i2>i
!€ jokk0>i>i

       
!€ vuot0e>i2>i
!€ vuoss0>i>i



!€ poik0a>i2>i
!€ poikk0>i>i

!"Gemination talo: talloi"
! !! __@RULENAME@__ 
!0:Cx <=> .#. :Cns :Vowel :Cx _ (:Vowel) %>:0 [:i - i3:] (i2:i) ;
!         where Cx in ( h j l m n r v ) ;
          

!   ! __@RULENAME@__ 
! 0:Cx <=>  Vowel Cx _ Vowel %>: i2:i ;
!        where Cx in ( h j l m n r v ) ;
 

!€ kiv0e>i2>i
!€ kivv0>i>i

  "Gemination Va"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | #: ] Cns:*   Vowel:* Vowel Cx _ a %>: :a ;
       where Cx in (  k p t s ) ;

!€ aik0a>^V
!€ aikka>a

  "Gemination VVa"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | #: ] Cns:* Vowel Cx _ a %>: :a ;
       where Cx in ( h j l m n r v ) ;
  

!€ kal0a>^V
!€ kalla>a

  "Gemination Ve"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ]   Vowel:* Vowel Cx _ e %>: :e ;
       where Cx in ( k p t s ) ;

!€ tek0e>^V
!€ tekke>e


!€ lauk0e>^V
!€ laukke>e


  "Gemination VVe"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ e %>: :e ;
       where Cx in ( h j l m n r v ) ;

!€ suome>^V
!€ suome>e



"Gemination Vo"
!! __@RULENAME@__ 
 0:Cx <=> [ .#. | #: ] Cns:*  Vowel:* Vowel Cx _ o %>: :o ;
        where Cx in (  k p t s) ;
 

!€ kat0o>^V>t
!€ katto>o>t

  "Gemination VVo"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ o %>: :o ;
       where Cx in ( h j l m n r v ) ;


!€ kir0o>^V>t
!€ kirro>o>t

    
"Gemination Vu"
!! __@RULENAME@__ 
 0:Cx <=> [ .#. | Cns ]  Vowel:* Vowel Cx _ u %>: :u ;
        where Cx in (  k p t s ) ;
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :u %>: [ :u | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;


!€ jout0u>^V
!€ jouttu>u

  "Gemination VVu"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ u %>: :u ;
       where Cx in ( h j l m n r v ) ;


!€ puh0u>^V
!€ puhhu>u

    
"Gemination Vy"
!! __@RULENAME@__ 
 0:Cx <=> [ .#. | Cns ]  Vowel:* Vowel Cx _ y %>: :y ;
        where Cx in (  k p t s ) ;
        

!€ näk0y>^V
!€ näkky>y       
        
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :y %>: [ :y | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

  "Gemination VVy"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ y %>: :y ;
       where Cx in ( h j l m n r v ) ;


!€ lev0y>^V
!€ levvy>y
    
"Gemination Vö"
!! __@RULENAME@__ 
 0:Cx <=> [ .#. | Cns ]  Vowel:* Vowel Cx _ ö %>: :ö ;
        where Cx in (  k p t s) ;
        

!€ näk0ö>^V
!€ näkkö>ö

! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :ö %>: [ :ö | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

  "Gemination VVö"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ ö %>: :ö ;
       where Cx in ( h j l m n r v ) ;
    

!€ pör0ö>^V
!€ pörrö>ö

   
"Gemination Vä"
!! __@RULENAME@__ 
 0:Cx <=> [ .#. | Cns ]  Vowel:* Vowel Cx _ ä %>: :ä ;
        where Cx in ( k p t s ) ;
 

!€ tiet0ä>^Vt
!€ tiettä>ät
       
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :ä %>: [ :ä | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

"Gemination VVä"
!! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns | #: ] Vowel Cx _ ä %>: :ä ; 
         where Cx                                  
         in    ( h j l m n r v ) ;                 


!€ el0ä>^V
!€ ellä>ä

    
"Gemination LIQa"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :a %>: :a ;   
        where Cx in ( j k p s t v ) ;
        

!€ jalk0a>^V
!€ jalkka>a

"Gemination LIQe"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :e %>: :e ;   
        where Cx in ( j k p s t v ) ;
        

!€ järk0e>^V
!€ järkke>e
    
"Gemination LIQo"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :o %>: :o ;   
        where Cx in ( j k p s t v ) ;
 

!€ murt0o>^V
!€ murtto>o
    
"Gemination LIQu"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :u %>: :u ;   
        where Cx in ( j k p s t v ) ;
 

!€ polk0u>^V
!€ polkku>u
   
"Gemination LIQy"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :y %>: :y ;   
        where Cx in ( j k p s t v ) ;

!€ särk0y>^V
!€ särkky>y        
    
"Gemination LIQö"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :ö %>: :ö ;   
        where Cx in ( j k p s t v ) ;

!€ lämp0ö>^V
!€ lämppö>ö
   
"Gemination LIQä"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :ä %>: :ä  ;   
        where Cx in ( j k p s t v ) ;


!€ pölk0ä>^V
!€ pölkkä>ä


!€ härk0ä>^V
!€ härkkä>ä

"Gemination LIQi"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :i %>: [:i - i7: ] ;   
       where Cx in ( j k p s t v ) ;
    

!€ kerk0i>^Vn
!€ kerkki>in

"Gemination LIQi7"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :Vowel %>:  i7: ;   
        where Cx in ( j k p s t v ) ;

!€ lauk0e>i7s
!€ laukke>is


!€ särk0y>i7s
!€ särkky>is

!! !Foot-governed affixation
    
!!  Lyhyt tai pitkä pääte tavurakenteen mukaan:

!! * __Pitkä__: painollisen lyhyen tavun jälkeen 
!! ** ''sillä''
!! ** ''tulla''
!! * __Lyhyt__: painollisen pitkän tavun jälkeen
!! ** ''kuula''
!! * __Pitkä__: painottoman, pitkän tavun jälkeen
!! ** ''emänällä''
!! ** ''sisarelle''
!! ** ''itteksemmä''
!! * __Lyhyt__: painottoman, lyhyen tavun jälkeen
!! ** ''linnala''
!! ** ''muistela''
!! ** ''sijola''
!! ** ''kantelheela''
!! ** ''hunteeraama''
!! ** ''saima''

!"Suffix shortening after short vowels in odd syllbles of consonant stems" != * @CODE@   
! Cx:0 <=> Cns+ Vowel ( Dummy: ) _ %>: Cy: Vowel ; 
!     where Cx in ( l r m n l   r   m   n   ) 
!           Cy in ( l r m n %^N %^N %^N %^N )
!           matched ;

!"Suffix lengthening after short vowels in odd syllbles of vowel stems" != * @CODE@  
! 0:Cx <=> Vowel Cns+ Vowel+ (%>:) Cns+  (%>:) Vowel %>: ( Dummy: ) _ Cx: Vowel ; 
!     where Cx in ( l r m n ) ;





!! !!Consonant gradation
!    ===================

!! !Gradation ppttkk : ptk

"Gradation ppttkk : ptk"
!! __@RULENAME@__ 
Cx:0 <=> [Vowel:|l|m|n|r] _ Cx (:h) (Dummy:) Vowel: (Vowel:0) (Dummy:) (:i) (l|r) %^WG:0 ;   !entinen omppu
            where Cx in ( k p t ) ;

!Cx:0 <=> Vowel: _ [Cx|%+:0] (h) (Dummy:) Vowel: (Vowel:0) (Dummy:) %^WG:0 ;  
!Cx:0 <=> Vowel: _ Cx (:h) (Dummy:) Vowel: (Vowel:0) (Dummy:) (:i) %^WG:0 ;   !toimii


!€ katto^WG>n
!€ ka0to0>n


!€ katt2o^WG>n
!€ katto0>n

!! !Gradation -p-

"Gradation p:v"
!! __@RULENAME@__ 
 p:v <=> Vowel: _ Vowel: (Cns:) Dummy:* (:i) Dummy:* %^WG:0 ;


!€ napa^WG>n
!€ nava0>n

!€ taipal^WG
!€ taival0

!! !Gradation of -k-

"Gradation i:0, in word poika: pojan"
!! __@RULENAME@__ 
i6:0 <=> _ k: a %^WG:0;


!€ poi6ka^WG>n
!€ po0ja0>n



! jälki: jäljen ; järki; järjen; kulkkeet: kuljen; särkkeet : särjen

"Gradation k:j" 
!k:j <=>    [a|e|i|i6:] _ [o|e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! pako  - pajon? conflict k:v
k:j <=>    [a|e|i|i6:] _ [e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! 
			i _ [ö:|o:]        (Dummy:) (:i) (Dummy:) %^WG:0 ; !		
         [ä|æ|e|i|i6:] _   [e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! m̈äki  - mäjen
             [e|i|i6:] _   [e:|i|ä|æ|a:] (Dummy:) (:i) (Dummy:) %^WG:0 ; ! aika  - aijan
                 [r|l] _    e:           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! jälki - jäljen
              Cns u _    e:           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! lukkeet  - lujen


!€ mäke^WG>n
!€ mäje0>n

!€ aika^WG>n
!€ aija0>n

!€ jälke^WG>n
!€ jälje0>n

!€ luke^WG>n
!€ luje0>n




        
"Gradation k:0" 
k:0 <=>                h _ Vowel           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! tuhka - tuhat
                       a _ a               (Dummy:) (:i) (Dummy:) %^WG:0 ; ! makaa - maata
                       y _ e               (Dummy:) (:i) (Dummy:) %^WG:0 ; ! kykenee - kyetä
      NotOUIF: [ l | r ] _ NotEYUF:        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! varas - varkaan
      NotOUIB: [ l | r ] _ NotEYUB:        (Dummy:) (:i) (Dummy:) %^WG:0 ; !märkä - märät
!  Vowel [Vowel - [e|i]] _ Vowel           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! kaiku - kaiun?
!   [ NotOUIF: | l | r ] _ NotEYUF:        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! varas - varkaan
!   [ NotOUIB: | l | r ] _ NotEYUB:        (Dummy:) (:i) (Dummy:) %^WG:0 ; !märkä - märät

!€ tuhka^WG>t
!€ tuh0a0>t
!€ 
!€ maka^WG>t^A
!€ ma0a0>ta
!€ 
!€ kyke^WG>t^A
!€ ky0e0>tä
!€ 
!€ sarka^WG>n
!€ sar0a0>n


!   a | e | i | i6: _ [o | e: | i] (Dummy:) (:i) (Dummy:) ^WG:0 ;
!   NotOUIB: | l | r _ NotEYUB: (Dummy:) (:i) (Dummy:) ^WG:0 ;
! Left context example:     a
! Right context example:    i +:0 ^WG:0




"k:v"
k:v <=>     [Vowel - i] _ [o|ö|u|y]    (Dummy:) (:i) (Dummy:) %^WG:0 ;
           \i [o|ö|u|y] _ [Vowel - e:] (Dummy:) (:i) (Dummy:) %^WG:0 ;
                  [o|ö] _  e:          (Dummy:) (:i) (Dummy:) %^WG:0 ;


!€ puku^WG>n
!€ puvu0>n


"Gradation nk:ng"
!! __@RULENAME@__ 
k:g <=> Vowel: n _  Vowel: (Dummy:) (:i) (Dummy:) %^WG:0 ;


!€ ranka^WG>n
!€ ranga0>n


!! !Gradation of -t-

"Gradation t:đ"
!! __@RULENAME@__ 
!t:đ <=> [ Vowel | l | r ] _  Vowel: (Dummy:) %^WG:0 ;  
!t:đ <=> Vowel  _  Vowel: (Dummy:) %^WG:0 ; 

 

 t:đ <=> Vowel  _  Vowel (Dummy: - %^E2I:) %^WG:0 [\i3:|\i6:|\i2:] ;  
         h _ Vowel: (Dummy:) (:i) (Dummy:) %^WG:0 ; 

!t:đ <=> Vowel  _  [Vowel: - [\i3:|\i6:|\i2:]] (Dummy:) %^WG:0 [\i3:|\i6:|\i2:] ;  

 ! lähteet : lähđen/lähen


!€ lahti^WG
!€ lahđi0


"ti:si"
!! __@RULENAME@__ 
t:s <=> Vowel n _ [ (e:) %^E2I: i3: | Vowel:0 [Dummy:* - %^WG:0] %>: i3: | (:s) e: %>: i2: [.#.|Cns]] ;
        Vowel   _ [ (e:) %^E2I:     | Vowel:0 [Dummy:* - %^WG:0] %>: i3: | (:s) e: %>: i2: [.#.|Cns]] ;
  Vowel [n|l|r] _ [                   Vowel:0            %^WG:0  %>: i3: ] ;

! ... not ready
!# vuot0e^E2I>i3
!0 vuoss00i>i

"Gradation Nt:NN"
!! __@RULENAME@__ 
t:Nx <=> Nx  _  :Vowel (Dummy:*) ([i|i2:]) Dummy:* %^WG:0 ?* \%^E2I:0 ;  
!t:Nx <=> Nx  _  Vowel: (Dummy:) ([i|i2:]) (Dummy:) %^WG:0 (%>:) \i3: ;  
    where Nx in ( l n r ) ; 
! NB nt:nn before i3, if the vowel before i3 is not dropped out. annoin, mutta lensin, lennoin


!€ lentä^WG>n
!€ lennä0>n

"Alveolar assimilation for consonant stems"
!! __@RULENAME@__ 
 %^N:Cx <=> Cx: %>: _ ;
    where Cx in ( l r s ) ; 


!€ tul>^N^U
!€ tul>lu

! ===============
!! !!!Vowel rules
! ===============

!! !!Vowel harmony 

"Back harmony"
!! __@RULENAME@__ 
 Vx:Vy <=> BackVowel: NonFront:* (:NonFront) NonFront:* %>: NonFront:* ([ NonFront: | :NonFront ]) NonFront:* _ ;  
         where Vx in ( %^A %^O %^U )
               Vy in (   a   o   u )
         matched ;

!€ kulke>^A
!€ kulke>a

!! !!Vowel copying and metathesis
         
"Vowel copying" ! 
!! __@RULENAME@__  
! %^V:Vx <=> :Vx [Dummy:* - %^HMETA:] %>: _ ;!(%>:) [ (%>:) Cns|.#.];![ Cns|[Vowel - i4:]|.#.] ;
!    where Vx in ( a e i u y ä ö ) ; ! o in next rule
     
%^V:Vx <=> :Vx Dummy:* %>: _ ;
    where Vx in ( a e i u y ä ö ) ; ! o in next rule
   
! poika : poikaa
! kive : kivee
    
"Vowel copying in long h forms of a-stems"
!! __@RULENAME@__ 


 %^V:o <=> .#. Cns:* (Vowel:) [i|e|a] Cns:* a: %>: %^HMETA: h _ %^AO: i2: ; 
                                  :o [Dummy:* - %^HMETA:] %>: _ [ (%>:) Cns|.#.] ;
                                            (:Cns)(:Cns) :o %>: _ ;
! Mikä säätö?
! traaka : traakkhoin
! sivakka : sivakkhoin 
!0 katto>^V alempi sääntö?
!0 katto0o


!€ paivukko>^V
!€ paivukko>o


"Vowel copying in short h-illative"
!! __@RULENAME@__
 %^V:Vx <=> Vowel Vx: %>: h _ [ n: | m: ] ;  
    where Vx in ( a e i o u y ä ö ) ; 


!€ maa>h^Vn
!€ maa>han

"Vowel copying in long h forms"
!! __@RULENAME@__ 
 %^V:Vy <=>  Vx: %>: %^HMETA: h _ [ %^V: | i2: ] ; 
    where Vx in ( a e i i2 o u y ä ö ) 
          Vy in ( a e i i  o u y ä ö ) 
             matched ;
 

"Vowel copying in long h forms 2"
!! __@RULENAME@__ 
 %^V:Vy <=>  Vx: %>: %^HMETA: h %^V: _ ; 
    where Vx in ( a e i i2 o u y ä ö )  
          Vy in ( a e i i  o u y ä ö ) 
             matched ;

! %^V:Vx <=> Vowel Vx: %>: %^HMETA: h %^V: _ ; 
    
"Stem deletion in h-illative"
!! __@RULENAME@__ 
! Vx:0 <=> Cns _ %>: %^HMETA:0 h [ %^V: | %^AO: | i: | o:] ; 
!    where Vx in ( a e i o u y ä ö ) ;

 Vx:0 <=> Cns _ %>: %^HMETA:0 h [ :Vx | (%^AO:) ] ; 
    where Vx in ( a e i o u y ä ö ) ;

! oppettaajhiin 
    
! "Stem deletion in h-illative b"
! !! __@RULENAME@__ 
!  Vx:0 <=> Cns _ %>: %^HMETA:0 h %^V: ; 
!     where Vx in ( a e i o u y ä ö ) ;  
       

!€ syksy>^HMETAh^V^Vn
!€ syks0>0hyyn


"e deletion in illative plural"
 e:0 <=> _ %^EDEL:0 %>: h ;
         _ %^E2I:0 %^HMETA:0 h i i ;
 ! vuothiin
  
 
"e deletion in oblique plural of e-stem nouns"
 e:0 <=> _ (%^WG:0) %>: i2: ;
        t:s _ %^E2I:0 %>: i2: ;


!! !!Stem alternation rules
!  ========================

"Stem shortening for consonant initial suffixes" !!= * @CODE@
 Vx:0 <=> Vx _ %^VDEL:0  (Dummy:) %>: [s|t] ;
     where Vx in ( a e i ä ) ;


!€ rakkaa^VDEL^WG>s
!€ ra0ka000>s

     



"e:0 in consonant stems"
!! __@RULENAME@__ 
e:0 <=> Cns: _ [ (Dummy:* - %^E2I:0) %>: [i3:|i4:] | %^EDEL:0 %>: t ] ;


!€ aukaise>i3
!€ aukais0>i


!! !!i rules
!  =========

!! The -i- rules require different i-s for different POS.

!! !a before i

"a:0 before Pret and Pl i when rounded root vowel"
!! __@RULENAME@__ 
a:0 <=> (\a) [(u) u|([u|o]) o] (:i|i6:) Cns: (Cns:) (Cns:) _  [(Dummy:* - %^AE:) %>:  [i2:|i3:] | [a:0]] ;
          [ #: | .#. ] (Cns:*) Vowel: Cns: (Cns:) [:a|:e] [:v|:m] _ %>: (Dummy:*) i2: (i) ;

!€ otta^WG>i2n
!€ o0t00>in 

!€ kajava>i2ssa
!€ kajav00issa 

"a:o before Pl i and Pret i"
!! __@RULENAME@__ 
!a:o <=>      .#.   (Cns:*) ([Vowel: - [o|u]]) [i|e|a] Cns:* (:Cns) (%^AO:0) _                   %>: [i2: | i3: ] ; !2-syll 
a:o <=>  [ #: | .#. ] Cns:* ([i|e|a]         ) [i|e|a] Cns:*  :Cns           _ (Dummy: - %^AE:0) %>: [i2: | i3: ] ;
                                                    (:Cns) [ :k | :l] (:i) :i :j _ (Dummy:*) i2: ;
                                                                        
                                                                 
                                                

!€ kal0a>i2en
!€ kallo>ien


!€ kulkkiija>i2
!€ kulkkiijo>i


"a:i in 3-syll stems with long a and i"
!! __@RULENAME@__ 
a:i <=> Vowel Cns Cns:* a a Cns:+ (:Cns) _ (%^WG:) %>: i2: ; ! 3-syll
      

! opettaajiissa

"a:e in passives"
!! __@RULENAME@__ 
! Vx:e <=> _ %^AE:0 (t) (t) ;
! 	where Vx in ( a ä ) ;

Vx:e <=> _ %^AE:0 (%^WG:0) %>: t (t) ;
 	where Vx in ( a ä ) ;

"ä:0 before i except in conditional"
!! __@RULENAME@__ 
 ä:0 <=> Cns: _ [(Dummy:* - %^AE:0) %>: [ i | i2: | i3: | i5: ] | [ä:0]];


!€ kehä>i2>ss^A
!€ keh0>i>ssä 
 

"Shortening of long vowel in front of i"
!! __@RULENAME@__ 
 Vx:0 <=> _ Vy %>: ([Dummy: - %^AE:]) [ i | i2: | i3: | i4:] ; 
     where Vx in ( a ä u y u y i ) 
           Vy in ( a ä u y o ö e )
          matched ;


!€ pää>i2>ss^A
!€ p0ä>i>ssä

! saa∂a : sain : saisin
! ottaa : otan : ottaisin i4 ei passaa kaikkialle!

          
!"Shortening of long vowel when vowel lengthening"
!! __@RULENAME@__ 
! %^V:0 <=> Vowel %>: _ %>: [ i3:i | i4:i ] ; 
 
! Tämä sääntö aiheuttaa paljon ratkaisemattomia konflikteja säännön "Vowel copying" kanssa.
! Mikä tämä sääntö on ja miten se eroaa edelisestä "Shortening of long vowel in front of i"?
! soppiit: sopi: sopisi
! juttaat: juti : jutaisi?
! saa∂a: sai : saisin
! jää∂ä : jäin : jäisin
! Poistettu toistaiseksi


"e:i in nom.sg. of e-stems"
!! __@RULENAME@__
 e:i <=> _ %^E2I:0 [ #: | .#.] ;  



!€ ove^E2I
!€ ovi0
 
!! * ove : ovi

"i:0 in par and gen pl of i-stems" ! ristien
 i:0 <=> Cns: _ %>: i2:i %>: [ %^V n | %^V: ] ;

! # risti>i2>^Vn
! 0 rist0>i>en



"i:0 in past tense and conditional of i-stems" ! leikin, tohtisin
 i:0 <=> Cns: _ Dummy:* %>: [ i3:i | i4:i ] ;



!€ leikki^WG>i3n
!€ lei0k00>in



!$# a
!$0 a



!%%%%%%%%%%%%%%%%%%%%

! The old a rules for refernce

!old a rules "a:0 before Pret and Pl i when rounded root vowel"
!old a rules !! __@RULENAME@__ 
!old a rules 
!old a rules a:0 <=> (\a) [(u) u|([u|o]) o] (i) Cns: (Cns:) (Cns:) _  [(Dummy:* - %^AE:) %>:  [i2:|i3:] | [a:0]] ;
!old a rules 
!old a rules !a:0 <=> \a [(u) u|([u|o]) o] (i) Cns: (Cns:) (Cns:) _  ([Dummy: - %^AE:]) %>: !Dummy:* [i2:|i3:] ;
!old a rules ! a:0 <=>          [u|o]     (i) Cns: (Cns:) (Cns:) _ (Dummy:) %>: [i2:|i3:] ;
!old a rules 
!old a rules 
!old a rules !€ otta^WG>i2n 
!old a rules !€ o0t000in  
!old a rules 
!old a rules 
!old a rules "a:o before Pl i and Pret i"
!old a rules !! __@RULENAME@__ 
!old a rules !a:o <=> .#. (Cns:*) [i|e|a] \o \u Cns:* (:Cns) %>: %^AO:o a:o _ %>: [i2:i | i3:i ] ; ! 2-syll
!old a rules a:o <=> .#. (Cns:*) [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) (%^AO:0) _  %>: [i2: | i3: ] ; !2-syll 
!old a rules 
!old a rules !a:o / <= .#. (Cns:*) [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) %^AO:0 _ %>: i [a:a] ; !2-syll
!old a rules 
!old a rules !"a never o in front of i:i"
!old a rules !! __@RULENAME@__ 
!old a rules !a:o /<= (.#.) (Cns:*) a:a (\%>:) _  i:i ;
!old a rules 
!old a rules !a:o /<= a _ (\%>:) i ;
!old a rules 
!old a rules        ! .#. Cns:* [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) [Dummy: - %^AE:] _ %^AO:0 [ i2:| i3: ] ;
!old a rules ! Vowel Cns Cns:* [Vowel: - [o|u]] [i|e|a] Cns:+ (:Cns)[Dummy: - %^AE:] _ [Dummy: - %^AE:] %>: [i2: | i3:] ; !3-syll
!old a rules  
!old a rules ! a:o <=>  .#. Cns:* ([Vowel: - [a|o|u]]) [i|e|a] Cns:* (:Cns) _ [Dummy: - %^AE:] %>: !i2: ; ! 2-syll
!old a rules !          .#. Cns:* ([Vowel: - [a|o|u]]) [i|e|a] Cns:* (:Cns) _  %^AO:0 ;
!old a rules !    Vowel Cns Cns:* ([Vowel  - [a|o|u]]) [i|e|a] Cns:+ (:Cns) _ [Dummy: - %^AE:] %>: i2: ; ! 3-syll
!old a rules !    Vowel Cns Cns:* ([Vowel  - [a|o|u]]) Vowel   Cns:+ (:Cns) _ [Dummy: - %^AE:] %>: i2: ; ! 3-syll
!old a rules !          .#. Cns:*   a                  ([a|u]) Cns:* (:Cns) _ [Dummy: - %^AE:] %>: i3: ;
!old a rules 
!old a rules 
!old a rules !€ kal0a>i2en
!old a rules !€ kallo0ien
!old a rules 
!old a rules 
!old a rules !€ kulkkiija>i2
!old a rules !€ kulkkiijo0i
!old a rules 
!old a rules !"a:o for HMETA"
!old a rules ! %^V:o <=> .#. Cns:* a ([a|u]) Cns:* (:Cns) [Dummy: - %^AE:] %>: %^HMETA:0 h _ %^AO:0 ;



!"Gemination"
! 0:Cx <=> [Vowel|l|r] Cy _ Vowel: %>: Vowel: ;   
!        where Cy in ( h j k l m n p r s t v ) 
!              Cx in ( h j k l m n p r s t v ) 
!    matched; 

!"Gemination of h"
!0:h <=> [ .#. | Cns ] Vowel h _ :Vx %>: :Vx ;
!         where Vx in ( a e i o u ä ö ) ;
!
!"Gemination of l"
!0:l <=> [ .#. | Cns ] Vowel h _ :Vx %>: :Vx ;
!         where Vx in ( a e i o u ä ö ) ;


!   "Gemination VVXi"     
!0:Cx <=> Cns:*  Vowel:* Vowel (Vowel) Cx _ [ Vowel | a:o ] %>: [:i | i2:i];
!                                         _ [ a:0 | e:0 ]  %>: :i i2:i ;
!      where Cx in ( k p t s ) ;       
 




