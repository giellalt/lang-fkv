!! !!!Phonological rules for Kven
!! !!Alphabet

Alphabet
a b c d đ e f g h i j k l m n o p q         !!= * @CODE@
r s t u ü v w x y z å ä ö æ ø               !!= * @CODE@
A B C D Đ E F G H I J K L M N O P           !!= * @CODE@
R S T U V W X Y Z Å Ä Ö Æ Ø                 !!= * @CODE@
%+:0                                        !!= * @CODE@ gemination
%^A:ä %^O:ö %^U:y                !!= * @CODE@ archi vowels for harmony
%^V %^N:n                        !!= * @CODE@ vowel copy sanoo, LNR copy purra
%>:0                             !!= * @CODE@ suff border can we live without it?
i2:i i3:i i4:i i5:i i6:i i7:i    !!= * @CODE@ 
!! **  i2 = noun pl, i3 = past tense, i4 = cond, i5 = superlative, i6 = pojan, i7 = cond of contr.v.
t2:t                      !!= * @CODE@ 
!! ** t2 = always t
%^HMETA:0                 !!= * @CODE@ h metateesi sykshyyn
%^WG:0                    !!= * @CODE@ weak grade hatun
%^E2I:0                   !!= * @CODE@ e to i in nom ove : ovi
%^VDEL:0 %^EDEL:0         !!= * @CODE@ -- DEL = deletion
%^AE:0                    !!= * @CODE@ -- a to e lauletaan
%^AO:0                    !!= * @CODE@ -- a to o sanoissa
#
;


!! !!Sets
Sets                                                            
FrontVowel = e i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø ;            !!= * @CODE@ 
BackVowel = a o u å ;                                       !!= * @CODE@ 
ArchiVowel =  %^A %^O %^U ;                                 !!= * @CODE@ 
SomeVowel = %^V ;                                           !!= * @CODE@ 
NeutralVowel = e i ;                                        !!= * @CODE@ 
Vowel   = FrontVowel BackVowel ArchiVowel SomeVowel ;       !!= * @CODE@ 
NotE    =   i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø a o u å ;       !!= * @CODE@ 
NotEY   =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø a o u å ;       !!= * @CODE@ 
NotEYU  =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø a o  å ;       !!= * @CODE@ 
NotOU   = e i i2 i3 i4 i5 i6 i7 y ä ö ü æ ø a ;             !!= * @CODE@ 
NotOUI  = e                     y ä ö ü æ ø a ;             !!= * @CODE@ 
NotOUIY = e                       ä ö   æ ø a ;             !!= * @CODE@ 
                                
NotOUIF =                      y ä ö ü æ ø   ;              !!= * @CODE@ and e? (removed 29.1.)
NotOUIB =                                  a ;              !!= * @CODE@ and e? (removed 29.1.)
NotEYUF =   i i2 i3 i4 i5 i6 i7   ä ö   æ ø         ;       !!= * @CODE@ 
NotEYUB =   i i2 i3 i4 i5 i6 i7             a o   å ;       !!= * @CODE@ 

ArchiCns = %^N ;                                            !!= * @CODE@ 
Cns = b c d đ f g h j k l m n p q r s t v w x z t2 %^N ;    !!= * @CODE@ 
Segment = Vowel Cns ;                                       !!= * @CODE@ 
Dummy = %> %+ %^WG %^E2I %^HMETA %^VDEL %^EDEL %^AE %^AO ;  !!= * @CODE@ 
NonFront = BackVowel ArchiVowel ArchiCns SomeVowel Cns NeutralVowel Dummy ;  !!= * @CODE@ 


!! !!!Rules
Rules 


!! !!Consonant rules
!     ===============


!! !Gemination

!! One rule for each vowel environment.

 
  "Gemination VXi"
  !! __@RULENAME@__ 
0:Cx <=> Cns:*  Vowel:* Vowel (Vowel) (Cx) _ :Vowel %>:0 [:i - i3:] (i2:i) ;
       where Cx in ( k p t s l) ;




!€# sop0i>^V
!€0 soppi0i


!€# aik0a>i2
!€0 aikko0i
       

!€# jok0e>i2>i
!€0 jokk00i0i

       
! vuot0e>i2>i
! vuoss00i0i



!€# poik0a>i2>i
!€0 poikk0i0i



!   ! __@RULENAME@__ 
! 0:Cx <=> [ .#. | Cns ] Vowel Cx _ Vowel %>:0 i2:i ;
!        where Cx in ( h j l m n r v ) ;
 

!€# kiv0e>i2>i
!€0 kivv00i0i

  "Gemination Va"
  !! __@RULENAME@__ 
0:Cx <=> Cns:* Vowel:* Vowel Cx _ a %>:0 :a ;
       where Cx in (  k p t s ) ;

!€# aik0a>^V
!€0 aikka0a

  "Gemination VVa"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ a %>:0 :a ;
       where Cx in ( h j l m n r v ) ;
  

!€# kal0a>^V
!€0 kalla0a

  "Gemination Ve"
  !! __@RULENAME@__ 
0:Cx <=> Cns:*  Vowel:* Vowel Cx _ e %>:0 :e ;
       where Cx in ( k p t s ) ;

!€# tek0e>^V
!€0 tekke0e


!€# lauk0e>^V
!€0 laukke0e


  "Gemination VVe"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ e %>:0 :e ;
       where Cx in ( h j l m n r v ) ;





!€# suome>^V
!€0 suome0e


    



"Gemination Vo"
!! __@RULENAME@__ 
 0:Cx <=> Cns:* Vowel:* Vowel Cx _ o %>:0 :o ;
        where Cx in (  k p t s) ;
 

!€# kat0o>^V>t
!€0 katto0o0t

  "Gemination VVo"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ o %>:0 :o ;
       where Cx in ( h j l m n r v ) ;


!€# kir0o>^V>t
!€0 kirro0o0t

    
"Gemination Vu"
!! __@RULENAME@__ 
 0:Cx <=> Cns:* Vowel:* Vowel Cx _ u %>:0 :u ;
        where Cx in (  k p t s ) ;
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :u %>:0 [ :u | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;


!€# jout0u>^V
!€0 jouttu0u

  "Gemination VVu"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ u %>:0 :u ;
       where Cx in ( h j l m n r v ) ;


!€# puh0u>^V
!€0 puhhu0u

    
"Gemination Vy"
!! __@RULENAME@__ 
 0:Cx <=> Cns:* Vowel:* Vowel Cx _ y %>:0 :y ;
        where Cx in (  k p t s ) ;
        

!€# näk0y>^V
!€0 näkky0y       
        
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :y %>:0 [ :y | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

  "Gemination VVy"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ y %>:0 :y ;
       where Cx in ( h j l m n r v ) ;


!€# lev0y>^V
!€0 levvy0y
    
"Gemination Vö"
!! __@RULENAME@__ 
 0:Cx <=> Cns:* Vowel:* Vowel Cx _ ö %>:0 :ö ;
        where Cx in (  k p t s) ;
        

!€# näk0ö>^V
!€0 näkkö0ö

! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :ö %>:0 [ :ö | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

  "Gemination VVö"
  !! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns ] Vowel Cx _ ö %>:0 :ö ;
       where Cx in ( h j l m n r v ) ;
    

!€# pör0ö>^V
!€0 pörrö0ö

   
"Gemination Vä"
!! __@RULENAME@__ 
 0:Cx <=> Cns:* Vowel:* Vowel Cx _ ä %>:0 :ä ;
        where Cx in ( k p t s ) ;
 

!€# tiet0ä>^Vt
!€0 tiettä0ät
       
! 0:Cx <=> .#. Cns:* Vowel:* Vowel Cx _ :ä %>:0 [ :ä | i7: ] ;   
!        where Cx in ( h j k l m n p r s t v ) ;

"Gemination VVä"
!! __@RULENAME@__ 
0:Cx <=> [ .#. | Cns | #: ] Vowel Cx _ ä %>:0 :ä ; 
         where Cx                                  
         in    ( h j l m n r v ) ;                 


!€# el0ä>^V
!€0 ellä0ä

    
"Gemination LIQa"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :a %>:0 :a ;   
        where Cx in ( j k p s t v ) ;
        

!€# jalk0a>^V
!€0 jalkka0a

"Gemination LIQe"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :e %>:0 :e ;   
        where Cx in ( j k p s t v ) ;
        

!€# järk0e>^V
!€0 järkke0e
    
"Gemination LIQo"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :o %>:0 :o ;   
        where Cx in ( j k p s t v ) ;
 

!€# murt0o>^V
!€0 murtto0o
    
"Gemination LIQu"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :u %>:0 :u ;   
        where Cx in ( j k p s t v ) ;
 

!€# polk0u>^V
!€0 polkku0u
   
"Gemination LIQy"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :y %>:0 :y ;   
        where Cx in ( j k p s t v ) ;

!€# särk0y>^V
!€0 särkky0y        
    
"Gemination LIQö"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :ö %>:0 :ö ;   
        where Cx in ( j k p s t v ) ;

!€# lämp0ö>^V
!€0 lämppö0ö
   
"Gemination LIQä"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :ä %>:0 :ä  ;   
        where Cx in ( j k p s t v ) ;


!€# pölk0ä>^V
!€0 pölkkä0ä


!€# härk0ä>^V
!€0 härkkä0ä

"Gemination LIQi"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :i %>:0 [:i - i7: ] ;   
       where Cx in ( j k p s t v ) ;
    

!€# kerk0i<^Vn
!€0 kerkki0in

"Gemination LIQi7"
!! __@RULENAME@__ 
 0:Cx <=> [l|r|n|m] Cx _ :Vowel %>:0  i7: ;   
        where Cx in ( j k p s t v ) ;

!€# lauk0e>i7s
!€0 laukke0is


!€# särk0y>i7s
!€0 särkky0is

!! !Foot-governed affixation
    
!!  Lyhyt tai pitkä pääte tavurakenteen mukaan:

!! * __Pitkä__: painollisen lyhyen tavun jälkeen 
!! ** ''sillä''
!! ** ''tulla''
!! * __Lyhyt__: painollisen pitkän tavun jälkeen
!! ** ''kuula''
!! * __Pitkä__: painottoman, pitkän tavun jälkeen
!! ** ''emänällä''
!! ** ''sisarelle''
!! ** ''itteksemmä''
!! * __Lyhyt__: painottoman, lyhyen tavun jälkeen
!! ** ''linnala''
!! ** ''muistela''
!! ** ''sijola''
!! ** ''kantelheela''
!! ** ''hunteeraama''
!! ** ''saima''

!"Suffix shortening after short vowels in odd syllbles of consonant stems" != * @CODE@   
! Cx:0 <=> Cns+ Vowel ( Dummy: ) _ %>: Cy: Vowel ; 
!     where Cx in ( l r m n l   r   m   n   ) 
!           Cy in ( l r m n %^N %^N %^N %^N )
!           matched ;

!"Suffix lengthening after short vowels in odd syllbles of vowel stems" != * @CODE@  
! 0:Cx <=> Vowel Cns+ Vowel+ (%>:) Cns+  (%>:) Vowel %>: ( Dummy: ) _ Cx: Vowel ; 
!     where Cx in ( l r m n ) ;





!! !!Consonant gradation
!    ===================

!! !Gradation ppttkk : ptk

"Gradation ppttkk : ptk"
!! __@RULENAME@__ 
Cx:0 <=> [Vowel:|l|m|n|r] _ Cx (:h) (Dummy:) Vowel: (Vowel:0) (Dummy:) (:i) (l|r) %^WG:0 ;   !entinen omppu
            where Cx in ( k p t ) ;

!Cx:0 <=> Vowel: _ [Cx|%+:0] (h) (Dummy:) Vowel: (Vowel:0) (Dummy:) %^WG:0 ;  
!Cx:0 <=> Vowel: _ Cx (:h) (Dummy:) Vowel: (Vowel:0) (Dummy:) (:i) %^WG:0 ;   !toimii


!€# katto^WG>n
!€0 ka0to00n


!€# katt2o^WG>n
!€0 katto00n

!! !Gradation -p-

"Gradation p:v"
!! __@RULENAME@__ 
 p:v <=> Vowel: _ Vowel: Dummy:* (:i) Dummy:* %^WG:0 ;


!€# napa^WG>n
!€0 nava00n

!! !Gradation of -k-

"Gradation i:0, in word poika: pojan"
!! __@RULENAME@__ 
i6:0 <=> _ k: a %^WG:0;


!€# poi6ka^WG>n
!€0 po0ja00n



! jälki: jäljen ; järki; järjen; kulkkeet: kuljen; särkkeet : särjen

"Gradation k:j" 
!k:j <=>    [a|e|i|i6:] _ [o|e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! pako  - pajon? conflict k:v
k:j <=>    [a|e|i|i6:] _ [e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! 
			i _ [ö:|o:]        (Dummy:) (:i) (Dummy:) %^WG:0 ; !		
         [ä|æ|e|i|i6:] _   [e:|i]        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! m̈äki  - mäjen
             [e|i|i6:] _   [e:|i|ä|æ|a:] (Dummy:) (:i) (Dummy:) %^WG:0 ; ! aika  - aijan
                 [r|l] _    e:           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! jälki - jäljen
              Cns u _    e:           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! lukkeet  - lujen


!€# mäke^WG>n
!€0 mäje00n

!€# aika^WG>n
!€0 aija00n

!€# jälke^WG>n
!€0 jälje00n

!€# luke^WG>n
!€0 luje00n




        
"Gradation k:0" 
k:0 <=>                h _ Vowel           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! tuhka - tuhat
                       a _ a               (Dummy:) (:i) (Dummy:) %^WG:0 ; ! makaa - maata
                       y _ e               (Dummy:) (:i) (Dummy:) %^WG:0 ; ! kykenee - kyetä
      NotOUIF: [ l | r ] _ NotEYUF:        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! varas - varkaan
      NotOUIB: [ l | r ] _ NotEYUB:        (Dummy:) (:i) (Dummy:) %^WG:0 ; !märkä - märät
!  Vowel [Vowel - [e|i]] _ Vowel           (Dummy:) (:i) (Dummy:) %^WG:0 ; ! kaiku - kaiun?
!   [ NotOUIF: | l | r ] _ NotEYUF:        (Dummy:) (:i) (Dummy:) %^WG:0 ; ! varas - varkaan
!   [ NotOUIB: | l | r ] _ NotEYUB:        (Dummy:) (:i) (Dummy:) %^WG:0 ; !märkä - märät

!€# tuhka^WG>t
!€0 tuh0a00t
!€ 
!€# maka^WG>t^A
!€0 ma0a00ta
!€ 
!€# kyke^WG>t^A
!€0 ky0e00tä
!€ 
!€# sarka^WG>n
!€0 sar0a00n


!   a | e | i | i6: _ [o | e: | i] (Dummy:) (:i) (Dummy:) ^WG:0 ;
!   NotOUIB: | l | r _ NotEYUB: (Dummy:) (:i) (Dummy:) ^WG:0 ;
! Left context example:     a
! Right context example:    i +:0 ^WG:0




"k:v"
k:v <=>     [Vowel - i] _ [o|ö|u|y]    (Dummy:) (:i) (Dummy:) %^WG:0 ;
           \i [o|ö|u|y] _ [Vowel - e:] (Dummy:) (:i) (Dummy:) %^WG:0 ;
                  [o|ö] _  e:          (Dummy:) (:i) (Dummy:) %^WG:0 ;


!€# puku^WG>n
!€0 puvu00n


"Gradation nk:ng"
!! __@RULENAME@__ 
k:g <=> Vowel: n _  Vowel: (Dummy:) (:i) (Dummy:) %^WG:0 ;


!€# ranka^WG>n
!€0 ranga00n


!! !Gradation of -t-

"Gradation t:đ"
!! __@RULENAME@__ 
!t:đ <=> [ Vowel | l | r ] _  Vowel: (Dummy:) %^WG:0 ;  
!t:đ <=> Vowel  _  Vowel: (Dummy:) %^WG:0 ; 

 

 t:đ <=> Vowel  _  Vowel (Dummy: - %^E2I:) %^WG:0 [\i3:|\i6:|\i2:] ;  
         h _ Vowel: (Dummy:) (:i) (Dummy:) %^WG:0 ; 

!t:đ <=> Vowel  _  [Vowel: - [\i3:|\i6:|\i2:]] (Dummy:) %^WG:0 [\i3:|\i6:|\i2:] ;  

 ! lähteet : lähđen/lähen


!€# lahti^WG
!€0 lahđi0


"ti:si"
!! __@RULENAME@__ 
t:s <=> Vowel n _ [ (e:) %^E2I: i3: | Vowel:0 [Dummy:* - %^WG:0] %>:0 i3: | (:s) e: %>:0 i2: [.#.|Cns]] ;
        Vowel   _ [ (e:) %^E2I:     | Vowel:0 [Dummy:* - %^WG:0] %>:0 i3: | (:s) e: %>:0 i2: [.#.|Cns]] ;
  Vowel [n|l|r] _ [                   Vowel:0            %^WG:0  %>:0 i3: ] ;

!# vuote^E2I... not ready
!0 vuoss00i0i

"Gradation Nt:NN"
!! __@RULENAME@__ 
t:Nx <=> Nx  _  :Vowel (Dummy:*) ([i|i2:]) Dummy:* %^WG:0 ?* \%^E2I:0 ;  
!t:Nx <=> Nx  _  Vowel: (Dummy:) ([i|i2:]) (Dummy:) %^WG:0 (%>:0) \i3: ;  
    where Nx in ( l n r ) ; 
! NB nt:nn before i3, if the vowel before i3 is not dropped out. annoin, mutta lensin, lennoin


!€# lentä^WG>n
!€0 lennä00n

"Alveolar assimilation for consonant stems"
!! __@RULENAME@__ 
 %^N:Cx <=> Cx: %>: _ ;
    where Cx in ( l r s ) ; 


!€# tul>^N^U
!€0 tul0lu

! ===============
!! !!!Vowel rules
! ===============

!! !!Vowel harmony 

"Back harmony"
!! __@RULENAME@__ 
 Vx:Vy <=> BackVowel: NonFront:* (:NonFront) NonFront:* %>: NonFront:* ([ NonFront: | :NonFront ]) NonFront:* _ ;  
         where Vx in ( %^A %^O %^U )
               Vy in (   a   o   u )
         matched ;

!€# kulke>^A
!€0 kulke0a

!! !!Vowel copying and metathesis
         
"Vowel copying" ! 
!! __@RULENAME@__  
! %^V:Vx <=> :Vx [Dummy:* - %^HMETA:] %>:0 _ ;!(%>:0) [ (%>:0) Cns|.#.];![ Cns|[Vowel - i4:]|.#.] ;
!    where Vx in ( a e i u y ä ö ) ; ! o in next rule
     
%^V:Vx <=> :Vx Dummy:* %>:0 _ ;
    where Vx in ( a e i u y ä ö ) ; ! o in next rule 
   
! poika : poikaa
! kive : kivee
    
"Vowel copying in long h forms of a-stems"
!! __@RULENAME@__ 


 %^V:o <=> .#. Cns:* (Vowel:) [i|e|a] Cns:* a: %>:0 %^HMETA: h _ %^AO: i2: ; 
                                  :o [Dummy:* - %^HMETA:] %>:0 _ [ (%>:0) Cns|.#.] ;
                                            :Cns :Cns :o %>:0 _ ;
! Mikä säätö?
! traaka : traakkhoin
! sivakka : sivakkhoin 
!0 katto>^V alempi sääntö?
!0 katto0o


!€# paivukko>^V
!€0 paivukko0o


"Vowel copying in short h-illative"
!! __@RULENAME@__
 %^V:Vx <=> Vowel Vx: %>:0 h _ [ n: | m: ] ;  
    where Vx in ( a e i o u y ä ö ) ; 


!€# maa>h^Vn
!€0 maa0han

"Vowel copying in long h forms"
!! __@RULENAME@__ 
 %^V:Vy <=>  Vx: %>:0 %^HMETA: h _ [ %^V: | i2: ] ; 
    where Vx in ( a e i i2 o u y ä ö ) 
          Vy in ( a e i i  o u y ä ö ) 
             matched ;
 

"Vowel copying in long h forms 2"
!! __@RULENAME@__ 
 %^V:Vy <=>  Vx: %>:0 %^HMETA: h %^V: _ ; 
    where Vx in ( a e i i2 o u y ä ö )  
          Vy in ( a e i i  o u y ä ö ) 
             matched ;

! %^V:Vx <=> Vowel Vx: %>:0 %^HMETA: h %^V: _ ; 
    
"Stem deletion in h-illative"
!! __@RULENAME@__ 
! Vx:0 <=> Cns _ %>:0 %^HMETA:0 h [ %^V: | %^AO: | i: | o:] ; 
!    where Vx in ( a e i o u y ä ö ) ;

 Vx:0 <=> Cns _ %>:0 %^HMETA:0 h [ :Vx | (%^AO:) ] ; 
    where Vx in ( a e i o u y ä ö ) ;

! oppettaajhiin 
    
! "Stem deletion in h-illative b"
! !! __@RULENAME@__ 
!  Vx:0 <=> Cns _ %>:0 %^HMETA:0 h %^V: ; 
!     where Vx in ( a e i o u y ä ö ) ;  
       

!€# syksy>^HMETAh^V^Vn
!€0 syks000hyyn


"e deletion in illative plural"
 e:0 <=> _ %^EDEL:0 %>:0 h ;
         _ %^E2I:0 %^HMETA:0 h i i ;
 ! vuothiin
  
 
"e deletion in oblique plural of e-stem nouns"
 e:0 <=> _ (%^WG:0) %>:0 i2: ;


!! !!Stem alternation rules
!  ========================

"Stem shortening for consonant initial suffixes" !!= * @CODE@
 Vx:0 <=> Vx _ %^VDEL:0  (Dummy:) %>:0 [s|t] ;
     where Vx in ( a e i ä ) ;


!€# rakkaa^VDEL^WG>s
!€0 ra0ka0000s

     



"e:0 in consonant stems"
!! __@RULENAME@__ 
e:0 <=> Cns: _ [ (Dummy:* - %^E2I:0) %>:0 [i3:|i4:] | %^EDEL:0 %>:0 t ] ;


!€# aukaise>i3
!€0 aukais00i


!! !!i rules
!  =========

!! The -i- rules require different i-s for different POS.

!! !a before i

"a:0 before Pret and Pl i when rounded root vowel"
!! __@RULENAME@__ 
a:0 <=> (\a) [(u) u|([u|o]) o] (:i|i6:) Cns: (Cns:) (Cns:) _  [(Dummy:* - %^AE:) %>:0  [i2:|i3:] | [a:0]] ;


!€# otta^WG>i2n 
!€0 o0t000in  

"a:o before Pl i and Pret i"
!! __@RULENAME@__ 
!a:o <=>      .#.   (Cns:*) ([Vowel: - [o|u]]) [i|e|a] Cns:* (:Cns) (%^AO:0) _                   %>:0 [i2: | i3: ] ; !2-syll 
a:o <=>  [ #: | .#. ] Cns:* ([i|e|a]         ) [i|e|a] Cns:*  :Cns           _ (Dummy: - %^AE:0) %>:0 [i2: | i3: ] ;

!€# kal0a>i2en
!€0 kallo0ien


!€# kulkkiija>i2
!€0 kulkkiijo0i


"a:i in 3-syll stems with long a and i"
!! __@RULENAME@__ 
a:i <=> Vowel Cns Cns:* a a Cns:+ (:Cns) _ (%^WG:) %>:0 i2: ; ! 3-syll

! opettaajiissa

"a:e in passives"
!! __@RULENAME@__ 
! Vx:e <=> _ %^AE:0 (t) (t) ;
! 	where Vx in ( a ä ) ;

Vx:e <=> _ %^AE:0 (%^WG:0) %>:0 t (t) ;
 	where Vx in ( a ä ) ;

"ä:0 before i except in conditional"
!! __@RULENAME@__ 
 ä:0 <=> Cns: _ [(Dummy:* - %^AE:0) %>:0 [ i | i2: | i3: | i5: ] | [ä:0]];


!€# kehä>i2>ss^A
!€0 keh00i0ssä 
 

"Shortening of long vowel in front of i"
!! __@RULENAME@__ 
 Vx:0 <=> _ Vy %>: ([Dummy: - %^AE:]) [ i | i2: | i3: | i4:] ; 
     where Vx in ( a ä u y u y i ) 
           Vy in ( a ä u y o ö e )
          matched ;


!€# pää>i2>ss^A
!€0 p0ä0i0ssä

! saa∂a : sain : saisin
! ottaa : otan : ottaisin i4 ei passaa kaikkialle!

          
!"Shortening of long vowel when vowel lengthening"
!! __@RULENAME@__ 
! %^V:0 <=> Vowel %>: _ %>:0 [ i3:i | i4:i ] ; 
 
! Tämä sääntö aiheuttaa paljon ratkaisemattomia konflikteja säännön "Vowel copying" kanssa.
! Mikä tämä sääntö on ja miten se eroaa edelisestä "Shortening of long vowel in front of i"?
! soppiit: sopi: sopisi
! juttaat: juti : jutaisi?
! saa∂a: sai : saisin
! jää∂ä : jäin : jäisin
! Poistettu toistaiseksi


"e:i in nom.sg. of e-stems"
!! __@RULENAME@__
 e:i <=> _ %^E2I:0 [ #: | .#. ] ;  


!€# ove^E2I
!€0 ovi0
 
!! * ove : ovi

"i:0 in par and gen pl of i-stems" ! ristien
 i:0 <=> Cns: _ %>:0 i2:i %>:0 [ %^V n | %^V: ] ;

! # risti>i2>^Vn
! 0 rist00i0en



"i:0 in past tense and conditional of i-stems" ! leikin, tohtisin
 i:0 <=> Cns: _ Dummy:* %>:0 [ i3:i | i4:i ] ;



!€# leikki^WG>i3n
!€0 lei0k000in







!%%%%%%%%%%%%%%%%%%%%

! The old a rules for refernce

!old a rules "a:0 before Pret and Pl i when rounded root vowel"
!old a rules !! __@RULENAME@__ 
!old a rules 
!old a rules a:0 <=> (\a) [(u) u|([u|o]) o] (i) Cns: (Cns:) (Cns:) _  [(Dummy:* - %^AE:) %>:0  [i2:|i3:] | [a:0]] ;
!old a rules 
!old a rules !a:0 <=> \a [(u) u|([u|o]) o] (i) Cns: (Cns:) (Cns:) _  ([Dummy: - %^AE:]) %>:0 !Dummy:* [i2:|i3:] ;
!old a rules ! a:0 <=>          [u|o]     (i) Cns: (Cns:) (Cns:) _ (Dummy:) %>:0 [i2:|i3:] ;
!old a rules 
!old a rules 
!old a rules !€# otta^WG>i2n 
!old a rules !€0 o0t000in  
!old a rules 
!old a rules 
!old a rules "a:o before Pl i and Pret i"
!old a rules !! __@RULENAME@__ 
!old a rules !a:o <=> .#. (Cns:*) [i|e|a] \o \u Cns:* (:Cns) %>:0 %^AO:o a:o _ %>:0 [i2:i | i3:i ] ; ! 2-syll
!old a rules a:o <=> .#. (Cns:*) [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) (%^AO:0) _  %>:0 [i2: | i3: ] ; !2-syll 
!old a rules 
!old a rules !a:o / <= .#. (Cns:*) [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) %^AO:0 _ %>:0 i [a:a] ; !2-syll
!old a rules 
!old a rules !"a never o in front of i:i"
!old a rules !! __@RULENAME@__ 
!old a rules !a:o /<= (.#.) (Cns:*) a:a (\%>:0) _  i:i ;
!old a rules 
!old a rules !a:o /<= a _ (\%>:0) i ;
!old a rules 
!old a rules        ! .#. Cns:* [Vowel: - [o|u]] [i|e|a] Cns:* (:Cns) [Dummy: - %^AE:] _ %^AO:0 [ i2:| i3: ] ;
!old a rules ! Vowel Cns Cns:* [Vowel: - [o|u]] [i|e|a] Cns:+ (:Cns)[Dummy: - %^AE:] _ [Dummy: - %^AE:] %>:0 [i2: | i3:] ; !3-syll
!old a rules  
!old a rules ! a:o <=>  .#. Cns:* ([Vowel: - [a|o|u]]) [i|e|a] Cns:* (:Cns) _ [Dummy: - %^AE:] %>:0 !i2: ; ! 2-syll
!old a rules !          .#. Cns:* ([Vowel: - [a|o|u]]) [i|e|a] Cns:* (:Cns) _  %^AO:0 ;
!old a rules !    Vowel Cns Cns:* ([Vowel  - [a|o|u]]) [i|e|a] Cns:+ (:Cns) _ [Dummy: - %^AE:] %>:0 i2: ; ! 3-syll
!old a rules !    Vowel Cns Cns:* ([Vowel  - [a|o|u]]) Vowel   Cns:+ (:Cns) _ [Dummy: - %^AE:] %>:0 i2: ; ! 3-syll
!old a rules !          .#. Cns:*   a                  ([a|u]) Cns:* (:Cns) _ [Dummy: - %^AE:] %>:0 i3: ;
!old a rules 
!old a rules 
!old a rules !€# kal0a>i2en
!old a rules !€0 kallo0ien
!old a rules 
!old a rules 
!old a rules !€# kulkkiija>i2
!old a rules !€0 kulkkiijo0i
!old a rules 
!old a rules !"a:o for HMETA"
!old a rules ! %^V:o <=> .#. Cns:* a ([a|u]) Cns:* (:Cns) [Dummy: - %^AE:] %>:0 %^HMETA:0 h _ %^AO:0 ;



!"Gemination"
! 0:Cx <=> [Vowel|l|r] Cy _ Vowel: %>:0 Vowel: ;   
!        where Cy in ( h j k l m n p r s t v ) 
!              Cx in ( h j k l m n p r s t v ) 
!    matched; 

!"Gemination of h"
!0:h <=> [ .#. | Cns ] Vowel h _ :Vx %>:0 :Vx ;
!         where Vx in ( a e i o u ä ö ) ;
!
!"Gemination of l"
!0:l <=> [ .#. | Cns ] Vowel h _ :Vx %>:0 :Vx ;
!         where Vx in ( a e i o u ä ö ) ;


!   "Gemination VVXi"     
!0:Cx <=> Cns:*  Vowel:* Vowel (Vowel) Cx _ [ Vowel | a:o ] %>:0 [:i | i2:i];
!                                         _ [ a:0 | e:0 ]  %>:0 :i i2:i ;
!      where Cx in ( k p t s ) ;       
 




