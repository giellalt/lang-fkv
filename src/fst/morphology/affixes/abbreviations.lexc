!LEXICON SYMBOLS
!  §+N+ABBR:§  # ; ! §:s
! §§+N+ABBR:§§ # ; ! §§:ain
!  $+N+ABBR:$  # ; ! $:s
!  €+N+ABBR:€  # ; ! €:s
!  £+N+ABBR:£  # ; ! £:s
!           ®+N+ABBR:®  #               ;
!           ©+N+ABBR:©  #               ;

! Now splitting according to POS, and according to dot or not
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

LEXICON ab-noun-itrab   +ABBR+Gram/IAbbr:    ab-noun ;
LEXICON ab-noun-trab    +ABBR+Gram/TAbbr:    ab-noun ;
LEXICON ab-noun-trnumab +ABBR+Gram/TNumAbbr: ab-noun ;

LEXICON ab-noun   !!= * **@CODE@**
         ab-nodot-noun ;
+Use/NG: ab-dot-noun   ;

LEXICON ab-adj-itrab +ABBR+Gram/IAbbr: ab-adj ;
LEXICON ab-adj-trab  +ABBR+Gram/TAbbr: ab-adj ;

LEXICON ab-adj   !!= * **@CODE@**
         ab-nodot-adj ;
+Use/NG: ab-dot-adj   ;

LEXICON ab-adv-itrab   +ABBR+Gram/IAbbr:    ab-adv ;

LEXICON ab-adv   !!= * **@CODE@**
         ab-nodot-adv ;
+Use/NG: ab-dot-adv   ;

LEXICON ab-num-itrab +ABBR+Gram/IAbbr: ab-num ;

LEXICON ab-num   !!= * **@CODE@**
         ab-nodot-num ;
+Use/NG: ab-dot-num   ;

!! ### Lexicons without final period

LEXICON ab-nodot-noun-itrab   +ABBR+Gram/IAbbr:    ab-nodot-noun ;
LEXICON ab-nodot-noun-trab    +ABBR+Gram/TAbbr:    ab-nodot-noun ;
LEXICON ab-nodot-noun-trnumab +ABBR+Gram/TNumAbbr: ab-nodot-noun ;

LEXICON ab-nodot-noun   !!= * **@CODE@**  The bulk
 nodot-attrnomaccgen-infl ;
 nodot-oblique-infl       ;

LEXICON ab-nodot-adj-itrab +ABBR+Gram/IAbbr: ab-nodot-adj ;

LEXICON ab-nodot-adj   !!= * **@CODE@**
 nodot-attrnomaccgen-infl ;

LEXICON ab-nodot-adv-itrab   +ABBR+Gram/IAbbr:    ab-nodot-adv ;
LEXICON ab-nodot-adv-trnumab +ABBR+Gram/TNumAbbr: ab-nodot-adv ;

LEXICON ab-nodot-adv   !!= * **@CODE@**
           #     ;
    +Attr: #     ;
           RHyph ;

LEXICON ab-nodot-num   !!= * **@CODE@**
 nodot-nomaccgen-infl ;

!! ### Lexicons with final period

LEXICON ab-dot-noun-itrab   +ABBR+Gram/IAbbr:    ab-dot-noun ;
LEXICON ab-dot-noun-trab    +ABBR+Gram/TAbbr:    ab-dot-noun ; 
LEXICON ab-dot-noun-trnumab +ABBR+Gram/TNumAbbr: ab-dot-noun ;

LEXICON ab-dot-noun   !!= * **@CODE@**  This is the lexicon for abbrs that must have a period.
 dot-attrnomaccgen-infl ;

LEXICON ab-dot-adj-itrab   +ABBR+Gram/IAbbr:    ab-dot-adj ;
LEXICON ab-dot-adj-trab    +ABBR+Gram/TAbbr:    ab-dot-adj ;

LEXICON ab-dot-adj   !!= * **@CODE@**  This is the lexicon for abbrs that must have a period.
 dot-attrnomaccgen-infl ;

LEXICON ab-dot-adv-itrab   +ABBR+Gram/IAbbr:     ab-dot-adv ;
LEXICON ab-dot-adv-numnoab +ABBR+Gram/NumNoAbbr: ab-dot-adv ;
LEXICON ab-dot-adv-trab    +ABBR+Gram/TAbbr:     ab-dot-adv ;
LEXICON ab-dot-adv-trnumab +ABBR+Gram/TNumAbbr:  ab-dot-adv ;
LEXICON ab-dot-adv   !!= * **@CODE@**  This is the lexicon for abbrs that must have a period.
 DOT ; ! Adv without case.

LEXICON ab-dot-num-trab  +Num+ABBR+Gram/TAbbr: ab-dot-num ;
LEXICON ab-dot-num   !!= * **@CODE@**  This is the lexicon for abbrs that must have a period.
 dot-nomaccgen-infl ;

!LEXICON ab-dot-cc-itrab +CC+ABBR+Gram/IAbbr: ab-dot-cc ;
LEXICON ab-dot-cc-trab  +CC+ABBR+Gram/TAbbr: ab-dot-cc ;
LEXICON ab-dot-cc   !!= * **@CODE@**
 DOT ;


LEXICON ab-dot-verb-itrab   +V+ABBR+Gram/IAbbr:    ab-dot-verb ;
LEXICON ab-dot-verb-trab    +V+ABBR+Gram/TAbbr:    ab-dot-verb ;

LEXICON ab-dot-verb   !!= * **@CODE@**
 +TV+Imprt: DOT ; ! Period.

!LEXICON ab-nodot-verb   !!= * __@CODE@__
! +TV+Imprt: # ; ! No period.

! riegádan:

LEXICON ab-dot-IVprfprc-trab
+V+ABBR+Gram/TAbbr:    ab-dot-IVprfprc ;

LEXICON ab-dot-IVprfprc
+Sg+IV+PrfPrc: DOT ; ! Period.


LEXICON nodot-attrnomaccgen-infl   !!= * **@CODE@**
 nodot-attr-infl      ;
 nodot-nomaccgen-infl ;

LEXICON nodot-attr-infl   !!= * **@CODE@**
   +Attr: #     ;

LEXICON nodot-nomaccgen-infl   !!= * **@CODE@**
 +Sg+Nom: #     ;
          RHyph ;

LEXICON nodot-oblique-infl
+Gen:n # ;     ! no period, that is.
+Ill:%:iin # ;  ! no period, that is.
+Ine:%:ss^A # ;  ! no period, that is
+Ela:%:st^A # ;  ! no period, that is.
+Com:%:ine # ; ! no period, that is.
+Ess:%:n^A # ;  ! no period, that is.
+All:%:lle # ; ! no period, that is. !SUB
+Ade:%:ll^A # ; ! no period, that is. !SUB
+Abl:%:lt^A # ;! no period, that is. !SUB
+Tra:%:ksi # ; ! no period, that is. !SUB

LEXICON dot-attrnomaccgen-infl   !!= * **@CODE@**
   dot-nomaccgen-infl ;
   dot-attr           ;

LEXICON dot-attr   !!= * **@CODE@**
   +Attr: DOT ;

LEXICON dot-nomaccgen-infl   !!= * **@CODE@**
 +Sg+Nom: DOT ;
 +Sg+Gen: DOT ;


LEXICON DOT   !!= * **@CODE@** - Adds the dot to dotted abbreviations.

 +Use/-PMatch:%. # ; ! We need the dot here for regular fsts
! Split the abbr + full stop in two segments, but only when using pmatch:
< "@P.Pmatch.Loc@" {.} "+CLB":0 "+Use/PMatch":0 > # ;
! Make a regular ABBR analysis AND backtrack to find alternative analyses:
< "+Use/PMatch":0 "@P.Pmatch.Backtrack@" 0:%. > # ;

! Gives:
!$ echo 'su.' \
!| hfst-tokenise -g tools/tokenisers/tokeniser-gramcheck-gt-desc.pmhfst 
!"<su.>"
!	"." CLB <W:0.0> "<.>"
!		"su" Adv ABBR Gram/NumNoAbbr <W:0.0> "<su>"
!	"su" Adv ABBR Gram/NumNoAbbr <W:0.0>
!	"." CLB <W:0.0> "<.>"
!		"son" Pron Pers Sg3 Gen <W:0.0> "<su>"
!	"." CLB <W:0.0> "<.>"
!		"son" Pron Pers Sg3 Acc <W:0.0> "<su>"
!:\n
!
! which is exactly what we want.





