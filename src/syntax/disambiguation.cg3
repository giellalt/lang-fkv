# cat fkvtext_analysed.txt | lookup2cg | vislcg3 -g disambiguation.cg3 -t
# echo 'sentence' | preprocess | lookup -flags mbTT 1.fst | lookup2cg|vislcg3 -g ../../syntax/1_disambiguation.cg3

#   ============================ #
#!! !!!Start making a syntactic disambiguator
#   ============================ #


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<...>" "<!>" "<?>" "<¶>" ;   
#!! Sentence delimiters are the following: "<.>" "<...>" "<!>" "<?>" "<¶>"




# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>) (sent);
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

LIST N = N ;  #!! * N = noun
LIST A = A ;  #!! * A = adjective
LIST Num = Num ;  #!! * Num = numeral
LIST V = V ;  #!! * V = verb
LIST CC = CC ; #!! * CC = conjunction
LIST CS = CS ; #!! * CS = subjunction
LIST Adv = Adv ; #!! * Adv = adverb
LIST Pr = Pr ;  #!! * Pr = preposition
LIST Po = Po ;  #!! * Po = postposition
LIST Pron = Pron ; #!! * Pron = pronoun
LIST Interj = Interj ; #!! * Interj = interjection
LIST Dem = Dem ; #!! * Dem = demonstrative
# Numerus
LIST Sg = Sg ;
LIST Pl = Pl ;
LIST Sg1 = Sg1 ;
LIST Sg2 = Sg2 ;
LIST Sg3 = Sg3 ;
LIST Pl1 = Pl1 ;
LIST Pl2 = Pl2 ;
LIST Pl3 = Pl3 ;
# Case
LIST Nom = Nom ; #!! * Nom = nominative
LIST Gen = Gen ;
LIST Acc = Acc ;
LIST Par = Par ;
LIST Ine = Ine ;
LIST Ill = Ill ;
LIST Ela = Ela ;
LIST Ade = Ade ;
LIST Abe = Abe ;
LIST All = All ;
LIST Abl = Abl ;
LIST Ess = Ess ;
LIST Tra = Tra ;
LIST Ins = Ins ;
LIST Com = Com ;
# Types
LIST Prop = Prop ;
LIST Interr = Interr ;
LIST Rel = Rel ;
LIST Relpronpl = (Pron Rel Pl) ; # "mikkä ja "jokka"
LIST Relpronsg = (Pron Rel Sg) ; # "mikä" ja "joka"
LIST Interrpronpl = (Pron Interr Pl) ; # "kuka" ja "mikä"

LIST Pers = Pers ;
LIST Indef = Indef ;
LIST ConNeg = ConNeg ;
LIST PrfPrc = PrfPrc ; 
LIST Imprt = Imprt ;
LIST Act = Act ;
LIST Neg = Neg ;

LIST COMMA = "," ;
LIST Foc/kaan = Foc/kaan ;
LIST Sem/Fem = Sem/Fem ;
LIST @CVP = @CVP ; # !! * @CVP = Conjunction or subjunction that conjoins finite verb phrases.
LIST @CNP = @CNP ; # !! * @CNP = Local conjunction or subjunction.



# Sets
# ====

LIST WORD = N V A Adv Pr Interj Po Num CC CS Pron  ;

# Sets for barriers
LIST NPMOD = (Pers Gen) (A Gen) (A Ine) (A Ade) (A Nom) (A Par) (A Acc) ;
SET NPMODADV = NPMOD OR Adv ;

SET NOT-NPMOD = WORD - NPMOD ;

SET NOT-NPMODADV = WORD - NPMODADV ;

LIST QVANT-ADV = "paljon" "vähän" "enämen" "vähemän" ;
LIST KUNKA = "kunka" "missä" ;


SET S-BOUNDARY = CS OR ("mutta") OR (":") OR Rel OR @CVP OR KUNKA ;

LIST VFIN = Ind Cond Pot Imprt   ;

LIST COPULAS = "olla" ;

LIST MOD-ASP =  "saađa" "häyttyyt"   ; #add lemmas

LIST AUX-OR-MAIN = "tulla" "osata" "haluta"  ; #add lemmas

SET AUX = COPULAS OR MOD-ASP OR AUX-OR-MAIN ;

SET SV-BOUNDARY = S-BOUNDARY OR VFIN   ;


# Here come the rules #

BEFORE-SECTIONS



SECTION
# SELECT:Dummyrule WORD IF (0 WORD) ;

SELECT:person_test V IF (-1 Pers) ; # exempel

SELECT:adv_after_V Adv IF (-1 V)
       (NEGATE 0 Dem OR Indef LINK *1 N BARRIER WORD - A)
       (NEGATE 0 Pron LINK 1 COMMA LINK 1 KUNKA OR Rel OR CS) ;

	#$ sykläsin+V noppeesti+Adv tähään+Dem koulhuun+N+Ill  # noppeesti
	#$ Konferansiprogramin saatat lukea tästä: www.fad.dep.no.
	#% on+V       tänä+Dem  A    vuona+N

REMOVE:prop_infrontof_kieli Prop (0 Gen OR Pron OR A LINK 1 ("kieli")) ;
	#$ Kainun kieli oon osa Ruijan kulttuuriperinnöstä.

REMOVE:PropInit Prop (*-1 BOS BARRIER WORD)  ((0 CC) OR (0 Pr LINK 1 N)) ;
	#$ Mutta kaikila kirjailiijoila

SELECT:PropNotInit Prop (NOT *-1 BOS BARRIER WORD) ;




SELECT:Prifgenpar Pr IF (1 Gen OR Par);
SELECT:Poifgen Po IF (-1 Gen OR Par)(NEGATE 0 ("aikkaa") LINK NOT -1 Pron + Gen) ;
  #$  Ja hän itte meni heiđän etheen, kumarsi seittemen kerttaa maahan siksi ko hän tuli veljen etheen(en setning hvor den skal fungere)
  #% Mutta heilä ei ole juurta, het uskothaan vain vähäsen aikkaa, ja ko heitä freistathaan niin het jääđhään pois (setning hvor den ikke skal fun).
  #$ eksempel med aikkaa (het uskothaan vain vähäsen aikkaa)

### Rules for mapping @CVP and @CNP on the CC
MAP @CVP TARGET CS ;

MAP @CNP TARGET CC IF (-1 N)(*1 N BARRIER NOT-NPMOD) ;
	#$ mitä Kväänin kieliraati ja Kväänin kielitinka oon tehneet ...



LIST SUBJ-CASE = Nom Par ;
 

SELECT:Pl3ollaifplrelpronandplinterrpron Pl3 IF (0 ("olla")) ((-1 (N Pl Nom) OR Pron + Pl3 + SUBJ-CASE OR Pron + Pl + SUBJ-CASE) OR (1 (N Pl) + SUBJ-CASE)) ;
  #$ Nämät käskyt mikkä mie tääpänä annan sinule
  #$ Kekkä oon nämät jokka oon sinun myötä?
  #$ Kaikki muut jokka oon tulheet ennen minuu
  #$ Kirjota: Autuhaat oon net jokka oon kuttuttu Karittan hääruokapöythään.
  #$ Nämät oon net lapset jokka Jumala hänen armossa oon suonut sinun palvelijale.
  #% ... Jumala hänen armossa oon suonut sinun palvelijale.
  #% Mikä oon sinun meininki näissä kaikissa ihmissä tässä joukossa ...
  #%  ... sillä Herra oon se joka tuomittee minun.
  #% Kansa sillon ko olen tehny jotaki mistä toiset ei tykätäl.

SELECT:Sg3ollaifplrelpronandplinterrpron Sg3 IF (0 ("olla")) (*-1 (N Sg Nom) OR  Relpronsg + Nom BARRIER WORD - Adv) ;
  #$ ... Jumala hänen armossa oon suonut sinun palvelijale.
  #$ Mikä oon sinun meininki näissä kaikissa ihmissä tässä joukossa ...
  #$  ... sillä Herra oon se joka tuomittee minun.
  #$ Kansa sillon ko olen tehny jotaki mistä toiset ei tykätäl.

SELECT:Sg3ollainpretandperf Sg3 IF (0 COPULAS LINK 1 PrfPrc)(*-1 BOS BARRIER WORD)  ;
#$ Oon kirjoitettu profeeta Esekielin tykönä luvussa 34, värsyt 23 - 31: 23. 
# ... jokka Jumala hänen armossa oon suonut sinun palvelijale.

SELECT:Sg3ollainpretandperf Sg3 IF (0 COPULAS LINK 1 Ade LINK *1 Nom OR Par BARRIER NOT-NPMODADV)(*-1 BOS OR S-BOUNDARY BARRIER WORD)  ;

# Oonhan meilä vielä kieli. 
# Oonko niilä semmmosta kulttuurii, mitä ... 
#$  Oon juuri tyven. 
#$ Oonko se tosi, ette semmoinen Alattion mies oon käyny aivan kiini Kööpenhaminassa asti ja puhunu kuninkhaan kans? 
# Oon aamu. 
# Oon hyvin näköistä. 
# Oonpa ihana paikka!
# Oonko Ollin Nuutti tullu takaisin kuninkhaan tykköö? 
# Ollin Nuutti likenee pitkin tietä ja oon heti ihmisten tykönä. 
# Universiteetin siljola oon oikhein iso kartano. 
# Siinä kartanossa oon semmoinen ymmyräinen loma
# ... mihin oon meininki lähteet? 
# Meilä oon vielä vaapa sija. 


SELECT:Relpronandnotintterpron Relpronsg IF (0 Interr)(*-1 N + Sg OR Pron + Sg OR Pron + Sg3 BARRIER WORD - CC) (NEGATE *1 ("?") BARRIER S-BOUNDARY) ;
# Kekkä oon nämät jokka oon sinun myötä?
# Anna se mikä oon sinun, olla sinun.
# ... ota vasthaan tämän lahjan mikä tuopi minun siunauksen, ja minkä tuothiin sinule ...
# Mutta Herra Jumala huusi miehele ja sanoi hänele: Missä sie olet?
# Mikä oon sinun meininki näissä kaikissa ihmissä tässä joukossa mikä oon sinun matkassa, ja jokka mie olen nyt kohðanu?"
# ... ota vasthaan tämän lahjan mikä tuopi minun siunauksen, ja minkä tuothiin sinule, sillä Jumala oon näyttäny minule. 

SELECT:Relpronandnotintterpron Relpronsg IF (0 Interr)(*-1 (PUNCT RIGHT) BARRIER WORD - CC LINK *-1 (PUNCT LEFT) LINK -1 N + Sg OR Pron + Sg OR Pron + Sg3 BARRIER WORD - CC) (NEGATE *1 ("?") BARRIER S-BOUNDARY) ;
# Raatimies Odd Keskitalo informeerasi sekretariaatin funkšuunista (toiminta vailuu kokonhans), minkä jälkhiin raatimiehet selvästi ilmasthiin, ette tarvithaan ainaski kohta 100% stillinki, jotta kyethään ottamhaan vaarhiin ja selviämhään kaikista tarpheista ja haastheista.

SELECT:interrpron Interr IF  (*1 ("?") ) ;
# Mikä oon sinun meininki näissä kaikissa ihmissä tässä joukossa mikä oon sinun matkassa, ja jokka mie olen nyt kohðanu?"

SELECT:differencebetweenniitäimprtandniitädemandpersifitssubj (Pron Dem Pl) OR (Pron Pers Pl3) IF (0 Par OR Nom)((1 VFIN + Pl3) OR (-1 VFIN + Pl3)) ;
# ... vaikka niitä oonki paljon, oon yksi ruumis, niin oon Kristuski.
# Net huuðethiin ...
# Autuhaat oon net jokka oon kuttuttu Karittan hääruokapöythään.
# Hän puhhuu omile lamphaile nimeltä ja viepi net ulos.
# Net menhään pakhoon sillä net ei tunneta outoihmisen ääntä.
# Net lyöđhään yli venheen.
# Net lullathiin minun pehmeesti,
# vapauttaat kaikki net jokka koko ijän kuoleman pölö stä olthiin olheet orjana.
# V Act Imprt Sg2

#SELECT:differencebetweenniitäimprtandniitädemandpersifitsobj
# Kuka käipi taivhaassa meiðän eðestä noutamassa niitä alas, niin ette saatama kuulla niitä ja elläät niitten tahðossa.
# Kuka käipi toisela puolela mertä noutamassa niitä, niin ette saatama kuulla niitä ja elläät niitten tahðossa.
# Kalliole puðonnu siemen merkittee niitä ...
# Mutta hyvhään maahaan puðonnut siemen merkittee niitä ...
# Pron Dem Pl Par

SELECT:paljonadvandnotpaljonoun Adv IF (0 ("paljon") LINK *1 N + Par + Pl BARRIER WORD -  (A Par)) ;

SELECT:Relpronifitsanounoracommabeforeit Relpronpl IF ((-1 N + Pl OR Pron + Pl OR Pron + Pl3) OR (-1 COMMA LINK -1 N))(NEGATE *1 ("?")) ;
   #$ Kaikki muut jokka oon tulheet ennen minuu
    #%Kekkä oon nämät jokka oon sinun myötä?

# REMOVE:differentiatebetweenhäntänounandthirdperpron  ("häntä") IF (0 Pers) (*-1 V BARRIER WORD - Adv LINK NOT 0 ("olla")) ;
#$ Ja Eesau juoksi häntä vasthaan, ja hän alkoi hyvväilemähään häntä, otti hänen kaulan ympäri ja summasteli häntä, ja het itkethiin.
#$  ... hän kävelee heiðän eðessä, ja lamphaat kyytäthään häntä ...

REMOVE:annaimperativeandnotannaname ("Anna") IF (0 Imprt) (1 Pron LINK 1 Relpronsg)  ;
# Anna se mikä oon sinun, olla sinun.
# N Prop Sem/Fem Sg Nom
# V Act Imprt Sg2

SELECT:tulinounfromtuliprtsg3 V + Sg3 IF  (-1 N + Par LINK *-1 QVANT-ADV BARRIER WORD - A) ;
# ... kumarsi seittemen kerttaa maahaan siksi ko hän tuli veljen etheen.
# Paljon ihmissii tuli nyt kaikista kaupungiista siltä alalta.
# QVANT-ADV



SELECT:dempronandnotpronpers Dem IF (1 A OR N) (NEGATE -1 Dem LINK -1 V) (NEGATE 0 Ade LINK NOT 1 Ade + N) ;
#$ Päivää jälkhiin se iso ihmisjoukko mikä oli tullu juhlhiin, sai kuula ette Jeesus oli tulemassa Jerusaleemhiin.
#% Ja se tapattui niinä päivinä ko keisari Aukustus oli päättäny ette koko mailma piti maksaat verroo.
#% ... niin ota vasthaan tämän lahjan minun käðestä, sillä tarkoitus oli nähðä sinun silmät ...

IFF:Imperativefromconneg ConNeg IF (0 Imprt)(*-1 Neg BARRIER WORD - Foc/kaan - Adv)(NEGATE -1 Foc/kaan LINK 2 S-BOUNDARY) ;
#% Anna se mikä oon sinun, olla sinun.
#% Ei yksikhään niistä jokka mie olin kuttunu.
# Jos mie nyt olen saannu hyväntahtoisuutta sinulta, niin ota vasthaan tämän lahjan minun käðestä
 # Mutta heilä ei ole juurta, het uskothaan vain vähäsen aikkaa ...
# ... ja nautinnosta niin ette het ei saa kypsyy sattoo.
# ei ole minun tiet, sanoo Herra.
# ... ja eikä mene sinne takasin ennen ko oon kastelu maan,
# Ei mikhään luotu ole hänele näkymätön.
# Mutta minule se ei merkitte mithään jos tet tuomitetta ...
# Sillä minula ei ole tiettoo misthään mitä mie olen tehny väärin.
# Mutta en mie sen tähđen ole viaton ...
# ... ja villielläimet ei tule syömhään heitä.

REMOVE:ImperativeafterNeg Imprt IF (0 Pron)(*-1 Neg BARRIER WORD - Foc/kaan - Adv) ;
	#$ Ei yksikhään niistä jokka mie olin kuttunu.


#SELECT:congruencebetweennounandverb  IF (-1 N OR 1 N LINK V) (NEGATE -1 Dem) ;
#$ Mikä oon sinun meininki näissä kaikissa ihmissä tässä joukossa mikä oon sinun matkassa ...
#% ... niin ota vasthaan tämän lahjan minun käðestä, sillä tarkoitus oli nähðä sinun silmät ...

#REMOVE:differencebetweenfunctionalsubjunctionandtemporalsubjuntion CS IF (CS + ("ko")

SELECT ConNeg IF (-1 Neg);

SELECT:interrel Interr OR Rel (1 CS) ; # Ei anna mithään?

#MAP (@GRAM-ERR) TARGET V (0 V + 3S LINK -1 N + S) ;

LIST WORDLEMMA = (".*"r) ;

REMOVE:errorth $$WORDLEMMA + (Err/Orth) (0 $$WORDLEMMA - (Err/Orth) ) ; #Removes Err/Orth analysis when there is an analysis with same lemma without Err/Orth


AFTER-SECTIONS
