# -*- cg-pre-pipe: "hfst-tokenise -g ../../tools/tokenisers/tokeniser-gramcheck-gt-desc.pmhfst" -*-
# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2017 UiT The arctic University of Norway
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@uit.no or feedback@divvun.no

# ==================================================================== #
#!! #      K V E N   G R A M M A R   C H E C K E R
# ==================================================================== #
# ====================================================================
# Copied from fin grc 7.11.22 but later throughly revised

#  Sámi language technology 2000-2026, UiT The arctic University of Norway #

# ---------------------- #
#   Development setup:   #
# ---------------------- #
#
# cd $GTHOME/lang-fkv
# ./autogen.sh
# ./configure --enable-grammarchecker
# make
# cd tools/grammarcheckers
# make dev
#
# ---------------------- #
#   Then edit/test as:   #
# ---------------------- #
#
# emacs grammarchecker.cg3  # and C-c C-i / C-c C-c if you have cg-mode installed
# echo "Hän ei tulee." | sh modes/fkvgram.mode  # from the terminal
#
# The basic pipeline, in case modes (above) does not work
#  echo "Hän ei tulee." | hfst-tokenise -g ../tokenisers/tokeniser-gramcheck-gt-desc.pmhfst | vislcg3 -g disambiguator.cg3  | vislcg3 -g grammarchecker.cg3 -t

# ---------------------- #
#   Other debug-pipes:   #
# ---------------------- #
# ls modes/
# ---------------------- #




      # =========================================== #
      #      Short table of contents                #
      # =========================================== #

      # Delimiters
      # Tags and sets
      # Grammar checking rules

# =============== #
#!! # DELIMITERS
# =============== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" sent ;

# ================== #
#!! # TAGS AND SETS
# ================== #

SETS


#!! ## Tags


#!! This section lists all the tags inherited from the fst, and used as tags
#!! in the syntactic analysis. The next section, **Sets**, contains sets defined
#!! on the basis of the tags listed here, those set names are not visible in the output.



# Tags declared as single-membered LISTs
# ======================================

#!! ### Beginning and end of sentence
LIST BOS = (>>>) (<s>) ;  #!! BOS
LIST EOS = (<<<) (</s>) ; #!! EOS

# We define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.
# CG3 doesn´t function without >>> and <<< !


#!! ### Parts of speech tags

LIST N = N ;		    #!! N
LIST A = A ;		    #!! A
LIST Adv = Adv ;	    #!! Adv
LIST V = V ;		    #!! V
LIST Pron = Pron ;	    #!! Pron
LIST CS = CS ;		    #!! CS
LIST CC = CC ;		    #!! CC
SET CC-CS = CC OR CS ;  #!! CC-CS
LIST Po = Po ;		    #!! Po
LIST Pr = Pr ;		    #!! Pr
LIST Pcle = Pcle ;	    #!! Pcle
LIST Num = Num ;	    #!! Num
LIST Interj = Interj ;  #!! Interj
LIST ABBR = ABBR ;	    #!! ABBR
LIST ACR = ACR ;	    #!! ACR
LIST CLB = CLB ;	    #!! CLB
LIST LEFT = LEFT ;	    #!! LEFT
LIST RIGHT = RIGHT ;    #!! RIGHT
LIST WEB = WEB ;	    #!! WEB
LIST QMARK = """ ; # "  #!! QMARK
LIST PPUNCT = PUNCT ;   #!! PPUNCT
SET PUNCT = PPUNCT - QMARK ;  #!! PUNCT

LIST COMMA = "," ;  #!! COMMA
LIST ¶ = ¶;   #!! ¶



#!! ### Tags for POS sub-categories

LIST Pers = Pers ;	    #!! Pers
LIST Dem = Dem ;	    #!! Dem
LIST Interr = Interr ;  #!! Interr
LIST Indef = Indef ;    #!! Indef
LIST Recipr = Recipr ;  #!! Recipr
LIST Refl = Refl ;	    #!! Refl
LIST Rel = Rel ;	    #!! Rel
LIST Coll = Coll ;	    #!! Coll
LIST NomAg = NomAg ;    #!! NomAg
LIST Prop = Prop ;	    #!! Prop
LIST Allegro = Allegro ; #!! Allegro
LIST Arab = Arab ; #!! Arab
LIST Rom = Rom ; #!! Romertall


#!! ### Tags for morphosyntactic properties

LIST Nom = Nom ; #!! Nom
LIST Gen = Gen ; #!! Gen
LIST Ill = Ill ; #!! Ill
LIST Ine = Ine ; #!! Ine
LIST Ela = Ela ; #!! Ela
LIST All = All ; #!! All
LIST Ade = Ade ; #!! Ade
LIST Abl = Abl ; #!! Abl
LIST Com = Com ; #!! Com
LIST Ess = Ess ; #!! Ess
LIST Par = Par ; #!! Ess
LIST Tra = Tra ; #!! Tra
LIST Abe = Abe ; #!! Abe
LIST Sg = Sg ;   #!! Sg
LIST Pl = Pl ;   #!! Pl


LIST Cmp/SplitR = Cmp/SplitR ;	   #!! Cmp/SplitR
LIST Cmp/SgNom = Cmp/SgNom ; #!! Cmp/SgNom Cmp/SgGen
LIST Cmp/SgGen = Cmp/SgGen ; #!! Cmp/SgGen

LIST Comp = Comp ;		   #!! Comp
LIST Superl = Superl ;	           #!! Superl
LIST Attr = Attr ;		   #!! Attr
LIST Ord = Ord ;		   #!! Ord
LIST Qst = Qst ;		   #!! Qst
LIST IV = IV ; #!! IV
LIST TV = TV ; #!! TV
LIST Prt = Prt;				   #!! Prt
LIST Prs = Prs ;			   #!! Prs
LIST Ind = Ind ;			   #!! Ind
LIST Act = Act ;
LIST Pot = Pot ;			   #!! Pot
LIST Cond = Cond ;			   #!! Cond
LIST Imprt = Imprt ;		   #!! Imprt
LIST ImprtII = ImprtII ;	   #!! ImprtII
LIST Sg1 = Sg1 (p1 sg) ;			   #!! Sg1
LIST Sg2 = Sg2 (p2 sg) ;			   #!! Sg2
LIST Sg3 = Sg3 (p3 sg) ;			   #!! Sg3
LIST Pl1 = Pl1 (p1 pl) ;			   #!! Pl1
LIST Pl2 = Pl2 (p2 pl);			   #!! Pl2
LIST Pl3 = Pl3 (p3 pl);			   #!! Pl3
LIST Inf = Inf ;			   #!! Inf
LIST Pss = Pss ;
LIST ConNeg = ConNeg ;		   #!! ConNeg
LIST Neg = Neg ;			   #!! Neg
LIST PrfPrc = PrfPrc ;		   #!! PrfPrc
LIST VGen = VGen ;			   #!! VGen
LIST PrsPrc = PrsPrc ;		   #!! PrsPrc
LIST Ger = Ger ;			   #!! Ger
LIST Sup = Sup ;			   #!! Sup
LIST Actio = Actio ;		   #!! Actio
LIST VAbess = VAbess ;		   #!! VAbess

LIST Foc/ka = Foc/ka ;

SET VFIN = Ind OR Cond OR Pot ;

LIST Err/Orth = Err/Orth Err/Orth-a/á Err/Orth-nom/gen Err/Orth-nom/acc Err/DerSub Err/CmpSub Err/UnspaceCmp Err/HyphSub Err/SpaceCmp Err/Spellrelax err_orth_mt ; #!! Err/Orth

LIST POGEN = "luona" "luota" "takia" "vuoksi"  ; # Only as POGEN
LIST POGEN/ADV = "takana" ;
LIST POELA = "huolimatta" ;

#!! ISO: Sekä adverbina että adpositiona käytettäviä sanoja:
#!! edellä, edelle, edeltä; edessä, eteen, edestä ; sisällä, sisälle, sisältä; sisässä, sisään, sisästä ; lähellä, lähelle, läheltä ; keskellä, keskelle, keskeltä ; ympärillä, ympärille, ympäriltä ; ohessa, oheen, ohesta ; takana, taakse, takaa ; alla, alle, alta ; yllä, ylle, yltä ; vailla, vaille ; vastassa, vastaan ; mukana, mukaan ; alitse, ylitse, ohitse ; ali, yli, läpi, ohi, ympäri, kohti, vastapäätä ; lisäksi, ilman, kesken, ennen

#!! ### Semantic tags

LIST Sem/Act = Sem/Act ;			    #!! Sem/Act
LIST Sem/Ani = Sem/Ani ;			    #!! Sem/Ani
LIST Sem/Atr = Sem/Atr ;			    #!! Sem/Atr
LIST Sem/Body = Sem/Body ;			    #!! Sem/Body
LIST Sem/Clth = Sem/Clth ;			    #!! Sem/Clth
LIST Sem/Domain = Sem/Domain ;			    #!! Sem/Domain
LIST Sem/Feat-phys = Sem/Feat-phys ;	    #!! Sem/Feat-phys
LIST Sem/Fem = Sem/Fem ;			    #!! Sem/Fem
LIST Sem/Group = Sem/Group ;			    #!! Sem/Group
LIST Sem/Lang = Sem/Lang ;			    #!! Sem/Lang
LIST Sem/Mal = Sem/Mal ;			    #!! Sem/Mal
LIST Sem/Measr = Sem/Measr ;			    #!! Sem/Measr
LIST Sem/Money = Sem/Money ;			    #!! Sem/Money
LIST Sem/Obj = Sem/Obj ;			    #!! Sem/Obj
LIST Sem/Obj-el = Sem/Obj-el ;			    #!! Sem/Obj-el
LIST Sem/Org = Sem/Org ;			    #!! Sem/Org
LIST Sem/Perc-emo = Sem/Perc-emo ;		    #!! Sem/Perc-emo
LIST Sem/Plc = Sem/Plc ;			    #!! Sem/Plc
LIST Sem/Sign = Sem/Sign ;			    #!! Sem/Sign
LIST Sem/State-sick = Sem/State-lang-sick ; #!! Sem/State-sick
LIST Sem/Sur = Sem/Sur ;			    #!! Sem/Sur
LIST Sem/Time = Sem/Time ;			    #!! Sem/Time
LIST Sem/Txt = Sem/Txt ;			    #!! Sem/Txt

LIST HUMAN = Sem/Fem Sem/Mal Sem/Sur ;	    #!! HUMAN

SET HAB-ACTOR = HUMAN ; # Goal: make this like the sme one. #!! HAB-ACTOR
SET HAB-ACTOR-NOT-HUMAN = Sem/Org ; # make this like the sme one #!! HAB-ACTOR-NOT-HUMAN

LIST PROP-ATTR = Sem/Mal Sem/Sur Sem/Fem ; #!! PROP-ATTR
LIST PROP-SUR = Sem/Sur Sem/Mal Sem/Fem ; #!! PROP-SUR

SET TIME-N-SET = N + Sem/Time ;  #!! TIME-N-SET


#!! ###  Syntactic tags

LIST @+FAUXV = @+FAUXV ;	         #!! @+FAUXV
LIST @+FMAINV = @+FMAINV ;	         #!! @+FMAINV
LIST @-FAUXV = @-FAUXV ;	         #!! @-FAUXV
LIST @-FMAINV = @-FMAINV ;	         #!! @-FMAINV
LIST @-FSUBJ> = @-FSUBJ> ;	         #!! @-FSUBJ>
LIST @-F<OBJ = @-F<OBJ ;	         #!! @-F<OBJ
LIST @-FOBJ> = @-FOBJ> ;	         #!! @-FOBJ>
LIST @SPRED<OBJ = @SPRED<OBJ ;       #!! @-FSPRED<OBJ
LIST @-F<ADVL = @-F<ADVL ;	         #!! @-F<ADVL
LIST @-FADVL> = @-FADVL> ;	         #!! @-FADVL>
LIST @-F<SPRED = @-F<SPRED ;         #!! @-F<SPRED
LIST @-F<OPRED = @-F<OPRED ;         #!! @-F<OPRED
LIST @-FSPRED> = @-FSPRED> ;         #!! @-FSPRED>
LIST @-FOPRED> = @-FOPRED> ;         #!! @-FOPRED>
SET FOBJ = @-F<OBJ OR @-FOBJ> ;      #!! @CODE@
SET FMAINV = @-FMAINV OR @+FMAINV ;  #!! @CODE@
SET FAUXV = @-FAUXV OR @+FAUXV ;     #!! @CODE@
LIST @>ADVL = @>ADVL ;               #!! @>ADVL
LIST @ADVL< = @ADVL< ;               #!! @ADVL<
LIST @<ADVL = @<ADVL ;               #!! @<ADVL
LIST @ADVL> = @ADVL> ;               #!! @ADVL>
LIST ADVL = @ADVL @ADVL> @ADVL< @<ADVL @>ADVL @-F<ADVL @-FADVL> ;  #!! @ADVL
LIST @HAB> = @HAB> ;                    #!! @HAB>
LIST @<HAB = @<HAB ;                    #!! @<HAB
SET @HAB = @<HAB OR @HAB> ;    #!! @CODE@
LIST @>N = @>N ;		      #!! @>N
LIST @Interj = @Interj ;    #!! @Interj
LIST @N< = @N< ;		      #!! @N<
LIST @>A = @>A ;		      #!! @>A
LIST @P< = @P< ;		      #!! @P<
LIST @>P = @>P ;		      #!! @>P
LIST @HNOUN = @HNOUN ;	      #!! @HNOUN
LIST @INTERJ = @INTERJ ;      #!! @INTERJ
LIST @>Num = @>Num;		      #!! @>Num
LIST @Pron< = @Pron< ;	      #!! @Pron<
LIST @>Pron = @>Pron ;	      #!! @>Pron
LIST @Num< = @Num< ;	      #!! @Num<
LIST @OBJ = @OBJ ;			  #!! @OBJ
LIST @<OBJ = @<OBJ ;		  #!! @<OBJ
LIST @OBJ> = @OBJ> ;		  #!! @OBJ>
LIST @OPRED = @OPRED ;		  #!! @OPRED
LIST @<OPRED = @<OPRED ;	  #!! @<OPRED
LIST @OPRED> = @OPRED> ;	  #!! @OPRED>
LIST @PCLE = @PCLE ;		  #!! @PCLE
LIST @COMP-CS< = @COMP-CS< ;  #!! @COMP-CS<
LIST @SPRED = @SPRED ;		  #!! @SPRED
LIST @<SPRED = @<SPRED ;	  #!! @<SPRED
LIST @SPRED> = @SPRED> ;	  #!! @SPRED>
LIST @SUBJ = @SUBJ ;		  #!! @SUBJ
LIST @<SUBJ = @<SUBJ ;		  #!! @<SUBJ
LIST @SUBJ> = @SUBJ> ;		  #!! @SUBJ>
SET SUBJ = @<SUBJ OR @SUBJ> OR @SUBJ ; #!! SUBJ
SET SPRED = @<SPRED OR @SPRED> OR @SPRED ; #!! SPRED
SET OPRED = @<OPRED OR @OPRED> OR @OPRED ; #!! OPRED
LIST @PPRED = @PPRED ;        #!! @PPRED
LIST @APP = @APP ;			   #!! @APP
LIST @APP-N< = @APP-N< ;	   #!! @APP-N<
LIST @APP-Pron< = @APP-Pron< ; #!! @APP-Pron<
LIST @APP>Pron = @APP>Pron ;   #!! @APP>Pron
LIST @APP-Num< = @APP-Num< ;   #!! @APP-Num<
LIST @APP-ADVL< = @APP-ADVL< ; #!! @APP-ADVL<
LIST @VOC = @VOC ;			   #!! @VOC
LIST @CVP = @CVP ;			   #!! @CVP
LIST @CNP = @CNP ;			   #!! @CNP
SET OBJ = (@<OBJ) OR (@OBJ>) OR (@OBJ) OR (@-F<OBJ) OR (@-FOBJ>) ; #!! OBJ
LIST <OBJ = @-F<OBJ @<OBJ ;     #!! <OBJ
LIST OBJ> = @OBJ> @-FOBJ> ;    #!! OBJ>
SET <OBJ-OTHERS = <OBJ OR (Gen) OR (Nom) OR (Ess) OR  (Adv) ; #!! <OBJ-OTHERS
SET OBJ>-OTHERS = OBJ> OR (Gen) OR (Nom) OR (Ess) OR  (Adv) ; #!! OBJ>-OTHERS
SET NOT-FAUXV = FMAINV OR OBJ + V OR ADVL + V ;
# Works after the mapping rules for verbs.
SET SYN-V = FMAINV OR FAUXV OR V + SUBJ OR OBJ + V OR ADVL + V OR (V @>N) OR (V @N<) OR (V @A<) ; #!! SYN-V
LIST @X = @X ;    #!! @X



# ========
  SETS
# ========


#!! ## Sets containing sets of lists and tags

#!! This part of the file lists a large number of sets based partly upon the tags defined above, and
#!! partly upon lexemes drawn from the lexicon.
#!! See the sourcefile itself to inspect the sets, what follows here is an overview of the set types.


#!! ### Sets for Single-word sets

LIST INITIAL = "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
        "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
        "á" "æ" "ø" "å" "ö" "ä" ; #!! INITIAL

#!! ### Sets for word or not

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR  \? ; #!! WORD
 # any word
# SET REAL-WORD = WORD - Num - Ord ; #!! REAL-WORD
SET REAL-WORD-NOT-ABBR = WORD - Num - Ord - (ABBR N) ; # This is former REALWORD-NOTABBR #!! REAL-WORD-NOT-ABBR
SET NOT-COMMA = WORD - COMMA ;  #!! NOT-COMMA


#!! ### Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ; #!! ADLVCASE

LIST CASE-AGREEMENT = Nom Acc Gen (Pl Ill) Loc Com Ess ; #!! CASE-AGREEMENT
LIST CASE = Nom Acc Gen Ill Loc Com Ess ; #!! CASE

SET NOT-NOM = CASE - Nom ; #!! NOT-NOM
SET NOT-GEN = CASE - Gen ; #!! NOT-GEN


#!! ### Verb sets
# ---------

# Verbs and their complements
# - - - - - - - - - - - - - -

SET NOT-V = WORD - V ; #!! NOT-V

#!! ### Sets for finiteness and mood
# - - - - - - - - - -

SET REAL-NEG = Neg - Sup ; #!! REAL-NEG
SET MOOD-V = Ind OR Pot OR Imprt OR ImprtII OR Cond OR (Neg Sup) ; #!! MOOD-V
SET NOT-PRFPRC = WORD - PrfPrc ; #!! NOT-PRFPRC


#!! ### Sets for person
# - - - -

LIST SG1-V = (V Sg1)  ; #!! SG1-V
LIST SG2-V = (V Sg2)  ;  #!! SG2-V
LIST SG3-V = (V Sg3)  ;  #!! SG3-V
LIST PL1-V = (V Pl1) ;   #!! PL1-V
LIST PL2-V = (V Pl2) ;   #!! PL2-V
LIST PL3-V = (V Pl3)  ;  #!! PL3-V

 # Note that imperative verbs are not included in these sets!

# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -
SET SG-V = SG1-V OR SG2-V OR SG3-V ;
SET PL-V = PL1-V OR PL2-V OR PL3-V ;


#!! ### Pronoun sets
# ------------

LIST MUN = (Pron Pers Sg1 Nom) (pers p1 sg nom) ;
LIST DON = (Pron Pers Sg2 Nom) (pers p2 sg nom) ;
LIST SON = (Pron Pers Sg3 Nom) (pers p3 sg nom) ;
LIST MII-PERS = (Pron Pers Pl1 Nom) (pers p1 pl nom) ;
LIST DII = (Pron Pers Pl2 Nom) (pers p2 pl nom) ;
LIST SII = (Pron Pers Pl3 Nom) (pers p3 pl nom) ;


LIST SG-DEM = (Pron Dem Sg Nom);
LIST PL-DEM = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;

LIST SG-PRON = (Pron Sg1) (Pron Sg2) (Pron Sg3) (Pron Sg) (pron p1 sg) (pron p2 sg) (pron p3 sg);
LIST PL-PRON = (Pron Pl1) (Pron Pl2) (Pron Pl3) (Pron Pl) (pron p1 pl) (pron p2 pl) (pron p3 pl);




#!! ### Adjectival sets and their complements
# -------------------------------------
SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-A = WORD - A ; # This is former NOT-ADJ
SET NOT-A-COMMA = WORD - A - COMMA ;
SET NOT-Attr = WORD - Attr ;
SET NOT-A-PCLE = WORD - A - Pcle ;
SET NOT-A-CC = WORD - A-CC ;

 # and many others

#!! ### Adverbial sets and their complements
# ------------------------------------
SET LEX-ADV = Adv - (AA) ;

SET NOT-ADV-DE         = (*) - Adv ;
SET NOT-ADV            = NOT-ADV-DE OR CLB ;
SET NOT-ADV-N          = NOT-ADV - N;
SET NOT-ADV-PCLE       = NOT-ADV - Pcle ;
SET NOT-ADV-INDEF      = NOT-ADV - Indef ;
SET NOT-A-ADV-PCLE     = (*) - Pcle - A - Adv ; #



#!! ### Sets of elements with common syntactic behaviour
# ================================================

SETS

#!! ### NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

LIST N-SG-NOM = (N Sg Nom);
LIST PROP = (N Prop);
SET COMMON-N = N - Prop;
SET HEAD-N = N - Cmp/SplitR ;
SET HEAD-N-NOM = (N Nom) - Cmp/SplitR ;

# SET SUBJECTHEAD = N OR A OR Pron - Refl ; # These, can be subject heads

SET NP = N OR A ;                      # anything that can take except numerals
SET NP-HEAD        = Pron OR HEAD-N ;
SET NP-HEAD-SG     = SG-PRON OR (N Sg) OR (A Sg) - Cmp/SplitR  ;
SET NP-HEAD-PL     = PL-PRON OR (N Pl) OR (A Pl) - Cmp/SplitR  ;
SET NP-HEAD-SG-NOM = SG-PRON + Nom OR (N Sg Nom) OR (A Sg Nom) - Cmp/SplitR ;
SET NP-HEAD-PL-NOM = PL-PRON + Nom OR (N Pl Nom) OR (N Coll Nom) OR (A Pl Nom) - Cmp/SplitR ;
SET NP-HEAD-NOM    = NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM ;
SET NP-HEAD-ACC    = (Pron Acc) OR (N Acc) OR (A Acc) - Cmp/SplitR - (Dem Attr);
SET NP-HEAD-GEN    = (Pron Gen) OR (N Gen) OR (A Gen) - Cmp/SplitR - (Dem Attr)   ;

#!! ### The PRE-NP-HEAD family of sets

#!! These sets model noun phrases (NPs). The idea is to first define whatever can
#!! occur in front of the head of the NP, and thereafter negate that with the
#!! expression **WORD - premodifiers**.


SET PRE-NP-HEAD = (Prop Attr) OR (Prop @>N) OR (A Attr) OR (ABBR Attr)
         OR (Pron Pers Gen) OR (N Gen) OR
         Num OR Cmp/SplitR OR CC OR (Pron Dem) OR (Pron Refl Gen) OR (Indef Attr) OR
         (PrfPrc @>N) OR PrsPrc OR (A Ord) ;
         # The strict version of items that can only be premodifiers, not parts of the predicate

SET PRE-A-N     = (Pron Pers Gen) OR (Pron Pers Acc) OR (Pron Indef) OR Num OR (A Ord) OR (Pron Dem) OR (Pron Refl Gen) OR (Pron Refl Acc) ;

SET NOT-PRE-A-N = WORD - PRE-A-N ;

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;



SET NOT-NPMOD     = WORD - PRE-NP-HEAD OR ABBR OR @CVP ;
# This is the previous NPNH (npnh) set.
# NOT-NPMOD  = "NOT-PRE-NP-HEAD"

SET NOT-NPMODADV    = WORD - PRE-NP-HEAD - Adv ;
SET NOT-NPMODADVPCLE    = WORD - PRE-NP-HEAD - Adv - Pcle;
SET NOT-NPMODADV-INDEF   = WORD - PRE-NP-HEAD - Adv - Indef ;


SET NOT-N = WORD - N ;
SET NOT-N-A = WORD - N - A ;
SET NOT-NP = Inf OR Pcle OR Interj OR CS ;
SET NUM = Num ;
SET NOT-NUM = WORD - Num ;
SET NOT-CC = WORD - CC ;
SET NOT-PCLE = WORD - Pcle ;
SET REAL-CLB = CLB - COMMA ;


#!! ### Postposition sets

#!! ### Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) OR (Pron Rel) ;

LIST BOUNDARYSYMBOLS =  "\;" ":" "-" "–" ;

SET S-BOUNDARY = BOUNDARYSYMBOLS OR (@CVP) ;

SET BOC = S-BOUNDARY OR BOS ;
SET BOC-PUNCT = BOC - ("-") - ("–") ;
SET EOC = S-BOUNDARY OR EOS ;

SET NP-BOUNDARY = BOS OR EOS OR REAL-CLB OR Inf OR VGen OR Sup OR Recipr OR Po OR Pr OR Pcle OR Interj OR CS OR CP OR @CVP ;
SET SV-BOUNDARY = S-BOUNDARY OR Inf OR Sup OR FMAINV ;

SET CCCOMMA = CC OR COMMA ;
	# remember that those are potential sentence boundaries, too









#!! ## Grammarchecker sets

LIST &double-space-before = &double-space-before ;
LIST &guillemets = &guillemets ;
LIST &msyn-agr-other-pl3 = &msyn-agr-other-pl3 ;
LIST &msyn-agr-other-sg1 = &msyn-agr-other-sg1 ;
LIST &msyn-agr-pl3-sg3 = &msyn-agr-pl3-sg3 ;
LIST &msyn-agr-pl3-pl1 = &msyn-agr-pl3-pl1 ;
LIST &msyn-agr-prfprcsg-prfprcpl = &msyn-agr-prfprcsg-prfprcpl ;
LIST &msyn-agr-sg2-sg1 = &msyn-agr-sg2-sg1 ;
LIST &msyn-agr-sg3-pl3 = &msyn-agr-sg3-pl3 ;
LIST &msyn-agr-sg3-sg1 = &msyn-agr-sg3-sg1 ;
LIST &msyn-agr-sg3-sg2 = &msyn-agr-sg3-sg2 ;
LIST &msyn-neg-posv = &msyn-neg-posv ;
LIST &msyn-po-asti = &msyn-po-asti ;
LIST &msyn-po-nogela = &msyn-po-notela ;
LIST &msyn-po-notgen = &msyn-po-notgen ;
LIST &space-after-paren-beg = &space-after-paren-beg ;
LIST &space-before-paren-end = &space-before-paren-end ;
LIST &space-before-punct-mark = &space-before-punct-mark ;
LIST &typo = &typo ;

	
MAPPING-PREFIX = & ;


# ======================== #
#!! # Grammarchecker rules 
# ======================== #

SECTION

#!! ## Speller rules

# Speller suggestions rule – add &SUGGESTWF to any spelling suggestion
# that we actually want to suggest to the user.
# The simplest is to just add it to all spelled words:
ADD:spell-it-all (&typo &SUGGESTWF) (<spelled>) ;
# But perhaps you want to only suggest spellings of words that are not inside "quotes":
ADD:spell-conservatively (&typo &SUGGESTWF) (<spelled>) IF (NEGATE -1 QMARK LINK 2 QMARK);


#!! ## Agreement rules


#!! ### Should be Sg1

	 # Sg1 SUBJ

ADD:msyn-agr-sg3-sg1 &msyn-agr-sg3-sg1 TARGET (V Act Sg3) IF
	(*-1 ("mie" Pron Pers Sg1 Nom) BARRIER (V Sg1));
COPY:msyn-agr-sg3-sg1 (Sg1 SUGGEST) EXCEPT (Sg3) TARGET &msyn-agr-sg3-sg1;
ADDRELATION:msyn-agr-sg3-sg1 ($2) (Pron Sg1) TO (*1 V LINK 0 (&msyn-agr-sg3-sg1));

#$ Mie tullee
#% Mie tulen

ADD:xmsyn-agr-sg2-sg1 (&msyn-agr-sg2-sg1) TARGET V IF
	(-1C Pron + Sg1 + Nom)
	(0 V + Act + Ind + Sg2)
	;
COPY:msyn-agr-sg2-sg1 (Sg1 &SUGGEST) EXCEPT (Sg2 &msyn-agr-sg2-sg1) TARGET (V Ind &msyn-agr-sg2-sg1);
ADDRELATION:msyn-agr-sg2-sg1 ($2) (Pron Sg1) TO (*1 V LINK 0 (&msyn-agr-sg2-sg1));

#% Mie tulet.
## Mie tulen.

#!! **Agreement rule:** msyn-agr-other-sg1, *Mun puátá/puáđám* ("other" is dangerous. Check!)
ADD:xmsyn-agr-other-sg1 (&msyn-agr-other-sg1) TARGET V IF
	(NOT -2 Sg1)
	(NOT -2 CC LINK -1 Sg + Nom)
	(-1C Pron + Sg1 + Nom)
	(0 V + Act + Ind)
	(NOT 0 Sg1)
	(NOT 1 ("-") LINK 1 ("uv") LINK 1 V + Sg1)
	(NOT 1 Adv LINK 1 V + Sg1);
COPY:msyn-agr-other-sg1 (Sg1 &SUGGEST) EXCEPT (VNOTSG1 &msyn-agr-other-sg1) TARGET (V Ind Prt &msyn-agr-other-sg1);
COPY:msyn-agr-other-sg1 (Sg1 &SUGGEST) EXCEPT (VNOTSG1 &msyn-agr-other-sg1) TARGET (V Ind Prs &msyn-agr-other-sg1);
COPY:msyn-agr-other-sg1 (Ind Prt Sg1 &SUGGEST) EXCEPT (Actio Num &msyn-agr-other-sg1) TARGET (V  &msyn-agr-other-sg1);


#!!  **Agreement rule:** msyn-agr-other-sg1
ADD:xmsyn-agr-other-sg1 (&msyn-agr-other-sg1) TARGET V IF
	(NOT -2 Sup OR Sg1)
	(-1C Pron + Sg1 + Nom)
	(-1 BOS OR PUNCT)
	(0 V + Act + Ind)
	(NOT 0 Sg1);
COPY:msyn-agr-other-sg1 (Sg1 &SUGGEST) EXCEPT (VNOTSG1 &msyn-agr-other-sg1) TARGET (V Ind Prs &msyn-agr-other-sg1);


#!! ### Should be Sg2

#!!  **Agreement rule:** msyn-agr-sg3-sg2
ADD:msyn-agr-sg3-sg2 &msyn-agr-sg3-sg2 TARGET (V Act Sg3) IF
	(*-1 ("sie" Pron Pers Sg2 Nom) BARRIER (V Sg2));
COPY:msyn-agr-sg3-sg2 (Sg2 SUGGEST) EXCEPT (Sg3) TARGET &msyn-agr-sg3-sg2;
ADDRELATION:msyn-agr-sg3-sg2 ($2) (Pron Sg1) TO (*1 V LINK 0 (&msyn-agr-sg3-sg2));

#$ Sie tullee
#% Sie tulet

#!! ### Should be Sg3

#!! relative sentences between the noun and the finite verb
	
LIST REL-ADV = ("mikä") ("joka") ("missä") ("mistä") ("mihin") ("jossa") ("josta") ("johon") ; #homonymous with relative pronouns
	
ADD:msyn-agr-pl3-sg3 (&msyn-agr-pl3-sg3) TARGET (Act Ind Prt Pl3) IF
	(*-1 Rel OR REL-ADV LINK *-1 (N Sg Nom) OR (Pron Nom Sg3) BARRIER NOT-COMMA)
	(NEGATE 0 (PrfPrc Sg) OR (ConNeg Sg));
	## Koira, josta hän välitti paljon, juoksivat pakoon.

COPY:msyn-agr-pl3-sg3 (Sg3 &SUGGEST) EXCEPT Pl3 TARGET (V Ind Pl3 &msyn-agr-pl3-sg3);

	
#!! ### Should be Pl1

#!!  **Agreement rule:** msyn-agr-sg3-pl3)
ADD:msyn-agr-pl3-pl1 (&msyn-agr-pl3-pl1) TARGET (V Act Pl3) IF
	(*-1 ("sie" Pron Pers Sg2 Nom) BARRIER (V Sg2));
COPY:msyn-agr-pl3-pl1 (Pl1 SUGGEST) EXCEPT (Pl3) TARGET &msyn-agr-pl3-pl1;
ADDRELATION:msyn-agr-pl3-pl1 ($2) (Pron Sg1) TO (*1 V LINK 0 (&msyn-agr-pl3-pl1));

#$ Met tulhaan
#% Met tulema



#!! ### Should be Pl2
#!! Not written, perhaps irrelevant.


#!! ### Should be Pl3
    
#!!  **Agreement rule:** msyn-agr-sg3-pl3)
ADD:msyn-agr-sg3-pl3 (&msyn-agr-sg3-pl3) TARGET (V Act Sg3) IF
	(*-1 ("net" Pron Pers Pl3 Nom) BARRIER (V Pl3))
	(NOT 0 ("olla"));
ADD:msyn-agr-sg3-pl3 (&msyn-agr-sg3-pl3) TARGET (V Act Sg3) IF
	(*-1 ("net" Pron Pers Pl3 Nom) BARRIER (V Pl3))
	(0 ("olla"))
	(NOT 1 (Pass PrfPrc));
COPY:msyn-agr-sg3-pl3 (Pl3 SUGGEST) EXCEPT (Sg3) TARGET &msyn-agr-sg3-pl3;
ADDRELATION:msyn-agr-sg3-pl3 ($2) (Pron Sg3) TO (*1 V LINK 0 (&msyn-agr-sg3-pl3));

#$ Net tullee
#% Net tulhaan


#!!  **Agreement rule:** msyn-agr-other-pl3, Subject to the left, *Toh puátá/puátih.*
ADD:xmsyn-agr-prsptc-pl3 (&msyn-agr-prsptc-pl3) TARGET V IF
	(*-1C Pron + Pl3 + Nom OR Dem + Pl + Nom OR N + Pl + Nom  BARRIER NOT-ADV-PCLE OR VFIN OR CC OR CLB OR PUNCT)
	(0C V + Act + PrsPrc + Pl + Nom)
	(NOT 1 Sg + Nom OR Pron + Sg3 + Nom);
COPY:msyn-agr-other-pl3 (Ind Prs Pl3 &SUGGEST) EXCEPT (PrsPrc Pl Nom  &msyn-agr-prsptc-pl3) TARGET (V &msyn-agr-prsptc-pl3);
#% Ne saavat
#$ Ne saađhaan


#!!  **Agreement rule:** msyn-agr-other-pl3, Subject to the left, *Toh puátá/puátih.*
ADD:xmsyn-agr-other-pl3 (&msyn-agr-other-pl3) TARGET V IF
	(NOT -2 Sup OR Sg3)
	(*-1C Pron + Pl3 + Nom OR Dem + Pl + Nom OR N + Pl + Nom  BARRIER NOT-ADV-PCLE OR VFIN OR CC OR CLB OR PUNCT)
	(0C V + Ind + Sg3)
	(NOT 0 Pl3)
	(NOT 1 Sg + Nom OR Pron + Sg3 + Nom);
COPY:msyn-agr-other-pl3 (Pl3 &SUGGEST) EXCEPT (VNOTPL3 &msyn-agr-other-pl3) TARGET (V Ind Prs &msyn-agr-other-pl3);
COPY:msyn-agr-other-pl3 (Pl3 &SUGGEST) EXCEPT (VNOTPL3 &msyn-agr-other-pl3) TARGET (V Ind Prt &msyn-agr-other-pl3);
#fi Ne tulee.
#% Toh puátá.
#$ Toh puátih.


#!!  **Agreement rule:** msyn-agr-other-pl3
ADD:xmsyn-agr-other-pl3 (&msyn-agr-other-pl3) TARGET V IF 
	(*-1 N + Pl + Nom BARRIER NOT-NPMODADV OR CC OR CLB OR PUNCT LINK NOT 0 ABBR)
	(NEGATE *-1 ("čuovâ" N Pl Nom) LINK 1 ("alne"))
	(0C V + Ind + Sg3 OR V + Cond + Sg3)
	(NOT 0 Pl3)
	(NOT 1 Sg + Nom OR Pron + Sg3 + Nom);

#!!  **Agreement rule:** msyn-agr-other-pl3
ADD:xmsyn-agr-other-pl3 (&msyn-agr-other-pl3) TARGET V IF 
 (NOT -2 Sup OR Sg3)
 (NOT *-1C Pron + Nom OR Dem + Nom OR N + Nom BARRIER NOT-ADV-PCLE)
 (0C V + Ind + Sg3 OR V + Cond + Sg3)
 (NOT 0 Pl3)
 (*1 N + Pl + Nom BARRIER NOT-NPMODADV - (A Pl Nom) - Ill  LINK NOT 0 ABBR LINK NOT 1 Pl3);

COPY:msyn-agr-other-pl3 (Pl3 &SUGGEST) EXCEPT (Sg3 &msyn-agr-other-pl3) TARGET (V Prs &msyn-agr-other-pl3);
COPY:msyn-agr-other-pl3 (Pl3 &SUGGEST) EXCEPT (Sg3 &msyn-agr-other-pl3) TARGET (V Prt &msyn-agr-other-pl3);
COPY:msyn-agr-other-pl3 (Pl3 Foc/ge &SUGGEST) EXCEPT (Sg3 Foc/ge &msyn-agr-other-pl3) TARGET (V Neg Ind &msyn-agr-other-pl3); # Neg
COPY:msyn-agr-other-pl3 (Pl3 &SUGGEST) EXCEPT (Sg3 &msyn-agr-other-pl3) TARGET (V Ind &msyn-agr-other-pl3); # Neg
ADDRELATION:msyn-agr-other-pl3 ($2) (N Pl Nom) FROM (-1 V LINK 0 (&msyn-agr-other-pl3));
ADDRELATION:msyn-agr-other-pl3 ($2) (N Pl Nom) FROM (-2 V LINK 0 (&msyn-agr-other-pl3));
	
#% Viäsust juáhhoo juávhoid tagareh almoneh, moh kovvejeh ääši.
#$ Viäsust juáhhojeh juávhoid tagareh almoneh, moh kovvejeh ääši.

#% Viäsust juáhhoo tagareh almoneh, moh kovvejeh ääši.
#$ Viäsust juáhhojeh tagareh almoneh, moh kovvejeh ääši.








#!! ### Agreement sg -> pl


ADD:msyn-agr-prfprcsg-prfprcpl (&msyn-agr-prfprcsg-prfprcpl) TARGET (V Act PrfPrc Sg Nom) IF
	(*-1 ("olla" V Act Ind Prs) BARRIER S-BOUNDARY LINK 0 Pl1 OR Pl2 OR Pl3)
	(NEGATE 0 (PrfPrc Pl) OR (ConNeg Pl));
COPY:msyn-agr-prfprcsg-prfprcpl (Pl Nom &SUGGEST) EXCEPT (Sg Nom &msyn-agr-prfprcsg-prfprcpl) TARGET (V Act PrfPrc Sg Nom &msyn-agr-prfprcsg-prfprcpl);
ADDRELATION:msyn-agr-prfprcsg-prfprcpl ($2) V + Act + Ind FROM (*1 V  LINK 0 (&msyn-agr-prfprcsg-prfprcpl));

#% Met olema sen sanonu.
## Met olema sen sanonheet.
#% Met olisimma lähättäny kuninkhaale preivin.
## Met olisimma lähättänheet kuninkhaale preivin.




ADD:msyn-agr-sg3-pl3 (&msyn-agr-sg3-pl3) TARGET (Act Ind Prt Sg3) IF
	(*-1 Rel OR REL-ADV LINK *-1 (N Pl Nom) OR (Pron Nom Pl3) BARRIER NOT-COMMA)
	(NEGATE 0 (PrfPrc Sg) OR (ConNeg Sg));
	## 

COPY:msyn-agr-sg3-pl3 (Pl3 &SUGGEST) EXCEPT Sg3 TARGET (V Ind Sg3 &msyn-agr-sg3-pl3);

#!! Prs Sg3 > Prs Pl3
	
ADD:msyn-agr-sg3-pl3 (&msyn-agr-sg3-pl3) TARGET (Act Ind Sg3) IF
	(*-1 (Pers Pl3 Nom) OR (N Pl Nom) OR (Dem Pl Nom) BARRIER (Sg Nom) OR S-BOUNDARY LINK NEGATE -1 CC LINK -1 (N Nom))
	(NEGATE 0 (Act Ind Pl3))
	(NEGATE *1 (Pass PrfPrc) BARRIER NOT-ADV);
	## He tulee.
	## Koirat tulee.
	## Ne tulee.

ADD:msyn-agr-sg3-pl3 (&msyn-agr-sg3-pl3) TARGET (Act Ind Sg3) IF
	(*-1 (N Pl Nom) BARRIER (N Sg Nom) OR S-BOUNDARY LINK -1 CC OR COMMA LINK -1 (N Nom))
	(NEGATE 0 (Act Ind Pl3))
	(0C V);
	## Koira ja kissa tulee.


COPY:msyn-agr-sg3-pl3 (Pl3 &SUGGEST) EXCEPT Sg3 TARGET (V Act Sg3 &msyn-agr-sg3-pl3);
ADDRELATION:msyn-agr-sg3-pl3 ($2) (Pl3) FROM (*1 V LINK 0 &msyn-agr-sg3-pl3);



#!! ### Negation verb rules

ADD:msyn-neg-posv (&msyn-neg-posv) TARGET V IF (*-1 Neg BARRIER NOT-ADV LINK NOT 0 Foc/ka)(0 VFIN)(NOT 0 ConNeg);
COPY:msyn-neg-posv (ConNeg &SUGGEST) EXCEPT (Sg3 &msyn-neg-posv) TARGET &msyn-neg-posv ;
## Mies ei tullee.




#!! ## Postposition rules

#3ADD:msyn-po-notgen (&msyn-po-notgen) TARGET N IF (NOT 0 Gen OR Par)(1C Po);
ADD:msyn-po-notgen (&msyn-po-notgen) TARGET N IF (NOT 0 Gen)(1 POGEN);
ADD:msyn-po-notela (&msyn-po-notgen) TARGET N IF (NOT 0 Ela)(1 POELA);
ADD:msyn-po-notela (&msyn-po-notela) TARGET N IF (NOT 0 Ela)(1 ("<*.matta>"r) + Po);

ADD:msyn-po-asti (&msyn-po-asti) TARGET N OR Pron IF
	(0 Nom OR Gen)
	(NOT 0 Ill)
	(1 ("asti"));
#$ Hän viipyy täällä iltaan asti
#% Hän viipyy täällä ilta asti 


# Huom: minusta huolimatta+Po
# Huom: Ill + asti, saakka
# Abl + katsoen
# TODO: List of gen-Po, par-Po, Ill-Po, Ela-Po






#!! ##  NP internal rules

# And one with a suggestion where we simply change the tag Sg into Attr:
# TODO: Give example on this:
ADD:attr-not-pred (&attr-not-pred) TARGET (A Sg) IF (NOT 0 Attr OR Gen) (1 CC LINK *1 COMMON-N) ;
COPY:attr-not-pred (Attr &SUGGEST) EXCEPT (Sg) TARGET (&attr-not-pred) ;

#!! The method is: Add &SUGGEST to a copied reading to *generate* a
#!! suggestion form from that reading. The copy should contain the error
#!! tag too – &-prefixed error tags are ignored when generating, but
#!! used to create human-readable messages.



#!! ##  Punctuation rules

#!! ### Punctuation marks

# Simple punctuation rules showing how to change the lemma in the suggestions:
ADD:use-guillemets (&guillemets) TARGET (""");
COPY:left-guillemet ("«" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;
COPY:right-guillemet ("»" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;

ADD:use-ellipsis (&ellipsis) TARGET ("...");
COPY:use-ellipsis ("…" &SUGGEST) EXCEPT ("...") TARGET (&ellipsis) ;


#!! ###  Spacing errors

#!! Here we find spacing rules.

ADD (&double-space-before)     (<doubleSpaceBefore>);
ADD (&space-after-paren-beg)   (<spaceAfterParenBeg>);
ADD (&space-before-paren-end)  (<spaceBeforeParenEnd>);
ADD (&space-before-punct-mark) (<SpaceBeforePunctMark>);


